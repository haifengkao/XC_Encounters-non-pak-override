INIT
CHARACTER:__Me
TRIGGER:%Point1 = CYS_Wolgraff_Pickup1_0a01b17e-fbd6-4d5d-8d57-40f60cb33e3c
TRIGGER:%Point2 = CYS_Wolgraff_Pickup2_dbcf72dc-1626-4cc3-8c7f-cc75a79da726
TRIGGER:%Point3 = CYS_Wolgraff_Pickup3_85596f53-0f04-430b-af1a-a4fcc0d492b1
INT:%Active = 1
INT:%End = 0

BEHAVIOUR
REACTION Main, 500
USAGE PEACE
CHECK "c1"
	IsEqual(%Active,1)
ACTIONS
	CharacterMoveTo(%Point1)
	CharacterLookFrom(%Point1)
	CharacterPlayAnimation("PickCoin_01")
	
	CharacterMoveTo(%Point2)
	CharacterLookFrom(%Point2)
	CharacterPlayAnimation("PickCoin_01")
	
	CharacterMoveTo(%Point3)
	CharacterLookFrom(%Point3)
	CharacterPlayAnimation("PickCoin_01")

	Sleep(4)
	
REACTION SourceAcademy, 500
USAGE PEACE
CHECK "c1"
	IsEqual(%End,1)
ACTIONS
	CharacterWander(TRIGGER:SAC_Behavior_WolgraffWander_c01f1bd5-0bcd-4a16-93d2-f48e291dcd68, 3.0)
	Sleep(5)
	
REACTION HealNormalPlayer, 500
USAGE PEACE
VARS
	FLOAT:_vitality
	CHARACTER:_Player
	CHARACTER:_Zombie
CHECK "c1&c2&c3&(c4|c5|c6|c7|c8|c9|(c10&c11))&!c12"
	CharacterGet(_Player,__Me,12,Lowest,Vitality,Player,null,null,null,__Me)
	CharacterGetStat(_vitality,_Player,Vitality)
	IsLessThen(_vitality,0.99)
	CharacterCanCast(__Me,Heal_RegenerateStart)
	CharacterCanCast(__Me,Target_MassHeal)
	CharacterCanCast(__Me,Target_FirstAid)
	CharacterCanCast(__Me,AMER_SKILL_Attonement)
	CharacterCanCast(__Me,AMER_SKILL_Cauterize)
	CharacterCanCast(__Me,AMER_SKILL_Soothe)
	CharacterCanCast(__Me,Heal_CureWoundsKnight)
	IsEqual(%CureWounds,1)
	CharacterHasTalent(_Player,Zombie)
ACTIONS
	IF "c1&!(c2&c3)"
		IsInDangerousSurface(__Me)
		IsInSurface(__Me,Ooze)
		CharacterHasTalent(__Me,Zombie)
	THEN
		DelayReaction("HealNormalPlayer",10)
		Goto("End")
	ENDIF
	IF "c1&!c2"
		CharacterCanCast(__Me,Target_MassHeal)
		CharacterGet(_Zombie,_Player,10,Random,null,Player,null,null,Zombie)
	THEN
		CharacterUseSkill(Target_MassHeal,_Player)
	ELIF "c1&!c2"	
		CharacterCanCast(__Me,AMER_SKILL_Soothe)
		CharacterGet(_Zombie,_Player,10,Random,null,Player,null,null,Zombie)
	THEN
		CharacterUseSkill(AMER_SKILL_Soothe,_Player)		
	ELIF "c1"
		CharacterCanCast(__Me,Heal_RegenerateStart)
	THEN
		CharacterUseSkill(Heal_RegenerateStart,_Player)	
	ELIF "c1"
		CharacterCanCast(__Me,Target_FirstAid)
	THEN
		CharacterUseSkill(Target_FirstAid,_Player)	
	ELIF "c1"
		CharacterCanCast(__Me,AMER_SKILL_Cauterize)
	THEN
		CharacterUseSkill(AMER_SKILL_Cauterize,_Player)
	ELIF "c1"
		CharacterCanCast(__Me,AMER_SKILL_Attonement)
	THEN
		CharacterUseSkill(AMER_SKILL_Attonement,_Player)
	ELIF "c1&!c2&c3"
		CharacterCanCast(__Me,Heal_CureWoundsKnight)
		IsInDangerousSurface(_Player)
		IsEqual(%CureWounds,1)
	THEN
		CharacterMoveTo(_Player,1)
		CharacterUseSkill(Heal_CureWoundsKnight,_Player)
		StartTimer("CureWoundsAgain",40,0)
		Set(%CureWounds,0)
	ENDIF
	Label("End")
INTERRUPT
	Reset()
	DelayReaction("HealNormalPlayer",10)
	
REACTION HealMyselfNormal, 490
USAGE PEACE
VARS
	FLOAT:_vitality
	CHARACTER:_Zombie
CHECK "c1&c2&(c3|c4|c5|c6|c7|c8|(c9&c10))&!c11"
	CharacterGetStat(_vitality,__Me,Vitality)
	IsLessThen(_vitality,0.99)
	CharacterCanCast(__Me,Heal_RegenerateStart)
	CharacterCanCast(__Me,Target_MassHeal)
	CharacterCanCast(__Me,Target_FirstAid)
	CharacterCanCast(__Me,AMER_SKILL_Attonement)
	CharacterCanCast(__Me,AMER_SKILL_Cauterize)
	CharacterCanCast(__Me,AMER_SKILL_Soothe)
	CharacterCanCast(__Me,Heal_CureWoundsKnight)
	IsEqual(%CureWounds,1)
	CharacterHasTalent(__Me,Zombie)
ACTIONS
	IF "c1&!(c2&c3)"
		IsInDangerousSurface(__Me)
		IsInSurface(__Me,Ooze)
		CharacterHasTalent(__Me,Zombie)
	THEN
		DelayReaction("HealMyselfNormal",10)
		Goto("End")
	ENDIF
	IF "c1&!c2"
		CharacterCanCast(__Me,Target_MassHeal)
		CharacterGet(_Zombie,__Me,10,Random,null,Player,null,null,Zombie)
	THEN
		CharacterUseSkill(Target_MassHeal,__Me)
	ELIF "c1&!c2"
		CharacterCanCast(__Me,AMER_SKILL_Soothe)
		CharacterGet(_Zombie,__Me,10,Random,null,Player,null,null,Zombie)
	THEN
		CharacterUseSkill(AMER_SKILL_Soothe,__Me)	
	ELIF "c1"
		CharacterCanCast(__Me,Heal_RegenerateStart)
	THEN
		CharacterUseSkill(Heal_RegenerateStart,__Me)
	ELIF "c1"
		CharacterCanCast(__Me,Target_FirstAid)
	THEN
		CharacterUseSkill(Target_FirstAid,__Me)
		
	ELIF "c1"
		CharacterCanCast(__Me,AMER_SKILL_Cauterize)
	THEN
		CharacterUseSkill(AMER_SKILL_Cauterize,__Me)
	ELIF "c1"
		CharacterCanCast(__Me,AMER_SKILL_Attonement)
	THEN
		CharacterUseSkill(AMER_SKILL_Attonement,__Me)	
	ELIF "c1&c2"
		CharacterCanCast(__Me,Heal_CureWoundsKnight)
		IsEqual(%CureWounds,1)
	THEN
		CharacterUseSkill(Heal_CureWoundsKnight,__Me)
		StartTimer("CureWoundsAgain",40,0)
		Set(%CureWounds,0)
	ENDIF
	Label("End")
INTERRUPT
	Reset()
	DelayReaction("HealMyselfNormal",10)	
	
EVENTS
EVENT Hired
ON
	OnCharacterEvent(__Me,"Hired")
ACTIONS
	Set(%Active,0)
	
EVENT CureWoundsTimer
ON
	OnTimer("CureWoundsAgain")
ACTIONS
	Set(%CureWounds,1)