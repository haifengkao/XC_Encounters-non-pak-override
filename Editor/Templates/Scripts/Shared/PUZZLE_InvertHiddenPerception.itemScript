INIT
ITEM:__Me
INT:%CurrentIsVisible=1
INT:%NewIsVisible=0
EXTERN FLOAT:%MinPerception=0
EXTERN FLOAT:%MinDistance=0

EVENTS

EVENT InitializeHidden
ON
	OnInit()
ACTIONS
	IF "c1"
		IsEqual(%CurrentIsVisible,1)
	THEN
		StartTimer("UpdateHidden",0.2,-1)
	ENDIF
	ItemSetOnStage(__Me,%CurrentIsVisible)
	
EVENT ShutdownHidden
ON
	OnShutdown()
ACTIONS
	StopTimer("UpdateHidden")

EVENT UpdateHidden
ON
	OnTimer("UpdateHidden")
ACTIONS
	Set(%NewIsVisible,1)
	IterateParty("HiddenObjectCheck")
	IF "!c1"
		IsEqual(%CurrentIsVisible,%NewIsVisible)
	THEN
		Set(%CurrentIsVisible,%NewIsVisible)
		PlayEffectAt(__Me, "FX_GP_TeleportSmoke_A")
		ItemSetOnStage(__Me,%CurrentIsVisible)
	ENDIF
	
	IF "c1"
		IsEqual(%CurrentIsVisible,0)
	THEN
		StopTimer("UpdateHidden")
	ENDIF

EVENT CheckHidden
VARS
CHARACTER:_Char
FLOAT:_Perception
FLOAT:_Dist
STRING:_Event
ON
	OnIterateCharacter(_Char,"HiddenObjectCheck")
ACTIONS
	IF "c1&!c2&c3&c4&!c5&c6" 
		IsEqual(%NewIsVisible,1)
		CharacterIsDead(_Char)
		CharacterGetStat(_Perception,_Char,TrapPerception)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Perception,%MinPerception)
		IsLessThen(_Dist,%MinDistance)
	THEN
		Set(%NewIsVisible,0)
		GetRandom(_Event, "FakePerceptionReveal", "FakePerceptionReveal2", "FakePerceptionReveal3", "FakePerceptionReveal4")
		CharacterEvent(_Char,_Event)
	ENDIF
	
EVENT StoryReveal
ON
	OnItemEvent(__Me,"StoryReveal")
ACTIONS
	PlayEffectAt(__Me, "FX_GP_TeleportSmoke_A")
	ItemSetOnStage(__Me,0)
	Set(%CurrentIsVisible,0)
	StopTimer("UpdateHidden")
