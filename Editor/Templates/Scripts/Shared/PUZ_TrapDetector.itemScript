INIT
ITEM:__Me
EXTERN INT:%CurrentIsActive=1
EXTERN FLOAT:%MinPerception=0
EXTERN FLOAT:%MinDistance=0
EXTERN STRING:%EventOn = "HIB_SentinelSpotted"
EXTERN STRING:%EventOff = "HIB_SentinelSpotted"
FIXEDSTRING:%MyEvent = ""
STRING:%MyStringEvent = ""

EVENTS

EVENT InitializeHidden
ON
	OnInit()
ACTIONS
	IF "c1"
		IsEqual(%CurrentIsActive,1)
	THEN
		StartTimer("UpdateHidden",0.2,-1)
	ENDIF
	Print(%MyStringEvent, "EventFrom_[1]", __Me)
	Cast(%MyEvent, %MyStringEvent)

EVENT ShutdownHidden
ON
	OnShutdown()
ACTIONS
	StopTimer("UpdateHidden")

EVENT UpdateHidden
ON
	OnTimer("UpdateHidden")
ACTIONS
	IterateParty(%MyEvent)
	IF "c1"
		IsEqual(%CurrentIsActive,0)
	THEN
		StopTimer("UpdateHidden")
	ENDIF

EVENT CheckHidden
VARS
CHARACTER:_Char
FLOAT:_Perception
FLOAT:_Dist
STRING:_CommentStr
FIXEDSTRING:_Comment
INT:_Number
ON
	OnIterateCharacter(_Char,%MyEvent)
ACTIONS
	IF "!c1&c2&c3&c4&c5&c6&c7" 
		CharacterIsDead(_Char)
		CharacterGetStat(_Perception,_Char,Perception)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Perception,%MinPerception)
		IsLessThen(_Dist,%MinDistance)
		IsEqual(%CurrentIsActive,1)
		CharacterIsPlayer(_Char)
	THEN
		Set(%CurrentIsActive,0)
		GetRandomBetween(_Number, 1, 20)
		Print(_CommentStr, "GEN_TrapArea_1_[1]", _Number)
		Cast(_Comment, _CommentStr)
		IF "c1"
			TranslatedStringKeyExists(_Comment)
		THEN
			DisplayText(_Char, _Comment, 5.0)
		ENDIF
	ENDIF

EVENT EventOn
ON
	OnCharacterItemEvent(_,_,%EventOn)
ACTIONS
	IF "c1"
		IsEqual(%CurrentIsActive,0)
	THEN
		Set(%CurrentIsActive,1)
		StartTimer("UpdateHidden",0.2,-1)
	ENDIF

EVENT EventOff
ON
	OnCharacterItemEvent(_,_,%EventOff)
ACTIONS
	IF "c1"
		IsEqual(%CurrentIsActive,1)
	THEN
		Set(%CurrentIsActive,0)
		StopTimer("UpdateHidden")
	ENDIF