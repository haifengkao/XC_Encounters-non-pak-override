#INCLUDE Base

INIT
USING Base
CHARACTER:__Me
CHARACTER:%debuffSkillTarget=null
EXTERN SKILL:%debuffSkill=Target_EnemyBleed
EXTERN INT:%rangeAdjustmentBoolDebuff=0
EXTERN INT:%frequencyDebuff=0
EXTERN INT:%frequencyDebuffStartAt=0
EXTERN INT:%priorityDebuff=300
EXTERN INT:%storeTargetDebuff=1
EXTERN COMPARE:%targetFindHowDebuff=Highest
EXTERN COMPAREFUNC:%targetFindWhatDebuff=Strength
EXTERN FLOAT:%targetHealthEqOrLowerDebuff=1
EXTERN STATUS:%targetStatusDebuff=null
EXTERN STATUS:%targetIsNOT=BLEEDING
EXTERN FIXEDSTRING:%saveAPTextDebuff="CMB_MageSkipsTurnToPrepare"
EXTERN INT:%displaysaveAPTextDebuff=1

BEHAVIOUR
REACTION CastSkillDebuff,0
VARS
	CHARACTER:_char
	FLOAT:_minRange
	FLOAT:_maxRange
	FLOAT:_hp
USAGE COMBAT
CHECK "c1&c2&c3&c4&!c5&!c6&!c7"
	CharacterCanCast(__Me,%debuffSkill,1)
	CharacterGetSkillRange(_minRange,_maxRange,__Me,%debuffSkill)
	CharacterGet(_char,__Me,30,%targetFindHowDebuff,%targetFindWhatDebuff,Enemy,null,%targetStatusDebuff,null,__Me)
	CharacterGetStat(_hp,_char,Vitality)
	IsGreaterThen(_hp,%targetHealthEqOrLowerDebuff)
	CharacterHasStatus(_char,INVISIBLE)
	CharacterHasStatus(_char,%targetIsNOT)
ACTIONS
	IF "c1|c2|c3|c4|c5|c6|c7"
		IsEqual(%storeTargetDebuff,0)
		IsEqual(%debuffSkillTarget,null)
		CharacterIsDead(%debuffSkillTarget)
		CharacterHasStatus(%debuffSkillTarget,CHARMED)
		CharacterIsAlly(__Me,%debuffSkillTarget)
		CharacterHasStatus(%debuffSkillTarget,INVISIBLE)
		CharacterHasStatus(%debuffSkillTarget,%targetIsNOT)
	THEN
		Set(%debuffSkillTarget,_char)
	ENDIF
	IF "c1"
		CharacterCanCast(__Me,%debuffSkill,0)
	THEN
		IF "c1"
			IsEqual(%rangeAdjustmentBoolDebuff,1)
		THEN
			Subtract(_maxRange,2.8)
		ENDIF		
		CharacterMoveInRange(%debuffSkillTarget,_minRange,_maxRange,1)
		CharacterUseSkill(%debuffSkill,%debuffSkillTarget)
		Set(%frequencyDebuffStartAt,0)
		SetPriority("CastSkillDebuff",0)
	ELSE
		Sleep(1)
		CharacterLookAt(%debuffSkillTarget)
		IF "c1"
			IsEqual(%displaysaveAPTextDebuff,1)
		THEN
			DisplayCombatInfoText(__Me,%saveAPTextDebuff,2)
		ELSE
			Sleep(1)
		ENDIF
		CharacterPlayEffect(__Me, "FX_GP_Status_SkipTurn_A_Icon", "Dummy_OverheadFX")
		CharacterEndTurn()
	ENDIF

EVENTS
EVENT AddSkillDebuff
ON
	OnInit()
ACTIONS
	CharacterAddSkill(%debuffSkill)
	
EVENT SetFrequencyDebuff
ON
	OnTurn()
ACTIONS
	IF "!c1&!c2"
		IsEqual(%storeTargetDebuff,1)
		IsEqual(%storeTargetDebuff,0)
	THEN
		DebugText(__Me,"Storetarget must be 1 or 0 (on or off)")
		Set(%storeTargetDebuff,1)
	ENDIF
	IF "!c1&!c2"
		IsEqual(%rangeAdjustmentBoolDebuff,1)
		IsEqual(%rangeAdjustmentBoolDebuff,0)
	THEN
		DebugText(__Me,"Range Adjustment must be 1 or 0 (on or off)")
		Set(%rangeAdjustmentBoolDebuff,1)
	ENDIF		
	IF "c1"
		IsLessThen(%frequencyDebuffStartAt,%frequencyDebuff)
	THEN		
		Add(%frequencyDebuffStartAt,1)
	ELSE
		SetPriority("CastSkillDebuff",%priorityDebuff)
	ENDIF