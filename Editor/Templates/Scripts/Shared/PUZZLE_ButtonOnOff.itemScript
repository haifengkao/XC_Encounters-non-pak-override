INIT
ITEM:__Me
EXTERN STRING:%EventOn=null
EXTERN STRING:%EventOff=null

EXTERN INT:%CurrentState = 0
INT:%WantedState = -1
CHARACTER:%UsedBy = null

EVENTS

EVENT InitState
ON
	OnInit()
ACTIONS
	Set(%UsedBy,null)
	IF "c1"
		IsEqual(%WantedState,-1)
	THEN
		Set(%WantedState,%CurrentState)
		Set(%CurrentState,-1)
	ENDIF
	
EVENT ButtonUse
VARS
CHARACTER:_Char
ON
	OnUseItem(_Char,__Me)
ACTIONS
	IF "c1"
		IsEqual(%CurrentState,0)
	THEN
		Set(%WantedState,1)
	ELSE
		Set(%WantedState,0)
	ENDIF
	IF "c1"
		IsEqual(%UsedBy,null)
	THEN
		Set(%UsedBy,_Char)
	ENDIF
	
BEHAVIOUR

REACTION StateChange,10
CHECK "!c1"
	IsEqual(%CurrentState,%WantedState)
ACTIONS
	IF "c1"
		IsEqual(%WantedState,0)
	THEN				
		ItemPlayAnimation("Off")
		ItemEvent(__Me,%EventOff)
		ItemPlayEffect(__Me, "FX_GP_CameraShake_B")
		CharacterItemEvent(%UsedBy,__Me,%EventOff)
	ELSE			
		ItemPlayAnimation("On")
		ItemEvent(__Me,%EventOn)
		ItemPlayEffect(__Me, "FX_GP_CameraShake_B")
		CharacterItemEvent(%UsedBy,__Me,%EventOn)
	ENDIF
	Set(%UsedBy,null)		
	Set(%CurrentState,%WantedState)
