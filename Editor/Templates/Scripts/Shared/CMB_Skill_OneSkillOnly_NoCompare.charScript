#INCLUDE Base

INIT
USING Base
CHARACTER:__Me
CHARACTER:%oneSkillTargetNC=null
EXTERN SKILL:%chooseSkillNC=null
EXTERN INT:%rangeAdjustmentBoolOSNC=0
EXTERN INT:%frequencyOSNC=0
EXTERN INT:%frequencyOSNCStartAt=0
EXTERN INT:%priorityOSNC=300
EXTERN INT:%storeTargetOSNC=1
EXTERN SURFACE:%targetOnSurfaceOSNC=null
EXTERN RELATION:%targetRelationOSNC=Ally
EXTERN FLOAT:%targetHealthEqOrLowerOSNC=1
EXTERN STATUS:%targetStatusOSNC=null
EXTERN FIXEDSTRING:%saveAPTextOneNC="CMB_MageSkipsTurnToPrepare"
EXTERN INT:%displaySaveAPTextOneNC=1

BEHAVIOUR
REACTION CastSkillOneNC,0
VARS
	CHARACTER:_char
	FLOAT:_minRange
	FLOAT:_maxRange
	FLOAT:_hp
USAGE COMBAT
CHECK "c1&c2&c3&c4&!c5"
	CharacterCanCast(__Me,%chooseSkillNC,1)
	CharacterGetSkillRange(_minRange,_maxRange,__Me,%chooseSkillNC)
	CharacterGet(_char,__Me,30,Random,null,%targetRelationOSNC,%targetOnSurfaceOSNC,%targetStatusOSNC)
	CharacterGetStat(_hp,_char,Vitality)
	IsGreaterThen(_hp,%targetHealthEqOrLowerOSNC)
ACTIONS
	IF "c1|c2|c3|c4|c5"
		IsEqual(%storeTargetOSNC,0)
		IsEqual(%oneSkillTargetNC,null)
		CharacterIsDead(%oneSkillTargetNC)
		CharacterHasStatus(%oneSkillTargetNC,CHARMED)
		CharacterIsAlly(__Me,%oneSkillTargetNC)
	THEN
		Set(%oneSkillTargetNC,_char)
	ENDIF
	IF "c1"
		CharacterCanCast(__Me,%chooseSkillNC,0)
	THEN
		IF "c1"
			IsEqual(%rangeAdjustmentBoolOSNC,1)
		THEN
			Subtract(_maxRange,3.0)
		ENDIF
		CharacterMoveInRange(%oneSkillTargetNC,_minRange,_maxRange,1)
		CharacterUseSkill(%chooseSkillNC,%oneSkillTargetNC)
		Set(%frequencyOSNCStartAt,0)
		SetPriority("CastSkillOneNC",0)
	ELSE
		Sleep(1)
		CharacterLookAt(%oneSkillTargetNC)
		IF "c1"
			IsEqual(%displaySaveAPTextOneNC,1)
		THEN
			DisplayCombatInfoText(__Me,%saveAPTextOneNC,2)
		ELSE
			Sleep(1)
		ENDIF
		CharacterPlayEffect(__Me, "FX_GP_Status_SkipTurn_A_Icon", "Dummy_OverheadFX")
		CharacterEndTurn()
	ENDIF

EVENTS
EVENT AddSkillOneNC
ON
	OnInit()
ACTIONS
	IF "!c1"
		IsEqual(%chooseSkillNC,null)
	THEN
		CharacterAddSkill(%chooseSkillNC)
	ENDIF
	
EVENT SetFrequenceOneNC
ON
	OnTurn()
ACTIONS
	IF "!c1&!c2"
		IsEqual(%storeTargetOSNC,1)
		IsEqual(%storeTargetOSNC,0)
	THEN
		DebugText(__Me,"Storetarget must be 1 or 0 (on or off)")
		Set(%storeTargetOSNC,1)
	ENDIF
	IF "!c1&!c2"
		IsEqual(%rangeAdjustmentBoolOSNC,1)
		IsEqual(%rangeAdjustmentBoolOSNC,0)
	THEN
		DebugText(__Me,"Range Adjustment must be 1 or 0 (on or off)")
		Set(%rangeAdjustmentBoolOSNC,1)
	ENDIF		
	IF "c1"
		IsEqual(%chooseSkillNC,null)
	THEN
		DebugText(__Me,"Skill script assigned but no skill selected")
	ENDIF
	IF "c1"
		IsLessThen(%frequencyOSNCStartAt,%frequencyOSNC)
	THEN		
		Add(%frequencyOSNCStartAt,1)
	ELSE
		SetPriority("CastSkillOneNC",%priorityOSNC)
	ENDIF