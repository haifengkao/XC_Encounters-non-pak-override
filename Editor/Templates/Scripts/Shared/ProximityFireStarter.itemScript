INIT
ITEM:__Me
INT:%TimerStopped=0
EXTERN FLOAT:%MinDistance=8
ITEM:__Me
INT:%FlameFX = 0
INT:%SmokeFX = 0
INT:%HasBeenWet = 0
INT:%FireStarted = 0
FLOAT:%SmokeTimer
EXTERN STRING:%FXName = "FX_Env_CampFire_A"

EVENTS

EVENT InitializeFire
ON
	OnInit()
ACTIONS
	IF "c1"
		IsEqual(%FireStarted,0)
	THEN
		StartTimer("UpdateFire",0.2,-1)
	ENDIF
	
EVENT ShutdownFire
ON
	OnShutdown()
ACTIONS
	StopTimer("UpdateFire")

EVENT UpdateFire
ON
	OnTimer("UpdateFire")
ACTIONS
	IterateParty("ProximityFireStarter_Iter")
	IF "c1"
		IsEqual(%FireStarted,1)
	THEN
		StopTimer("UpdateFire")
	ENDIF

EVENT CheckFire
VARS
CHARACTER:_Char
FLOAT:_Perception
FLOAT:_Dist
STRING:_Event
ON
	OnIterateCharacter(_Char,"ProximityFireStarter_Iter")
ACTIONS
	IF "c1&!c2&c3&c4&c5" 
		IsEqual(%FireStarted,0)
		CharacterIsDead(_Char)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Dist,%MinDistance)
		IsEqual(%TimerStopped,0)
	THEN
		Set(%FlameFX,0)
		Set(%SmokeFX,0)
		Set(%FireStarted,1)
		ItemPlayLoopEffect(%FlameFX,__Me,%FXName,"Dummy_FX")
	ENDIF
	
EVENT WhenRaining
ON
	OnItemStatus(__Me,WET)
ACTIONS
	IF "c1&c2"
		IsEqual(%HasBeenWet,0)
		IsEqual(%FireStarted,1)
	THEN
		StopLoopEffect(%FlameFX)
		Set(%FlameFX,0)
		ItemPlayLoopEffect(%SmokeFX,__Me,"FX_Env_Smoke_Fire_A","Dummy_FX")
		Set(%HasBeenWet,1)
		GetRandomBetween(%SmokeTimer,60,120)
		StartTimer("SmokeClears",%SmokeTimer,1)
	ENDIF

EVENT SmokeTimerEnds
ON
	OnTimer("SmokeClears")
ACTIONS
	StopLoopEffect(%SmokeFX)
	Set(%SmokeFX,0)
	
EVENT OnDestroy
ON
	OnShutdown()
ACTIONS
	StopLoopEffect(%SmokeFX)
	Set(%SmokeFX,0)
	StopLoopEffect(%FlameFX)
	Set(%FlameFX,0)
