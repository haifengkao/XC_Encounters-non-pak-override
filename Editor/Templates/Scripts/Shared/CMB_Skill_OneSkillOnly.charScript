#INCLUDE Base

INIT
USING Base
CHARACTER:__Me
CHARACTER:%oneSkillTarget=null
EXTERN SKILL:%chooseSkill=null
EXTERN INT:%rangeAdjustmentBoolOS=0
EXTERN INT:%frequencyOS=0
EXTERN INT:%frequencyOSStartAt=0
EXTERN INT:%priorityOS=300
EXTERN INT:%storeTargetOS=1
EXTERN COMPARE:%targetFindHowOS=Random
EXTERN COMPAREFUNC:%targetFindWhatOS=Vitality
EXTERN SURFACE:%targetOnSurfaceOS=null
EXTERN RELATION:%targetRelationOS=Enemy
EXTERN FLOAT:%targetHealthEqOrLowerOS=1
EXTERN STATUS:%targetStatusOS=null
EXTERN FIXEDSTRING:%saveAPTextOne="CMB_MageSkipsTurnToPrepare"
EXTERN INT:%displaySaveAPTextOne=1

BEHAVIOUR
REACTION CastSkill1,0
VARS
	CHARACTER:_char
	FLOAT:_minRange
	FLOAT:_maxRange
	FLOAT:_hp
USAGE COMBAT
CHECK "c1&c2&c3&c4&!c5&!c6"
	CharacterCanCast(__Me,%chooseSkill,1)
	CharacterGetSkillRange(_minRange,_maxRange,__Me,%chooseSkill)
	CharacterGet(_char,__Me,30,%targetFindHowOS,%targetFindWhatOS,%targetRelationOS,%targetOnSurfaceOS,%targetStatusOS,null,__Me)
	CharacterGetStat(_hp,_char,Vitality)
	IsGreaterThen(_hp,%targetHealthEqOrLowerOS)
	CharacterHasStatus(_char,INVISIBLE)
ACTIONS
	IF "c1|c2|c3|c4|c5|c6"
		IsEqual(%storeTargetOS,0)
		IsEqual(%oneSkillTarget,null)
		CharacterIsDead(%oneSkillTarget)
		CharacterHasStatus(%oneSkillTarget,CHARMED)
		CharacterIsAlly(__Me,%oneSkillTarget)
		CharacterHasStatus(%oneSkillTarget,INVISIBLE)
	THEN
		Set(%oneSkillTarget,_char)
	ENDIF
	IF "c1"
		CharacterCanCast(__Me,%chooseSkill,0)
	THEN
		IF "c1"
			IsEqual(%rangeAdjustmentBoolOS,1)
		THEN
			Subtract(_maxRange,3.0)
		ENDIF
		CharacterMoveInRange(%oneSkillTarget,_minRange,_maxRange,1)
		CharacterUseSkill(%chooseSkill,%oneSkillTarget)
		Set(%frequencyOSStartAt,0)
		SetPriority("CastSkill1",0)
	ELSE
		Sleep(1)
		CharacterLookAt(%oneSkillTarget)
		IF "c1"
			IsEqual(%displaySaveAPTextOne,1)
		THEN
			DisplayCombatInfoText(__Me,%saveAPTextOne,2)
		ELSE
			Sleep(1)
		ENDIF
		CharacterPlayEffect(__Me, "FX_GP_Status_SkipTurn_A_Icon", "Dummy_OverheadFX")
		CharacterEndTurn()
	ENDIF

EVENTS
EVENT AddSkill1
ON
	OnInit()
ACTIONS
	IF "!c1"
		IsEqual(%chooseSkill,null)
	THEN
		CharacterAddSkill(%chooseSkill)
	ENDIF
	
EVENT SetFrequence1
ON
	OnTurn()
ACTIONS
	IF "!c1&!c2"
		IsEqual(%storeTargetOS,1)
		IsEqual(%storeTargetOS,0)
	THEN
		DebugText(__Me,"Storetarget must be 1 or 0 (on or off)")
		Set(%storeTargetOS,1)
	ENDIF
	IF "!c1&!c2"
		IsEqual(%rangeAdjustmentBoolOS,1)
		IsEqual(%rangeAdjustmentBoolOS,0)
	THEN
		DebugText(__Me,"Range Adjustment must be 1 or 0 (on or off)")
		Set(%rangeAdjustmentBoolOS,1)
	ENDIF	
	IF "c1"
		IsEqual(%chooseSkill,null)
	THEN
		DebugText(__Me,"Skill script assigned but no skill selected")
	ENDIF
	IF "c1"
		IsLessThen(%frequencyOSStartAt,%frequencyOS)
	THEN		
		Add(%frequencyOSStartAt,1)
	ELSE
		SetPriority("CastSkill1",%priorityOS)
	ENDIF