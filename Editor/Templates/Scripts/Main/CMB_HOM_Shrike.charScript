#INCLUDE DefaultCharacter
INIT
	USING DefaultCharacter	//required for OVERRIDE below
	CHARACTER:__Me
	CHARACTER:%PurifyingFire_Target	
	CHARACTER:%BanishSummon_Target
	CHARACTER:%CharacterNearObject
	ITEM:%DetectedDestructibleObject
	ITEM:%SummonedVoidCrystal_01
	ITEM:%SummonedVoidCrystal_02
	ITEM:%SummonedVoidCrystal_03
	ITEM:%SummonedVoidCrystal_04
	INT:%CurrentCrystalCount=0
	CHARACTERTEMPLATE:%VoidCaster_RT="Summons_Shadows_Caster_Nightmare_776bb9db-aa6f-433d-9dd2-731c5270d0e3"
	ITEMTEMPLATE:%VoidCrystal_RT="LUC_VoidCrystal_deaf6338-05a6-4988-8d28-b9a2829db4ea"
	FLOAT:%NegaShrike_SightRange
	FLOAT3:%TargetsPosition
	INT:%Summon_CommonValue_Current=1
	INT:%Summon_CommonValue_Desired=2
	INT:%CrystalCreation_Current=2	
	INT:%CrystalCreation_Desired=2	
	INT:%CrystalCountdown_Current=0	
	INT:%CrystalCountdown_Desired=2
	INT:%CurrentPhase=1
	INT:%Bool_LowHeath=0
	INT:%SummonManager=0	//0 = grunt, 1 = caster, 2 = mage
	INT:%ReusableHandle1
	INT:%ReusableHandle2
	INT:%ReusableHandle3
	INT:%ReusableHandle4
	INT:%Bool_FXRestore_01=0
	INT:%Bool_FXRestore_02=0
	INT:%Bool_FXRestore_03=0
	INT:%Bool_FXRestore_04=0

	//only 2 phases for the Shrike - no phase manager in this one
	
//--------
BEHAVIOUR
REACTION NegaShrike_Phase1_KeepYourDistance,0
USAGE COMBAT
VARS
	CHARACTER:_Enemy
CHECK "c1&c2"
	IsEqual(%CurrentPhase,1)
	CharacterGet(_Enemy,__Me,%NegaShrike_SightRange,Lowest,Distance,Enemy)
ACTIONS
	DebugText(__Me,"Keeping my distances!")
	CharacterMoveInRange(_Enemy,7,12,1)
	CharacterLookAt(_Enemy)
	SetPriority("NegaShrike_Phase1_KeepYourDistance",0)
INTERRUPT
ON
	OnMovementFailed(_)
ACTIONS
	SetPriority("NegaShrike_Phase1_KeepYourDistance",0)

REACTION NegaShrike_Phase1_SummonVoidGrunt,0
USAGE COMBAT
VARS
	CHARACTER:_Enemy
	FLOAT3:_MyPosition
	FLOAT3:_Direction
CHECK "c1&c2&c3&c4"
	IsEqual(%CurrentPhase,1)
	CharacterCanCast(__Me,"Summon_EnemyShadowGrunt_NM")
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)
	GetPosition(__Me,_MyPosition)
ACTIONS
	DebugText(__Me,"Summoning Grunt!")
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DebugText(__Me,"Summoning anim is WIP!")
	DisplayText(__Me,"CMB_END_ShrikeSummon_01",3)
	CharacterUseSkill("Summon_EnemyShadowGrunt_NM",_MyPosition)
	Add(%SummonManager,1)
	Set(%Summon_CommonValue_Current,0)
	SetPriority("NegaShrike_Phase1_SummonVoidGrunt",0)	
	
REACTION NegaShrike_Phase1_SummonVoidCaster,0
USAGE COMBAT
VARS
	CHARACTER:_Enemy
	FLOAT3:_MyPosition
	FLOAT3:_Direction
CHECK "c1&c2&c3&c4"
	IsEqual(%CurrentPhase,1)
	CharacterCanCast(__Me,"Summon_EnemyShadowCaster_NM")
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)
	GetPosition(__Me,_MyPosition)
ACTIONS
	DebugText(__Me,"Summoning Caster!")
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DebugText(__Me,"Summoning anim is WIP!")
	DisplayText(__Me,"CMB_END_ShrikeSummon_02",3)
	CharacterUseSkill("Summon_EnemyShadowCaster_NM",_MyPosition)
	Add(%SummonManager,1)
	Set(%Summon_CommonValue_Current,0)		
	SetPriority("NegaShrike_Phase1_SummonVoidCaster",0)
	
REACTION NegaShrike_Phase1_SummonVoidMage,0
USAGE COMBAT
VARS
	CHARACTER:_Enemy
	FLOAT3:_MyPosition
	FLOAT3:_Direction
CHECK "c1&c2&c3&c4"
	IsEqual(%CurrentPhase,1)
	CharacterCanCast(__Me,"Summon_EnemyShadowMage_NM")
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)
	GetPosition(__Me,_MyPosition)
ACTIONS
	DebugText(__Me,"Summoning Mage!")
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DebugText(__Me,"Summoning anim is WIP!")
	DisplayText(__Me,"CMB_END_ShrikeSummon_03",3)
	CharacterUseSkill("Summon_EnemyShadowMage_NM",_MyPosition)
	Set(%SummonManager,0)
	Set(%Summon_CommonValue_Current,0)	
	SetPriority("NegaShrike_Phase1_SummonVoidMage",0)
	
REACTION NegaShrike_Phase1_CastBlitzBolt,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_TargetsResistance
	FLOAT:_MinRange
	FLOAT:_MaxRange
CHECK "c1&c2&c3&c4&!c5&!c6&c7&c8"
	IsEqual(%CurrentPhase,1)																	//c1
	CharacterCanCast(__Me,"Projectile_EnemyBlitzBolt")											//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Lowest,AirResistance,Enemy)				//c3
	CharacterGetStat(_TargetsResistance,_Target,AirResistance)									//c4
	IsGreaterThen(_TargetsResistance,100)														//!c5
	CharacterHasStatus(_Target,"STUNNED")														//!c6
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Projectile_EnemyBlitzBolt")				//c7
	IsRandom(0.4)																				//c8
ACTIONS
	DebugText(__Me,"Blitz bolt!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	IF "c1"
		IsRandom(0.3)
	THEN
		DisplayText(__Me,"CMB_END_ShrikeCastBlitzBolt",3)
	ENDIF
	CharacterUseSkill("Projectile_EnemyBlitzBolt",_Target)
	
REACTION NegaShrike_Phase1_CastVoidHailAttack,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_TargetsResistance
	FLOAT:_MinRange
	FLOAT:_MaxRange
CHECK "c1&c2&c3&c4&c5"
	IsEqual(%CurrentPhase,1)																	//c1
	CharacterCanCast(__Me,"Projectile_Enemy_VoidHailAttack_Shrike")									//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Lowest,Vitality,Enemy)						//c3
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Projectile_Enemy_VoidHailAttack_Shrike")			//c4
	IsRandom(0.4)																				//c5
ACTIONS
	DebugText(__Me,"Void attack!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	IF "c1"
		IsRandom(0.3)
	THEN
		DisplayText(__Me,"CMB_END_ShrikeCastDarkHail",3)
	ENDIF
	CharacterUseSkill("Projectile_Enemy_VoidHailAttack_Shrike",_Target)
	
REACTION NegaShrike_Phase1_CastVoidShard,50
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange
CHECK "c1&c2&c3&c4"
	IsEqual(%CurrentPhase,1)																	//c1
	CharacterCanCast(__Me,"Projectile_EnemyVoidShard")											//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Lowest,AirResistance,Enemy)				//c3
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Projectile_EnemyVoidShard")				//c4
ACTIONS
	DebugText(__Me,"Void Shard!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	CharacterUseSkill("Projectile_EnemyVoidShard",_Target)

REACTION NegaShrike_Phase1_SummonVoidCrystals,0
USAGE COMBAT
VARS
	CHARACTER:_Target
	CHARACTER:_CharacterCheck
	INT:_Successes=0
	INT:_Failures=0
	FLOAT3:_PosOffset
	FLOAT:_Angle
	FLOAT:_ActionPoints
	FLOAT:_Dist
CHECK "c1&!c2&!c3&!c4&!c5&c6&!c7&c8&c9"						
	IsEqual(%CurrentPhase,1)													//c1
	CharacterHasStatus(__Me,"MUTED")											//!c2
	CharacterHasStatus(__Me,"FEAR")												//!c3
	CharacterHasStatus(__Me,"CHARMED")											//!c4
	CharacterHasStatus(__Me,"FROZEN")											//!c5
	CharacterGetStat(_ActionPoints,__Me,"ActionPoints")							//c6
	IsLessThen(_ActionPoints,4)													//!c7
	CharacterGet(_Target,__Me,20,Lowest,Distance,Enemy)							//c8
	CharacterIsInParty(_Target)													//c9
ACTIONS
	DebugText(__Me,"Summoning Void Crystals!")
	CharacterFleeFrom(Enemy,6)
	CharacterLookAt(_Target)
	CharacterPlayEffect(__Me,"FX_Skills_WitchCraft_Generic_A_Prepare")
	CharacterPlayAnimation("cast_self_start",1,1)
	CharacterPlayAnimation("cast_self_loop",1,0)
	WHILE "c1&c2"
		IsLessThen(_Successes,4)
		IsLessThen(_Failures,20)
	DO
		IF "c1"
			GetPosition(_Target,%TargetsPosition)
		THEN
			Set(_PosOffset,{1.3;0;0})
			GetRandomBetween(_Angle,0,360)
			GetRandomBetween(_Dist,3.5,5.5)
			RotateY(_PosOffset,_Angle)
			Multiply(_PosOffset,_Dist)	
			Add(%TargetsPosition,_PosOffset)
		ENDIF
		CharacterPlayAnimation("cast_self_cast",1,0)
		IF "c1&!c2"
			FindValidPosition(%TargetsPosition,2)
			CharacterGet(_CharacterCheck,%TargetsPosition,3,Lowest,Distance,Enemy)
		THEN
			PlayEffectAt(%TargetsPosition,"FX_Skills_WitchCraft_Generic_A_Cast_A")
			IF "c1"
				IsEqual(_Successes,0)
			THEN
				PlayEffectAt(__Me,"FX_Skills_WitchCraft_Generic_A_Cast_A")
				CharacterLookAt(%TargetsPosition)
				SpawnItem(%VoidCrystal_RT,%TargetsPosition,%SummonedVoidCrystal_01)
				PlayLoopEffectAt(%ReusableHandle1,%TargetsPosition,"FX_GP_LightPillarPurple_A")
				Set(%Bool_FXRestore_01,1)
			ELIF "c1"
				IsEqual(_Successes,1)
			THEN
				PlayEffectAt(__Me,"FX_Skills_WitchCraft_Generic_A_Cast_A")
				CharacterLookAt(%TargetsPosition)
				SpawnItem(%VoidCrystal_RT,%TargetsPosition,%SummonedVoidCrystal_02)
				PlayLoopEffectAt(%ReusableHandle2,%TargetsPosition,"FX_GP_LightPillarPurple_A")
				Set(%Bool_FXRestore_02,1)
			ELIF "c1"
				IsEqual(_Successes,2)
			THEN
				PlayEffectAt(__Me,"FX_Skills_WitchCraft_Generic_A_Cast_A")
				CharacterLookAt(%TargetsPosition)
				SpawnItem(%VoidCrystal_RT,%TargetsPosition,%SummonedVoidCrystal_03)
				PlayLoopEffectAt(%ReusableHandle3,%TargetsPosition,"FX_GP_LightPillarPurple_A")
				Set(%Bool_FXRestore_03,1)
			ELIF "c1"
				IsEqual(_Successes,3)
			THEN
				PlayEffectAt(__Me,"FX_Skills_WitchCraft_Generic_A_Cast_A")
				CharacterLookAt(%TargetsPosition)
				SpawnItem(%VoidCrystal_RT,%TargetsPosition,%SummonedVoidCrystal_04)
				PlayLoopEffectAt(%ReusableHandle4,%TargetsPosition,"FX_GP_LightPillarPurple_A")
				Set(%Bool_FXRestore_04,1)
			ENDIF
			Sleep(0.2)
			Add(_Successes,1)
			DisplayText(__Me,"CMB_END_Shrike_VoidCrystal,3")
		ELSE
			Add(_Failures,1)
		ENDIF
	ENDWHILE
	IF "c1"
		IsEqual(_Failures,20)
	THEN
		Set(_Failures,0)
		CharacterEndTurn()
	ENDIF	
	CharacterUseActionPoints(__Me,4)
	Set(%CrystalCreation_Current,0)
	Set(_Successes,0)
	Set(_Failures,0)	
	SetPriority("NegaShrike_Phase1_SummonVoidCrystals",0)
INTERRUPT
	Reset()
	
REACTION NegaShrike_Phase1_EndTurn,20
USAGE COMBAT
CHECK "c1"
	IsEqual(%CurrentPhase,1)
ACTIONS
	DebugText(__Me,"Ending Turn!")
	CharacterEndTurn()
	
REACTION NegaShrike_Phase2_CastBoost,300
USAGE COMBAT
CHECK "c1&c2&!c3"
	IsEqual(%CurrentPhase,2)																	//c1
	CharacterCanCast(__Me,"Target_EnemyTargetedDamageBoost")									//c2
	CharacterHasStatus(__Me,"BOOST")															//!c3
ACTIONS
	DebugText(__Me,"Boost!")
	CharacterUseSkill("Target_EnemyTargetedDamageBoost",__Me)
	
REACTION NegaShrike_Phase2_CastRage,300
USAGE COMBAT
CHECK "c1&c2&!c3"
	IsEqual(%CurrentPhase,2)																	//c1
	CharacterCanCast(__Me,"Shout_EnemyRage")													//c2
	CharacterHasStatus(__Me,"RAGED")															//!c3
ACTIONS
	DebugText(__Me,"Rage!")
	CharacterUseSkill("Shout_EnemyRage",__Me)	

REACTION NegaShrike_Phase2_CastStun,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "c1&c2&c3&!c4&c5&c6"
	IsEqual(%CurrentPhase,2)																	//c1	
	CharacterCanCast(__Me,"Target_EnemyStun")													//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Lowest,Vitality,Enemy)						//c3
	CharacterHasStatus(_Target,"STUNNED")														//!c4	
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyStun")							//c5
	IsRandom(0.5)																				//c6
ACTIONS
	DebugText(__Me,"Stun!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	IF "c1"
		IsRandom(0.3)
	THEN
		DisplayText(__Me,"CMB_END_Shrike_CastStun",3)
	ENDIF
	CharacterUseSkill("Target_EnemyStun",_Target)
	
REACTION NegaShrike_Phase2_CastFear,600
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "c1&c2&c3&!c4&!c5"
	IsEqual(%CurrentPhase,2)																	//c1	
	CharacterCanCast(__Me,"Shout_EnemyFear")													//c2
	CharacterGet(_Target,__Me,4,Lowest,Vitality,Enemy)											//c3
	CharacterHasStatus(_Target,"STUNNED")														//!c4	
	CharacterHasStatus(_Target,"FEAR")															//!c5	
ACTIONS
	DebugText(__Me,"Fear!")
	IF "c1"
		IsRandom(0.3)
	THEN
		DisplayText(__Me,"CMB_END_Shrike_CastFear",3)
	ENDIF
	CharacterUseSkill("Shout_EnemyFear",_Target)

REACTION NegaShrike_Phase2_CastMarkOfDeath,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "c1&c2&c3&!c4&c5"
	IsEqual(%CurrentPhase,2)																	//c1	
	CharacterCanCast(__Me,"Target_EnemyMarkOfDeath")											//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Highest,Vitality,Enemy)					//c3
	CharacterHasStatus(_Target,"MARK_OF_DEATH")													//!c4	
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyMarkOfDeath")					//c5
ACTIONS
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	IF "c1"
		IsRandom(0.3)
	THEN
		DisplayText(__Me,"CMB_END_Shrike_CastMarkOfDeath",3)
	ENDIF
	CharacterUseSkill("Target_EnemyMarkOfDeath",_Target)
	
REACTION NegaShrike_AnyPhase_CastCurse,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "!c1&c2&c3&!(c4|c5|c6|c7)&c8&c9"
	IsCasual()																					//!c1	
	CharacterCanCast(__Me,"Target_EnemyTargetedCurse")											//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Highest,Strength,Enemy)					//c3
	CharacterHasStatus(_Target,"CURSED")														//!c4	
	CharacterHasStatus(_Target,"STUNNED")														//!c5	
	CharacterHasStatus(_Target,"FROZEN")														//!c6	
	CharacterHasStatus(_Target,"MARK_OF_DEATH")													//!c7	
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyTargetedCurse")				//c8
	IsRandom(0.4)																				//c9
ACTIONS
	DebugText(__Me,"Curse!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	CharacterUseSkill("Target_EnemyTargetedCurse",_Target)	
	
REACTION NegaShrike_AnyPhase_CastBlind,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "!c1&c2&c3&!c4&c5&c6"
	IsCasual()																					//!c1	
	CharacterCanCast(__Me,"Target_EnemyBlind")													//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Highest,Dexterity,Enemy)					//c3
	CharacterHasStatus(_Target,"BLIND")															//!c4	
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyBlind")						//c5
	IsRandom(0.4)																				//c6
ACTIONS
	DebugText(__Me,"Blind!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	IF "c1"
		IsRandom(0.3)
	THEN
		DisplayText(__Me,"CMB_END_Shrike_CastBlind",3)
	ENDIF
	CharacterUseSkill("Target_EnemyBlind",_Target)
	
REACTION NegaShrike_AnyPhase_CastDecayingTouch,300
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_TargetsVitality
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "!c1&c2&c3&c4&c5&!c6&c7"
	IsCasual()																					//!c1	
	CharacterCanCast(__Me,"Target_EnemyDecayingTouch")											//c2
	CharacterGet(_Target,__Me,%NegaShrike_SightRange,Highest,Dexterity,Enemy)					//c3
	CharacterGetStat(_TargetsVitality,_Target,Vitality)											//c4
	IsLessThen(_TargetsVitality,0.7)															//c5
	CharacterHasStatus(_Target,"DECAYING_TOUCH")												//!c6	
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyDecayingTouch")				//c7
ACTIONS
	DebugText(__Me,"Decaying Touch!")
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	CharacterUseSkill("Target_EnemyDecayingTouch",_Target)

REACTION NegaShrike_AnyPhase_CastPurifyingFire,900
USAGE COMBAT
VARS
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "c1&c2"
	CharacterCanCast(__Me,"Target_EnemyPurifyingFire")
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyPurifyingFire")
ACTIONS
	Set(%PurifyingFire_Target,null)
	IterateCharactersInCombat(__Me,"CMB_NegaShrike_ScanForEnemiesWithPositiveStatuses",Lowest,Distance)
	IF "!c1&c2"
		IsEqual(%PurifyingFire_Target,null)
		CharacterCanSee(__Me,%PurifyingFire_Target)
	THEN
		DebugText(__Me,"PurFire!")
		CharacterMoveInRange(%PurifyingFire_Target,_MinRange,_MaxRange,0)
		DisplayText(__Me,"CMB_END_Shrike_CastPurifyingFire",3)
		CharacterUseSkill("Target_EnemyPurifyingFire",%PurifyingFire_Target)
	ELSE
		SetPriority("NegaShrike_AnyPhase_CastPurifyingFire",0)
	ENDIF
INTERRUPT
	Reset()

REACTION NegaShrike_AnyPhase_CastBanishSummon,800
USAGE COMBAT
VARS
	FLOAT:_MinRange
	FLOAT:_MaxRange	
CHECK "c1&c2"
	CharacterCanCast(__Me,"Target_EnemyBanishSummon")
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyBanishSummon")
ACTIONS
	Set(%BanishSummon_Target,null)
	IterateCharactersInCombat(__Me,"CMB_NegaShrike_ScanForEnemySummons",Lowest,Distance)
	IF "!c1&c2"
		IsEqual(%BanishSummon_Target,null)
		CharacterCanSee(__Me,%BanishSummon_Target)
	THEN
		DebugText(__Me,"BanSummon!")
		CharacterMoveInRange(%BanishSummon_Target,_MinRange,_MaxRange,0)
		DisplayText(__Me,"CMB_END_Shrike_CastBanishSummon",3)
		CharacterUseSkill("Target_EnemyBanishSummon",%BanishSummon_Target)
	ELSE
		SetPriority("NegaShrike_AnyPhase_CastBanishSummon",0)
	ENDIF
INTERRUPT
	Reset()
	
REACTION NegaShrike_AnyPhase_ScanForCrystals,0
USAGE COMBAT
VARS
	ITEM:_TargetedItem
	FLOAT:_MinRange
	FLOAT:_MaxRange
	FLOAT:_DistBtwMeAndCrystal
CHECK "c1&c2"
	CharacterCanCast(__Me,"Projectile_EnemyVoidShard")
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Projectile_EnemyVoidShard")
ACTIONS
	Set(%CharacterNearObject,null)
	IterateItemsNear(__Me,%NegaShrike_SightRange,"CMB_Shrike_ScanForCrystal")
	IF "!c1&c2&c3"
		IsEqual(%CharacterNearObject,null)
		ItemGet(_TargetedItem,%CharacterNearObject,4,Lowest,Distance)	
		CharacterCanSee(__Me,_TargetedItem)
	THEN
		DebugText(__Me,"Scan for Crystals!")
		CharacterMoveInRange(_TargetedItem,_MinRange,_MaxRange,0)
		CharacterUseSkill("Projectile_EnemyVoidShard",_TargetedItem)
	ELSE
		SetPriority("NegaShrike_AnyPhase_ScanForCrystals",0)
	ENDIF
	SetPriority("NegaShrike_AnyPhase_ScanForCrystals",0)
	
REACTION NegaShrike_Special_SummonNegaAhru, 1300
USAGE COMBAT
VARS
	CHARACTER:_Enemy
    FLOAT:_MyVitality	
	FLOAT3:_MyPosition
	FLOAT3:_Direction	
CHECK "c1&c2&c3&c4&c5"
	CharacterGetStat(_MyVitality,__Me,Vitality)						//c1
	IsLessThen(_MyVitality,0.9)										//c2
	CharacterCanCast(__Me,"Summon_EnemyArhu_NM")					//c3
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)				//c4
	GetPosition(__Me,_MyPosition)									//c5
ACTIONS
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DisplayText(__Me,"CMB_END_Shrike_SummonNegaArhu",3)
	CharacterUseSkill("Summon_EnemyArhu_NM",_MyPosition)
	SetPriority("NegaShrike_Special_SummonNegaAhru",0)
	
REACTION NegaShrike_Special_SummonNegaIcara, 1200
USAGE COMBAT
VARS
	CHARACTER:_Enemy
    FLOAT:_MyVitality	
	FLOAT3:_MyPosition
	FLOAT3:_Direction	
CHECK "c1&c2&c3&c4&c5"
	CharacterGetStat(_MyVitality,__Me,Vitality)						//c1
	IsLessThen(_MyVitality,0.7)										//c2
	CharacterCanCast(__Me,"Summon_EnemyIcara_NM")					//c3
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)				//c4
	GetPosition(__Me,_MyPosition)									//c5
ACTIONS
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DisplayText(__Me,"CMB_END_Shrike_SummonNegaIcara",3)
	CharacterUseSkill("Summon_EnemyIcara_NM",_MyPosition)
	SetPriority("NegaShrike_Special_SummonNegaIcara",0)
	SetPriority("NegaShrike_Special_SummonNegaAhru",0)
	
REACTION NegaShrike_Special_SummonNegaZandalor, 1100
USAGE COMBAT
VARS
	CHARACTER:_Enemy
    FLOAT:_MyVitality	
	FLOAT3:_MyPosition
	FLOAT3:_Direction	
CHECK "c1&c2&c3&c4&c5"
	CharacterGetStat(_MyVitality,__Me,Vitality)						//c1
	IsLessThen(_MyVitality,0.5)										//c2
	CharacterCanCast(__Me,"Summon_EnemyZandalor_NM")				//c3
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)				//c4
	GetPosition(__Me,_MyPosition)									//c5
ACTIONS
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DisplayText(__Me,"CMB_END_Shrike_SummonNegaZandalor",3)
	CharacterUseSkill("Summon_EnemyZandalor_NM",_MyPosition)
	SetPriority("NegaShrike_Special_SummonNegaIcara",0)
	SetPriority("NegaShrike_Special_SummonNegaAhru",0)
	SetPriority("NegaShrike_Special_SummonNegaZandalor",0)
	
REACTION NegaShrike_Special_SummonNegaZixzax, 1000
USAGE COMBAT
VARS
	CHARACTER:_Enemy
    FLOAT:_MyVitality	
	FLOAT3:_MyPosition
	FLOAT3:_Direction	
CHECK "c1&c2&c3&c4&c5"
	CharacterGetStat(_MyVitality,__Me,Vitality)						//c1
	IsLessThen(_MyVitality,0.3)										//c2
	CharacterCanCast(__Me,"Summon_EnemyZixzax_NM")					//c3
	CharacterGet(_Enemy,__Me,50,Lowest,Distance,Enemy)				//c4
	GetPosition(__Me,_MyPosition)									//c5
ACTIONS
	GetDirection(__Me,_Enemy,_Direction)
	Multiply(_Direction,FLOAT:2)
	Add(_MyPosition,_Direction)
	DisplayText(__Me,"CMB_END_Shrike_SummonNegaZixzax",3)
	CharacterUseSkill("Summon_EnemyZixzax_NM",_MyPosition)
	SetPriority("NegaShrike_Special_SummonNegaIcara",0)
	SetPriority("NegaShrike_Special_SummonNegaAhru",0)
	SetPriority("NegaShrike_Special_SummonNegaZandalor",0)
	SetPriority("NegaShrike_Special_SummonNegaZixzax",0)

//--------
EVENTS
EVENT DefaultIterateSetTarget OVERRIDE 			//stunned targets are more interesting
VARS 
	CHARACTER:_Char
	CHARACTER:_previousSetTarget
	FLOAT:_targetDistance
	INT:_alreadyTargetedBy
	FLOAT:_targetClosestThisScore=0
	FLOAT:_movement
ON
	OnIterateCharacter(_Char,"DefaultSetTargetClosestIterator")
ACTIONS
	IF "!c1&!c2&c3&c4&c5&!c6&!c7"
		CharacterIsDead(_Char)
		IsEqual(__Me,_Char)
		CharacterIsEnemy(__Me,_Char)
		GetDistance(_targetDistance,__Me,_Char)
		CharacterGetHostileCount(_alreadyTargetedBy,_Char)
		CharacterHasStatus(_Char,"INVISIBLE")
		CharacterHasStatus(_Char,"SNEAKING")
	THEN
		IF "c1&c2"
			CharacterGetEnemy(_previousSetTarget,__Me)
			IsEqual(_previousSetTarget,_Char)
		THEN
			Multiply(_targetDistance,0.8)
		ENDIF
		IF "c1"
			IsEqual(_alreadyTargetedBy,0)
		THEN
			Multiply(_targetDistance,0.5)
		ENDIF
		IF "c1"
			IsEqual(_alreadyTargetedBy,1)
		THEN
			Multiply(_targetDistance,0.6)
		ENDIF
		IF "c1"
			CharacterHasStatus(_Char,"STUNNED")
		THEN
			Multiply(_targetDistance,0.4)
		ENDIF	
		IF "c1"
			CharacterIsSummon(_Char)
		THEN
			Multiply(_targetDistance,1.5)
		ENDIF	
		IF "c1&!c2"
			IsGreaterThen(_alreadyTargetedBy,1)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Add(_alreadyTargetedBy,0.6)
			Multiply(_alreadyTargetedBy,_alreadyTargetedBy)
			Multiply(_targetDistance,_alreadyTargetedBy)
		ENDIF
		IF "c1&c2"
			IsGreaterThen(_alreadyTargetedBy,3)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Multiply(_targetDistance,2)
		ENDIF
		IF "!c1"
			CharacterCanSee(__Me,_Char)
		THEN
			Multiply(_targetDistance,3)
		ENDIF
		IF "c1&!c2"
			CharacterHasTalent(_Char,Stench)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Multiply(_targetDistance,4)
		ENDIF
		IF "c1&c2"
			CharacterHasTalent(_Char,Stench)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Multiply(_targetDistance,2)
		ENDIF
		Set(_targetClosestThisScore,_targetDistance)
		IF "!c1"
			IsGreaterThen(_targetClosestThisScore,%setTargetDefaultBestScore)
		THEN
			Set(%setTargetDefaultBestScore,_targetClosestThisScore)
			CharacterSetEnemy(__Me,_Char)
		ENDIF
	ENDIF
	
EVENT OnInit_GiveSpells_FXRestore
ON
	OnInit()
ACTIONS
	//StartTimer("CMB_END_ShrikeSpawningFX_Timer",0.2,0)
	IF "c1"
		CharacterGetStat(%NegaShrike_SightRange,__Me,Sight)
	THEN
		IF "!c1"
			IsEqual(%CurrentPhase,2)
		THEN
			SetPriority("Combat_AttackSetEnemy",0)						
			SetPriority("Combat_MoveToSetEnemy",0)
			SetPriority("Combat_TakeCover",0)
		ENDIF
		CharacterAddSkill("Summon_EnemyShadowGrunt_NM")
		CharacterAddSkill("Summon_EnemyShadowCaster_NM")
		CharacterAddSkill("Summon_EnemyShadowMage_NM")
		CharacterAddSkill("Projectile_EnemyBlitzBolt")
		CharacterAddSkill("Target_EnemyTargetedDamageBoost")
		CharacterAddSkill("Target_EnemyTargetedCurse")
		CharacterAddSkill("Target_EnemyMarkOfDeath")
		CharacterAddSkill("Target_EnemyBanishSummon")
		CharacterAddSkill("Target_EnemyPurifyingFire")
		CharacterAddSkill("Projectile_Enemy_VoidHailAttack_Shrike")
		CharacterAddSkill("Projectile_EnemyVoidShard")
		CharacterAddSkill("Target_EnemyStun")
		CharacterAddSkill("Shout_EnemyRage")
		CharacterAddSkill("Shout_EnemyFear")
		CharacterAddSkill("Summon_EnemyIcara_NM")
		CharacterAddSkill("Summon_EnemyArhu_NM")
		CharacterAddSkill("Summon_EnemyZandalor_NM")
		CharacterAddSkill("Summon_EnemyZixzax_NM")
	ENDIF
	IF "c1"
		IsEqual(%Bool_FXRestore_01,1)
	THEN
		DebugText(__Me,"Code issue - FX1 can't resume!")
		ItemPlayLoopEffect(%ReusableHandle1,%SummonedVoidCrystal_01,"FX_GP_LightPillarPurple_A")
	ENDIF
	IF "c1"
		IsEqual(%Bool_FXRestore_02,1)
	THEN
		DebugText(__Me,"Code issue - FX2 can't resume!")
		ItemPlayLoopEffect(%ReusableHandle2,%SummonedVoidCrystal_02,"FX_GP_LightPillarPurple_A")
	ENDIF
	IF "c1"
		IsEqual(%Bool_FXRestore_03,1)
	THEN
		DebugText(__Me,"Code issue - FX3 can't resume!")
		ItemPlayLoopEffect(%ReusableHandle3,%SummonedVoidCrystal_03,"FX_GP_LightPillarPurple_A")
	ENDIF
	IF "c1"
		IsEqual(%Bool_FXRestore_04,1)
	THEN
		DebugText(__Me,"Code issue - FX4 can't resume!")
		ItemPlayLoopEffect(%ReusableHandle4,%SummonedVoidCrystal_04,"FX_GP_LightPillarPurple_A")
	ENDIF	

EVENT OnCombatStarted_StartSpawnTimer
ON
	OnCombatStarted()
ACTIONS
	StartTimer("CMB_END_Shrike_SpawnVoidCreatures_Timer",4,0)
	
EVENT OnTimer_SpawnVoidCreatures
VARS
	TRIGGER:_Trigger01="END_ShrikeFight_Summon01_f4f18de6-2817-4838-83f6-08d0db96964e"
	TRIGGER:_Trigger02="END_ShrikeFight_Summon02_52da50c8-9e9d-465e-941d-29f593e50648"
ON
	OnTimer("CMB_END_Shrike_SpawnVoidCreatures_Timer")
ACTIONS
	SpawnCharacter(_,%VoidCaster_RT,_Trigger01,1,1,__Me)
	SpawnCharacter(_,%VoidCaster_RT,_Trigger02,1,1,__Me)
	
EVENT OnTurn_GeneralControl
VARS
	FLOAT:_DistanceToEnemy
	FLOAT:_MySightRange		
	CHARACTER:_ClosestEnemy		
ON
	OnTurn()
ACTIONS
	SetPriority("NegaShrike_AnyPhase_CastPurifyingFire",900)
	SetPriority("NegaShrike_AnyPhase_CastBanishSummon",800)
	SetPriority("NegaShrike_AnyPhase_ScanForCrystals",500)
	Set(%CurrentCrystalCount,0)
	IterateItemsNear(__Me,%NegaShrike_SightRange,"CMB_Shrike_ScanForCrystal")
	IF "c1"								//VOID SUMMONING CONTROL
		IsLessThen(%Summon_CommonValue_Current,%Summon_CommonValue_Desired)
	THEN
		Add(%Summon_CommonValue_Current,1)
	ELSE
		IF "c1"
			IsEqual(%SummonManager,0)
		THEN
			SetPriority("NegaShrike_Phase1_SummonVoidGrunt",500)
		ELIF "c1"
			IsEqual(%SummonManager,1)
		THEN
			SetPriority("NegaShrike_Phase1_SummonVoidCaster",500)
		ELIF "c1"
			IsEqual(%SummonManager,2)
		THEN
			SetPriority("NegaShrike_Phase1_SummonVoidMage",500)
		ENDIF
	ENDIF
	IF "c1&c2"								//CRYSTAL CREATION CONTROL
		IsLessThen(%CurrentCrystalCount,3)
		IsLessThen(%CrystalCreation_Current,%CrystalCreation_Desired)
	THEN		
		Add(%CrystalCreation_Current,1)
	ELIF "c1&c2"
		IsLessThen(%CurrentCrystalCount,2)	
		IsEqual(%CrystalCreation_Current,%CrystalCreation_Desired)	
	THEN
		SetPriority("NegaShrike_Phase1_SummonVoidCrystals",500)
	ENDIF
	IF "c1&c2"								//CRYSTAL SELF-DESTRUCT
		IsGreaterThen(%CurrentCrystalCount,0)
		IsLessThen(%CrystalCountdown_Current,%CrystalCountdown_Desired)
	THEN		
		Add(%CrystalCountdown_Current,1)
	ELIF "c1&c2"
		IsGreaterThen(%CurrentCrystalCount,0)	
		IsEqual(%CrystalCountdown_Current,%CrystalCountdown_Desired)	
	THEN
		IterateItemsNear(__Me,50,"CMB_Shrike_Cleanup_Items")
		Set(%CrystalCountdown_Current,0)
	ENDIF
	IF "c1&c2&!c3&c4&c5"					//DISTANCE CONTROL
		IsEqual(%CurrentPhase,1)
		CharacterGet(_ClosestEnemy,__Me,%NegaShrike_SightRange,Lowest,Distance,Enemy)
		CharacterIsDead(_ClosestEnemy)
		GetDistance(_DistanceToEnemy,__Me,_ClosestEnemy)
		IsLessThen(_DistanceToEnemy,12)
	THEN
		SetPriority("NegaShrike_Phase1_KeepYourDistance",1500)
	ENDIF
	
EVENT OnIterateCharacter_ScanForNearbyCharacters
VARS
	CHARACTER:_DetectedCharNearCrystal
ON
	OnIterateCharacter(_DetectedCharNearCrystal,"Crystal_ScanForNearbyCharacters")
ACTIONS
	IF "!c1"
		CharacterIsDead(_DetectedCharNearCrystal)
	THEN
		CharacterApplyStatus(_DetectedCharNearCrystal,"STUNNED",1)
		Set(_DetectedCharNearCrystal,null)	
	ENDIF
	
EVENT OnIterateCharacter_ScanForEnemiesWithPositiveStatuses
VARS
	CHARACTER:_Enemy
ON
	OnIterateCharacter(_Enemy,"CMB_NegaShrike_ScanForEnemiesWithPositiveStatuses")
ACTIONS
	IF "!c1&c2&(c3|c4)"
		CharacterIsDead(_Enemy)
		CharacterIsEnemy(__Me,_Enemy)
		CharacterHasStatus(_Enemy,"SHIELD")
		CharacterHasStatus(_Enemy,"HEALING")
	THEN
		Set(%PurifyingFire_Target,_Enemy)
	ENDIF

EVENT OnIterateCharacter_ScanForEnemySummons
VARS
	CHARACTER:_Enemy
ON
	OnIterateCharacter(_Enemy,"CMB_NegaShrike_ScanForEnemySummons")
ACTIONS
	IF "!c1&c2&c3"
		CharacterIsDead(_Enemy)
		CharacterIsEnemy(__Me,_Enemy)
		CharacterIsSummon(_Enemy)
	THEN
		Set(%BanishSummon_Target,_Enemy)
	ENDIF	

EVENT OnIterateItem_ScanForVoidCrystals
VARS 
	ITEM:_DetectedCrystal
	ITEMTEMPLATE:_DetectedCrystal_RT
ON
	OnIterateItem(_DetectedCrystal,"CMB_Shrike_ScanForCrystal")
ACTIONS
	IF "!c1&c2&c3"
		ItemIsDestroyed(_DetectedCrystal)	
		ItemGetTemplate(_DetectedCrystal,_DetectedCrystal_RT)
		IsEqual(_DetectedCrystal_RT,%VoidCrystal_RT)
	THEN
		IterateCharactersNear(_DetectedCrystal,4,"Iterate_ScanFor_CharactersNearCrystal",Lowest,Distance)
		Add(%CurrentCrystalCount,1)
	ENDIF
	Set(_DetectedCrystal,null)

EVENT OnIterateCharacter_ScanFor_CharactersNearCrystal
VARS
	CHARACTER:_DetectedCharNearCrystal
ON
	OnIterateCharacter(_DetectedCharNearCrystal,"Iterate_ScanFor_CharactersNearCrystal")
ACTIONS
	IF "!(c1|c2|c3|c4)"
		CharacterIsDead(_DetectedCharNearCrystal)
		CharacterIsAlly(__Me,_DetectedCharNearCrystal)
		IsEqual(__Me,_DetectedCharNearCrystal)
		CharacterHasStatus(_DetectedCharNearCrystal,"STUNNED")
	THEN
		Set(%CharacterNearObject,_DetectedCharNearCrystal)
	ENDIF
	Set(_DetectedCharNearCrystal,null)

EVENT OnDamage_PhaseManager
VARS
	FLOAT:_MyVitality
ON
	OnDamage(_,_,_,_)
ACTIONS
	IF "c1&c2&c3"
		CharacterGetStat(_MyVitality,__Me,Vitality)
		IsLessThen(_MyVitality,0.6)
		IsEqual(%Bool_LowHeath,0)
	THEN
		SetPriority("Combat_AttackSetEnemy",7)						
		SetPriority("Combat_MoveToSetEnemy",6)
		Set(%CurrentPhase,2)
		Set(%Bool_LowHeath,1)
		DebugText(__Me,"Entering Phase 2!")		
	ENDIF

EVENT OnItemDestroyed_Crystal_CleanUp
VARS
	ITEM:_Item
ON
	OnItemDestroyed(_Item)
ACTIONS
	IF "c1"
		IsEqual(_Item,%SummonedVoidCrystal_01)
	THEN
		StopLoopEffect(%ReusableHandle1)	
		Set(%Bool_FXRestore_01,0)
	ELIF "c1"
		IsEqual(_Item,%SummonedVoidCrystal_02)
	THEN
		StopLoopEffect(%ReusableHandle2)	
		Set(%Bool_FXRestore_02,0)
	ELIF "c1"
		IsEqual(_Item,%SummonedVoidCrystal_03)
	THEN
		StopLoopEffect(%ReusableHandle3)	
		Set(%Bool_FXRestore_03,0)
	ELIF "c1"
		IsEqual(_Item,%SummonedVoidCrystal_04)
	THEN
		StopLoopEffect(%ReusableHandle4)	
		Set(%Bool_FXRestore_04,0)
	ENDIF

EVENT OnTimer_PlaySpawnFX
ON
	OnTimer("CMB_END_ShrikeSpawningFX_Timer")											//this timer is commented out
ACTIONS
	CharacterPlayEffect(__Me,"FX_GP_CameraShake_A")
	CharacterPlayEffect(__Me,"FX_Creatures_Shadow_Spawn_A")
	CharacterPlayLoopEffect(%ReusableHandle1,__Me,"FX_GP_LightPillarBlue_A")			//replace FX
	StartTimer("CMB_END_ShrikeSpawningFX_Timer2",10,0)
	StartTimer("CMB_END_ShrikeSpawningFX_Timer_Stop",8,0)

EVENT OnTimer_PlaySpawnFX2
ON
	OnTimer("CMB_END_ShrikeSpawningFX_Timer2")
ACTIONS
	//CharacterPlayEffect(__Me,"FX_Env_Crystal_Healing_Explode_A","Bip01 Head")
	CharacterPlayEffect(__Me,"FX_GP_CameraShake_A")
	
EVENT OnTimer_StopSpawnFX
ON
	OnTimer("CMB_END_ShrikeSpawningFX_Timer_Stop")
ACTIONS
	StopLoopEffect(%ReusableHandle1)
	
EVENT OnDie_KillLoopFX
ON
	OnDie(__Me,_,_,_)
ACTIONS
	StopLoopEffect(%ReusableHandle1)
	StopLoopEffect(%ReusableHandle2)
	StopLoopEffect(%ReusableHandle3)
	StopLoopEffect(%ReusableHandle4)
	Set(%Bool_FXRestore_01,0)
	Set(%Bool_FXRestore_02,0)
	Set(%Bool_FXRestore_03,0)
	Set(%Bool_FXRestore_04,0)
	IterateCharactersNear(__Me,50,"CMB_Shrike_Cleanup",Lowest,Distance)
	IterateItemsNear(__Me,50,"CMB_Shrike_Cleanup_Items")	

EVENT OnCombatEnded_KillLoopFX
ON
	OnCombatEnded()
	OnDeactivate()
ACTIONS
	SetPriority("NegaShrike_Special_SummonNegaIcara",1300)
	SetPriority("NegaShrike_Special_SummonNegaAhru",1200)
	SetPriority("NegaShrike_Special_SummonNegaZandalor",1100)
	SetPriority("NegaShrike_Special_SummonNegaZixzax",1000)
	StopLoopEffect(%ReusableHandle1)
	StopLoopEffect(%ReusableHandle2)
	StopLoopEffect(%ReusableHandle3)
	StopLoopEffect(%ReusableHandle4)
	Set(%Bool_FXRestore_01,0)
	Set(%Bool_FXRestore_02,0)
	Set(%Bool_FXRestore_03,0)
	Set(%Bool_FXRestore_04,0)	
	IterateCharactersNear(__Me,50,"CMB_Shrike_Cleanup",Lowest,Distance)
	IterateItemsNear(__Me,50,"CMB_Shrike_Cleanup_Items")	
	
EVENT OnIterateCharacter_Cleanup
VARS
	CHARACTER:_Character
	CHARACTERTEMPLATE:_CharacterTemplate
ON
	OnIterateCharacter(_Character,"CMB_Shrike_Cleanup")
ACTIONS
	IF "c1&(c2|c3|c4|c5|c6|c7|c8)"
		CharacterGetTemplate(_Character,_CharacterTemplate)															//c1
		IsEqual(_CharacterTemplate,"Summons_Shadow_Grunt_Nightmare_a9dc7c0e-c2b4-4922-a7fb-9e125ce755f9")			//c2
		IsEqual(_CharacterTemplate,"Summons_Shadows_Caster_Nightmare_776bb9db-aa6f-433d-9dd2-731c5270d0e3")			//c3
		IsEqual(_CharacterTemplate,"Summons_Shadow_Mage_Nightmare_c550dcfb-4028-47bc-a9a5-6e0955d746e8")			//c4
		IsEqual(_CharacterTemplate,"END_ShrikeNightmare_Arhu_6fcffb91-4f4c-4680-b1fa-a72f3d4e1ffa")					//c5
		IsEqual(_CharacterTemplate,"END_ShrikeNightmare_Icara_0d48ddcb-76b0-46ee-8175-8f75e3858d7b")				//c6
		IsEqual(_CharacterTemplate,"END_ShrikeNightmare_Zandalor_0b7a6136-c227-43fa-b7f8-b19885f67c8c")				//c7
		IsEqual(_CharacterTemplate,"END_ShrikeNightmare_ZixZax_64742f7a-f92e-41f1-ad9c-5abf08ed4bd9")				//c8
	THEN
		CharacterDie(_Character)
	ENDIF

EVENT OnIterateItems_Cleanup
VARS
	ITEM:_Item
	ITEMTEMPLATE:_ItemTemplate
ON
	OnIterateItem(_Item,"CMB_Shrike_Cleanup_Items")
ACTIONS
	IF "c1&c2"
		ItemGetTemplate(_Item,_ItemTemplate)
		IsEqual(_ItemTemplate,%VoidCrystal_RT)
	THEN
		ItemDie(_Item)
	ENDIF