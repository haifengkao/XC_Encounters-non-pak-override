#INCLUDE State_Manager

INIT
USING State_Manager
CHARACTER:__Me
CHARACTER:%Daddy = LUC_TrollBridge6_12faf797-183b-44a8-a6b5-4d6ca6f73e98
INT:%LUC_BabyTrollSad = 0
INT:%LUC_BabyTrollCheer = 0
INT:%LUC_BabyTrollButterflies = 0

BEHAVIOUR

REACTION State_CryingForDaddy, 0
USAGE PEACE
VARS
ACTIONS
	IF "!c1"
		IsInDialog(__Me)
	THEN
		CharacterMoveTo(%Daddy)
		CharacterLookAt(%Daddy)
		Set(%LUC_BabyTrollSad,1)
		Set(%LUC_BabyTrollCheer,0)
		Set(%LUC_BabyTrollButterflies,0)
		Sleep(15)
		DialogStart("LUC_AD_TrollBridge6",__Me)
	ENDIF
	SetPriority(%currentAction, 0)

REACTION State_CheeringForDaddy, 0
USAGE PEACE
VARS
ACTIONS
	IF "!c1"
		IsInDialog(__Me)
	THEN
		CharacterFleeFrom(All,7)
		CharacterLookAt(%Daddy)
		Set(%LUC_BabyTrollSad,0)
		Set(%LUC_BabyTrollCheer,1)
		Set(%LUC_BabyTrollButterflies,0)
		Sleep(15)
		DialogStart("LUC_AD_TrollBridge6",__Me)
	ENDIF
	SetPriority(%currentAction, 0)

REACTION State_ActionManager, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction, "Action_Butterflies", "Action_Idle")
	SetPriority(%currentAction, 1100)

REACTION Action_Butterflies, 0
USAGE PEACE
VARS
	FLOAT:_Time
ACTIONS
	GetRandom(_Time,10,12)
	CharacterWander(FLOAT:5,_Time,1,%Daddy)
	IF "!c1"
		IsInDialog(__Me)
	THEN
		Set(%LUC_BabyTrollButterflies,1)
		Set(%LUC_BabyTrollSad,0)
		Set(%LUC_BabyTrollCheer,0)
		DialogStart("LUC_AD_TrollBridge6",__Me)
	ENDIF
	SetPriority(%currentAction, 0)

REACTION Action_Idle, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Animation
ACTIONS
	GetRandom(_Animation,"idle1","still")
	CharacterPlayAnimation(_Animation)
	Sleep(5)
	SetPriority(%currentAction, 0)