#INCLUDE State_Manager

INIT
	USING State_Manager
	CHARACTER:__Me
	EXTERN TRIGGER:%PointAfterQuest = null
	
BEHAVIOUR

REACTION State_BeforeTempleQuest, 0
	USAGE PEACE
	ACTIONS
		GetWeightedRandom(%currentAction, "Action_Cheer",INT:4, "Action_Listen",INT:4, "Action_Talk",INT:2)
		SetPriority(%currentAction, 1100)
		
REACTION State_AfterTheQuest, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_Wandering")
		SetPriority(%currentAction, 1100)
		
REACTION Action_Cheer, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Animation
	ACTIONS
		GetRandom(_Animation, "Worship_01", "Clap_01", "Clap_02", "Clap_04")
		CharacterPlayAnimation(_Animation)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)
		
REACTION Action_Listen, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Animation
	ACTIONS
		GetRandom(_Animation, "Attentive_01", "still")
		CharacterPlayAnimation(_Animation)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)
		
REACTION Action_Talk, 0
	USAGE PEACE
	ACTIONS
		Sleep(15)
		DialogStart("LUC_AD_ChurchMiner", __Me)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)

REACTION Action_Wandering, 0
	USAGE PEACE
	ACTIONS
		CharacterWander(FLOAT:10,15,0,%PointAfterQuest)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)
		
EVENTS

EVENT endQuest
	ON
		OnGlobalEventSet("LUC_MinersExitChurch")
	ACTIONS
		Set(%currentState, "State_AfterTheQuest")