#INCLUDE State_Manager

INIT
	USING State_Manager
	CHARACTER:__Me
	CHARACTER:%SkeletonQueen=CYS_SkeletonQueen_38d5e375-b8b2-4400-bb21-049c41cc9915
	INT:%SpawnSkeletons = 0
	
//----------------------------	
BEHAVIOUR
REACTION State_Idle, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction, "Action_Idle")
	SetPriority(%currentAction, 1100)

REACTION Action_Idle, 0
USAGE PEACE
ACTIONS
	IF "!c1"
		CharacterHasStatus(__Me,SITTING)
	THEN
		CharacterUseItem(FUR_Skeleton_Throne_A_001_198767e2-77c0-42e9-819d-764f8f75505a)
	ENDIF
	Sleep(1.0)
	

REACTION Action_SpawnSkeletons, 3000
USAGE ALL
CHECK "c1"
	IsEqual(%SpawnSkeletons,1)
ACTIONS
	CharacterMoveTo(TRIGGER:CYS_SkeletonKingExit_92df405f-8e2b-4d07-ad3e-4f31e36c317b,0,1,0)
	CharacterEvent(CYS_SkeletonQueen_c9dad59b-5778-4064-8787-3f8a4529dad9,"ExitThrone")
	DisplayText(__Me,"CYS_SkeletonKing_Summon")
	Sleep(1.0)
	CharacterPlayAnimation("cast_shout_cast")
	CharacterEvent(__Me,"SummonSkeletons")
	SetPriority("Action_SpawnSkeletons",0)
	CharacterEndTurn()
	Sleep(1.0)

REACTION TalkToQueen,0	
USAGE COMBAT
VARS
	FIXEDSTRING:_KingText
ACTIONS
	GetRandom(_KingText,"CYS_SkeletonKing_OnTurnText_01","CYS_SkeletonKing_OnTurnText_02","CYS_SkeletonKing_OnTurnText_03","CYS_SkeletonKing_OnTurnText_04")
	DisplayText(__Me,_KingText,4)
	Sleep(4)
	CharacterEvent(__Me,"CYS_SkeletonKing_KingsText_Event")	
	SetPriority("TalkToQueen",0)
	
//----------------------------	
EVENTS
EVENT OnTurnTalkToQueen
ON
	OnTurn()
ACTIONS
	IF "!c1&c2"
		CharacterIsDead(%SkeletonQueen)	
		IsRandom(0.5)
	THEN	
		SetPriority("TalkToQueen",700)
	ENDIF
	
EVENT ReceiveEvent_QueenHasSpoken
VARS
	FIXEDSTRING:_KingsReply
ON
	OnCharacterEvent(%SkeletonQueen,"CYS_SkeletonKing_QueensText_Event")
ACTIONS
	GetRandom(_KingsReply,"CYS_SkeletonKing_KingsReply_01","CYS_SkeletonKing_KingsReply_02","CYS_SkeletonKing_KingsReply_03","CYS_SkeletonKing_KingsReply_04")
	DisplayText(__Me,_KingsReply,4)			
	
EVENT QueenDiesYouAreSad
	ON
		OnDie(%SkeletonQueen,_,_,_)
	ACTIONS
		IF "!c1" //either this, or assert -_-
			CharacterIsDead(__Me)
		THEN
			DisplayText(__Me,"CYS_SkeletonKing_SadKing",4)		
		ENDIF