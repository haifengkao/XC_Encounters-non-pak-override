#INCLUDE DefaultCharacter

INIT
USING DefaultCharacter
CHARACTER:__Me
CHARACTER:%Albert = LUC_Albert_86f51bc8-485b-4fc2-9328-c2b91dd8db36
TRIGGER:%Comment = LUC_PointErnestoComment_15b24e8c-9559-4076-b4b6-c5f714297ce3
TRIGGER:%Lookout1 = LUC_PointErnestoLookout_147fa0e8-a69a-42cf-9d2e-cffe578c8b37
TRIGGER:%Lookout2 = LUC_PointErnestoLookout2_4ee0dac0-b361-4879-b55c-c1d9d095a3e7
TRIGGER:%Feel = LUC_PointErnestoFeel_7ea48110-a7ee-410f-bda4-8ad7b5a4b6c1
CHARACTER:%Player = null

FIXEDSTRING:%CurrentAction = ""
FIXEDSTRING:%PreviousAction = ""

BEHAVIOUR

REACTION ChooseAction, 1060
USAGE PEACE
ACTIONS
	GetRandom(%CurrentAction, "GoToLookout1", "GoToLookout2", "TalkToAlbert")
	GotoIfEqual(%PreviousAction, %CurrentAction, "Start")
	Set(%PreviousAction, %CurrentAction)
	SetPriority(%CurrentAction, 1070)

REACTION GoToLookout1, 0
USAGE PEACE
ACTIONS
	CharacterMoveTo(%Lookout1)
	CharacterLookFrom(%Lookout1)
	CharacterPlayAnimation("Think_01")
	SetPriority(%CurrentAction,0)
INTERRUPT
	SetPriority(%CurrentAction,0)

REACTION GoToLookout2, 0
USAGE PEACE
ACTIONS
	CharacterMoveTo(%Lookout2)
	CharacterLookFrom(%Lookout2)
	CharacterPlayAnimation("Think_02")
	SetPriority(%CurrentAction,0)
INTERRUPT
	SetPriority(%CurrentAction,0)
	
REACTION TalkToAlbert, 0
USAGE PEACE
ACTIONS
	DisplayText(%Albert,"LUC_Ernesto_Script_0",3) // Boss! Boss! I think I got through!
	Sleep(3)
	CharacterMoveTo(%Comment,1)
	CharacterLookFrom(%Comment)
	DisplayText(__Me,"LUC_Ernesto_Script_1",3) // Show me first, I've heard that line before.
	CharacterMoveTo(%Feel,0)
	CharacterLookFrom(%Feel)
	CharacterPlayAnimation("Fidget_Low_01")
	DisplayText(__Me,"LUC_Ernesto_Script_2",3) // Did you even try and put your hand inside the hole before calling me?
	Sleep(3)
	CharacterPlayAnimation("Fidget_Low_02")
	DisplayText(__Me,"LUC_Ernesto_Script_3",3) // There is another rock behind that one. Keep Digging!
	Sleep(3)
	DisplayText(%Albert,"LUC_Ernesto_Script_4",3) // Owkay boss. Whatever you say, boss.
	CharacterMoveTo(%Comment,0)
	CharacterLookFrom(%Comment)
	Sleep(15)
	SetPriority(%CurrentAction,0)
INTERRUPT
	SetPriority(%CurrentAction,0)

STORY
	SCRIPTFRAME TurnOffScript
		ACTIONS
			SetPriority("ChooseAction",0)
	
	SCRIPTFRAME TurnOnScript
		ACTIONS
			SetPriority("ChooseAction",1060)


	