#INCLUDE State_Manager

INIT
USING State_Manager
CHARACTER:__Me
EXTERN CHARACTER:%Friend = null
EXTERN STRING:%DialogName = null

BEHAVIOUR

REACTION State_Waiting, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Animation
	FLOAT:_Time
ACTIONS
	CharacterLookAt(%Friend)
	GetRandom(_Animation,"Bored_01","Bored_02","Bored_03","Annoyed_01","Dust_Off_01","Shrug_01")
	GetRandom(_Time,3,4,5,6,7,8)
	Sleep(_Time)
	CharacterPlayAnimation(_Animation)

REACTION State_Dialog, 0
USAGE PEACE
CHECK "!c1&!c2"
	IsInDialog(%Friend)
	IsInDialog(__Me)
ACTIONS
	IF "!c1"
		CharacterIsDead(%Friend)
	THEN
		DialogStart(%DialogName,%Friend,__Me)
	ENDIF
	Sleep(15)
	Set(%currentState,"State_Waiting")
INTERRUPT
	Set(%currentState,"State_Waiting")

EVENTS

EVENT StartDialog
ON
	OnCharacterEvent(__Me,"DialogRequest")
ACTIONS
	Set(%currentState,"State_Dialog")