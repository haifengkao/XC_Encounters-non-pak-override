INIT 
	CHARACTER:__Me
	EXTERN FLOAT:%Reactivity_Seconds = 1.0
	EXTERN FLOAT:%PercentageVisionWhereAttackOnSight0To1 = 0.8
	INT:%SpottedHandle = 0
	FLOAT:%Threshold
	FLOAT:%SightRange
	EXTERN FIXEDSTRING:%Timer = "DAF_AnotherGenericTimer"
	EXTERN STRING:%WhatRaceAmI = "Immaculate" //"Cyclops", "Immaculate", "Demon", "Goblin", "Orc", "DeathKnight", "Ram"
	
//------------------------
BEHAVIOUR
REACTION EnemySpottedTimer,11000
USAGE PEACE
VARS
	CHARACTER:_Player
	FIXEDSTRING:_SpottedText
	FIXEDSTRING:_AggroText		
	FLOAT:_Distance
CHECK "!c1&!c2&!c3&!c4&c5&c6&c7"
	CharacterHasStatus(__Me,DYING)
	CharacterHasStatus(__Me,STUNNED)
	CharacterHasStatus(__Me,CHARMED)
	CharacterHasStatus(__Me,FEAR)
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party) //Party = anything player controlled (player chars, companions)
	CharacterCanSee(__Me,_Player)
	GetDistance(_Distance,__Me,_Player)
ACTIONS
	CharacterLookAt(_Player, 0, 5)
	StopTimer("DAF_Cyclops_ForgottenTimer")
	IF "c1"
		IsEqual(%SpottedHandle, 0)
	THEN
		CharacterPlayLoopEffect(%SpottedHandle,__Me,"FX_GP_ExclamationMark_B","Dummy_CastFX")
		StartTimer(%Timer, 1.0, 0)
	ENDIF
	IF "c1"
		IsGreaterThen(_Distance, %Threshold)
	THEN
		IF "c1"
			IsEqual(%WhatRaceAmI,"Cyclops")
		THEN
			GetRandom(_SpottedText,"DAF_Cyclops_Spotted_01","DAF_Cyclops_Spotted_02")
		ELIF "c1"
			IsEqual(%WhatRaceAmI,"Immaculate")
		THEN
			GetRandom(_SpottedText,"DAF_Immaculate_Spotted_01","DAF_Immaculate_Spotted_02","DAF_Immaculate_Spotted_03")
		ELIF "c1"
			IsEqual(%WhatRaceAmI,"Demon")
		THEN
			GetRandom(_SpottedText,"LUC_DemonPlains_Spotted_01","LUC_DemonPlains_Spotted_02","LUC_DemonPlains_Spotted_03","LUC_DemonPlains_Spotted_04","LUC_DemonPlains_Spotted_05")
		ELIF "c1"
			IsEqual(%WhatRaceAmI,"Orc")
		THEN
			GetRandom(_SpottedText,"DAF_Orc_Spotted_01","DAF_Orc_Spotted_02","DAF_Orc_Spotted_03")	//TWEAK THIS
		ELIF "c1"
			IsEqual(%WhatRaceAmI,"Goblin")
		THEN
			GetRandom(_SpottedText,"LUC_GoblinBridges_Spotted_01","LUC_GoblinBridges_Spotted_02","LUC_GoblinBridges_Spotted_03","LUC_GoblinBridges_Spotted_04","LUC_GoblinBridges_Spotted_05")
		ELIF "c1"
			IsEqual(%WhatRaceAmI,"DeathKnight")
		THEN
			GetRandom(_SpottedText,"DAF_DeathKnight_Spotted_01","DAF_DeathKnight_Spotted_02","DAF_DeathKnight_Spotted_03")
		ELIF "c1"
			IsEqual(%WhatRaceAmI,"Ram")
		THEN
			GetRandom(_SpottedText,"CMB_WolfReaction")
		ENDIF
		DisplayText(__Me,_SpottedText,3)
		Sleep(%Reactivity_Seconds)
	ENDIF
	IF "c1"
		CharacterCanSee(__Me,_Player)
	THEN
		SetPriority("YouCanNowFight",12000)
	ELSE
		StartTimer("DAF_Cyclops_ForgottenTimer",3.0,0)	
	ENDIF
		
REACTION YouCanNowFight,0	
USAGE PEACE
VARS
	CHARACTER:_Player
	FIXEDSTRING:_AggroText		
CHECK "!c1&!c2&!c3&!c4&c5&c6"
	CharacterHasStatus(__Me,DYING)
	CharacterHasStatus(__Me,STUNNED)
	CharacterHasStatus(__Me,CHARMED)
	CharacterHasStatus(__Me,FEAR)
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party)
	CharacterCanSee(__Me,_Player)
ACTIONS
	CharacterLookAt(_Player, 0, 5)
	IF "c1"
		IsEqual(%WhatRaceAmI,"Cyclops")
	THEN
		GetRandom(_AggroText,"DAF_Cyclops_CallForHelp_01")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Immaculate")
	THEN
		GetRandom(_AggroText,"DAF_Immaculate_Aggro_01","DAF_Immaculate_Aggro_02","DAF_Immaculate_Aggro_03")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Demon")
	THEN
		GetRandom(_AggroText,"LUC_DemonPlains_Aggro_01","LUC_DemonPlains_Aggro_02","LUC_DemonPlains_Aggro_03")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Orc")
	THEN
		GetRandom(_AggroText,"DAF_Orc_Aggro_01","DAF_Orc_Aggro_02","DAF_Orc_Aggro_03")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Goblin")
	THEN
		GetRandom(_AggroText,"LUC_GoblinBridges_Aggro_01","LUC_GoblinBridges_Aggro_02","LUC_GoblinBridges_Aggro_03")			
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"DeathKnight")
	THEN
		GetRandom(_AggroText,"DAF_DeathKnight_Aggro_01")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Ram")
	THEN
		GetRandom(_AggroText,"GEN_GoHostile")				
	ENDIF
	DisplayText(__Me,_AggroText,3)
	CharacterSetCanFight(__Me,1,1)
	SetPriority("YouCanNowFight",0)
		
//------------------------		
EVENTS
EVENT InitializeInfiltration
ON
	OnInit()
VARS
	FLOAT:_Range
ACTIONS
	IF "c1"
		CharacterGetSightRange(_Range,__Me)
	THEN
		CharacterSetCanFight(__Me,0)
		Set(%Threshold,%PercentageVisionWhereAttackOnSight0To1)
		Set(%SightRange,_Range)
		Multiply(%Threshold,_Range)
	ENDIF
	IF "c1"
		IsHardcore()
	THEN
		Set(%Reactivity_Seconds,0.7)
	ENDIF
		
EVENT KillExclamationMark
ON
	OnTimer(%Timer)
	OnDeactivate()
ACTIONS
	StopLoopEffect(%SpottedHandle)
	Set(%SpottedHandle,0)

EVENT ForgottenTarget
VARS
	FIXEDSTRING:_ForgottenText
ON
	OnTimer("DAF_Cyclops_ForgottenTimer")
ACTIONS
	IF "c1"
		IsEqual(%WhatRaceAmI,"Cyclops")
	THEN
		GetRandom(_ForgottenText,"DAF_Cyclops_Forgotten_01")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Immaculate")
	THEN
		GetRandom(_ForgottenText,"DAF_Immaculate_Forgotten_01","DAF_Immaculate_Forgotten_02","DAF_Immaculate_Forgotten_03")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Demon")
	THEN
		GetRandom(_ForgottenText,"LUC_DemonPlains_Forgotten_01","LUC_DemonPlains_Forgotten_02","LUC_DemonPlains_Forgotten_03")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Orc")
	THEN
		GetRandom(_ForgottenText,"DAF_Orc_Forgotten_01","DAF_Orc_Forgotten_02","DAF_Orc_Forgotten_03")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Goblin")
	THEN
		GetRandom(_ForgottenText,"LUC_GoblinBridges_Forgotten_01","LUC_GoblinBridges_Forgotten_02","LUC_GoblinBridges_Forgotten_03")			
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"DeathKnight")
	THEN
		GetRandom(_ForgottenText,"DAF_DeathKnight_Forgotten_01")
	ELIF "c1"
		IsEqual(%WhatRaceAmI,"Ram")
	THEN
		GetRandom(_ForgottenText,"LUCS_WC_BridgeAmbush06")				
	ENDIF
	DisplayText(__Me,_ForgottenText,3)	
		
EVENT TakingDamage
VARS
	CHARACTER:_Char
ON
	OnDamage(_,_,_Char,_)
ACTIONS
	IF "!c1&c2"
		IsEqual(_Char, null)
		CharacterCheckRelation(_Char, Party)
	THEN
		CharacterSetCanFight(__Me,1,1)
	ENDIF
	
EVENT ExitCombatDisableCanFight
ON
	OnCombatEnded()
ACTIONS
	CharacterSetCanFight(__Me,0)
	SetPriority("YouCanNowFight",0)