#INCLUDE BaseEscortScript

INIT
	USING BaseEscortScript
	CHARACTER:__Me

BEHAVIOUR

REACTION State_Cowering, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_Cower")
		SetPriority(%currentAction, 1100)

REACTION State_EscortDone, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction, "Action_WaitingForOthers")
	SetPriority(%currentAction, 1100)

REACTION State_AfterEscort, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_Idle")
		SetPriority(%currentAction, 1100)
	
REACTION Action_Idle, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Animation
	ACTIONS
		GetRandom(_Animation,"Dust_Off_01","Sigh_01","Tired_01","Yawn_01")
		CharacterPlayAnimation(_Animation)
		Sleep(8)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)		
		
REACTION Action_Cower, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Animation
		CHARACTER:_Player
	ACTIONS
		GetRandom(_Animation,"Cower_01","Cower_02")
		CharacterPlayAnimation(_Animation)
		IF "c1&!c2"
			IsRandom(0.4)
			CharacterGet(_Player,__Me,5,Lowest,Distance,Player)
		THEN
			DialogStart("CYS_AD_ArcheologistCower",__Me)
		ENDIF
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)		
		
REACTION Action_WaitingForOthers, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Animation
	FLOAT:_Range
	FLOAT:_CurDist
	CHARACTER:_Player
ACTIONS
	IF "c1&c2"
		CharacterGet(_Player,__Me,5,Lowest,Distance,Player)
		IsRandom(0.4)
	THEN
		CharacterLookAt(_Player)
		GetRandom(_Animation,"Shout_01","Wave_02")
	ELSE
		GetRandom(_Animation,"Dust_Off_01","Tired_01")
	ENDIF
	CharacterPlayAnimation(_Animation)
	Sleep(4)
	SetPriority(%currentAction, 0)	
INTERRUPT
	SetPriority(%currentAction, 0)	

	
REACTION Combat_Cower, 0
USAGE COMBAT
VARS
	FIXEDSTRING:_Animation
	CHARACTER:_Enemy
	FIXEDSTRING:_Text
ACTIONS
	IF "c1"
		CharacterGet(_Enemy,__Me,null,Lowest,Distance,Enemy)
	THEN
		CharacterLookAt(_Enemy)
	ENDIF
	GetRandom(_Animation,"Cower_01","Cower_02")
	CharacterPlayAnimation(_Animation)
	
	GetRandom(_Text,"CYS_Archaeologist_CombatHelp_1","CYS_Archaeologist_CombatHelp_2","CYS_Archaeologist_CombatHelp_3")
	DisplayText(__Me,_Text)
	Sleep(1)
	CharacterEndTurn()
	
REACTION Combat_Cower_Waiting, 0
USAGE WAITING
VARS
	FIXEDSTRING:_Animation
	CHARACTER:_Enemy
	FIXEDSTRING:_Text
ACTIONS
	IF "c1"
		CharacterGet(_Enemy,__Me,null,Lowest,Distance,Enemy)
	THEN
		CharacterLookAt(_Enemy)
	ENDIF
	GetRandom(_Animation,"Cower_01","Cower_02")
	CharacterPlayAnimation(_Animation)
	Sleep(1)

	
EVENTS

EVENT GoesToTavern
ON
	OnCharacterEvent(__Me,"AtTavern")
ACTIONS
	Set(%currentState, "State_InTavern")	

EVENT EscortStart
ON
	OnCharacterEvent(__Me,"EscortStart")
ACTIONS
	SetPriority("Combat_Cower", 2000)
	SetPriority("Combat_Cower_Waiting", 2000)
	
EVENT EscortDone
ON
	OnCharacterEvent(__Me,"ReachedWaypoint6")
ACTIONS
	SetPriority("Combat_Cower", 0)
	SetPriority("Combat_Cower_Waiting", 0)
