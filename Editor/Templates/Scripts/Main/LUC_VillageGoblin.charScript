#INCLUDE State_Manager

INIT

USING State_Manager
EXTERN TRIGGER:%tent_IN 	 = null
EXTERN TRIGGER:%tent_OUT 	 = null
TRIGGER:%villageArea 		 = LUC_OwnerShip_GoblinVillage_b2d52f76-b276-440e-ba18-c1d9b8a84a44 
CHARACTER:__Me

BEHAVIOUR

REACTION State_ActionManager, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction, "Action_Wander", "Action_GoIntoTent")
	//only go to the tent once in a row, but free to wander X times in a row
	IF "c1"
		IsEqual(%currentAction,"Action_GoIntoTent")
	THEN
		GotoIfEqual(%previousAction, %currentAction, "Start")
	ENDIF
	Set(%previousAction, %currentAction)
	SetPriority(%currentAction, 1100)
	
REACTION Action_Wander, 0
USAGE PEACE
ACTIONS
	CharacterWander(%villageArea, 5)
	Sleep(2)
	SetPriority(%currentAction, 0)
	
REACTION Action_GoIntoTent, 0
USAGE PEACE
VARS
	FLOAT:_sleepTime
	FLOAT:_Dist
ACTIONS
	CharacterMoveTo(%tent_OUT, 0)
	IF "c1&c2"
		GetDistance(_Dist,__Me,%tent_OUT)
		IsLessThen(_Dist,1.5)
	THEN
		CharacterForceUpdate(1)
		CharacterTeleportTo(%tent_IN)
		GetRandom(_sleepTime, 5, 8, 12, 15)
		Sleep(_sleepTime)
		CharacterTeleportTo(%tent_OUT)
		CharacterForceUpdate(0)
	ENDIF
	SetPriority(%currentAction, 0)
	
	