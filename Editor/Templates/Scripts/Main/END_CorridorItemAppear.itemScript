INIT
ITEM:__Me
INT:%CurrentIsVisible=0
INT:%NewIsVisible=0
INT:%TimerStopped=0
EXTERN FLOAT:%MinPerception=0
EXTERN FLOAT:%MinDistance=0
EXTERN INT:%IsTrap = 1

EVENTS

EVENT InitializeHidden
ON
	OnInit()
ACTIONS
	IF "c1"
		IsEqual(%CurrentIsVisible,0)
	THEN
		StartTimer("UpdateHidden",0.2,-1)
	ENDIF
	SetVisible(__Me,%CurrentIsVisible)
	
EVENT ShutdownHidden
ON
	OnShutdown()
ACTIONS
	StopTimer("UpdateHidden")

EVENT UpdateHidden
ON
	OnTimer("UpdateHidden")
ACTIONS
	Set(%NewIsVisible,0)
	IterateParty("HiddenObjectCheck")
	IF "!c1&c2"
		IsEqual(%CurrentIsVisible,%NewIsVisible)
		IsEqual(%TimerStopped,0)
	THEN
		Set(%CurrentIsVisible,%NewIsVisible)
		SetVisible(__Me,%CurrentIsVisible)
	ENDIF
	
	IF "c1"
		IsEqual(%CurrentIsVisible,1)
	THEN
		StopTimer("UpdateHidden")
	ENDIF

EVENT CheckHidden
VARS
CHARACTER:_Char
FLOAT:_Perception
FLOAT:_Dist
STRING:_Event
ON
	OnIterateCharacter(_Char,"HiddenObjectCheck")
ACTIONS
	IF "c1&!c2&c3&c4&!c5&c6&c7" 
		IsEqual(%NewIsVisible,0)
		CharacterIsDead(_Char)
		CharacterGetStat(_Perception,_Char,TrapPerception)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Perception,%MinPerception)
		IsLessThen(_Dist,%MinDistance)
		IsEqual(%TimerStopped,0)
	THEN
		Set(%NewIsVisible,1)
		IF "c1"
			IsEqual(%IsTrap, 0)
		THEN
			SetMaterial(__Me,"39ae3071-a9bd-42cf-9291-82df3c272d6e",3,1,1,1,1,1,0)
			//GetRandom(_Event, "HiddenPerceptionReveal", "HiddenPerceptionReveal2", "HiddenPerceptionReveal3", "HiddenPerceptionReveal4")
			//CharacterEvent(_Char,_Event)
		ELSE
			GetRandom(_Event, "HiddenTrapReveal", "HiddenTrapReveal2", "HiddenTrapReveal3", "HiddenTrapReveal4")
			CharacterEvent(_Char,_Event)
		ENDIF
	ENDIF
	
EVENT StoryReveal
ON
	OnItemEvent(__Me,"StoryReveal")
ACTIONS
	Set(%CurrentIsVisible,1)
	Set(%NewIsVisible,1)
	Set(%TimerStopped,1)
	SetVisible(__Me,1)
	StopTimer("UpdateHidden")
