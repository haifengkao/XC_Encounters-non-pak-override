INIT
	CHARACTER:__Me
	CHARACTER:%Owner
	CHARACTERTEMPLATE:%FireWardTemplate="Summons_Ward_Fire_84a171b0-c44d-4689-b8fc-25e32676cd76"
	CHARACTERTEMPLATE:%WaterWardTemplate="Summons_Ward_Water_52c2556e-5867-4203-b7cb-7a68f5863469"
	CHARACTERTEMPLATE:%AirWardTemplate="Summons_Ward_Air_dd564396-1c61-44b9-b20a-2e35de986a2c"
	CHARACTERTEMPLATE:%EarthWardTemplate="Summons_Ward_Earth_cabefda4-6839-44e7-8e5e-7c1942fc13ca"
	CHARACTERTEMPLATE:%IronWardTemplate="Summons_Ward_Physical_c542a8df-8352-4cc5-a839-e5016c1f29a6"
	CHARACTERTEMPLATE:%MagicWardTemplate="Summons_Ward_Magical_98be4473-a55a-495a-adac-0bda6dc69343"
	STATUS:%ChosenStatus="FIRE_IMMUNITY"
	STATUS:%BonusStatus="VOID_AURA"
	STATUS:%StatusToCure="BURNING"
	
//----------
BEHAVIOUR
REACTION Ward_EndTurn,500
USAGE COMBAT
ACTIONS
	Sleep(0.2)
	CharacterEndTurn()
	
REACTION Ward_FollowsOwner,100
USAGE ALL
ACTIONS
	CharacterFollow(%Owner,-1,1)	
	
REACTION Ward_Error_NoTemplate,0
USAGE COMBAT
ACTIONS
	DebugText(__Me,"Error: invalid template!")
	Sleep(3)
	CharacterEndTurn()

REACTION Ward_Error_NoOwner,0
USAGE COMBAT
ACTIONS
	DebugText(__Me,"Error: no owner!")
	Sleep(3)
	CharacterEndTurn()

//----------
EVENTS
EVENT OnSummonStatus_ApplyStatusToOwner
VARS
	CHARACTERTEMPLATE:_WardsTemplate
ON
	OnCharacterStatus(__Me,"SUMMONING")					//OnInit() doesn't work bc/ too early, owner isn't set yet
ACTIONS
	SetPriority("Combat_AttackSetEnemy",0)
	SetPriority("Combat_MoveToSetEnemy",0)
	IF "c1&c2"
		CharacterGetOwner(%Owner,__Me)
		CharacterGetTemplate(__Me,_WardsTemplate)
	THEN
	//----
		IF "c1"
			IsEqual(%Owner,null)
		THEN
			SetPriority("Ward_Error_NoOwner",520)
		ENDIF
	//----		
		IF "c1"											//FIRE - fire imm, removes burning, hastens
			IsEqual(_WardsTemplate,%FireWardTemplate)
		THEN
			CharacterApplyStatus(__Me,"FIRE_IMMUNITY",-1,1)
			Set(%ChosenStatus,"FIRE_IMMUNITY")
			Set(%StatusToCure,"BURNING")
			Set(%BonusStatus,"HASTED")
		ELIF "c1"										//WATER - water imm, removes wet, heals
			IsEqual(_WardsTemplate,%WaterWardTemplate)
		THEN
			CharacterApplyStatus(__Me,"WATER_IMMUNITY",-1,1)
			Set(%ChosenStatus,"WATER_IMMUNITY")
			Set(%StatusToCure,"WET")
			Set(%BonusStatus,"HEALING")			
		ELIF "c1"										//AIR - air imm, boosts damage
			IsEqual(_WardsTemplate,%AirWardTemplate)
		THEN
			CharacterApplyStatus(__Me,"AIR_IMMUNITY",-1,1)
			Set(%ChosenStatus,"AIR_IMMUNITY")
			Set(%BonusStatus,"ETHEREAL_SOLES")		
		ELIF "c1"										//EARTH - earth imm, removes poison, blesses
			IsEqual(_WardsTemplate,%EarthWardTemplate)
		THEN
			CharacterApplyStatus(__Me,"EARTH_IMMUNITY",-1,1)
			Set(%ChosenStatus,"EARTH_IMMUNITY")
			Set(%StatusToCure,"POISONED")
			Set(%BonusStatus,"BLESSED")					
		ELIF "c1"
			IsEqual(_WardsTemplate,%IronWardTemplate)	//IRON - slash, pierce, crush imm
		THEN
			CharacterApplyStatus(__Me,"PHYSICAL_IMMUNITY",-1,1)
			Set(%ChosenStatus,"PHYSICAL_IMMUNITY")
		ELIF "c1"
			IsEqual(_WardsTemplate,%MagicWardTemplate)	//ELEMENTAL - fire, water, air, earth imm
		THEN
			CharacterApplyStatus(__Me,"ELEMENTAL_IMMUNITY",-1,1)
			Set(%ChosenStatus,"ELEMENTAL_IMMUNITY")
		ELSE
			SetPriority("Ward_Error_NoTemplate",510)
		ENDIF
		IF "!c1&!c2"
			CharacterIsDead(__Me)
			CharacterIsDead(%Owner)
		THEN
			CharacterApplyStatus(%Owner,%ChosenStatus,-1,1)
		ENDIF
	//----
		IF "!(c1|c2|c3)"
			IsEqual(%BonusStatus,"VOID_AURA")
			CharacterIsDead(__Me)
			CharacterIsDead(%Owner)			
		THEN
			CharacterApplyStatus(%Owner,%BonusStatus,-1,1)
		ENDIF
	//----		
		IF "c1&!(c2|c3)"
			CharacterHasStatus(%Owner,%StatusToCure)
			CharacterIsDead(__Me)
			CharacterIsDead(%Owner)			
		THEN
			CharacterRemoveStatus(%Owner,%StatusToCure)
			CharacterPlayEffect(%Owner,"FX_Skills_Fire_PurifyingFire_Impact_A")
		ENDIF
	ENDIF

EVENT OnDie_RemoveStatusFromOwner
ON
	OnDie(__Me,_,_,_)
ACTIONS
	CharacterPlayEffect(%Owner,"FX_Skills_Fire_PurifyingFire_Impact_A")
	CharacterRemoveStatus(%Owner,%ChosenStatus)
	IF "c1"
		CharacterHasStatus(%Owner,%BonusStatus)
	THEN
		CharacterRemoveStatus(%Owner,%BonusStatus)
	ENDIF