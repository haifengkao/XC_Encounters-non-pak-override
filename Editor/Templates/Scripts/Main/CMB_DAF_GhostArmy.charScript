INIT
	CHARACTER:__Me
	CHARACTER:%Ghost_Boar="DAF_PhantomForest_Ghost_Boar_01_b9944dbf-c8df-40e9-915f-26b739f872e4"
	CHARACTER:%Ghost_Bull="DAF_PhantomForest_Ghost_Deer_01_33389a61-0f7a-4fe4-9e5e-51b7347e36a0"
	CHARACTER:%Ghost_Wolf="DAF_PhantomForest_Ghost_Wolf_01_e584bd89-8c38-4254-8a76-094704fca13d"	

//----------------
BEHAVIOUR
REACTION GhostBoar_CastFortify,500
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange
CHECK "c1&c2&c3&!(c4|c5|c6)"
	CharacterCanCast(__Me,"Target_EnemyFortify")								//c1
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyFortify")		//c2
	CharacterGet(_Target,__Me,30,Lowest,Vitality,Ally)							//c3
	IsEqual(_Target,%Ghost_Bull)												//!c4
	IsEqual(_Target,%Ghost_Wolf)												//!c5
	CharacterHasStatus(_Target,"FORTIFIED")										//!c6
ACTIONS
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	CharacterUseSkill("Target_EnemyFortify",_Target)

REACTION GhostBull_CastBlessedEarth,500
USAGE COMBAT
CHECK "c1"
	CharacterCanCast(__Me,"Shout_EnemyBlessedEarth")
ACTIONS
	CharacterUseSkill("Shout_EnemyBlessedEarth",__Me)
	
REACTION GhostWolf_CastDamageBoost,500
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange
CHECK "c1&c2&c3&!(c4|c5|c6)"
	CharacterCanCast(__Me,"Target_EnemyTargetedDamageBoost")					//c1
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyFortify")		//c2
	CharacterGet(_Target,__Me,30,Highest,Strength,Ally)							//c3
	IsEqual(_Target,%Ghost_Boar)												//!c4
	IsEqual(_Target,%Ghost_Bull)												//!c5
	CharacterHasStatus(_Target,"BOOST")											//!c6
ACTIONS
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	CharacterUseSkill("Target_EnemyTargetedDamageBoost",_Target)	
	
REACTION GhostWolf_CastHaste,500
USAGE COMBAT
VARS
	CHARACTER:_Target
	FLOAT:_MinRange
	FLOAT:_MaxRange
CHECK "c1&c2&c3&!(c4|c5|c6)"
	CharacterCanCast(__Me,"Target_EnemyHaste")									//c1
	CharacterGetSkillRange(_MinRange,_MaxRange,__Me,"Target_EnemyFortify")		//c2
	CharacterGet(_Target,__Me,30,Lowest,Speed,Ally)								//c3
	IsEqual(_Target,%Ghost_Boar)												//!c4
	IsEqual(_Target,%Ghost_Bull)												//!c5
	CharacterHasStatus(_Target,"HASTED")										//!c6
ACTIONS
	CharacterMoveInRange(_Target,_MinRange,_MaxRange,1)
	CharacterUseSkill("Target_EnemyHaste",_Target)	
	
//----------------
EVENTS
EVENT OnInit_ApplyMaterial_GiveSpells
ON
	OnInit()
ACTIONS
	IF "c1"
		IsEqual(__Me,%Ghost_Boar)
	THEN
		CharacterAddSkill("Target_EnemyTargetedDamageBoost")
	ELIF "c1"
		IsEqual(__Me,%Ghost_Bull)
	THEN
		CharacterAddSkill("Shout_EnemyBlessedEarth")
		CharacterAddSkill("Target_EnemyFortify")
	ELIF "c1"
		IsEqual(__Me,%Ghost_Wolf)
	THEN
		CharacterAddSkill("Target_EnemyHaste")
	ENDIF