INIT 
	CHARACTER:__Me
	EXTERN FLOAT:%Reactivity_Seconds = 2.0
	EXTERN FLOAT:%PercentageVisionWhereAttackOnSight0To1 = 0.50
	EXTERN INT:%bool_IsBurning = 0
	INT:%SpottedHandle = 0
	FLOAT:%Threshold
	FLOAT:%SightRange
	EXTERN FIXEDSTRING:%Timer = "CYS_AnotherGenericTimer"
	
//------------------------
BEHAVIOUR
REACTION EnemySpottedTimer,11000
USAGE PEACE
VARS
	CHARACTER:_Player
	FIXEDSTRING:_SpottedText
	FIXEDSTRING:_AggroText		
	FLOAT:_Distance
CHECK "!c1&!c2&!c3&!c4&c5&c6&c7"
	CharacterHasStatus(__Me,DYING)
	CharacterHasStatus(__Me,STUNNED)
	CharacterHasStatus(__Me,CHARMED)
	CharacterHasStatus(__Me,FEAR)
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party) //Party = anything player controlled (player chars, companions)
	CharacterCanSee(__Me,_Player)
	GetDistance(_Distance,__Me,_Player)
ACTIONS
	CharacterLookAt(_Player, 0, 5)
	StopTimer("CYS_CharredPlains_ForgottenTimer")
	IF "c1"
		IsEqual(%SpottedHandle,0)
	THEN
		CharacterPlayLoopEffect(%SpottedHandle,__Me,"FX_GP_ExclamationMark_B","Dummy_CastFX")
		StartTimer(%Timer,1.0,0)
	ENDIF
	IF "c1"
		IsGreaterThen(_Distance, %Threshold)
	THEN
		GetRandom(_SpottedText,"CYS_CharredPlains_Spotted_01","CYS_CharredPlains_Spotted_02","CYS_CharredPlains_Spotted_03","CYS_CharredPlains_Spotted_04","CYS_CharredPlains_Spotted_05")
		DisplayText(__Me,_SpottedText,3)
		Sleep(%Reactivity_Seconds)
	ENDIF
	IF "c1"
		CharacterCanSee(__Me,_Player)
	THEN
		SetPriority("BurningSkeletonCanNowFight",12000)
	ELSE
		StartTimer("CYS_CharredPlains_ForgottenTimer",3.0,0)	
	ENDIF
		
REACTION BurningSkeletonCanNowFight,0	
USAGE PEACE
VARS
	CHARACTER:_Player
	FIXEDSTRING:_AggroText		
CHECK "!c1&!c2&!c3&!c4&c5&c6"
	CharacterHasStatus(__Me,DYING)
	CharacterHasStatus(__Me,STUNNED)
	CharacterHasStatus(__Me,CHARMED)
	CharacterHasStatus(__Me,FEAR)
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party)
	CharacterCanSee(__Me,_Player)
ACTIONS
	CharacterLookAt(_Player, 0, 5)
	GetRandom(_AggroText,"CYS_CharredPlains_Aggro_01","CYS_CharredPlains_Aggro_02","CYS_CharredPlains_Aggro_03")
	DisplayText(__Me,_AggroText,3)
	CharacterSetCanFight(__Me,1,1)
	SetPriority("BurningSkeletonCanNowFight",0)
		
REACTION SaySomethingForTheSakeOfIt,0
USAGE COMBAT
VARS
	CHARACTER:_Player	
	FIXEDSTRING:_FlavorText
CHECK "c1&c2"
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party)
	CharacterCanSee(__Me,_Player)	
ACTIONS
	GetRandom(_FlavorText,"CYS_CharredPlains_CombatFlavor_01","CYS_CharredPlains_CombatFlavor_02","CYS_CharredPlains_CombatFlavor_03","CYS_CharredPlains_CombatFlavor_04","CYS_CharredPlains_CombatFlavor_05")
	DisplayText(__Me,_FlavorText,2)		
	SetPriority("SaySomethingForTheSakeOfIt",0)

//------------------------		
EVENTS
EVENT StartBurning
ON
	OnCharacterStatus(__Me,BURNING)
ACTIONS
	IF "c1"
		CharacterHasStatus(__Me,WEAK)
	THEN
		CharacterRemoveStatus(__Me,WEAK)
	ENDIF
		
EVENT NotBurningAnymore
ON
	OnCharacterStatusRemoved(__Me,BURNING)
ACTIONS
	CharacterApplyStatus(__Me,WEAK,-1)

//-----INFILTRATION MATTERS		
EVENT InitializeBurningSkeleton
ON
	OnInit()
VARS
	FLOAT:_Range
ACTIONS
	Set(%SpottedHandle,0)
	StartTimer("CYS_CharredPlains_CheckSurface_Timer",5.0,0) //gotta start it somewhere
	IF "c1"
		CharacterGetSightRange(_Range,__Me)
	THEN
		Set(%Threshold,%PercentageVisionWhereAttackOnSight0To1)
		Set(%SightRange,_Range)
		Multiply(%Threshold,_Range)
		CharacterSetCanFight(__Me,0)
	ENDIF
	IF "c1"
		IsHardcore()
	THEN
		Set(%Reactivity_Seconds,1)
	ENDIF	
		
EVENT KillExclamationMark
ON
	OnTimer(%Timer)
	OnDeactivate()
ACTIONS
	StopLoopEffect(%SpottedHandle)
	Set(%SpottedHandle,0)

EVENT BurningSkeletonForgotTarget
VARS
	FIXEDSTRING:_ForgottenText
ON
	OnTimer("CYS_CharredPlains_ForgottenTimer")
ACTIONS
	GetRandom(_ForgottenText,"CYS_CharredPlains_Forgotten_01","CYS_CharredPlains_Forgotten_02","CYS_CharredPlains_Forgotten_03")
	DisplayText(__Me,_ForgottenText,3)	

EVENT BurningSkeletonTakingDamage
VARS
	CHARACTER:_Char
ON
	OnDamage(_,_,_Char,_)
ACTIONS
	IF "!c1&c2"
		IsEqual(_Char, null)
		CharacterCheckRelation(_Char, Party)
	THEN
		CharacterSetCanFight(__Me,1,1)
	ENDIF

EVENT BurningSkeletonExitCombatDisableCanFight
ON
	OnCombatEnded()
ACTIONS
	CharacterSetCanFight(__Me,0)
	SetPriority("BurningSkeletonCanNowFight",0)

//-----OTHER MATTERS	
EVENT OnTurnDoVariousThings
ON
	OnTurn()
ACTIONS
	IF "!c1&!c2&!c3&!c4&c5"
		CharacterHasStatus(__Me,DYING)
		CharacterHasStatus(__Me,STUNNED)
		CharacterHasStatus(__Me,CHARMED)
		CharacterHasStatus(__Me,FEAR)
		IsRandom(0.5)
	THEN
		SetPriority("SaySomethingForTheSakeOfIt",500)
	ENDIF
	