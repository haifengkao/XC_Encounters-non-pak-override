INIT 
	CHARACTER:__Me
	EXTERN FLOAT:%Reactivity_Seconds = 2.0
	EXTERN FLOAT:%PercentageVisionWhereAttackOnSight0To1 = 0.50
	INT:%SpottedHandle = 0
	FLOAT:%Threshold
	FLOAT:%SightRange
	EXTERN FIXEDSTRING:%Timer = ""
	INT:%CanSpeak = 1
	
//------------------------
BEHAVIOUR
REACTION EnemySpottedTimer,11000
USAGE PEACE
VARS
	CHARACTER:_Player
	FIXEDSTRING:_SpottedText
	FIXEDSTRING:_AggroText		
	FLOAT:_Distance
CHECK "!c1&!c2&!c3&!c4&c5&c6&c7"
	CharacterHasStatus(__Me,DYING)
	CharacterHasStatus(__Me,STUNNED)
	CharacterHasStatus(__Me,CHARMED)
	CharacterHasStatus(__Me,FEAR)
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party) //Party = anything player controlled (player chars, companions)
	CharacterCanSee(__Me,_Player)
	GetDistance(_Distance,__Me,_Player)
ACTIONS
	CharacterLookAt(_Player,0,5)
	StopTimer("LUC_GoblinBridges_ForgottenTimer")
	IF "c1"
		IsEqual(%SpottedHandle, 0)
	THEN
		CharacterPlayLoopEffect(%SpottedHandle,__Me,"FX_GP_ExclamationMark_B","Dummy_CastFX")
		StartTimer(%Timer, 1.0, 0)
	ENDIF
	IF "c1"
		IsGreaterThen(_Distance, %Threshold)
	THEN
		GetRandom(_SpottedText,"LUC_GoblinBridges_Spotted_01","LUC_GoblinBridges_Spotted_02","LUC_GoblinBridges_Spotted_03","LUC_GoblinBridges_Spotted_04","LUC_GoblinBridges_Spotted_05")
		IF "c1"
			IsEqual(%CanSpeak, 1)
		THEN
			DisplayText(__Me,_SpottedText,3)
			CharacterEvent(__Me, "GoblinBridgesShutUp")
			StartTimer("GoblinBridgesShutUp", 5, 0)
		ENDIF
		Sleep(%Reactivity_Seconds)
	ENDIF
	IF "c1"
		CharacterCanSee(__Me,_Player)
	THEN
		SetPriority("YouCanNowFight",12000)
	ELSE
		StartTimer("LUC_GoblinBridges_ForgottenTimer",3.0,0)	
	ENDIF
		
REACTION YouCanNowFight,0	
USAGE PEACE
VARS
	CHARACTER:_Player
	FIXEDSTRING:_AggroText		
CHECK "!c1&!c2&!c3&!c4&c5&c6"
	CharacterHasStatus(__Me,DYING)
	CharacterHasStatus(__Me,STUNNED)
	CharacterHasStatus(__Me,CHARMED)
	CharacterHasStatus(__Me,FEAR)
	CharacterGet(_Player,__Me,%SightRange,Lowest,Distance,Party)
	CharacterCanSee(__Me,_Player)
ACTIONS
	CharacterLookAt(_Player,0,5)
	GetRandom(_AggroText,"LUC_GoblinBridges_Aggro_01","LUC_GoblinBridges_Aggro_02","LUC_GoblinBridges_Aggro_03")
	IF "c1"
		IsEqual(%CanSpeak, 1)
	THEN
		DisplayText(__Me,_AggroText,3)
		CharacterEvent(__Me, "GoblinBridgesShutUp")
		StartTimer("GoblinBridgesShutUp", 5.0, 0)
	ENDIF
	CharacterSetCanFight(__Me,1,1)
	SetPriority("YouCanNowFight",0)
		
//------------------------		
EVENTS
EVENT Initialize
ON
	OnInit
VARS
	FLOAT:_Range
ACTIONS
	Set(%SpottedHandle,0)
	IF "c1"
		CharacterGetSightRange(_Range,__Me)
	THEN
		Set(%Threshold,%PercentageVisionWhereAttackOnSight0To1)
		Set(%SightRange,_Range)
		Multiply(%Threshold,_Range)
		CharacterSetCanFight(__Me,0)
	ENDIF
	IF "c1"
		IsHardcore()
	THEN
		Set(%Reactivity_Seconds,1)
	ENDIF
		
EVENT KillExclamationMark
ON
	OnTimer(%Timer)
	OnDeactivate()	
ACTIONS
	StopLoopEffect(%SpottedHandle)
	Set(%SpottedHandle,0)

EVENT ForgottenTarget
VARS
	FIXEDSTRING:_ForgottenText
ON
	OnTimer("LUC_GoblinBridges_ForgottenTimer")
ACTIONS
	GetRandom(_ForgottenText,"LUC_GoblinBridges_Forgotten_01","LUC_GoblinBridges_Forgotten_02","LUC_GoblinBridges_Forgotten_03")
	DisplayText(__Me,_ForgottenText,3)	
		
EVENT TakingDamage
VARS
	CHARACTER:_Char
ON
	OnDamage(_,_,_Char,_)
ACTIONS
	IF "!c1&c2"
		IsEqual(_Char, null)
		CharacterCheckRelation(_Char, Party)
	THEN
		CharacterSetCanFight(__Me,1,1)
	ENDIF
	
EVENT ExitCombatDisableCanFight
ON
	OnCombatEnded()
ACTIONS
	CharacterSetCanFight(__Me,0)
	SetPriority("YouCanNowFight",0)
	
EVENT ShutUp
ON
	OnCharacterEvent(_, "GoblinBridgesShutUp")
ACTIONS
	Set(%CanSpeak,0)
	
EVENT SpeakUp
ON
	OnCharacterEvent(_, "GoblinBridgesSpeakUp")
ACTIONS
	Set(%CanSpeak,1)
	
EVENT Speak
ON
	OnTimer("GoblinBridgesShutUp")
ACTIONS
	CharacterEvent(__Me, "GoblinBridgesSpeakUp")
