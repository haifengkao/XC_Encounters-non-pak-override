#INCLUDE State_Manager

INIT
	USING State_Manager
	CHARACTER:__Me
	INT:%CounterIdle = 0
	EXTERN STRING:%StayingFlag = ""
	STRING:%Emotion = "Normal"
	EXTERN STRING:%GoingFlag = ""
	EXTERN TRIGGER:%LeavingPoint = null
	
	
BEHAVIOUR

REACTION State_BeforeQuest, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_Idle", "Action_PlayEmotion")
		SetPriority(%currentAction, 1100)
		
REACTION State_AfterQuest, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_PlayEmotion", "Action_Idle")
		SetPriority(%currentAction, 1100)
				
REACTION Action_PlayEmotion, 0
	USAGE PEACE 
	VARS
		FIXEDSTRING:_Animation
	ACTIONS
		IF "c1"
			IsEqual(%Emotion, "Happy")
		THEN
			GetRandom(_Animation,"Cheer_01","Cheer_02","Cheer_03")
			CharacterLookFrom(%LeavingPoint)
		ELIF "c1"
			IsEqual(%Emotion, "Angry")
		THEN
			GetRandom(_Animation,"Annoyed_01","emotion_angry","emotion_sad")
		ELIF "c1"
			IsEqual(%Emotion, "Normal")
		THEN
			GetRandom(_Animation,"Bored_01","Shrug_01","still")
		ELSE
			Set(_Animation, "still")
		ENDIF
		CharacterPlayAnimation(_Animation)
		Sleep(1)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)
	
REACTION Action_Idle, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Animation = ""
	ACTIONS
		IF "c1"
			IsEqual(%CounterIdle, INT:3)
		THEN
			Set(%CounterIdle, INT:0)
			SetPriority(%currentAction, 0)
		ELSE
			GetRandom(_Animation, "idle1", "idle2", "idle3")
			CharacterPlayAnimation(_Animation)
			Add(%CounterIdle, INT:1)
		ENDIF
	INTERRUPT
		Reset()

EVENTS

EVENT ShipLeaving
ON
	OnGlobalEventSet(%GoingFlag)
ACTIONS
	Set(%currentState, "State_AfterQuest")
	Set(%Emotion, "Happy")
	
EVENT StaysHere
ON
	OnGlobalEventSet(%StayingFlag)
ACTIONS
	Set(%currentState, "State_AfterQuest")
	Set(%Emotion, "Angry")