#INCLUDE DefaultCharacter
INIT
USING DefaultCharacter
CHARACTER:__Me

//-----------------------------
EVENTS
EVENT DefaultIterateSetTarget	OVERRIDE
VARS 
	CHARACTER:_Char
	CHARACTER:_previousSetTarget
	FLOAT:_targetDistance
	INT:_alreadyTargetedBy
	FLOAT:_targetClosestThisScore=0
	FLOAT:_movement
	CHARACTERTEMPLATE:_Temp
ON
	OnIterateCharacter(_Char,"DefaultSetTargetClosestIterator")
ACTIONS
	IF "!c1&!c2&c3&c4&c5&!c6&!c7&c8&!(c9|c10|c11|c12|c13)"
		CharacterIsDead(_Char)
		IsEqual(__Me,_Char)
		CharacterIsEnemy(__Me,_Char)
		GetDistance(_targetDistance,__Me,_Char)
		CharacterGetHostileCount(_alreadyTargetedBy,_Char)
		CharacterHasStatus(_Char,INVISIBLE)
		CharacterHasStatus(_Char,SNEAKING)
		CharacterGetTemplate(_Char, _Temp)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_e3f8248f-be02-4a55-a610-628fe0132971)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Air_aabfa6f5-5dd1-4e4e-b72b-a2faf20a9c5b)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Fire_c7087e6b-745f-4150-9c94-99131692cc85)	
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Poison_056fe4f6-8483-4261-87fd-105572fdc124)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Water_c94de303-d3d1-4d09-b4ef-870624feef67)		
	THEN
		IF "c1&c2"
			CharacterGetEnemy(_previousSetTarget,__Me)
			IsEqual(_previousSetTarget,_Char)
		THEN
			Multiply(_targetDistance,0.8)
		ENDIF
		IF "c1"
			IsEqual(_alreadyTargetedBy,0)
		THEN
			Multiply(_targetDistance,0.5)
		ENDIF
		IF "c1"
			IsEqual(_alreadyTargetedBy,1)
		THEN
			Multiply(_targetDistance,0.6)
		ENDIF
		IF "c1"
			CharacterIsSummon(_Char)
		THEN
			Multiply(_targetDistance,1.5)
		ENDIF
		IF "c1&!c2"
			IsGreaterThen(_alreadyTargetedBy,1)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Add(_alreadyTargetedBy,0.6)
			Multiply(_alreadyTargetedBy,_alreadyTargetedBy)
			Multiply(_targetDistance,_alreadyTargetedBy)
		ENDIF
		IF "c1&c2"
			IsGreaterThen(_alreadyTargetedBy,3)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Multiply(_targetDistance,2)
		ENDIF
		IF "!c1"
			CharacterCanSee(__Me,_Char)
		THEN
			Multiply(_targetDistance,3)
		ENDIF
		IF "c1&!c2"
			CharacterHasTalent(_Char,Stench)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Multiply(_targetDistance,4)
		ENDIF
		IF "c1&c2"
			CharacterHasTalent(_Char,Stench)
			CharacterHasStatus(_Char,AGGRO_MARKED)
		THEN
			Multiply(_targetDistance,2)
		ENDIF
		Set(_targetClosestThisScore,_targetDistance)
		IF "!c1"
			IsGreaterThen(_targetClosestThisScore,%setTargetDefaultBestScore)
		THEN
			Set(%setTargetDefaultBestScore,_targetClosestThisScore)
			CharacterSetEnemy(__Me,_Char)
		ENDIF
	ENDIF
	
EVENT DontAttackAlliesOrInvisibles OVERRIDE // And mushrooms too, better safe than sorry
VARS
	CHARACTER:_Target
	CHARACTERTEMPLATE:_Temp
ON
	OnTurn()
ACTIONS
	IF "c1&(c2|c3|(c4&(c5|c6|c7|c8|c9)))"
		CharacterGetEnemy(_Target,__Me)
		CharacterIsAlly(__Me,_Target)
		CharacterHasStatus(_Target,INVISIBLE)
		CharacterGetTemplate(_Target, _Temp)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_e3f8248f-be02-4a55-a610-628fe0132971)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Air_aabfa6f5-5dd1-4e4e-b72b-a2faf20a9c5b)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Fire_c7087e6b-745f-4150-9c94-99131692cc85)	
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Poison_056fe4f6-8483-4261-87fd-105572fdc124)
		IsEqual(_Temp, CHARACTERTEMPLATE:Env_Evil_Mushroom_B_Summon_Water_c94de303-d3d1-4d09-b4ef-870624feef67)
	THEN
		CharacterSetEnemy(__Me,null)
		Set(%defaultEvaluateTarget,1)
	ENDIF