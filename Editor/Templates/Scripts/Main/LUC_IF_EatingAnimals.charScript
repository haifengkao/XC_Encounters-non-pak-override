#INCLUDE State_Manager

INIT
	USING State_Manager

	CHARACTER:__Me
	EXTERN ITEM:%DeliciousBush1 = null
	EXTERN ITEM:%DeliciousBush2 = null
	EXTERN ITEM:%DeliciousBush3 = null
	EXTERN ITEM:%DeliciousBush4 = null
	INT:%CloseToBush = 0
	ITEM:%CurrentBush = null
	
//------------------------
BEHAVIOUR

REACTION State_Eating, 0
	USAGE PEACE
CHECK "c1"
	IsEqual(%CloseToBush,1)
ACTIONS
	CharacterLookAt(%CurrentBush,0)
	GetWeightedRandom(%currentAction,"Action_Eat",INT:5,"Action_Idle",INT:1)
	SetPriority(%currentAction, 1100)

REACTION Action_Eat, 0
	USAGE PEACE
ACTIONS
	CharacterPlayAnimation(use_eat)
	SetPriority(%currentAction,0)

REACTION Action_Idle, 0
	USAGE PEACE
VARS
	FIXEDSTRING:_anim
ACTIONS
	GetRandom(_anim,"idle1","idle2","idle3")
	CharacterPlayAnimation(_anim)
	SetPriority(%currentAction,0)
		
REACTION FleeFromPlayer,1200
	USAGE PEACE
VARS
	CHARACTER:_char
CHECK "c1&!c2"
	CharacterGet(_char,__Me,6,Random,null,Neutral)
	CharacterHasTalent(_char, AnimalEmpathy)
ACTIONS
	Set(%CloseToBush,0)
	CharacterFleeFrom(Player,20)
	SetPriority(BackToEating,1150)
	
REACTION BackToEating,1150
	USAGE PEACE
VARS
	FLOAT:_isitsafe
	FLOAT:_Bush1
	FLOAT:_Bush2
	FLOAT:_Bush3
	FLOAT:_Bush4
	FLOAT:_ClosestBush
ACTIONS
	Set(%CloseToBush,1)
	GetRandomBetween(_isitsafe,3.0,8.0)
	Sleep(_isitsafe)
	IF "c1&c2&c3&c4"
		GetDistance(_Bush1, __Me, %DeliciousBush1)
		GetDistance(_Bush2, __Me, %DeliciousBush2)
		GetDistance(_Bush3, __Me, %DeliciousBush3)
		GetDistance(_Bush4, __Me, %DeliciousBush4)
	THEN
		Set(_ClosestBush, _Bush1)
		Set(%CurrentBush, %DeliciousBush1)
		IF "c1"
			IsGreaterThen(_ClosestBush, _Bush2)
		THEN
			Set(_ClosestBush, _Bush2)
			Set(%CurrentBush, %DeliciousBush2)
		ENDIF
		IF "c1"
			IsGreaterThen(_ClosestBush, _Bush3)
		THEN
			Set(_ClosestBush, _Bush3)
			Set(%CurrentBush, %DeliciousBush3)
		ENDIF
		IF "c1"
			IsGreaterThen(_ClosestBush, _Bush4)
		THEN
			Set(_ClosestBush, _Bush4)
			Set(%CurrentBush, %DeliciousBush4)
		ENDIF
	ENDIF
	CharacterMoveTo(%CurrentBush,0)
	SetPriority(BackToEating,0)
INTERRUPT
	Reset()