#INCLUDE State_Manager

INIT
	USING State_Manager
	CHARACTER:__Me
	EXTERN TRIGGER:%GuardTrigger = null
	EXTERN STRING:%ADPlayerGoingOut = ""
	EXTERN STRING:%ADPlayerGoingIn = ""
	EXTERN TRIGGER:%GuardPoint = null
	INT:%GreetPlayer = 1

BEHAVIOUR

REACTION State_Guarding, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_Guarding")
		SetPriority(%currentAction, 1100)

REACTION Action_Guarding, 0
USAGE PEACE
VARS
	CHARACTER:_Player
CHECK "c1&c2"
		CharacterGet(_Player,__Me,6,Random,null,Player,null,null)
		CharacterCanSee(__Me,_Player)
ACTIONS
	IF "c1"
		IsEqual(%GreetPlayer,1)
	THEN
		Set(%GreetPlayer,2)	
		IF "c1"
			CharacterIsInTrigger(_Player,%GuardTrigger)
		THEN
			DialogStart(%ADPlayerGoingOut,__Me)
			CharacterPlayAnimation("Salute_01")
			Sleep(15.0)
		ELSE
			DialogStart(%ADPlayerGoingIn,__Me)
			Sleep(15.0)
		ENDIF	
	ENDIF
	SetPriority(%currentAction, 0)
INTERRUPT
	Reset()
	SetPriority(%currentAction, 0)
	
REACTION Action_ResetGreetState, 1200
	VARS
		CHARACTER:_Player
	USAGE PEACE
	CHECK "!c1&!c2"
		CharacterGet(_Player,__Me,12,Random,null,Player,null,null)
		IsEqual(%GreetPlayer,0)
	ACTIONS
		Set(%GreetPlayer,0)
		
REACTION Action_ReturnAtGate, 10000
	USAGE PEACE
	CHECK "!c1"
		CharacterIsInTrigger(__Me, %GuardTrigger)
	ACTIONS
		CharacterMoveTo(%GuardPoint, 1)
		CharacterLookFrom(%GuardPoint)

EVENTS

EVENT OnSpottingPlayer
VARS
	CHARACTER:_Player
ON
	OnSight(_Player)
ACTIONS
	IF "c1&c2"
		CharacterIsPlayer(_Player)
		IsEqual(%GreetPlayer,0)
	THEN
		Set(%GreetPlayer,1)
	ENDIF
	
EVENT GateGuardInit
ON
	OnInit()
ACTIONS
	SetPriority("AutomatedDialog_Interrupt",0)
	
