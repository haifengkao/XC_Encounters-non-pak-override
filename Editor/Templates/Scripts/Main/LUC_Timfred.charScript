#INCLUDE State_Manager

INIT
USING State_Manager
CHARACTER:__Me
TRIGGER:%Point1 = LUC_PointTimfred1_d4010c92-98ab-465e-a846-951f7480120f
TRIGGER:%Point2 = LUC_PointTimfred2_b26b7608-7937-485a-88e6-7094124330d6
TRIGGER:%Point3 = LUC_PointTimfred3_e8b0b44a-358c-4cf3-bd6e-1a8ef6dc9ca3
CHARACTER:%Roy = LUC_Roy_0f9254f3-9dff-4404-975e-b6e1a11c3762
CHARACTER:%Bruce = LUC_Bruce_896b0a60-d511-4560-921a-bc18dc0b8872
CHARACTER:%Maurice = LUC_Maurice_723d15e1-d8a8-4797-b9a4-d7d4dc271727

BEHAVIOUR

REACTION State_RunningAround, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction,"Action_TryToLeave","Action_TalkToCows")
	SetPriority(%currentAction,1100)

REACTION State_TalkingToBruce, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Text
	FIXEDSTRING:_Animation
	CHARACTER:_Char
ACTIONS
	GetRandom(_Animation,"idle1","use_eat")
	CharacterMoveTo(%Point1,1)
	CharacterLookAt(%Bruce)
	CharacterPlayAnimation(_Animation)
	IF "c1"
		CharacterGet(_Char, __Me, 10, Random, Distance, All, null, null, AnimalEmpathy)
	THEN
		GetRandom(_Text,"LUC_Timfred_1","LUC_Timfred_2")
		DisplayText(__Me,_Text,5)
	ENDIF
	Sleep(15)
	Set(%currentState,"State_RunningAround")
INTERRUPT
	Set(%currentState,"State_RunningAround")

REACTION State_InTrollCave, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Animation
ACTIONS
	Sleep(2)
	GetRandom(_Animation,"idle1","use_eat")
	CharacterPlayAnimation(_Animation)

REACTION Action_TryToLeave, 0
USAGE PEACE
ACTIONS
	Sleep(2)
	CharacterWander(FLOAT:3,3,0,%Point1)
	Sleep(2)
	CharacterMoveTo(%Point2)
	CharacterLookFrom(%Point2)
	Sleep(1)
	CharacterEvent(%Roy,"TimfredLeaves")
	Sleep(10)
	SetPriority(%currentAction,0)
INTERRUPT
	SetPriority(%currentAction,0)

REACTION Action_TalkToCows, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Text
	FIXEDSTRING:_Animation
	CHARACTER:_Char
ACTIONS
	GetRandom(_Animation,"idle1","use_eat")
	Sleep(2)
	CharacterWander(FLOAT:3,3,0,%Point1)
	Sleep(2)	
	CharacterMoveTo(%Point3)
	CharacterLookFrom(%Point3)
	Sleep(1)
	CharacterPlayAnimation(_Animation)
	IF "c1"
		CharacterGet(_Char, __Me, 10, Random, Distance, All, null, null, AnimalEmpathy)
	THEN
		GetRandom(_Text,"LUC_Timfred_3","LUC_Timfred_4")
		DisplayText(__Me,_Text,5)
	ENDIF
	Sleep(15)
	SetPriority(%currentAction,0)
INTERRUPT
	SetPriority(%currentAction,0)

EVENTS

EVENT BruceTalks
ON
	OnCharacterEvent(__Me,"BruceTalks")
ACTIONS
	IF "!c1"
		IsInDialog(__Me)
	THEN
		Set(%currentState,"State_TalkingToBruce")
	ENDIF