#INCLUDE State_Manager

INIT
	USING State_Manager
	CHARACTER:__Me
	CHARACTER:%Buddy = LUC_IV_Seargiu_d7149598-d1e6-4ace-b795-a75747acf0ce
	INT:%IsAvailable = 1
	TRIGGER:%LookFrom = LUC_IV_AttenberahShootApprentice_796c1a0a-5994-40d9-9ee4-5a49a67ae67c
	
BEHAVIOUR
	
REACTION State_GuardDuty, 0
	USAGE PEACE
	ACTIONS
		CharacterLookFrom(%LookFrom)
		GetWeightedRandom(%currentAction, "Action_Idling", INT:5, "Action_ExpressSelf", INT:3, "Action_Chat", INT:2)
		SetPriority(%currentAction, 1100)

REACTION Action_Idling, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Anim
	ACTIONS
		GetRandom(_Anim, "idle1", "idle2")
		CharacterPlayAnimation(_Anim)
		Sleep(3)
		SetPriority(%currentAction, 0)
		
REACTION Action_Chat, 0
	USAGE PEACE
	VARS
		INT:_BuddyAvailable
	CHECK "!c1&!c2&!c3&c4&c5&c6"
		IsInDialog(__Me)
		IsInDialog(%Buddy)
		CharacterIsDead(%Buddy)
		IsEqual(%IsAvailable, 1)
		GetVar(_BuddyAvailable, %Buddy, "IsAvailable")
		IsEqual(_BuddyAvailable, 1)
	ACTIONS
		CharacterLookAt(%Buddy)
		CharacterEvent(__Me, "IWantToTalk")
		Sleep(3)
		SetPriority(%currentAction, 0)
	INTERRUPT
		SetPriority(%currentAction, 0)
		
		
REACTION Action_ExpressSelf, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Talk
	ACTIONS
		Set(%IsAvailable, 0)
		GetRandom(_Talk, "LUC_AttenGuard_1", "LUC_AttenGuard_2", "LUC_AttenGuard_3", "LUC_Tundrin_1")
		DisplayText(__Me, _Talk, 4)
		Sleep(2)
		IF "c1"
			IsEqual(_Talk, "LUC_Tundrin_1")
		THEN
			CharacterPlayAnimation("cast_self_cast")
			CharacterPlayEffect(__Me, "FX_Skills_Water_IceShard_Prepare_A")
			DisplayText(__Me, "LUC_Tundrin_2", 4)
		ENDIF
		Sleep(8)
		Set(%IsAvailable, 1)
		SetPriority(%currentAction, 0)
