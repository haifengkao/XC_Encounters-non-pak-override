#INCLUDE Base

INIT
USING Base
CHARACTER:__Me
TRIGGER:%Point1 = LUC_PointKate1_3a67b37c-62e8-41c3-9681-c4fd7c599e18
CHARACTER:%Ruby = LUC_Ruby_d385b82c-5d8c-4703-b621-c8840f39e9bc
CHARACTER:%Bonnie = LUC_Bonnie_bb136602-128a-4fe0-a6eb-eaaec76683f1
INT:%State = 0

BEHAVIOUR

REACTION WaitingForClient,1500
USAGE PEACE
CHECK "c1"
	IsEqual(%State,3)
VARS
	FIXEDSTRING:_Animation
ACTIONS
	GetRandom(_Animation,"Dust_Off_01","Flirt_01","Tired_01")
	CharacterPlayAnimation(_Animation)
	Sleep(10)
INTERRUPT
	Reset()

REACTION TalkBonnie,1150
USAGE PEACE
CHECK "c1"
	IsEqual(%State,2)
ACTIONS
	Set(%State,0)
	CharacterLookAt(%Bonnie)
	DialogStart("LUC_AD_BonnieKate",%Bonnie,__Me)
	Sleep(5)
INTERRUPT
	Reset()

REACTION TalkRuby,1100
USAGE PEACE
CHECK "c1"
	IsEqual(%State,1)
ACTIONS
	Set(%State,0)
	CharacterLookAt(%Ruby)
	DialogStart("LUC_AD_RubyKate",%Ruby,__Me)
	Sleep(5)
INTERRUPT
	Reset()

REACTION Patrol,1050
USAGE PEACE
VARS
	FIXEDSTRING:_Animation
ACTIONS
	Sleep(2)
	CharacterMoveTo(%Point1)
	CharacterLookFrom(%Point1)
	GetRandom(_Animation,"Attention_01","Attention_02","Wave_01","Wave_02")
	CharacterPlayAnimation(_Animation)
	Sleep(5)
INTERRUPT
	Reset()

EVENTS

EVENT RubyTalks
ON
	OnCharacterEvent(__Me,"RubyTalks")
ACTIONS
	IF "!c1"
		IsInDialog(__Me)
	THEN
		Set(%State,1)
	ENDIF

EVENT BonnieTalks
ON
	OnCharacterEvent(__Me,"BonnieTalks")
ACTIONS
	IF "!c1"
		IsInDialog(__Me)
	THEN
		Set(%State,2)
	ENDIF

EVENT WaitingForClient
ON
	OnCharacterEvent(__Me,"ProstituteReachedBed")
ACTIONS
	Set(%State,3)

EVENT BackToCamp
ON
	OnCharacterEvent(__Me,"ProstituteReturned")
ACTIONS
	Set(%State,0)