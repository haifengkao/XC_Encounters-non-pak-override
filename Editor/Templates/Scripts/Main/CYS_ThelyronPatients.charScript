#INCLUDE State_Manager

INIT
USING State_Manager
CHARACTER:__Me
EXTERN ITEM:%Bed = null

BEHAVIOUR

REACTION State_IsInPain, 0
USAGE PEACE
ACTIONS 
	GetRandom(%currentAction, "Action_Moaning")
	SetPriority(%currentAction, 1100)
		
REACTION State_IsRecovering, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction, "Action_Comforting")
	SetPriority(%currentAction, 1100)
	
REACTION State_IsDoomed, 0
USAGE PEACE
ACTIONS
	GetRandom(%currentAction, "Action_Moaning")
	Set(%previousState, "State_IsDoomed")
	
REACTION Action_Moaning, 0
USAGE PEACE
VARS
	FLOAT:_Wait
ACTIONS
	IF "!c1&!c2"
		IsEqual(%Bed,null)
		CharacterHasStatus(__Me,LYING)
	THEN
		CharacterUseItem(%Bed)
	ENDIF	
	IF "c1"
		IsRandom(0.7)
	THEN
		DialogStart("CYS_AD_ThelyronPatientPain",__Me)
		GetRandomBetween(_Wait,5.0,8.0)
		Sleep(_Wait)
	ENDIF
	SetPriority(%currentAction, 0)	
	
REACTION Action_Comforting, 0
USAGE PEACE
ACTIONS
	IF "!c1&!c2"
		IsEqual(%Bed,null)
		CharacterHasStatus(__Me,LYING)
	THEN
		CharacterUseItem(%Bed)
	ENDIF	
	IF "c1"
		IsRandom(0.4)
	THEN
		DialogStart("CYS_AD_ThelyronPatientComfortsOther",__Me)
	ENDIF
	Sleep(2.0)
	SetPriority(%currentAction, 0)	
INTERRUPT
	SetPriority(%currentAction, 0)	