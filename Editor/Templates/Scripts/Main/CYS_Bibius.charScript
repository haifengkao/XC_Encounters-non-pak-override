#INCLUDE State_Manager

INIT
USING State_Manager
CHARACTER:__Me
CHARACTER:%Junius = CYS_IntroGuards_Junius_1d8192ef-dff6-47c9-8c2f-ba44e76f4b78
CHARACTER:%escortee = Player1_dac1443f-a866-4ab3-b240-e705c0b20ec5

BEHAVIOUR

REACTION State_GuardingBridge, 0
USAGE PEACE
VARS
	FIXEDSTRING:_Animation
ACTIONS
	GetRandom(_Animation,"Bored_01","Bored_02","Bored_03","Tired_01","Yawn_01")
	CharacterPlayAnimation(_Animation)
	Sleep(6)
	
REACTION State_Escorting, 0
	USAGE PEACE
	ACTIONS
		GetRandom(%currentAction, "Action_Escort")
		SetPriority(%currentAction, 1100)

REACTION State_WaitingPatiently, 0
	USAGE PEACE
	ACTIONS
		Sleep(10)
		
REACTION State_BeingYelledAt, 0
	USAGE PEACE
	VARS
		FIXEDSTRING:_Anim
	ACTIONS
		CharacterMoveTo(TRIGGER:CYS_PointBrandon_2396bd0e-4167-447f-8028-d6b459ed8e9f)
		CharacterLookAt(TRIGGER:CYS_ArhuGuardAfterFight_81cdcb5f-36a9-44a7-8be7-bd8ea35115fb)
		GetRandom(_Anim, "Think_01", "Sooth_01", "Sigh_01", "Shrug_01", "Depressed_01")
		CharacterPlayAnimation(_Anim,1,1)
		Sleep(4)

REACTION State_Dialog, 0
USAGE PEACE
VARS
	FLOAT:_Distance
CHECK "!c1&!c2"
	IsInDialog(%Junius)
	IsInDialog(__Me)
ACTIONS
	IF "!c1&c2&c3"
		CharacterIsDead(%Junius)
		GetDistance(_Distance,__Me,%Junius)
		IsLessThen(_Distance,10)
	THEN
		DialogStart("CYS_AD_IntroGuards",__Me,%Junius)
	ENDIF
	Set(%currentState,"State_GuardingBridge")
INTERRUPT
	Set(%currentState,"State_GuardingBridge")

REACTION Action_Escort, 0
USAGE PEACE
ACTIONS
	CharacterFollow(%escortee, 10, 1)
	SetPriority(%currentAction, 0)
INTERRUPT
	SetPriority(%currentAction, 0)

EVENTS

EVENT StartDialog
ON
	OnCharacterEvent(__Me,"DialogRequest")
ACTIONS
	Set(%currentState,"State_Dialog")
	
EVENT ShamefullReply
ON
	OnCharacterEvent(__Me, "ShamefullReply")
VARS
	FIXEDSTRING:_Text
ACTIONS
	IF "!c1"
		CharacterIsDead(__Me)
	THEN
		GetRandom(_Text, "CYS_BibiusJuniusComplain_1", "CYS_BibiusJuniusComplain_2", "CYS_BibiusJuniusComplain_3", "CYS_BibiusJuniusComplain_4")
		DisplayText(__Me, _Text, 4)
	ENDIF