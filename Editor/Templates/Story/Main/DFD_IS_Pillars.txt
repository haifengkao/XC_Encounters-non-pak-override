Version 1
SubGoalCombiner SGC_AND
INITSECTION
DFD_IS_Pillar(1,ITEM_DFD_IS_Pillar1_Off,ITEM_DFD_IS_Pillar1_On);
DFD_IS_Pillar(2,ITEM_DFD_IS_Pillar2_Off,ITEM_DFD_IS_Pillar2_On);
DFD_IS_Pillar(3,ITEM_DFD_IS_Pillar3_Off,ITEM_DFD_IS_Pillar3_On);
ItemSetOnstage(ITEM_DFD_IS_Pillar1_On,0);
ItemSetOnstage(ITEM_DFD_IS_Pillar2_On,0);
ItemSetOnstage(ITEM_DFD_IS_Pillar3_On,0);

ItemSetOnstage(ITEM_DFD_IS_Book1,0);

ItemSetOnstage(ITEM_DFD_IS_Book2,0);

ItemSetOnstage(ITEM_DFD_IS_Book3,0);



DFD_IS_StatueSpawn(TRIGGER_DFD_IS_Statue_Materialistic,"Materialistic",1,ITEM_DFD_IS_Speaker_Mater,"PUZ_TraitStatue_Left_deebb46e-59d3-4d19-a106-91cbbbdb47a4",0.0);

DFD_IS_StatueSpawn(TRIGGER_DFD_IS_Statue_Spiritual,"Spiritual",1,ITEM_DFD_IS_Speaker_Spirit,"PUZ_TraitStatue_Right_4a1ddccb-1269-4ea1-b966-255574b8316d",0.0);

DFD_IS_StatueSpawn(TRIGGER_DFD_IS_Statue_Altruistic,"Altruistic",2,ITEM_DFD_IS_Speaker_Alt,"PUZ_TraitStatue_Right_4a1ddccb-1269-4ea1-b966-255574b8316d",-1.57);

DFD_IS_StatueSpawn(TRIGGER_DFD_IS_Statue_Egotistical,"Egotistical",2,ITEM_DFD_IS_Speaker_Ego,"PUZ_TraitStatue_Left_deebb46e-59d3-4d19-a106-91cbbbdb47a4",-1.57);

DFD_IS_StatueSpawn(TRIGGER_DFD_IS_Statue_Bold,"Bold",3,ITEM_DFD_IS_Speaker_Bold,"PUZ_TraitStatue_Left_deebb46e-59d3-4d19-a106-91cbbbdb47a4",1.57);

DFD_IS_StatueSpawn(TRIGGER_DFD_IS_Statue_Timid,"Timid",3,ITEM_DFD_IS_Speaker_Timid,"PUZ_TraitStatue_Right_4a1ddccb-1269-4ea1-b966-255574b8316d",1.57);

DFD_IS_DestroyedStatues("PUZ_TraitStatue_Right_4a1ddccb-1269-4ea1-b966-255574b8316d","PUZ_TraitStatue_Right_Destruct_39136da9-cea9-4bdd-81d7-404142501e0e");
DFD_IS_DestroyedStatues("PUZ_TraitStatue_Left_deebb46e-59d3-4d19-a106-91cbbbdb47a4","PUZ_TraitStatue_Left_Destruct_8086ce48-61e5-4487-9126-b7bdd853edbe");



DFD_IS_DamageOnFault(100);

KBSECTION
IF
RegionStarted("DAF_Inner_Sanctum")
THEN
Proc_DFD_IS_Pillars_Reset(0);

IF
RegionEnded("DAF_Inner_Sanctum")
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
THEN
ItemHandleDelete(_ItemHandle);
NOT DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem);

IF
DFD_IS_PillarLit(1)
AND
DFD_IS_PillarLit(2)
AND
DFD_IS_PillarLit(3)
THEN
ItemSetOnstage(ITEM_DFD_IS_Book1,1);
ItemSetOnstage(ITEM_DFD_IS_Book2,1);
ItemSetOnstage(ITEM_DFD_IS_Book3,1);
QuestUpdate("DFD_InnerSanctum","StatueAll");
GoalCompleted;

IF
CharacterUsedItemTemplate(_Player,_,_ItemHandle)
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
AND
StringConcatenate("DFD_IS_",_Trait,_String)
THEN
ItemDisplayText(_SpeakerItem,_String);

IF
CharacterDestroyedItemTemplate(_Player,_,_ItemHandle)
AND
NOT DB_OnlyOnce("DFD_IS_Resetting")
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
AND
DB_Companion(_Player)
THEN
Proc_DFD_IS_DamageOnFault(_Player);
ItemDisplayText(_SpeakerItem,"DFD_IS_Statue_CompanionKill");
TimerLaunch("DFD_IS_StatueReset",4000);
QuestUpdate("DFD_InnerSanctum","StatueFail");
DB_OnlyOnce("DFD_IS_Resetting");

IF
CharacterDestroyedItemTemplate(_Player,_,_ItemHandle)
AND
NOT DB_OnlyOnce("DFD_IS_Resetting")
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
AND
NOT DB_Companion(_Player)
AND
CharacterHasTrait(_Player,_Trait,1)
THEN
Proc_DFD_IS_DamageOnFault(_Player);
ItemDisplayText(_SpeakerItem,"DFD_IS_Statue_WrongKill");
TimerLaunch("DFD_IS_StatueReset",4000);
QuestUpdate("DFD_InnerSanctum","StatueFail");
DB_OnlyOnce("DFD_IS_Resetting");

IF
CharacterDestroyedItemTemplate(_Player,_,_ItemHandle)
AND
NOT DB_OnlyOnce("DFD_IS_Resetting")
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
AND
NOT DB_Companion(_Player)
AND
CharacterHasTrait(_Player,_Trait,0)
AND
DFD_IS_StatueSpawn(_,_Trait,_PillarIndex,_,_,_)
AND
DFD_IS_Pillar(_PillarIndex,_PillarOff,_PillarOn)
AND
NOT DFD_IS_PillarLit(_PillarIndex)
THEN
ItemSetOnstage(_PillarOff,0);
ItemSetOnstage(_PillarOn,1);
DFD_IS_PillarLit(_PillarIndex);
ItemDisplayText(_SpeakerItem,"DFD_IS_Statue_CorrectKill");
QuestUpdate("DFD_InnerSanctum","Statue1");

IF
CharacterDestroyedItemTemplate(_Player,_,_ItemHandle)
AND
NOT DB_OnlyOnce("DFD_IS_Resetting")
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
THEN
DB_DFD_IS_TraitDestroyed(_Trait);

IF
TimerFinished("DFD_IS_StatueReset")
THEN
NOT DB_OnlyOnce("DFD_IS_Resetting");
Proc_DFD_IS_Pillars_Reset(1);
CharacterTeleportPartyToTrigger(TRIGGER_DAF_InnerSanctum_In,"");

PROC
Proc_DFD_IS_DamageOnFault((CHARACTER)_Player)
AND
DFD_IS_DamageOnFault(_Damage)
AND
IntegerProduct(_Damage,2,_NewDamage)
THEN
CharacterApplyDamage(_Player,_Damage,"EARTH");
NOT DFD_IS_DamageOnFault(_Damage);
DFD_IS_DamageOnFault(_NewDamage);

PROC
Proc_DFD_IS_DamageOnFault((CHARACTER)_Player)
AND
DFD_IS_DamageOnFault(_Damage)
AND
_Damage > 10000
THEN
NOT DFD_IS_DamageOnFault(_Damage);
DFD_IS_DamageOnFault(10000);

PROC
Proc_DFD_IS_Pillars_Reset((INTEGER)_RestoreDestroyed)
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
THEN
ItemHandleDelete(_ItemHandle);
NOT DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem);

PROC
Proc_DFD_IS_Pillars_Reset((INTEGER)_RestoreDestroyed)
AND
DFD_IS_StatueSpawn(_Trigger,_Trait,_Pillar,_SpeakerItem,_ItemTemplate,_Yaw)
AND
TriggerGetPosition(_Trigger,_X,_Y,_Z)
AND
CreateItemTemplateAtPosition(_ItemTemplate,_X,_Y,_Z,_ItemHandle)
THEN
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem);
ItemHandleToTransform(_ItemHandle,_X,_Y,_Z,0.0,_Yaw,0.0,1,CHARACTER_NULL);

PROC
Proc_DFD_IS_Pillars_Reset((INTEGER)_RestoreDestroyed)
AND
DFD_IS_Pillar(_Pillar,_PillarOff,_PillarOn)
THEN
ItemSetOnstage(_PillarOff,1);
ItemSetOnstage(_PillarOn,0);

PROC
Proc_DFD_IS_Pillars_Reset(1)
AND
DB_DFD_IS_TraitDestroyed(_Trait)
THEN
NOT DB_DFD_IS_TraitDestroyed(_Trait);

PROC
Proc_DFD_IS_Pillars_Reset(1)
AND
DFD_IS_PillarLit(_Pillar)
THEN
NOT DFD_IS_PillarLit(_Pillar);

PROC
Proc_DFD_IS_Pillars_Reset(_)
AND
DFD_IS_DestroyedStatuesHandles((INTEGER)_DestroyHandle)
THEN
ItemHandleDelete(_DestroyHandle);
NOT DFD_IS_DestroyedStatuesHandles(_DestroyHandle);

PROC
Proc_DFD_IS_Pillars_Reset(0)
AND
DB_DFD_IS_TraitDestroyed(_Trait)
AND
DFD_IS_StatueHandle(_ItemHandle,_Trait,_SpeakerItem)
AND
DFD_IS_StatueSpawn(_Trigger,_Trait,_,_,_Templ,_Yaw)
AND
DFD_IS_DestroyedStatues(_Templ,_DestroyedTempl)
AND
TriggerGetPosition(_Trigger,_X,_Y,_Z)
AND
CreateItemTemplateAtPosition(_DestroyedTempl,_X,_Y,_Z,_DestroyHandle)
THEN
DFD_IS_DestroyedStatuesHandles(_DestroyHandle);
ItemHandleDelete(_ItemHandle);
ItemHandleToTransform(_DestroyHandle,_X,_Y,_Z,0.0,_Yaw,0.0,1,CHARACTER_NULL);

PROC
Proc_DFD_IS_Pillars_Reset(0)
AND
DFD_IS_PillarLit(_Pillar)
AND
DFD_IS_Pillar(_Pillar,_PillarOff,_PillarOn)
THEN
ItemSetOnstage(_PillarOff,0);
ItemSetOnstage(_PillarOn,1);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
