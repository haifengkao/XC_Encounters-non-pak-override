Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_LUC_Roy,"LUC_Roy");
DB_Dialogs(CHARACTER_LUC_Timfred,"LUC_Timfred");
DB_Dialogs(CHARACTER_LUC_Bruce,"LUC_Bruce");
DB_Dialogs(CHARACTER_LUC_Doreen,CHARACTER_LUC_Maurice,"LUC_DoreenMaurice");
DB_Dialogs(CHARACTER_CYS_Bertia,"CYS_Bertia");

DB_LUC_Animals(CHARACTER_LUC_Bruce,TRIGGER_CYS_PointBruce,TRIGGER_DAF_PointBruce);
DB_LUC_Animals(CHARACTER_LUC_Doreen,TRIGGER_CYS_PointDoreen,TRIGGER_DAF_PointDoreen);
DB_LUC_Animals(CHARACTER_LUC_Maurice,TRIGGER_CYS_PointMaurice,TRIGGER_DAF_PointMaurice);

DB_NoRunAway(CHARACTER_LUC_Roy);

DB_ShovelArea(TRIGGER_DAF_ShovelDoreenSecret, "DAF_ShovelDoreeen", ITEM_DAF_ShovelDoreenSecretDirt);
DB_ShovelRewardItemAppear("DAF_ShovelDoreeen", ITEM_DAF_ShovelDoreenSecretChest, TRIGGER_DAF_ShovelDoreenSecretPoint);
KBSECTION
//REGION TIMFRED LEAVES
IF
DialogEnded("LUC_Timfred",_Instance)
AND
CharacterGetVarInteger(CHARACTER_LUC_Timfred,"LUC_TimfredLeaves",1)
THEN
CharacterSetVarInteger(CHARACTER_LUC_Timfred,"LUC_TimfredLeaves",0);
PROC_LUC_TimfredLeaves();
TimerLaunch("LUC_Timfred",2000);

IF
TimerFinished("LUC_Timfred")
THEN
CharacterDisplayText(CHARACTER_LUC_Roy,"LUC_Roy_Story_0");
ChangeAttitude(CHARACTER_LUC_Roy,CHARACTER_Player1,-25);
ChangeAttitude(CHARACTER_LUC_Roy,CHARACTER_Player2,-25);

PROC
PROC_LUC_TimfredLeaves()
THEN
CharacterSetHasDialog(CHARACTER_LUC_Timfred,0);
CharacterDisplayText(CHARACTER_LUC_Timfred,"LUC_Timfred_Story_1");

IF
CharacterDisplayTextEnded(CHARACTER_LUC_Timfred,"LUC_Timfred_Story_1")
THEN
CharacterDisappearOutOfSight(CHARACTER_LUC_Timfred,90,1,"LUC_TimfredLeft",1);

IF
CharacterEvent(CHARACTER_LUC_Timfred,"LUC_TimfredLeft")
THEN
GlobalSetEvent("LUC_TimfredInTrollCave");
CharacterSetOnStage(CHARACTER_LUC_Timfred,1);
CharacterTeleportToTrigger(CHARACTER_LUC_Timfred,TRIGGER_LUC_PointTimfredTroll,"");
CharacterSetHasDialog(CHARACTER_LUC_Timfred,1);
CharacterSetVarFixedString(CHARACTER_LUC_Timfred,"currentState","State_InTrollCave");
//END_REGION

//REGION ANIMALS DELIVERED
IF
RegionEnded("LucullaForest")
AND
CharacterGetVarInteger(CHARACTER_LUC_Roy,"LUC_CattleToBeDelivered",1)
THEN
CharacterSetVarInteger(CHARACTER_LUC_Roy,"LUC_CattleToBeDelivered",0);
GlobalSetEvent("LUC_AnimalsDelivered");
CharacterSetOnStage(CHARACTER_LUC_Timfred,0);
CharacterSetOnStage(CHARACTER_LUC_Bruce,0);
CharacterSetOnStage(CHARACTER_LUC_Doreen,0);
CharacterSetOnStage(CHARACTER_LUC_Maurice,0);
CharacterSetOnStage(CHARACTER_LUC_Roy,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_LUC_Timfred,"LUC_Timfred");
ProcRemoveDialogEntryForSpeaker(CHARACTER_LUC_Doreen,"LUC_DoreenMaurice");
ProcRemoveDialogEntryForSpeaker(CHARACTER_LUC_Roy,"LUC_Roy");
ProcRemoveDialogEntryForSpeaker(CHARACTER_LUC_Bruce,"LUC_Bruce");

IF
GlobalEventSet("LUC_KnowDoreenFate")
THEN
QuestUpdate("LUC_AnimalMerchant","Update4");
QuestClose("LUC_AnimalMerchant");
//END_REGION

//REGION ANIMALS PURCHASED
IF
CharacterDied(CHARACTER_LUC_Roy)
THEN
GlobalSetEvent("LUC_AnimalsPurchased");
QuestUpdate("LUC_AnimalMerchant","Update3");
QuestClose("LUC_AnimalMerchant");

IF
GlobalEventSet("LUC_AnimalsPurchased")
THEN
CHARACTER_LUC_Roy.isLeaving();

IF
DialogEnded("LUC_Roy",_Instance)
AND
CHARACTER_LUC_Roy.isLeaving()
THEN
NOT CHARACTER_LUC_Roy.isLeaving();
CharacterSetHasDialog(CHARACTER_LUC_Roy,0);
CharacterDisappearOutOfSight(CHARACTER_LUC_Roy,90,1,"LUC_RoyToCyseal",1);

IF
CharacterEvent(CHARACTER_LUC_Roy,"LUC_RoyToCyseal")
THEN
CharacterSetOnStage(CHARACTER_LUC_Roy,1);
CharacterTeleportToTrigger(CHARACTER_LUC_Roy,TRIGGER_CYS_PointRoyCyseal,"");
CharacterSetHasDialog(CHARACTER_LUC_Roy,1);
//END_REGION

//REGION ANIMALS TO CYSEAL
IF
GlobalEventSet("LUC_AnimalsGoToCyseal")
THEN
CHARACTER_LUC_Doreen.isLeavingToCyseal();

IF
DialogEnded("LUC_DoreenMaurice",_Instance)
AND
CHARACTER_LUC_Doreen.isLeavingToCyseal()
THEN
NOT CHARACTER_LUC_Doreen.isLeavingToCyseal();
CharacterSetVarFixedString(CHARACTER_LUC_Bruce,"currentState","State_AfterLuculla");
PROC_LUC_MoveAnimals("LUC_AnimalToCyseal");
PROC_LUC_TimfredLeaves();

PROC
PROC_LUC_MoveAnimals((STRING)_Destination)
AND
DB_LUC_Animals(_Character,_,_)
THEN
CharacterSetHasDialog(_Character,0);
CharacterDisappearOutOfSight(_Character,160,0,_Destination,1);

IF
CharacterEvent(_Character,"LUC_AnimalToCyseal")
AND
DB_LUC_Animals(_Character,_TriggerCyseal,_)
THEN
CharacterSetOnStage(_Character,1);
CharacterTeleportToTrigger(_Character,_TriggerCyseal,"");
CharacterSetHasDialog(_Character,1);
//END_REGION

//REGION ANIMALS TO DARK FOREST
IF
GlobalEventSet("LUC_AnimalsGoToFarmlands")
THEN
CHARACTER_LUC_Doreen.isLeavingToDF();

IF
DialogEnded("Luc_DoreenMaurice",_Instance)
AND
CHARACTER_LUC_Doreen.isLeavingToDF()
THEN
NOT CHARACTER_LUC_Doreen.isLeavingToDF();
CharacterSetVarFixedString(CHARACTER_LUC_Bruce,"currentState","State_AfterLuculla");
PROC_LUC_MoveAnimals("LUC_AnimalToDF");
PROC_LUC_TimfredLeaves();

IF
CharacterKilledByCharacter(_Animal,_Player)
AND
DB_LUC_Animals(_Animal,_,_)
AND
NOT DB_Dead(CHARACTER_LUC_Roy)
AND
CharacterCanSeeCharacter(CHARACTER_LUC_Roy,_Player,1)
AND
NOT DB_Combat(CHARACTER_LUC_Roy,_)
THEN
DialogRequestStopForCharacter(_Animal);
ProcMakeNPCHostile(_Player,CHARACTER_LUC_Roy);

IF
CharacterEvent(_Character,"LUC_AnimalToDF")
AND
DB_LUC_Animals(_Character,_,_TriggerDF)
THEN
CharacterSetOnStage(_Character,1);
CharacterTeleportToTrigger(_Character,_TriggerDF,"");
CharacterSetHasDialog(_Character,1);
//END_REGION

//REGION TIMFRED IN TROLL CAVE
IF
CharacterDied(CHARACTER_LUC_TrollKingGuard_01)
THEN
GlobalSetEvent("LUC_TimfredTrollDead");

IF
DialogEnded("LUC_Timfred",_)
AND
CharacterGetVarInteger(CHARACTER_LUC_Timfred,"LUC_TimfredDistractsTroll",1)
THEN
CharacterSetVarInteger(CHARACTER_LUC_Timfred,"LUC_TimfredDistractsTroll",0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_LUC_Timfred,"LUC_Timfred");
CharacterSetHasDialog(CHARACTER_LUC_Timfred,0);
CharacterMoveToCharacter(CHARACTER_LUC_Timfred,CHARACTER_LUC_TrollKingGuard_01,1,"LUC_TimfredAtTroll",0);

IF
CharacterEvent(CHARACTER_LUC_Timfred,"LUC_TimfredAtTroll")
THEN
DialogStartTwoSpeakerDialog("LUC_AD_TimfredTroll",CHARACTER_LUC_Timfred,CHARACTER_LUC_TrollKingGuard_01);

IF
AutomatedDialogEnded("LUC_AD_TimfredTroll",_)
THEN
CharacterDisappearOutOfSight(CHARACTER_LUC_Timfred,90,1,"",1);
CharacterDisappearOutOfSight(CHARACTER_LUC_TrollKingGuard_01,90,1,"",1);
//END_REGION

IF
TextEventSet("teleport")
THEN
CharacterTeleportToTrigger(CHARACTER_LUC_Timfred,TRIGGER_LUC_PointBruce3,"");
CharacterSetVarFixedString(CHARACTER_LUC_Timfred,"currentState","State_InTrollCave");

IF
GlobalEventSet("LUC_ReavealDoreenSecret")
THEN
AddSecret("LUC_DoreenSecret");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
