Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_HIB_IcePrisons(CHARACTER_HIB_IcePrisoner1,ITEM_HIB_IcePrison1,"HIB_IcePrisoner1",3);
DB_HIB_IcePrisons(CHARACTER_HIB_IcePrisoner2,ITEM_HIB_IcePrison2,"HIB_IcePrisoner2",3);
DB_HIB_IcePrisons(CHARACTER_HIB_IcePrisoner3,ITEM_HIB_IcePrison3,"HIB_IcePrisoner3",2);

DB_IcePrisoners(CHARACTER_HIB_IcePrisoner1);
DB_IcePrisoners(CHARACTER_HIB_IcePrisoner2);
DB_IcePrisoners(CHARACTER_HIB_IcePrisoner3);

DB_HostileWardenQuestLogs("HIB_IcePrisoner1","ArrokaSide");
DB_HostileWardenQuestLogs("HIB_IcePrisoner2","AntzigarSide");

DB_FriendlyWardenQuestLogs("HIB_IcePrisoner1","ArrokaAgainst");
DB_FriendlyWardenQuestLogs("HIB_IcePrisoner2","AntzigarAgainst");

DB_HIB_IcePrisonWardens("HIB_IcePrisoner1",CHARACTER_HIB_IcePrisonWarden1_1,1,"HIB_IcePrisoner1_Warden");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner1",CHARACTER_HIB_IcePrisonWarden1_2,0,"");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner1",CHARACTER_HIB_IcePrisonWarden1_3,0,"");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner2",CHARACTER_HIB_IcePrisonWarden2_1,1,"HIB_IcePrisoner2_Warden");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner2",CHARACTER_HIB_IcePrisonWarden2_2,0,"");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner2",CHARACTER_HIB_IcePrisonWarden2_3,0,"");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner3",CHARACTER_HIB_IcePrisonWarden3_1,0,"");
DB_HIB_IcePrisonWardens("HIB_IcePrisoner3",CHARACTER_HIB_IcePrisonWarden3_2,1,"HIB_IcePrisoner3_Warden");

DB_HIB_FallbackIcePrisonWardenTriggers("HIB_IcePrisoner3",TRIGGER_HIB_SnowMenFallbackTrigger3);
DB_HIB_FallbackIcePrisonWardenTriggers("HIB_IcePrisoner2",TRIGGER_HIB_SnowMenFallbackTrigger2);
DB_HIB_FallbackIcePrisonWardenTriggers("HIB_IcePrisoner1",TRIGGER_HIB_SnowMenFallbackTrigger1);

PROC_HIB_Init_IcePrisoners();
PROC_HIB_Init_IcePrisonWardens();

DB_HIBIcePrisonerDeathpdates(CHARACTER_HIB_IcePrisoner1,"ArrokaDeath");
DB_HIBIcePrisonerDeathpdates(CHARACTER_HIB_IcePrisoner2,"AntzigarDeath");
DB_HIBIcePrisonerDeathpdates(CHARACTER_HIB_IcePrisoner3,"HaizeaDeath");

DB_HIBIcePrisonerKilledUpdates(CHARACTER_HIB_IcePrisoner1,"ArrokaKilled");
DB_HIBIcePrisonerKilledUpdates(CHARACTER_HIB_IcePrisoner2,"AntzigarKilled");
DB_HIBIcePrisonerKilledUpdates(CHARACTER_HIB_IcePrisoner3,"HaizeaKilled");
KBSECTION
//REGION ICE PRISONS
PROC
PROC_HIB_Init_IcePrisoners()
AND
DB_HIB_IcePrisons(_Prisoner,_,_,_)
THEN
CharacterApplyStatus(_Prisoner,"FROZEN",-1.0,1);
IsStoryNpc(_Prisoner);
SetStoryNpcStatus(_Prisoner);
DB_IsNotMessingAround(_Prisoner);
DB_NoInitialTrade(_Prisoner);

PROC
PROC_HIB_Init_IcePrisonWardens()
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,_,_)
THEN
CharacterSetOnStage(_Warden,0);
KillCounterDB(_Warden,_ID);
DB_IsNotMessingAround(_Warden);
DB_NoInitialTrade(_Warden);

IF
ItemDestroyed(_IcePrison)
AND
DB_HIB_IcePrisons(_Prisoner,_IcePrison,_ID,_)
THEN
PROC_HIB_Free_IcePrisoner(_Prisoner);
PROC_HIB_Spawn_IcePrisonWardens(_ID);
TimerLaunch(_ID,2000);

PROC
PROC_HIB_Free_IcePrisoner((CHARACTER)_Prisoner)
THEN
QuestAdd("HIB_CrystalPrisoners");
DB_HIB_Freed(_Prisoner);
QuestUpdate("HIB_CrystalPrisoners","Start");
CharacterRemoveStatus(_Prisoner,"FROZEN");
NOT IsStoryNpc(_Prisoner);
SetStoryNpcStatus(_Prisoner);

IF
CharacterStatusRemoved(_Prisoner, "FROZEN")
AND
DB_HIB_IcePrisons(_Prisoner,_,_,_)
AND
NOT DB_HIB_Freed(_Prisoner)
THEN
CharacterApplyStatus(_Prisoner, "FROZEN", -1.0, 1);

PROC
PROC_HIB_Spawn_IcePrisonWardens((STRING)_ID)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,_,_)
THEN
CharacterAppear(_Warden,1);

IF
TimerFinished(_ID)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,_,_)
AND
DB_CurrentLevel("ThePrisonGardenOfStoneKing")
THEN
PROC_HIB_Talk_IcePrisonWardens(_Warden);

IF
TimerFinished(_ID)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,1,_Dialog)
AND
NOT DB_CurrentLevel("ThePrisonGardenOfStoneKing")
AND
DB_HIB_FallbackIcePrisonWardenTriggers(_ID,_Trigger)
THEN
DB_OneShot_DialogTrigger_NewSystem(_Trigger,_Dialog,_Warden);

IF
CharacterAttackedByCharacter(_SomeWarden,_)
AND
DB_HIB_IcePrisonWardens(_ID,_SomeWarden,_,_)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,1,_)
AND
DB_HIB_WardenDialogQueued(_Warden,_Dialog)
AND
DB_HIB_IcePrisons(_,_,_ID,_CounterAmount)
AND
DB_OneShot_DialogTrigger_NewSystem(_Trigger,_GroupID,_Warden)
THEN
PROC_HIB_Hostile_IcePrisonWardens(_ID);
KillCounter(_ID,_CounterAmount);
NOT DB_OneShot_DialogTrigger_NewSystem(_Trigger,_GroupID,_Warden);

PROC
PROC_HIB_Talk_IcePrisonWardens((CHARACTER)_Warden)
AND
DB_HIB_IcePrisonWardens(_,_Warden,1,_Dialog)
AND
CharacterGetClosestAlivePlayer(_Warden,_Player)
THEN
ProcHIBStartWardenDialogWithPlayer(_Warden,_Player,_Dialog);

PROC
ProcHIBStartWardenDialogWithPlayer((CHARACTER)_Warden,(CHARACTER)_Player,(STRING)_Dialog)
AND
NOT _Player.DB_IsInDialog()
THEN
DB_HIBHandledWardenDialog(1);
PROC_MendatoryDialog(_Dialog,_Warden,_Player);

PROC
ProcHIBStartWardenDialogWithPlayer((CHARACTER)_Warden,(CHARACTER)_Player,(STRING)_Dialog)
AND
NOT DB_HIBHandledWardenDialog(1)
AND
_Player.DB_IsInDialog()
AND
CharacterGetOtherPlayer(_Player,_Other)
AND
NOT DB_Dead(_Other)
AND
NOT _Other.DB_IsInDialog()
THEN
DB_HIBHandledWardenDialog(1);
PROC_MendatoryDialog(_Dialog,_Warden,_Other);

PROC
ProcHIBStartWardenDialogWithPlayer((CHARACTER)_Warden,(CHARACTER)_Player,(STRING)_Dialog)
AND
NOT DB_HIBHandledWardenDialog(1)
THEN
DB_HIB_WardenDialogQueued(_Warden,_Dialog);

PROC
ProcHIBStartWardenDialogWithPlayer((CHARACTER)_Warden,(CHARACTER)_Player,(STRING)_Dialog)
THEN
NOT DB_HIBHandledWardenDialog(1);

IF
DB_HIB_WardenDialogQueued(_Warden,_Dialog)
AND
DB_CurrentLevel("ThePrisonGardenOfStoneKing")
AND
_Player.DB_IsPlayer()
AND
NOT DB_Companion(_Player)
AND
NOT DB_Dead(_Player)
AND
NOT _Player.DB_IsInDialog()
THEN
NOT DB_HIB_WardenDialogQueued(_Warden,_Dialog);
PROC_MendatoryDialog(_Dialog,_Warden,_Player);

IF
CharacterAttackedByCharacter(_SomeWarden,_)
AND
DB_HIB_IcePrisonWardens(_ID,_SomeWarden,_,_)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,1,_)
AND
DB_HIB_WardenDialogQueued(_Warden,_Dialog)
AND
DB_HIB_IcePrisons(_,_,_ID,_CounterAmount)
THEN
PROC_HIB_Hostile_IcePrisonWardens(_ID);
KillCounter(_ID,_CounterAmount);
NOT DB_HIB_WardenDialogQueued(_Warden,_Dialog);

IF
DialogEnded("HIB_IcePrisoner2_Warden",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_IcePrisonWarden2_1,"HIB_WardenGoHostile",1)
AND
CharacterConsume(CHARACTER_HIB_IcePrisoner2, "Quest_PotionFreezeImmunity", _)
THEN
NOP(1);

IF
DialogEnded(_Dialog,_)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,1,_Dialog)
AND
CharacterGetVarInteger(_Warden,"HIB_WardenGoHostile",1)
AND
DB_HIB_IcePrisons(_,_,_ID,_CounterAmount)
THEN
PROC_HIB_Hostile_IcePrisonWardens(_ID);
KillCounter(_ID,_CounterAmount);
ProcHIBSetHostileWardenUpdate(_ID);
ProcHIBPrisonerNeedsHelp(_ID);

PROC
ProcHIBPrisonerNeedsHelp((STRING)_ID)
AND
DB_HIB_IcePrisons(_Prisoner,_,_ID,_)
THEN
_Prisoner.HIBNeedsHelp();

PROC
ProcHIBSetHostileWardenUpdate((STRING)_ID)
AND
DB_HostileWardenQuestLogs(_ID,_Update)
THEN
QuestUpdate("HIB_CrystalPrisoners",_Update);

PROC
PROC_HIB_Hostile_IcePrisonWardens((STRING)_ID)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,_,_)
THEN
CharacterSetFaction(_Warden,"Evil NPC");

IF
DialogEnded(_Dialog,_)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,1,_Dialog)
AND
DB_HIB_IcePrisons(_Prisoner,_,_ID,_)
AND
CharacterGetVarInteger(_Warden,"HIB_PrisonerGoHostile",1)
THEN
CharacterSetFaction(_Prisoner,"Evil NPC");
_Prisoner.HIB_IcePrisonerHostile();
ProcHIBSetFriendlyWardenUpdate(_ID);

PROC
ProcHIBSetFriendlyWardenUpdate((STRING)_ID)
AND
DB_FriendlyWardenQuestLogs(_ID,_Update)
THEN
QuestUpdate("HIB_CrystalPrisoners",_Update);

PROC
ReactOnKillCounter(_KillCounter)
AND
DB_HIB_IcePrisons(_Prisoner,_,_KillCounter,_)
AND
NOT DB_Dead(_Prisoner)
THEN
NOT _Prisoner.HIBNeedsHelp();
DB_Dialogs(_Prisoner,_KillCounter);
ProcHIBStartPrisonerDialog(_Prisoner,_KillCounter);

PROC
ProcHIBStartPrisonerDialog((CHARACTER)_Prisoner,(STRING)_KillCounter)
AND
CharacterGetClosestAlivePlayer(_Prisoner,_Player)
AND
CharacterGetDistanceToCharacter(_Prisoner,_Player,_Dist)
AND
_Dist<10.0
THEN
PROC_MendatoryDialog(_KillCounter,_Prisoner,_Player);


IF
CharacterDied(_Prisoner)
AND
_Prisoner.HIB_IcePrisonerHostile()
AND
DB_HIB_IcePrisons(_Prisoner,_,_ID,_)
AND
DB_HIBIcePrisonerKilledUpdates(_Prisoner,_Upd)
THEN
NOT DB_HIBIcePrisonerKilledUpdates(_Prisoner,_Upd);
QuestUpdate("HIB_CrystalPrisoners",_Upd);

IF
CharacterDied(_Prisoner)
AND
_Prisoner.HIB_IcePrisonerHostile()
AND
DB_HIB_IcePrisons(_Prisoner,_,_ID,_)
AND
DB_HIB_IcePrisonWardens(_ID,_Warden,_,_)
THEN
_Warden.NeedsToVanish();
CharacterPlayAnimation(_Warden,"Burrow","HIB_WardenDisappears");
NOT DB_IcePrisoners(_Prisoner);

IF
CharacterDied(_Prisoner)
AND
DB_HIB_IcePrisons(_Prisoner,_,_,_)
AND
_Prisoner.HIBNeedsHelp()
AND
DB_HIBIcePrisonerDeathpdates(_Prisoner,_Upd)
THEN
NOT DB_HIBIcePrisonerDeathpdates(_Prisoner,_Upd);
QuestUpdate("HIB_CrystalPrisoners",_Upd);
NOT _Prisoner.HIBNeedsHelp();
NOT DB_IcePrisoners(_Prisoner);
ProcHIBCheckIcePrisonerQuestClose();

IF
CharacterEvent(_Warden,"HIB_WardenDisappears")
THEN
NOT _Warden.NeedsToVanish();
CharacterSetOnStage(_Warden,0);
ProcHIBCheckIfCanCloseWardenQuest();

PROC
ProcHIBCheckIfCanCloseWardenQuest()
AND
NOT NeedsToVanish(_)
THEN
ProcHIBCheckIcePrisonerQuestClose();


//END_REGION

//REGION ICE PRISONER 1
IF
DialogEnded("HIB_IcePrisoner1",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_IcePrisoner1,"HIB_IcePrisoner1Leaves",1)
THEN
CharacterSetVarInteger(CHARACTER_HIB_IcePrisoner1,"HIB_IcePrisoner1Leaves",0);
CharacterSetHasDialog(CHARACTER_HIB_IcePrisoner1,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_HIB_IcePrisoner1,"HIB_IcePrisoner1");
CharacterDisappearOutOfSight(CHARACTER_HIB_IcePrisoner1,0,1,"",1);
NOT DB_IcePrisoners(CHARACTER_HIB_IcePrisoner1);
ProcHIBCheckIcePrisonerQuestClose();
//END_REGION

//REGION ICE PRISONER 2
IF
DialogEnded("HIB_IcePrisoner2",_)
THEN
CharacterSetHasDialog(CHARACTER_HIB_IcePrisoner2,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_HIB_IcePrisoner2,"HIB_IcePrisoner2");
CharacterDisappearOutOfSight(CHARACTER_HIB_IcePrisoner2,0,1,"",1);
NOT DB_IcePrisoners(CHARACTER_HIB_IcePrisoner2);
ProcHIBCheckIcePrisonerQuestClose();
//END_REGION

//REGION ICE PRISONER 3
IF
DialogEnded("HIB_IcePrisoner3_Warden",_)
THEN
CharacterDisplayText(CHARACTER_HIB_IcePrisoner3,"HIB_Haizea_WarCry");
QuestUpdate("HIB_CrystalPrisoners","HaizeaSide");

IF
DialogStarted("HIB_IcePrisoner3",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
AND
ItemIsInPartyInventory(ITEM_LUC_WitchCave_LakeSpell,_Player,0,1)
THEN
GlobalSetEvent("HIB_HasRiftSpell");

IF
DialogEnded("HIB_IcePrisoner3",_)
THEN
GlobalClearEvent("HIB_HasRiftSpell");

IF
DialogCharacterEventSet("HIB_TakeRiftSpell",_Player,_)
AND
ItemIsInPartyInventory(ITEM_LUC_WitchCave_LakeSpell,_Player,1,1)
THEN
ItemToCharacter(ITEM_LUC_WitchCave_LakeSpell,CHARACTER_HIB_IcePrisoner3);

IF
DialogEnded("HIB_IcePrisoner3",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_IcePrisoner3,"HIB_IcePrisoner3Leaves",1)
THEN
CharacterSetVarInteger(CHARACTER_HIB_IcePrisoner3,"HIB_IcePrisoner3Leaves",0);
CharacterSetHasDialog(CHARACTER_HIB_IcePrisoner3,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_HIB_IcePrisoner3,"HIB_IcePrisoner3");
CharacterDisappearOutOfSight(CHARACTER_HIB_IcePrisoner3,0,1,"",1);
NOT DB_IcePrisoners(CHARACTER_HIB_IcePrisoner3);
ProcHIBCheckIcePrisonerQuestClose();

IF
DialogEnded("HIB_IcePrisoner3",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_IcePrisoner3,"HIB_IcePrisoner3Attacks",1)
THEN
CharacterSetFaction(CHARACTER_HIB_IcePrisoner3,"Evil NPC");
QuestUpdate("HIB_CrystalPrisoners","HaizeaKeepRift");
NOT DB_IcePrisoners(CHARACTER_HIB_IcePrisoner3);
ProcHIBCheckIcePrisonerQuestClose();

//END_REGION

IF
GlobalEventSet("CIRWon_IcePrisoner1")
THEN
QuestUpdate("HIB_CrystalPrisoners","ArrokaPrisonWay");

IF
GlobalEventSet("HIB_IcePrisoner2ToldTreasure")
THEN
QuestUpdate("HIB_CrystalPrisoners","AntzigarTreasure");

PROC
ProcHIBCheckIcePrisonerQuestClose()
AND
NOT DB_IcePrisoners(_)
THEN
QuestClose("HIB_CrystalPrisoners");
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
