Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_OneShotPlayerTrigger(TRIGGER_DAF_Knight_Guard_AD);
SneakTriggerSpotter(TRIGGER_DFD_KT_Statue_Dialog,CHARACTER_DFD_KT_SpeakerStatue);
DB_OneShot_DialogTrigger_NewSystem(TRIGGER_DAF_Knight_Guard_Spotted,"DAF_Knight_Guard",CHARACTER_DAF_Knight_Guard1);
DB_Dialogs(CHARACTER_DAF_Knight_Guard1,"DAF_Knight_Guard");
DB_Dialogs(CHARACTER_DFD_KT_SpeakerStatue,"DFD_KT_Statues");
DB_Dialogs(CHARACTER_DFD_KT_Watcher_Male,CHARACTER_DFD_KT_Watcher_Female,"DFD_KT_Watchers");
ProcTriggerRegisterForPlayers(TRIGGER_DFD_KT_HeatMaze);
DB_OneShotPlayerTrigger(TRIGGER_DFD_KT_Watchers);

ItemSetOnstage(ITEM_DFD_KT_Tomb_Open,0);
ItemSetOnstage(ITEM_DFD_KT_Starstone,0);

DB_DAF_Knight_Guard(CHARACTER_DAF_Knight_Guard1);
DB_DAF_Knight_Guard(CHARACTER_DAF_Knight_Guard2);
DB_DAF_Knight_Guard(CHARACTER_DAF_Knight_Guard3);
DB_DAF_Knight_Guard(CHARACTER_DAF_Knight_Guard4);
DB_DAF_Knight_Guard(CHARACTER_DAF_Knight_Guard5);
SneakTriggerSpotter(TRIGGER_DAF_Knight_Guard_Spotted,CHARACTER_DAF_Knight_Guard1);
SneakTriggerSpotter(TRIGGER_DAF_Knight_Guard_Spotted,CHARACTER_DAF_Knight_Guard2);
SneakTriggerSpotter(TRIGGER_DAF_Knight_Guard_Spotted,CHARACTER_DAF_Knight_Guard3);
SneakTriggerSpotter(TRIGGER_DAF_Knight_Guard_Spotted_Combat,CHARACTER_DAF_Knight_Guard4);
SneakTriggerSpotter(TRIGGER_DAF_Knight_Guard_Spotted_Combat,CHARACTER_DAF_Knight_Guard5);

DB_KnightsTomb_Lever(ITEM_DAF_Knights_Tomb_Switch1,ITEM_DAF_Knights_Orb_Red1,ITEM_DAF_Knights_Orb_Red4,ITEM_DAF_Knights_Orb_Green1,ITEM_DAF_Knights_Orb_Green4);
DB_KnightsTomb_Lever(ITEM_DAF_Knights_Tomb_Switch2,ITEM_DAF_Knights_Orb_Red2,ITEM_DAF_Knights_Orb_Red5,ITEM_DAF_Knights_Orb_Green2,ITEM_DAF_Knights_Orb_Green5);
DB_KnightsTomb_Lever(ITEM_DAF_Knights_Tomb_Switch3,ITEM_DAF_Knights_Orb_Red3,ITEM_DAF_Knights_Orb_Red6,ITEM_DAF_Knights_Orb_Green3,ITEM_DAF_Knights_Orb_Green6);

DB_KnightsTomb_Titan(CHARACTER_DFD_KT_Titan1,ITEM_DFD_KT_Titan_Statue1,1,"Appear");
DB_KnightsTomb_Titan(CHARACTER_DFD_KT_Titan2,ITEM_DFD_KT_Titan_Statue2,600,"CommentedOut");
DB_KnightsTomb_Titan(CHARACTER_DFD_KT_Titan3,ITEM_DFD_KT_Titan_Statue3,250,"Appear");
DB_KnightsTomb_Titan(CHARACTER_DFD_KT_Titan4,ITEM_DFD_KT_Titan_Statue4,400,"Appear");
DB_KnightsTomb_Titan(CHARACTER_DFD_KT_Titan5,ITEM_DFD_KT_Titan_Statue5,800,"Appear");
DB_KnightsTomb_Titan(CHARACTER_DFD_KT_Titan6,ITEM_DFD_KT_Titan_Statue6,1250,"CommentedOut");

DB_KnightsTomb_Warm_Trigger(TRIGGER_DFD_KT_Path_Warm1);

DB_KnightsTomb_Cold_Trigger(TRIGGER_DFD_KT_Path_Cold1);
DB_KnightsTomb_Cold_Trigger(TRIGGER_DFD_KT_Path_Cold2);
DB_KnightsTomb_Cold_Trigger(TRIGGER_DFD_KT_Path_Cold3);

DB_KnightsTomb_Kill_Trigger(TRIGGER_DFD_KT_Path_Death1);
DB_KnightsTomb_Kill_Trigger(TRIGGER_DFD_KT_Path_Death2);
DB_KnightsTomb_Kill_Trigger(TRIGGER_DFD_KT_Path_Death3);
DB_KnightsTomb_Kill_Trigger(TRIGGER_DFD_KT_Path_Death4);

DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch1,ITEM_DFD_KT_Coal_Basket1,ITEM_DFD_KT_Coal_Basket2);
DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch2,ITEM_DFD_KT_Coal_Basket7,ITEM_DFD_KT_Coal_Basket10);
DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch3,ITEM_DFD_KT_Coal_Basket3,ITEM_DFD_KT_Coal_Basket5);
DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch4,ITEM_DFD_KT_Coal_Basket4,ITEM_DFD_KT_Coal_Basket6);
DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch5,ITEM_DFD_KT_Coal_Basket8,ITEM_DFD_KT_Coal_Basket9);
DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch5,ITEM_DFD_KT_Coal_Basket11,ITEM_DFD_KT_Coal_Basket12);
DB_KnightsTomb_TorchTrigger(TRIGGER_DFD_KT_Torch5,ITEM_DFD_KT_Coal_Basket13,ITEM_DFD_KT_Coal_Basket14);

ItemSetOnstage(ITEM_DFD_KT_Statbook,0);

ProcDisablePlayerTrade(CHARACTER_DFD_KT_SpeakerStatue);
KBSECTION
//REGION KT Titan Sign
IF
CharacterUsedItem(_Character,ITEM_DAF_TitanPlaque)
AND
NOT DB_GlobalEvent("UnderstandTitan")
THEN
ItemDisplayText(ITEM_DAF_TitanPlaque,"DAF_TitanPlaqueNoDic");

IF
CharacterUsedItem(_Character,ITEM_DAF_TitanPlaque)
AND
DB_GlobalEvent("UnderstandTitan")
THEN
ItemDisplayText(ITEM_DAF_TitanPlaque,"DAF_TitanPlaqueDic");

//END_REGION


//REGION KT Orc Guards

PROC

ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DAF_Knight_Guard_AD)

THEN

DialogStartTwoSpeakerDialog("DAF_AD_Knight_Guard",CHARACTER_DAF_Knight_Guard1,CHARACTER_DAF_Knight_Guard2);



PROC

ProcCharInTriggerSpotted((CHARACTER)_Player,TRIGGER_DAF_Knight_Guard_Spotted_Combat)

AND

DB_DAF_Knight_Guard(_Character)

AND

NOT DB_GlobalEvent("CIRWon_KnightGuard")

THEN

DialogRequestStopForCharacter(CHARACTER_DAF_Knight_Guard1);

ProcSetRelationToPlayers(_Character,0);



IF

DialogEnded("DAF_Knight_Guard",_)

AND

DB_GlobalEvent("DAF_Knight_Guard_Fight")

AND

DB_DAF_Knight_Guard(_Character)

THEN

ProcSetRelationToPlayers(_Character,0);

//END_REGION



//REGION KT Gate

IF

CharacterUsedItem(_,_Item)

AND

DB_KnightsTomb_Lever(_Item,_Orb_Red_Right,_Orb_Red_Left,_Orb_Green_Right,_Orb_Green_Left)

AND

ItemGetPosition(_Orb_Red_Right,_XR,_YR,_ZR)

AND

ItemGetPosition(_Orb_Red_Left,_XL,_YL,_ZL)

THEN

ItemSetCanInteract(_Item,0);

ItemSetOnstage(_Orb_Red_Right,0);

ItemSetOnstage(_Orb_Red_Left,0);

ItemTeleportToPosition(_Orb_Green_Right,_XR,_YR,_ZR);

ItemTeleportToPosition(_Orb_Green_Left,_XL,_YL,_ZL);

NOT DB_KnightsTomb_Lever(_Item,_Orb_Red_Right,_Orb_Red_Left,_Orb_Green_Right,_Orb_Green_Left);

ProcTestKnightsTombDoor();



PROC

ProcTestKnightsTombDoor()

AND

NOT DB_KnightsTomb_Lever(_,_,_,_,_)

THEN

ItemUnlockAndOpen(ITEM_DAF_KnightsTomb_Gate);

//END_REGION



//REGION KT Torches

IF

DB_KnightsTomb_TorchTrigger(_Trigger,_,_)

THEN

ProcTriggerRegisterForPlayers(_Trigger);



IF

CharacterEnteredTrigger(_,_Trigger)

AND

DB_KnightsTomb_TorchTrigger(_Trigger,_Torch,_Torch2)

AND

_Player.DB_IsPlayer()

THEN

ItemApplyStatus(_Torch,"BURNING",-1.0);

ItemApplyStatus(_Torch2,"BURNING",-1.0);

CharacterPlayEffect(_Player,"FX_GP_CameraShake_A");

NOT DB_KnightsTomb_TorchTrigger(_Trigger,_Torch,_Torch2);

//END_REGION



//REGION KT Titan Fight

PROC

ProcCharInTriggerSpotted((CHARACTER)_Player,TRIGGER_DFD_KT_Statue_Dialog)

THEN


ProcDoTwoSpeakerDialog("DFD_KT_Statues", CHARACTER_DFD_KT_SpeakerStatue, _Player);



IF
TextEventSet("titan")
THEN
ItemToCharacter(ITEM_DAF_TitanDictionary,CHARACTER_Player1);

IF
DialogEnded("DFD_KT_Statues",_)
THEN
QuestUpdate("DAF_War", "WatchfulGuardian");

IF
DialogEnded("DFD_KT_Statues",_)
AND
DB_GlobalEvent("DFD_KT_Statue_Fight")
THEN
Proc_KnightsTombTitansFight();
//ProcSetRelationToPlayers(CHARACTER_DFD_KT_SpeakerStatue,0);
CharacterDie(CHARACTER_DFD_KT_SpeakerStatue,0);

IF
DB_KnightsTomb_Titan(_Character,_,_,_)
THEN
CharacterSetOnStage(_Character,0);
CharacterApplyStatus(_Character,"UNSHEATHED",-1.0,1);

PROC
Proc_KnightsTombTitansFight()
AND
DB_KnightsTomb_Titan(_Character,_Item,_Time,_)
THEN
CharTimer(_Character,"KnightsTomb_Titan_cometolife",_Time);

IF
CharTimerFinished(_Character,"KnightsTomb_Titan_cometolife")
AND
DB_KnightsTomb_Titan(_Character,_Item,_,"Appear")
AND
ItemGetPosition(_Item,_X,_Y,_Z)
THEN
CharacterAppearAtPosition(_Character,_X,_Y,_Z,1);
ItemTimer(_Item,"KnightsTomb_TitanGoOffStage",100);

IF
ItemTimerFinished(_Item,"KnightsTomb_TitanGoOffStage")
THEN
ItemSetOnstage(_Item,0);
//END_REGION

//REGION KT Invisible maze
IF
DB_KnightsTomb_Warm_Trigger(_Trigger)
THEN
ProcTriggerRegisterForPlayers(_Trigger);

IF
DB_KnightsTomb_Cold_Trigger(_Trigger)
THEN
ProcTriggerRegisterForPlayers(_Trigger);

IF
DB_KnightsTomb_Kill_Trigger(_Trigger)
THEN
ProcTriggerRegisterForPlayers(_Trigger);

IF
CharacterEnteredTrigger(_Character,_Trigger)
AND
DB_KnightsTomb_Warm_Trigger(_Trigger)
AND
CharacterHasStatus(_Character,"WARM",0)
THEN
CharacterRemoveStatus(_Character,"CHILLED");
CharacterApplyStatus(_Character,"WARM",-1.0,1);

IF
CharacterLeftTrigger(_Character,_Trigger)
AND
DB_KnightsTomb_Warm_Trigger(_Trigger)
THEN
CharacterRemoveStatus(_Character,"WARM");

IF
CharacterLeftTrigger(_Character,_Trigger)
AND
DB_KnightsTomb_Warm_Trigger(_Trigger)
AND
DB_InRegion(_Character,TRIGGER_DFD_KT_HeatMaze)
AND
CharacterHasStatus(_Character,"CHILLED",0)
THEN
CharacterApplyStatus(_Character,"CHILLED",-1.0,1);

IF
CharacterEnteredTrigger(_Character,_Trigger)
AND
DB_KnightsTomb_Kill_Trigger(_Trigger)
THEN
CharacterPlayEffect(_Character,"FX_Skills_Air_Storm_Impact_A");
CharacterDie(_Character,0);

IF
CharacterLeftTrigger(_Character,TRIGGER_DFD_KT_HeatMaze)
THEN
CharacterRemoveStatus(_Character,"WARM");
CharacterRemoveStatus(_Character,"CHILLED");
//END_REGION

//REGION KT Tomb
IF
CharacterUsedItem(_,ITEM_DFD_KT_Tomb_Closed)
THEN
ItemSetOnstage(ITEM_DFD_KT_Tomb_Closed,0);
ItemSetOnstage(ITEM_DFD_KT_Tomb_Open,1);
ItemSetOnstage(ITEM_DFD_KT_Starstone,1);

IF
TextEventSet("watchers")
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_DFD_KT_Watcher_Male,"DFD_KT_Watchers");
ProcRemoveDialogEntryForSpeaker(CHARACTER_DFD_KT_Watcher_Female,"DFD_KT_Watchers");
CharacterSetHasDialog(CHARACTER_DFD_KT_Watcher_Male,0);
CharacterSetHasDialog(CHARACTER_DFD_KT_Watcher_Female,0);
CharacterTeleportToTrigger(CHARACTER_DFD_KT_Watcher_Male,TRIGGER_HOM_Watcher1,"");
CharacterTeleportToTrigger(CHARACTER_DFD_KT_Watcher_Female,TRIGGER_HOM_Watcher2,"");
DB_OneShotPlayerTrigger(TRIGGER_HOM_Watchers);
Proc_PrepHOMWatcherScene();

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DFD_KT_Watchers)
THEN
DialogStartTwoSpeakerDialog("DFD_KT_AD_Watchers", CHARACTER_DFD_KT_Watcher_Male,CHARACTER_DFD_KT_Watcher_Female);

IF
DialogEnded("DFD_KT_Watchers",_)
AND
DB_GlobalEvent("DFD_KT_Watchers_Homestead")
AND
CharacterGetPosition(CHARACTER_DFD_KT_Watcher_Male,_X,_Y,_Z)
AND
CharacterGetPosition(CHARACTER_DFD_KT_Watcher_Female,_X2,_Y2,_Z2)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_DFD_KT_Watcher_Male,"DFD_KT_Watchers");
ProcRemoveDialogEntryForSpeaker(CHARACTER_DFD_KT_Watcher_Female,"DFD_KT_Watchers");
CharacterSetHasDialog(CHARACTER_DFD_KT_Watcher_Male,0);
CharacterSetHasDialog(CHARACTER_DFD_KT_Watcher_Female,0);
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X2,_Y2,_Z2);
CharacterTeleportToTrigger(CHARACTER_DFD_KT_Watcher_Male,TRIGGER_HOM_Watcher1,"");
CharacterTeleportToTrigger(CHARACTER_DFD_KT_Watcher_Female,TRIGGER_HOM_Watcher2,"");
DB_OneShotPlayerTrigger(TRIGGER_HOM_Watchers);
Proc_PrepHOMWatcherScene();

PROC
Proc_PrepHOMWatcherScene()
AND
DB_HomesteadImps(_Character,_,_,_Trigger)
THEN
CharacterLookFromTrigger(_Character,_Trigger,1);
CharacterSetReactionPriority(_Character, "Cutscene", 50000);
CharacterSetHasDialog(_Character,0);
CharacterTeleportToTrigger(_Character,_Trigger,"");

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_HOM_Watchers)
THEN
QuestUpdate("DAF_Watchers","Homestead2");
QuestClose("DAF_Watchers");
DialogStartTwoSpeakerDialog("HOM_AD_Watchers",CHARACTER_DFD_KT_Watcher_Female,CHARACTER_HOM_RedImpLeader);

IF
AutomatedDialogEnded("HOM_AD_Watchers",_)
AND
CharacterPlayLoopEffect(CHARACTER_DFD_KT_Watcher_Female,"FX_Skills_Air_FeatherFall_Prepare_A","",_FXHandle)
THEN
CharacterPlayAnimation(CHARACTER_DFD_KT_Watcher_Female,"Cast_Ritual_01","animdone");
DFD_KT_Watcher_FX(_FXHandle);

IF
CharacterEvent(CHARACTER_DFD_KT_Watcher_Female,"animdone")
AND
DFD_KT_Watcher_FX(_FXHandle)
THEN
StopLoopEffect(_FXHandle);
NOT DFD_KT_Watcher_FX(_FXHandle);
Poof(CHARACTER_DFD_KT_Watcher_Female);
Poof(CHARACTER_DFD_KT_Watcher_Male);
ProcDAFRemoveRedImps();
GlobalSetEvent("HOM_RedImpsGone");

PROC
ProcDAFRemoveRedImps()
AND
DB_HomesteadImps(_Char,_,_,_)
THEN
Poof(_Char);

IF
DialogEnded("DFD_KT_Watchers",_)
AND
DB_GlobalEvent("DFD_KT_Watchers_Cyseal")
AND
CharacterGetPosition(CHARACTER_DFD_KT_Watcher_Male,_X,_Y,_Z)
AND
CharacterGetPosition(CHARACTER_DFD_KT_Watcher_Female,_X2,_Y2,_Z2)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_DFD_KT_Watcher_Male,"DFD_KT_Watchers");
ProcRemoveDialogEntryForSpeaker(CHARACTER_DFD_KT_Watcher_Female,"DFD_KT_Watchers");
CharacterSetHasDialog(CHARACTER_DFD_KT_Watcher_Male,0);
CharacterSetHasDialog(CHARACTER_DFD_KT_Watcher_Female,0);
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X2,_Y2,_Z2);
CharacterTeleportToTrigger(CHARACTER_DFD_KT_Watcher_Male,TRIGGER_CYS_Watcher1,"");
CharacterTeleportToTrigger(CHARACTER_DFD_KT_Watcher_Female,TRIGGER_CYS_Watcher2,"");
DB_OneShotPlayerTrigger(TRIGGER_CYS_Watchers);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_CYS_Watchers)
THEN
DialogStartTwoSpeakerDialog("CYS_AD_Watchers",CHARACTER_DFD_KT_Watcher_Male,CHARACTER_DFD_KT_Watcher_Female);
QuestUpdate("DAF_Watchers","Cyseal2");
QuestClose("DAF_Watchers");

IF
AutomatedDialogEnded("CYS_AD_Watchers",_)
THEN
Poof(CHARACTER_DFD_KT_Watcher_Female);
Poof(CHARACTER_DFD_KT_Watcher_Male);

IF
DialogCharacterEventSet("GiveWatcherStatBook",_Player,_)
THEN
ItemSetOnstage(ITEM_DFD_KT_Statbook,1);
ItemToCharacter(ITEM_DFD_KT_Statbook,_Player);

IF
CharacterUsedItem(_Character,ITEM_DFD_KT_Statbook)
THEN
CharacterAddAttributePoint(_Character,1);
ItemToCharacter(ITEM_DFD_KT_Statbook,CHARACTER_LUC_PickAxeHolderHack);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
