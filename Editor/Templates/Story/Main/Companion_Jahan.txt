Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_Jahan,"CYS_Jahan");
DB_DismissEvent(CHARACTER_Jahan,"JahanDismissed");
DB_CustomWarningDialog(CHARACTER_Jahan,"Jahan_please_Dont_attack");
DB_AttackDialog(CHARACTER_Jahan,"Companion_Jahan_Attacks");
DB_NoRunAway(CHARACTER_Jahan);
ProcTriggerRegisterForPlayers(TRIGGER_LUC_JahanSeesDeadAnimals);
TriggerRegisterForCharacter(TRIGGER_DAF_JAH_Truth,CHARACTER_Jahan);
TriggerRegisterForCharacter(TRIGGER_LUC_Jahan_CC_Desert,CHARACTER_Jahan);

DB_JahanExitsDesert_Trigger(TRIGGER_LUC_JAH_CC_DesertExit1);
DB_JahanExitsDesert_Trigger(TRIGGER_LUC_JAH_CC_DesertExit2);
DB_JahanExitsDesert_Trigger(TRIGGER_LUC_JAH_CC_DesertExit3);
DB_JahanExitsDesert_Trigger(TRIGGER_LUC_JAH_CC_DesertExit4);

DB_JAH_Disagree(0);

DB_JAH_WastingTimeCounter(0);

DB_DAF_JAH_CC_Demons1(CHARACTER_DAF_ImmaFort_DemonBoss);
//DB_DAF_JAH_CC_Demons1(CHARACTER_DAF_ImmaFort_Demon_01);
//DB_DAF_JAH_CC_Demons1(CHARACTER_DAF_ImmaFort_Demon_02);
//DB_DAF_JAH_CC_Demons1(CHARACTER_DAF_ImmaFort_Demon_03);
//DB_DAF_JAH_CC_Demons1(CHARACTER_DAF_ImmaFort_Demon_04);

DB_DAF_JAH_CC_Demons2(CHARACTER_DAF_DarkMarsh_MudDemon_01);
DB_DAF_JAH_CC_Demons2(CHARACTER_DAF_DarkMarsh_MudDemon_02);
DB_DAF_JAH_CC_Demons2(CHARACTER_DAF_DarkMarsh_MudDemon_03);
DB_DAF_JAH_CC_Demons2(CHARACTER_DAF_DarkMarsh_MudDemon_04);
DB_DAF_JAH_CC_Demons2(CHARACTER_DAF_DarkMarsh_MudDemon_05);
KBSECTION
//REGION Party join/leave
IF
DialogCharacterEventSet("Jahan_Recruited",_Player,_)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_Jahan,"CYS_Jahan");
DB_Dialogs(CHARACTER_Jahan,"JAH_Default");
DB_CompanionRecruited(CHARACTER_Jahan);
CharacterMakeCompanion(CHARACTER_Jahan,_Player);
Proc_LimitAbilitiesForCompanion(CHARACTER_Jahan);
CharacterSetEvent(CHARACTER_Jahan,"Hired");
DB_Companion(CHARACTER_Jahan);
ProcSetPartyFullIfLoneWolf();

IF
DialogEnded("JAH_Default",_)
AND 
DB_GlobalEvent("JahanDismissed")
AND
DB_Companion(CHARACTER_Jahan)
THEN
ProcDismissJahan();

PROC
ProcDoDismissOfCompanion(CHARACTER_Jahan)
THEN
ProcDismissJahan();

PROC
ProcDismissJahan()
AND
DB_Companion(CHARACTER_Jahan)
THEN
OnCompanionDismissed(CHARACTER_Jahan);
ProcHACKFixCompanionReputation(CHARACTER_Jahan);

PROC
ProcDismissJahan()
AND
NOT DB_GlobalEvent("HallOfHeroesUnlocked")
AND
CharacterGetPosition(CHARACTER_Jahan,_X,_Y,_Z)
AND
DB_Companion(CHARACTER_Jahan)
THEN
ProcCancelAllCompanionDialogs(CHARACTER_Jahan);
NOT DB_Companion(CHARACTER_Jahan);
CharacterDetachFromGroup(CHARACTER_Jahan);
CharacterMakeNPC(CHARACTER_Jahan);
CharacterRemoveFromParty(CHARACTER_Jahan);
CharacterTeleportToTrigger(CHARACTER_Jahan,TRIGGER_CYS_Tavern_Jahan,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PROC_END_RemoveCompInDBRoomB(CHARACTER_Jahan);

PROC
ProcDismissJahan()
AND
DB_GlobalEvent("HallOfHeroesUnlocked")
AND
CharacterGetPosition(CHARACTER_Jahan,_X,_Y,_Z)
AND
DB_Companion(CHARACTER_Jahan)
THEN
NOT DB_Companion(CHARACTER_Jahan);
CharacterDetachFromGroup(CHARACTER_Jahan);
CharacterMakeNPC(CHARACTER_Jahan);
CharacterRemoveFromParty(CHARACTER_Jahan);
CharacterTeleportToTrigger(CHARACTER_Jahan,TRIGGER_HOM_HoH_Jahan_Neutral,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PROC_END_RemoveCompInDBRoomB(CHARACTER_Jahan);

IF
DialogCharacterEventSet("JahanRejoins",_Player,_ID)
THEN
DB_Companion(CHARACTER_Jahan);
CharacterMakeCompanion(CHARACTER_Jahan,_Player);
DialogClearCharacterEvent("JahanRejoins",_Player,_ID);
ProcSetPartyFullIfLoneWolf();

IF
GlobalEventSet("HallOfHeroesUnlocked")
AND
DB_GlobalEvent("JahanDismissed")
THEN
CharacterTeleportToTrigger(CHARACTER_Jahan,TRIGGER_HOM_HoH_Jahan_Neutral,"");
//END_REGION

//REGION Altruism
IF
GlobalEventSet("JAH_PlusDisagree")
AND
DB_JAH_Disagree(_OldVal)
AND
IntegerSum(_OldVal,1,_NewVal)
THEN
NOT DB_JAH_Disagree(_OldVal);
DB_JAH_Disagree(_NewVal);
GlobalClearEvent("JAH_PlusDisagree");
PROC_CheckIfJahanIsDisagree(_NewVal);

IF
GlobalEventSet("JAH_MinDisagree")
AND
DB_JAH_Disagree(_OldVal)
AND
IntegerSum(_OldVal,-1,_NewVal)
THEN
NOT DB_JAH_Disagree(_OldVal);
DB_JAH_Disagree(_NewVal);
GlobalClearEvent("JAH_MinDisagree");
GlobalClearEvent("JAH_Disagree");
PROC_CheckIfJahanIsDisagree(_NewVal);

PROC
PROC_CheckIfJahanIsDisagree((INTEGER)_NewVal)
AND
_NewVal >= 0
THEN
GlobalSetEvent("JAH_Disagree");
//END_REGION

//REGION Jahan Wasting Time counter
IF
GlobalEventSet("JAH_ComplainsAboutWastingTime")
AND
DB_JAH_WastingTimeCounter(_OldVal)
AND
IntegerSum(_OldVal,1,_NewVal)
THEN
NOT DB_JAH_WastingTimeCounter(_OldVal);
DB_JAH_WastingTimeCounter(_NewVal);
GlobalClearEvent("JAH_ComplainsAboutWastingTime");

IF
GlobalEventSet("JAH_ComplainsAboutWastingTime")
AND
DB_JAH_WastingTimeCounter(3)
AND
QueryOnlyOnce("JAH_ExplainsWhyHesInAHurry")
THEN
GlobalSetEvent("JAH_ExplainsWhyHesInAHurry");
NOT DB_CompanionDialogsFinished(CHARACTER_Jahan,"JAH_Default");
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_Default",CHARACTER_Jahan);
//END_REGION

//REGION Jahan Story Party Dialogs
IF
DialogEnded("JAH_Default",_)
AND
DB_GlobalEvent("Start_JAH_PD1")
AND
NOT DB_OnlyOnce("JAH_PD_Story1")
THEN
DB_OnlyOnce("JAH_PD_Story1");
ProcDefinePartyDialog("JAH_PD_Story1");

IF
DialogEnded("JAH_Default",_)
AND
DB_GlobalEvent("Start_JAH_PD2")
AND
NOT DB_OnlyOnce("JAH_PD_Story2")
THEN
DB_OnlyOnce("JAH_PD_Story2");
ProcDefinePartyDialog("JAH_PD_Story2");

IF
DialogEnded("JAH_Default",_)
AND
DB_GlobalEvent("Start_JAH_PD3")
AND
NOT DB_OnlyOnce("JAH_PD_Story3")
THEN
DB_OnlyOnce("JAH_PD_Story3");
ProcDefinePartyDialog("JAH_PD_Story3");
//END_REGION

//REGION Companion comments & dialogs
IF
DialogEnded("CYS_Maxine",_)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Maxine",CHARACTER_CYS_Maxine);

IF
DialogEnded("CYS_SamsonGhost",_)
AND
DB_GlobalEvent("CYS_KnowsSamsonKiller")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Samson",CHARACTER_CYS_SamsonGhost);

IF
GameBookInterfaceClosed(ITEM_CYS_JakeClue3,_Player)
AND
_Player.DB_IsPlayer()
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Esmeralda",_Player);

IF
CharacterLeveledUp(_Player)
AND
_Player.DB_IsPlayer()
AND
CharacterGetLevel(_Player,_Level)
AND
_Level > 5
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_LevelUp",_Player);

IF
DialogEnded("LUC_GV_Totem", _)
AND
ItemGetClosestPlayer(ITEM_LUC_GV_Totem,_Player)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_LUC_Totem",_Player);

IF
DialogEnded("LUC_Roy",_)
AND
DB_GlobalEvent("LUC_KnowsCattleDestination")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_LUC_Roy",CHARACTER_LUC_Roy);

IF
DialogEnded("LUC_BjornMount",_)
AND
DB_GlobalEvent("LUC_KillBjornMount")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_LUC_Alfie",CHARACTER_LUC_BjornMount);

IF
DialogEnded("LUC_Bjorn",_)
AND
DB_GlobalEvent("LUC_AbandonBjornMount")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_LUC_Alfie",CHARACTER_LUC_BjornMount);

IF
DialogEnded("LUC_TrollBridge2",_)
AND
DB_GlobalEvent("LUC_RubyWithTroll")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_LUC_FumbleRuby",CHARACTER_LUC_BrokenBridgeTroll02);

IF
DialogEnded("HIB_IronMaiden",_)
AND
DB_GlobalEvent("HIB_IronMaiden")
AND
ItemGetClosestPlayer(ITEM_HIB_IronMaiden,_Player)
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_IronMaiden",_Player);

IF
CharacterDied(CHARACTER_LUC_SpiderBoss)
AND
CharacterGetClosestPlayer(CHARACTER_LUC_SpiderBoss,_Player)
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_Spiders",_Player);

IF
ItemDestroyed(ITEM_LUC_BG_BloodStone)
AND
TriggerGetClosestPlayer(TRIGGER_LUC_BG_BloodStoneComment,_Player)
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_VoidDemon",_Player);

IF
CharacterSawCharacter(CHARACTER_Jahan,CHARACTER_LUC_TrollKing)
THEN
CHARACTER_LUC_TrollKing.seen();

IF
CharacterUsedItem(_Player,ITEM_LUC_TrollCave_Exit)
AND
CHARACTER_LUC_TrollKing.seen()
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_TrollKing",_Player);

IF
CharacterEnteredTrigger(CHARACTER_Jahan,TRIGGER_LUC_JahanSeesDeadAnimals)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_LUC_WitchCave",CHARACTER_Jahan);

IF
CharacterEnteredTrigger(CHARACTER_Jahan,TRIGGER_LUC_Jahan_CC_Desert)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_LucDesertEntrance",CHARACTER_Jahan);

IF
GlobalEventSet("HIB_KilledStoneImmaculates")
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_Demons",CHARACTER_HIB_Leandra);

PROC
Launch_PlayerComment(_Player, "HIB_CommentBuildersCamp")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_HIB_Imps",_Player);

IF
CharacterEvent(CHARACTER_HIB_WhiteStag,"HIB_WhiteStagDisappears")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_HIB_Stag",CHARACTER_HIB_WhiteStag);

PROC
Launch_PlayerComment(_Player, "HIB_WolfAmbush3")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_HIB_Wolves",_Player);

PROC
Launch_PlayerComment(_Player, "HIB_CommentWhiteStag")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_HIB_Wolves",_Player);

IF
CharacterEnteredRegion(CHARACTER_Jahan,"DarkForest")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_DAFEntrance",CHARACTER_Jahan);

IF
GlobalEventSet("DAF_KnowsAboutLovers")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Lovers",CHARACTER_DAF_Orc_Lover);

IF
DialogEnded("DAF_RatCatcher", _)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Ratcatcher",CHARACTER_DAF_Ratcatcher);

IF
DialogEnded("DAF_ForestSpiritApprentice",_)
AND
DB_GlobalEvent("KnowsAboutAnguishDemon")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Balberith",CHARACTER_DAF_ForestSpiritApprentice);

IF
DialogEnded("DAF_Swampdeer",_)
AND
DB_GlobalEvent("KnowsAboutAnguishDemon")
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_Balberith",CHARACTER_DAF_Swampdeer);

IF
CharacterEnteredTrigger(CHARACTER_Jahan,TRIGGER_DAF_JAH_Truth)
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_Truth",CHARACTER_Jahan);

IF
CharacterDied(CHARACTER_DAF_AnguishDemon_Fighter)
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_DefeatedDemon",CHARACTER_DAF_AnguishDemon_Fighter);

IF
DB_JahanExitsDesert_Trigger(_Trigger)
THEN
TriggerRegisterForCharacter(_Trigger,CHARACTER_Jahan);

IF
CharacterEnteredTrigger(CHARACTER_Jahan,_Trigger)
AND
DB_JahanExitsDesert_Trigger(_Trigger)
AND
QueryOnlyOnce("JAH_AfterSandstorm")
THEN
GlobalSetEvent("JAH_AfterSandstorm");
NOT DB_CompanionDialogsFinished(CHARACTER_Jahan,"JAH_Default");
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_Default",CHARACTER_Jahan);

IF
Sees(CHARACTER_Jahan,_Demon)
AND
DB_DAF_JAH_CC_Demons1(_Demon)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_DafDemons1",CHARACTER_Jahan);

IF
Sees(CHARACTER_Jahan,_Demon)
AND
DB_DAF_JAH_CC_Demons2(_Demon)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_DafDemons2",CHARACTER_Jahan);

IF
DialogEnded("LUC_Guards_Intro",_Inst)
AND
DB_DialogNPCs(_Inst,_NPC,1)
AND
QueryOnlyOnce("JAH_HeardAboutSilverglenDisease")
THEN
GlobalSetEvent("JAH_HeardAboutSilverglenDisease");
NOT DB_CompanionDialogsFinished(CHARACTER_Jahan,"JAH_Default");
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_Default",_NPC);
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_SilverglenGuardTalk",_NPC);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
