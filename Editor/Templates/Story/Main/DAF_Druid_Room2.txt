Version 1
SubGoalCombiner SGC_AND
INITSECTION
CharacterSetOnStage(CHARACTER_DFD_Druid_Dog,0);
CharacterSetOnStage(CHARACTER_DFD_Druid_Cat,0);
CharacterSetOnStage(CHARACTER_DFD_Druid_Rat,0);
DB_Druid_Maze_Animal(CHARACTER_DFD_Druid_Chicken);

DB_DruidMaze_FoodProto(0,"Quest_Bone_A_a9c36c74-00c9-46fc-b686-ebfcae861d26",CHARACTER_DFD_Druid_Dog,-45.0);
DB_DruidMaze_FoodProto(1,"Quest_Food_Fish_A_ae611a85-2882-4bee-abeb-8b71249a11fc",CHARACTER_DFD_Druid_Cat,45.0);
DB_DruidMaze_FoodProto(2,"Quest_Food_Cheese_Piece_A_46d0e7c7-d9f0-462d-945e-cae3bcfef28d",CHARACTER_DFD_Druid_Rat,0.0);

DB_Druid_Maze_Lever(ITEM_DFD_Druid_Dog_Lever,CHARACTER_DFD_Druid_Dog);
DB_Druid_Maze_Lever(ITEM_DFD_Druid_Cat_Lever,CHARACTER_DFD_Druid_Cat);
DB_Druid_Maze_Lever(ITEM_DFD_Druid_Rat_Lever,CHARACTER_DFD_Druid_Rat);

DB_Druid_Maze_Door(ITEM_DFD_Druid_Room2_Door1,5,9,ITEM_DFD_Druid_Room2_Door1_Fire1,ITEM_DFD_Druid_Room2_Door1_Fire2);
DB_Druid_Maze_Door(ITEM_DFD_Druid_Room2_Door2,9,5,ITEM_DFD_Druid_Room2_Door2_Fire1,ITEM_DFD_Druid_Room2_Door2_Fire2);
DB_Druid_Maze_Door(ITEM_DFD_Druid_Room2_Door3,5,1,ITEM_DFD_Druid_Room2_Door3_Fire1,ITEM_DFD_Druid_Room2_Door3_Fire2);

Stack_Internal("BellegarMazeDoors","Door3");
Stack_Internal("BellegarMazeDoors","Door2");
Stack_Internal("BellegarMazeDoors","Door1");

DB_PlayerComment_Trigger(TRIGGER_DFD_Druid_Room2_Enter,"DFD_Druid_Room2_Enter");
DB_PlayerComments("DFD_Druid_Room2_Enter","DFD_Druid_Room2_Enter_1_1",1,1);
DB_PlayerComments("DFD_Druid_Room2_Enter","DFD_Druid_Room2_Enter_2_1",2,1);
DB_PlayerComments("DFD_Druid_Room2_Enter","DFD_Druid_Room2_Enter_2_2",2,2);

DB_PlayerComments("DFD_Druid_Room2_Moved","DFD_Druid_Room2_Moved_1_1",1,1);
DB_PlayerComments("DFD_Druid_Room2_Moved","DFD_Druid_Room2_Moved_2_1",2,1);
DB_PlayerComments("DFD_Druid_Room2_Moved","DFD_Druid_Room2_Moved_2_2",2,2);

CharacterSetOnStage(CHARACTER_DAF_Bellegar_Maze,0);
ItemSetOnstage(ITEM_DAF_Bellegar_GoldPile,0);
KBSECTION
IF
RegionStarted("DarkForest")
AND
DB_GlobalEvent("CYS_Bellegar_Freed")
AND
NOT DB_OnlyOnce("DAF_Bellegar_Encounter2")
THEN
DB_OnlyOnce("DAF_Bellegar_Encounter2");
ItemSetOnstage(ITEM_DAF_Bellegar_GoldPile,1);

IF
TextEventSet("unlockbellegar")
AND
NOT DB_OnlyOnce("DAF_Bellegar_Encounter2")
AND
NOT DB_Dead(CHARACTER_CYS_Bellegar)
THEN
DB_OnlyOnce("DAF_Bellegar_Encounter2");
ItemSetOnstage(ITEM_DAF_Bellegar_GoldPile,1);

IF
CharacterDied(CHARACTER_CYS_Bellegar)
AND
DB_OnlyOnce("DAF_Bellegar_Encounter2")
THEN
ItemSetOnstage(ITEM_DAF_Bellegar_GoldPile,0);
CharacterSetOnStage(CHARACTER_DAF_Bellegar_Maze,0);

IF
CharacterEnteredTrigger(_,TRIGGER_DFD_Druid_Room2_Enter)
THEN
QuestUpdate("DAF_Bellegar_Maze","Maze");

IF
CharacterUsedItem(_Character,ITEM_DAF_Bellegar_GoldPile)
AND
NOT DB_OnlyOnce("DAF_BellegarGold")
THEN
DB_OnlyOnce("DAF_BellegarGold");
ItemSetOnstage(ITEM_DAF_Bellegar_GoldPile,0);
Foop(CHARACTER_DAF_Bellegar_Maze);
PROC_MendatoryDialog("DAF_Bellegar_Maze",CHARACTER_DAF_Bellegar_Maze,_Character);

IF
DialogEnded("DAF_Bellegar_Maze",_)
THEN
ProcDoWaypointUnlock(ITEM_WAYP_Bellegar_Maze);
Poof(CHARACTER_DAF_Bellegar_Maze);
QuestAdd("DAF_Bellegar_Maze");
QuestUpdate("DAF_Bellegar_Maze","Start");

IF
RegionStarted("DAF_Druid_Dungeon")
THEN
ProcGenerateDruidMaze();

IF
RegionEnded("DAF_Druid_Dungeon")
THEN
Proc_ClearDruidMaze();

//REGION Polymorph Levers
PROC
ProcChangeDruidMazeAnimal((CHARACTER)_NewAnimal)
AND
DB_Druid_Maze_Animal(_OldAnimal)
AND
_OldAnimal != _NewAnimal
AND
CharacterGetPosition(_OldAnimal,_X,_Y,_Z)
THEN
CharacterTeleportToPosition(_NewAnimal,_X,_Y,_Z,"");
Poof(_OldAnimal);
CharacterSetOnStage(_NewAnimal,1);
NOT DB_Druid_Maze_Animal(_OldAnimal);
DB_Druid_Maze_Animal(_NewAnimal);
TimerCancel("DruidMaze");
TimerLaunch("DruidMaze",3000);

IF
CharacterUsedItem(_Character,_Lever)
AND
DB_Druid_Maze_Lever(_Lever,_Animal)
AND
NOT DB_DruidMaze_QueuePolymorphs(1)
THEN
ProcChangeDruidMazeAnimal(_Animal);

IF
CharacterUsedItem(_Character,_Lever)
AND
DB_Druid_Maze_Lever(_Lever,_Animal)
AND
DB_DruidMaze_QueuePolymorphs(1)
THEN
Proc_DruidMaze_QueuePolymorph(_Animal);

PROC
Proc_DruidMaze_QueuePolymorph((CHARACTER)_Animal)
AND
DB_DruidMaze_QueuedAnimal(_OldAnimal)
THEN
NOT DB_DruidMaze_QueuedAnimal(_OldAnimal);

PROC
Proc_DruidMaze_QueuePolymorph((CHARACTER)_Animal)
THEN
DB_DruidMaze_QueuedAnimal(_Animal);


//END_REGION


//REGION Maze creation
PROC
ProcGenerateDruidMaze()
THEN
ProcChangeDruidMazeAnimal(CHARACTER_DFD_Druid_Chicken);
CharacterTeleportToTrigger(CHARACTER_DFD_Druid_Chicken,TRIGGER_DFD_Druid_Maze_Start,"");
Proc_ClearDruidMaze();
Proc_MakeDruidMaze(9,9);
Proc_DruidMazeRestart(1,5);

IF
TextEventSet("maze")
THEN
Proc_ClearDruidMaze();
Proc_MakeDruidMaze(9,9);
Proc_DruidMazeRestart(1,5);

IF
TextEventSet("maze2")
THEN
Proc_ClearDruidMaze();

PROC
Proc_ClearDruidMaze()
AND
DB_DruidMaze_Cell(_Height,_Width,_X,_Y,_Z,_ItemHandle,_FoodProto)
THEN
ItemHandleDelete(_ItemHandle);
NOT DB_DruidMaze_Cell(_Height,_Width,_X,_Y,_Z,_ItemHandle,_FoodProto);

PROC
Proc_MakeDruidMaze((INTEGER)_Height,(INTEGER)_Width)
AND
CharacterGetPosition(CHARACTER_DFD_Druid_Chicken,_X,_Y,_Z)
THEN
Proc_MakeDruidMaze_Intern(_Height,_Width,_X,_Y,_Z);

PROC
Proc_MakeDruidMaze_Intern((INTEGER)_Height,(INTEGER)_Width,(REAL)_X,(REAL)_Y,(REAL)_Z)
AND
_Height >= 1
AND
IntegerSubtract(_Height,1,_HeightMinusOne)
AND
RealSum(_X,2.0,_NewX)
THEN
Proc_MakeDruidMaze_Row(_Height,_Width,_X,_Y,_Z);
Proc_MakeDruidMaze_Intern(_HeightMinusOne,_Width,_NewX,_Y,_Z);

PROC
Proc_MakeDruidMaze_Row((INTEGER)_Height,(INTEGER)_Width,(REAL)_X,(REAL)_Y,(REAL)_Z)
AND
_Width >= 1
AND
IntegerSubtract(_Width,1,_WidthMinusOne)
AND
RealSum(_Z,2.0,_NewZ)
THEN
Proc_MakeDruidMaze_Cell(_Height,_Width,_X,_Y,_Z);
Proc_MakeDruidMaze_Row(_Height,_WidthMinusOne,_X,_Y,_NewZ);

PROC
Proc_MakeDruidMaze_Cell((INTEGER)_Height,(INTEGER)_Width,(REAL)_X,(REAL)_Y,(REAL)_Z)
AND
Random(3,_Rand)
AND
DB_DruidMaze_FoodProto(_Rand,_FoodProto,_,_Yaw)
AND
CreateItemTemplateAtPosition(_FoodProto,_X,_Y,_Z,_ItemHandle)
THEN
DB_DruidMaze_Cell(_Height,_Width,_X,_Y,_Z,_ItemHandle,_FoodProto);
ItemHandleToTransform(_ItemHandle,_X,_Y,_Z,0.0,_Yaw,0.0,1,CHARACTER_NULL);

//END_REGION

//REGION Animal Behaviour
PROC
Proc_DruidMazeRestart((INTEGER)_StartX,(INTEGER)_StartY)
AND
DB_Druid_Maze_Animal(_Animal)
AND
DB_DruidMaze_Cell(_StartX,_StartY,_X,_Y,_Z,_ItemHandle,_)
THEN
NOT DB_DruidMaze_QueuePolymorphs(1);
Proc_DruidMaze_ResetVars();
ItemHandleDelete(_ItemHandle);
CharacterTeleportToPosition(_Animal,_X,_Y,_Z,"");
DB_Druid_Maze_CurrentPos(_StartX,_StartY);
TimerCancel("DruidMaze");
TimerLaunch("DruidMaze",3000);

PROC
Proc_DruidMaze_ResetVars()
AND
NOT DB_DruidMaze_QueuePolymorphs(1)
AND
DB_Druid_Maze_CurrentPos(_OldCellX,_OldCellY)
AND
DB_DruidMaze_Cell(_OldCellX,_OldCellY,_OldX,_OldY,_OldZ,_OldItemHandle,_OldFoodProto)
THEN
NOT DB_DruidMaze_Cell(_OldCellX,_OldCellY,_OldX,_OldY,_OldZ,_OldItemHandle,_OldFoodProto);
Proc_MakeDruidMaze_Cell(_OldCellX,_OldCellY,_OldX,_OldY,_OldZ);
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_OldX,_OldY,_OldZ);

PROC
Proc_DruidMaze_ResetVars()
AND
DB_DruidMaze_QueuedAnimal(_Animal)
THEN
NOT DB_DruidMaze_QueuedAnimal(_Animal);

PROC
Proc_DruidMaze_ResetVars()
AND
DB_Druid_Maze_CurrentPos(_StartX,_StartY)
THEN
NOT DB_Druid_Maze_CurrentPos(_StartX,_StartY);

IF
TimerFinished("DruidMaze")
AND
DB_Druid_Maze_Animal(_Animal)
AND
DB_Druid_Maze_CurrentPos(_X,_Y)
AND
Random(4,_RandDir)
THEN
Proc_ClearDesiredPos();
Proc_DruidMaze_Check4Dirs(_X,_Y,_RandDir);
Proc_MoveToDesiredPos();

PROC
Proc_ClearDesiredPos()
AND
DB_DruidMaze_DesiredPos(_X,_Y)
THEN
NOT DB_DruidMaze_DesiredPos(_X,_Y);

PROC
Proc_DruidMaze_Check4Dirs((INTEGER)_X,(INTEGER)_Y,0)
AND
IntegerSubtract(_X,1,_Left)
AND
IntegerSum(_X,1,_Right)
AND
IntegerSubtract(_Y,1,_Down)
AND
IntegerSum(_Y,1,_Up)
THEN
Proc_DruidMaze_CheckIfInterested(_X,_Up);
Proc_DruidMaze_CheckIfInterested(_Right,_Y);
Proc_DruidMaze_CheckIfInterested(_X,_Down);
Proc_DruidMaze_CheckIfInterested(_Left,_Y);

PROC
Proc_DruidMaze_Check4Dirs((INTEGER)_X,(INTEGER)_Y,1)
AND
IntegerSubtract(_X,1,_Left)
AND
IntegerSum(_X,1,_Right)
AND
IntegerSubtract(_Y,1,_Down)
AND
IntegerSum(_Y,1,_Up)
THEN
Proc_DruidMaze_CheckIfInterested(_Right,_Y);
Proc_DruidMaze_CheckIfInterested(_X,_Down);
Proc_DruidMaze_CheckIfInterested(_Left,_Y);
Proc_DruidMaze_CheckIfInterested(_X,_Up);

PROC
Proc_DruidMaze_Check4Dirs((INTEGER)_X,(INTEGER)_Y,2)
AND
IntegerSubtract(_X,1,_Left)
AND
IntegerSum(_X,1,_Right)
AND
IntegerSubtract(_Y,1,_Down)
AND
IntegerSum(_Y,1,_Up)
THEN
Proc_DruidMaze_CheckIfInterested(_X,_Down);
Proc_DruidMaze_CheckIfInterested(_Left,_Y);
Proc_DruidMaze_CheckIfInterested(_X,_Up);
Proc_DruidMaze_CheckIfInterested(_Right,_Y);

PROC
Proc_DruidMaze_Check4Dirs((INTEGER)_X,(INTEGER)_Y,3)
AND
IntegerSubtract(_X,1,_Left)
AND
IntegerSum(_X,1,_Right)
AND
IntegerSubtract(_Y,1,_Down)
AND
IntegerSum(_Y,1,_Up)
THEN
Proc_DruidMaze_CheckIfInterested(_Left,_Y);
Proc_DruidMaze_CheckIfInterested(_X,_Up);
Proc_DruidMaze_CheckIfInterested(_Right,_Y);
Proc_DruidMaze_CheckIfInterested(_X,_Down);

PROC
Proc_DruidMaze_CheckIfInterested((INTEGER)_CellX,(INTEGER)_CellY)
AND
NOT DB_DruidMaze_DesiredPos(_,_)
AND
DB_Druid_Maze_Animal(_Animal)
AND
DB_DruidMaze_Cell(_CellX,_CellY,_X,_Y,_Z,_ItemHandle,_FoodProto)
AND
DB_DruidMaze_FoodProto(_,_FoodProto,_Animal,_)
THEN
DB_DruidMaze_DesiredPos(_CellX,_CellY);

PROC
Proc_MoveToDesiredPos()
AND
DB_DruidMaze_DesiredPos(_CellX,_CellY)
AND
DB_Druid_Maze_Animal(_Animal)
AND
DB_DruidMaze_Cell(_CellX,_CellY,_X,_Y,_Z,_ItemHandle,_FoodProto)
THEN
DB_DruidMaze_QueuePolymorphs(1);
CharacterMoveToPosition(_Animal,_X,_Y,_Z,0,"DruidMaze_ArrivedOnNewCell");

IF
CharacterEvent(_Animal,"DruidMaze_ArrivedOnNewCell")
AND
DB_DruidMaze_DesiredPos(_CellX,_CellY)
AND
DB_DruidMaze_Cell(_CellX,_CellY,_X,_Y,_Z,_ItemHandle,_FoodProto)
AND
DB_Druid_Maze_CurrentPos(_OldCellX,_OldCellY)
AND
DB_DruidMaze_Cell(_OldCellX,_OldCellY,_OldX,_OldY,_OldZ,_OldItemHandle,_OldFoodProto)
THEN
NOT DB_DruidMaze_QueuePolymorphs(1);
NOT DB_DruidMaze_Cell(_OldCellX,_OldCellY,_OldX,_OldY,_OldZ,_OldItemHandle,_OldFoodProto);
Proc_MakeDruidMaze_Cell(_OldCellX,_OldCellY,_OldX,_OldY,_OldZ);
PlayScaledEffectAtPosition("FX_GP_TeleportSmoke_A",0.5,_OldX,_OldY,_OldZ);
NOT DB_Druid_Maze_CurrentPos(_OldCellX,_OldCellY);
DB_Druid_Maze_CurrentPos(_CellX,_CellY);
TimerLaunch("DruidMaze",3000);
ItemHandleDelete(_ItemHandle);
Proc_DruidMaze_PlayerComment_Moved();

PROC
Proc_DruidMaze_PlayerComment_Moved()
AND
DB_Druid_Maze_Animal(_Animal)
AND
CharacterGetClosestPlayer(_Animal,_Player)
AND
NOT DB_OnlyOnce("DFD_Druid_Room2_Moved")
THEN
DB_OnlyOnce("DFD_Druid_Room2_Moved");
Launch_PlayerComment(_Player,"DFD_Druid_Room2_Moved");

IF
CharacterEvent(_,"DruidMaze_ArrivedOnNewCell")
AND
DB_DruidMaze_QueuedAnimal(_Animal)
THEN
ProcChangeDruidMazeAnimal(_Animal);
NOT DB_DruidMaze_QueuedAnimal(_Animal);

IF
CharacterEvent(_Animal,"DruidMaze_ArrivedOnNewCell")
AND
DB_Druid_Maze_CurrentPos(_X,_Y)
AND
DB_Druid_Maze_Door(_Door,_X,_Y,_Torch1,_Torch2)
AND
CharacterGetClosestPlayer(_Animal,_Player)
AND
TopOfStack("BellegarMazeDoors",_QuestState)
THEN
ItemUnlockAndOpen(_Door);
NOT DB_Druid_Maze_Door(_Door,_X,_Y,_Torch1,_Torch2);
ItemApplyStatus(_Torch1,"BURNING",-1.0);
ItemApplyStatus(_Torch2,"BURNING",-1.0);
CharacterDisplayText(_Player,"DFD_Druid_DoorOpened");
QuestUpdate("DAF_Bellegar_Maze",_QuestState);
PopStack("BellegarMazeDoors");
ProcCheckIfMazeDone();

PROC
ProcCheckIfMazeDone()
AND
NOT DB_Druid_Maze_Door(_,_,_,_,_)
THEN
QuestClose("DAF_Bellegar_Maze");
Proc_ClearDruidMaze();
CharacterSetOnStage(CHARACTER_DFD_Druid_Chicken,0);
CharacterSetOnStage(CHARACTER_DFD_Druid_Dog,0);
CharacterSetOnStage(CHARACTER_DFD_Druid_Rat,0);
CharacterSetOnStage(CHARACTER_DFD_Druid_Cat,0);

IF
DB_Dead(_Character)
AND
DB_CurrentLevel("DAF_Druid_Dungeon")
AND
DB_Druid_Maze_Lever(_,_Character)
THEN
ProcChangeDruidMazeAnimal(CHARACTER_DFD_Druid_Chicken);
CharacterResurrect(_Character);
Proc_DruidMazeRestart(1,5);

IF
DB_Dead(CHARACTER_DFD_Druid_Chicken)
AND
DB_CurrentLevel("DAF_Druid_Dungeon")
THEN
CharacterResurrect(CHARACTER_DFD_Druid_Chicken);
Proc_DruidMazeRestart(1,5);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
