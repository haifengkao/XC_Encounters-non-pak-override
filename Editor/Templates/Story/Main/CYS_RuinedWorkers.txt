Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_CYS_RuinedWorker1,"CYS_RuinedWorkers");
DB_Dialogs(CHARACTER_CYS_RuinedWorker2,"CYS_RuinedWorkers");
DB_Dialogs(CHARACTER_CYS_RuinedWorker3,"CYS_RuinedWorkers");

DB_Dialogs(CHARACTER_CYS_CaptainJack,"CYS_CaptainJack");

CYS_RuinedWorker(CHARACTER_CYS_RuinedWorker3);
CYS_RuinedWorker(CHARACTER_CYS_RuinedWorker2);
CYS_RuinedWorker(CHARACTER_CYS_RuinedWorker1);

KBSECTION
//REGION GO TO CAPTAIN JACK 
IF
GlobalEventSet("CYS_SailorsToJack")
THEN
CHARACTER_CYS_RuinedWorker1.isGoToCaptain();
QuestUpdate("CYS_Ruined","Update3");
QuestClose("CYS_Ruined");

IF
DialogEnded("CYS_RuinedWorkers",_)
AND
CHARACTER_CYS_RuinedWorker1.isGoToCaptain()
AND
NOT CHARACTER_CYS_RuinedWorker1.isFoundJobHurray()
THEN
CHARACTER_CYS_RuinedWorker1.isFoundJobHurray();
PROC_CYS_MoveWorkersToJack();
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_WastingTimeSailors",CHARACTER_CYS_RuinedWorker1);

PROC
PROC_CYS_MoveWorkersToJack()
AND
CYS_RuinedWorker(_Worker)
THEN
CharacterMoveToCharacter(_Worker,CHARACTER_CYS_CaptainJack,1,"RunToJack",0);
DialogRequestStopForCharacter(_Worker);
CharacterSetHasDialog(_Worker,0);

IF
CharacterEvent(_Character,"RunToJack")
AND
NOT CHARACTER_CYS_CaptainJack.hasHired()
THEN
CHARACTER_CYS_CaptainJack.hasHired();
CharacterSetHasDialog(CHARACTER_CYS_CaptainJack,0);
DialogStartFourSpeakerDialog("CYS_AD_JackHiring",CHARACTER_CYS_CaptainJack,CHARACTER_CYS_RuinedWorker1,CHARACTER_CYS_RuinedWorker2,CHARACTER_CYS_RuinedWorker3);

IF
AutomatedDialogEnded("CYS_AD_JackHiring",_)
THEN
ProcCharacterMoveToTrigger(CHARACTER_CYS_CaptainJack,TRIGGER_CYS_PointCaptainJack4,1,"CYS_JackToWork");
ProcCharacterMoveToTrigger(CHARACTER_CYS_RuinedWorker1,TRIGGER_CYS_PointCaptainJack1,1,"CYS_JackToWork");
ProcCharacterMoveToTrigger(CHARACTER_CYS_RuinedWorker2,TRIGGER_CYS_PointCaptainJack2,1,"CYS_JackToWork");
ProcCharacterMoveToTrigger(CHARACTER_CYS_RuinedWorker3,TRIGGER_CYS_PointCaptainJack3,1,"CYS_JackToWork");

IF
CharacterEvent(_Character,"CYS_JackToWork")
THEN
IncreaseAttitude(CHARACTER_Player1,_Character,20);
IncreaseAttitude(CHARACTER_Player2,_Character,20);
CharacterSetHasDialog(_Character,1);
GlobalSetEvent("CYS_ThanksTalk");
//END_REGION

//REGION GO TO FABULOUS FIVE
IF
GlobalEventSet("CYS_SailorsToFab")
THEN
CHARACTER_CYS_RuinedWorker1.isGotoFab();
QuestUpdate("CYS_Ruined","Update5");
QuestClose("CYS_Ruined");

IF
DialogEnded("CYS_RuinedWorkers",_)
AND
CHARACTER_CYS_RuinedWorker1.isGotoFab()
THEN
NOT CHARACTER_CYS_RuinedWorker1.isGotoFab();
PROC_CYS_MoveWorkersToMendius();
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_WastingTimeSailors",CHARACTER_CYS_RuinedWorker1);

PROC
PROC_CYS_MoveWorkersToMendius()
AND
CYS_RuinedWorker(_Worker)
THEN
CharacterSetHasDialog(_Worker,0);
DialogRequestStopForCharacter(_Worker);
ProcRemoveDialogEntryForSpeaker(_Worker,"CYS_RuinedWorkers");
CharacterMoveToTrigger(_Worker,TRIGGER_CYS_PointScaredWorker2,1,"CYS_RuinedWorkerRanOff");

IF
CharacterEvent(_Worker,"CYS_RuinedWorkerRanOff")
THEN
CharacterDisappearOutOfSight(_Worker,180,1,"",1);

IF
TextEventSet("runtomendius")
THEN
PROC_CYS_MoveWorkersToMendius();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
