Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_NoDefaultBehavior(CHARACTER_CYS_Adventurer);

CharacterSetHitpointsPercentage(CHARACTER_CYS_Adventurer,80);

DB_Dialogs(CHARACTER_CYS_Adventurer,"CYS_MysteriousAdventurer");

DB_CYSAdventureSkeletons(CHARACTER_CYS_ArcheologistSkeleton01);
DB_CYSAdventureSkeletons(CHARACTER_CYS_ArcheologistSkeleton02);
DB_CYSAdventureSkeletons(CHARACTER_Cyseal_AdventurerKilledSkeleton_01);
DB_CYSAdventureSkeletons(CHARACTER_Cyseal_AdventurerKilledSkeleton_02);

CharacterSetOnStage(CHARACTER_CYS_AdventurerKiller,0);

DB_PlayerComments("CYS_CommentBurialMountDog","CYS_CommentBurialMountDog_1_1",1,1);
DB_PlayerComments("CYS_CommentBurialMountDog","CYS_CommentBurialMountDog_2_1",2,1);
DB_PlayerComments("CYS_CommentBurialMountDog","CYS_CommentBurialMountDog_2_2",2,2);

DB_PlayerComment_Trigger(TRIGGER_CYS_BoxDogWarningArea,"CYS_CommentBurialMountDog");

DB_NoDefaultBehavior(CHARACTER_CYS_ZombieBurialDog);
DB_EvilDude(CHARACTER_CYS_ZombieBurialDog);

DB_CYSAdventurerEscortLastEvent("ReachedWaypoint6");

DB_OneShot_DialogTrigger(TRIGGER_CYS_BoxArcheologistStartDialog,"CYS_MysteriousAdventurer",CHARACTER_CYS_Adventurer);
DB_CYSSkeletonSpawns("ReachedWaypoint2",TRIGGER_CYS_PointArchealogistAmbush1,CHARACTER_CYS_ArcheologistAmbush1);
DB_CYSSkeletonSpawns("ReachedWaypoint4",TRIGGER_CYS_PointArchealogistAmbush2,CHARACTER_CYS_ArcheologistAmbush2);
DB_CYSSkeletonSpawns("ReachedWaypoint4",TRIGGER_CYS_PointArchealogistAmbush3,CHARACTER_CYS_ArcheologistAmbush3);

DB_CYSSkeletonSpawns_HC("ReachedWaypoint4",TRIGGER_CYS_PointArchealogistAmbush4,CHARACTER_CYS_ArcheologistAmbush4);
DB_CYSSkeletonSpawns_HC("ReachedWaypoint4",TRIGGER_CYS_PointArchealogistAmbush5,CHARACTER_CYS_ArcheologistAmbush5);
DB_CYSSkeletonSpawns_HC("ReachedWaypoint4",TRIGGER_CYS_PointArchealogistAmbush6,CHARACTER_CYS_ArcheologistAmbush6);
DB_CYSSkeletonSpawns_HC("ReachedWaypoint4",TRIGGER_CYS_PointArchealogistAmbush7,CHARACTER_CYS_ArcheologistAmbush7);

DB_CYSSkeletonAmbushArea(TRIGGER_CYS_BoxArcheologistAmbush1,"ReachedWaypoint2");
DB_CYSSkeletonAmbushArea(TRIGGER_CYS_BoxArcheologistAmbush2,"ReachedWaypoint4");

DB_CYSArchaeologistCheers(0,"CYS_ArcheologistVictoryCheer1");
DB_CYSArchaeologistCheers(1,"CYS_ArcheologistVictoryCheer2");	
DB_CYSArchaeologistCheers(2,"CYS_ArcheologistVictoryCheer3");

ItemSetOnstage(ITEM_CYS_Adventurer_Corpse,0);
KBSECTION
//REGION abandoning the archelogist 
IF
CharacterLeftTrigger(_Player,TRIGGER_CYS_Box_ArcheologistKillTrigger)
AND
NOT DB_InRegion(_,TRIGGER_CYS_Box_ArcheologistKillTrigger)
THEN
CharacterSetOnStage(CHARACTER_CYS_Adventurer,0);
ItemSetOnstage(ITEM_CYS_Adventurer_Corpse,1);
CharacterSetOnStage(CHARACTER_CYS_AdventurerKiller,1);
ProcTriggerUnregisterForPlayers(TRIGGER_CYS_Box_ArcheologistKillTrigger);


IF
CharacterSawCharacter(_Player,CHARACTER_CYS_AdventurerKiller)
AND
_Player.DB_IsPlayer()
AND
DB_GlobalEvent("CYS_AbandonArchaeologist")
THEN
QuestUpdate("CYS_Archeologist","update9");

IF
GlobalEventSet("CYS_AbandonArchaeologist")
THEN
ProcTriggerRegisterForPlayers(TRIGGER_CYS_Box_ArcheologistKillTrigger);
DecreaseAttitude(CHARACTER_Player1,CHARACTER_CYS_Adventurer,15);
DecreaseAttitude(CHARACTER_Player2,CHARACTER_CYS_Adventurer,15);
ProcCYSRegisterArcheologistAmbushAreas();

//END_REGION

//REGION escorting the archelogist
IF
DB_CYSAdventureSkeletons(_Char)
THEN
CharacterSetOnStage(_Char,0);

IF
GlobalEventSet("CYS_EscortArchaeologist")
THEN
CharacterSetEvent(CHARACTER_CYS_Adventurer,"EscortStart");
CHARACTER_CYS_Adventurer.isInEscort();
CharacterSetVarFixedString(CHARACTER_CYS_Adventurer, "currentState", "State_DoEscort");
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Adventurer,"CYS_MysteriousAdventurer");
DB_AD_Dialog(CHARACTER_CYS_Adventurer,"CYS_AD_ArcheologistEscort");

IF
CharacterEvent(CHARACTER_CYS_Adventurer,_Event)
AND
DB_CYSAdventurerEscortLastEvent(_Event)
THEN
NOT DB_CYSAdventurerEscortLastEvent(_Event);
CharacterSetVarFixedString(CHARACTER_CYS_Adventurer, "currentState", "State_EscortDone");
DB_OneShot_DialogTrigger(TRIGGER_CYS_ArcheologistEndTalkArea,"CYS_ArcheologistEscortDone",CHARACTER_CYS_Adventurer);
NOT DB_AD_Dialog(CHARACTER_CYS_Adventurer,"CYS_AD_ArcheologistEscort");

IF
DB_CYSSkeletonSpawns(_,_,_Char)
THEN
CharacterSetOnStage(_Char,0);

IF
DB_CYSSkeletonSpawns_HC(_,_,_Char)
THEN
CharacterSetOnStage(_Char,0);

IF
CharacterEvent(CHARACTER_CYS_Adventurer,_Event)
THEN
ProcCYSSpawnArchaeologistEnemies(_Event);

PROC
ProcCYSRegisterArcheologistAmbushAreas()
AND
DB_CYSSkeletonAmbushArea(_Trigger,_)
THEN
DB_OneShotPlayerTrigger(_Trigger);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,_Trigger)
AND
DB_CYSSkeletonAmbushArea(_Trigger,_Event)
THEN
NOT DB_CYSSkeletonAmbushArea(_Trigger,_Event);
ProcCYSSpawnArchaeologistEnemies(_Event);

PROC
ProcCYSSpawnArchaeologistEnemies((STRING)_Event)
AND
DB_CYSSkeletonSpawns(_Event,_Trigger,_Character)
THEN
CharacterAppearAtTrigger(_Character,_Trigger,1);

PROC
ProcCYSSpawnArchaeologistEnemies((STRING)_Event)
AND
IsHardcoreMode(1)
AND
DB_CYSSkeletonSpawns_HC(_Event,_Trigger,_Character)
THEN
CharacterAppearAtTrigger(_Character,_Trigger,1);

IF
CharacterDying(_Character)
AND
DB_CYSSkeletonSpawns(_Event,_Trigger,_Character)
THEN
NOT DB_CYSSkeletonSpawns(_Event,_Trigger,_Character);
ProcCYSArchaeologistCheckComment(_Event);

IF
CharacterDying(_Character)
AND
DB_CYSSkeletonSpawns_HC(_Event,_Trigger,_Character)
THEN
NOT DB_CYSSkeletonSpawns_HC(_Event,_Trigger,_Character);
ProcCYSArchaeologistCheckComment(_Event);

PROC
ProcCYSArchaeologistCheckComment((STRING)_Event)
AND
IsHardcoreMode(0)
AND
NOT DB_CYSSkeletonSpawns(_Event,_,_)
AND
NOT DB_Dead(CHARACTER_CYS_Adventurer)
AND
Random(3,_Index)
AND
DB_CYSArchaeologistCheers(_Index,_Cheer)
THEN
CharacterDisplayText(CHARACTER_CYS_Adventurer,_Cheer);

PROC
ProcCYSArchaeologistCheckComment((STRING)_Event)
AND
IsHardcoreMode(1)
AND
NOT DB_CYSSkeletonSpawns(_Event,_,_)
AND
NOT DB_CYSSkeletonSpawns_HC(_Event,_,_)
AND
NOT DB_Dead(CHARACTER_CYS_Adventurer)
AND
Random(3,_Index)
AND
DB_CYSArchaeologistCheers(_Index,_Cheer)
THEN
CharacterDisplayText(CHARACTER_CYS_Adventurer,_Cheer);

IF
CharacterDying(CHARACTER_CYS_Adventurer)
AND
CHARACTER_CYS_Adventurer.isInEscort()
THEN
CharacterDisplayText(CHARACTER_CYS_Adventurer,"CYS_Archaeologist_DeathTaunt");

IF
CharacterDied(CHARACTER_CYS_Adventurer)
AND
CHARACTER_CYS_Adventurer.isInEscort()
THEN
NOT CHARACTER_CYS_Adventurer.isInEscort();
GlobalSetEvent("CYS_ArcheologistDiedInEscort");
QuestUpdate("CYS_Archeologist","update8");

IF
DialogEnded("CYS_ArcheologistEscortDone",_)
THEN
Proc_CompanionComment(CHARACTER_Jahan,"JAH_CC_WastingTimeArcheologist",CHARACTER_CYS_Adventurer);
CharacterSetHasDialog(CHARACTER_CYS_Adventurer,0);
NOT CHARACTER_CYS_Adventurer.isInEscort();
QuestUpdate("CYS_Archeologist","Update4");
DB_Dialogs(CHARACTER_CYS_Adventurer,"CYS_MysteriousAdventurer");
ProcCharacterMoveToTrigger(CHARACTER_CYS_Adventurer,TRIGGER_CYS_PointArcheologistBren2,1,"");
CharacterSetVarFixedString(CHARACTER_CYS_Adventurer, "currentState", "State_AfterEscort");
CharacterSetVarInteger(CHARACTER_CYS_Adventurer,"AtTavern",1);
GlobalSetEvent("CYS_ArcheologistSaved");
IncreaseAttitude(CHARACTER_Player1,CHARACTER_CYS_Adventurer,15);
IncreaseAttitude(CHARACTER_Player1,CHARACTER_CYS_Adventurer,15);
//END_REGION

//REGION skeletons
IF
CharacterEvent(CHARACTER_CYS_ZombieBurialDog,"SpottedPlayer")
THEN
CharacterSetScriptframe(CHARACTER_CYS_ZombieBurialDog,"WakeSkeletons");

IF
CharacterScriptFrameFinished(CHARACTER_CYS_ZombieBurialDog,"WakeSkeletons")
THEN
CharacterSetFaction(CHARACTER_CYS_ZombieBurialDog,"Evil NPC");

IF
CharacterEvent(_Character,"Wake")
THEN
ProcCYSSpawnAdventureSkeleton(_Character);

PROC
ProcCYSSpawnAdventureSkeleton((CHARACTER)_Character)
AND
DB_CYSAdventureSkeletons(_Character)
THEN
NOT DB_CYSAdventureSkeletons(_Character);
CharacterAppear(_Character,1);

IF
CharacterDying(CHARACTER_CYS_ZombieBurialDog)
AND
DB_CYSAdventureSkeletons(_Character)
THEN
ProcCYSSpawnAdventureSkeleton(_Character);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
