Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_OneShotPlayerTrigger(TRIGGER_DAF_DruidScene);

DB_DAF_ZandalorHouseGuard(CHARACTER_DAF_Human_ZandalorHouse1);
DB_DAF_ZandalorHouseGuard(CHARACTER_DAF_Human_ZandalorHouse2);
DB_DAF_ZandalorHouseGuard(CHARACTER_DAF_Orc_ZandalorHouse1);
DB_DAF_ZandalorHouseGuard(CHARACTER_DAF_Orc_ZandalorHouse2);
KBSECTION
//REGION Trap Scene
IF
CharacterAttackedByCharacter(_Character,_Player)
AND
_Player.DB_IsPlayer()
AND
DB_DAF_ZandalorHouseGuard(_Character)
AND
DB_DAF_ZandalorHouseGuard(_Character2)
THEN
ProcSetRelationToPlayers(_Character2,0);
CharacterPurgeQueue(_Character2);
GoalCompleted;

IF
DialogStarted(_,_ID)
AND
DB_DialogNPCs(_Id,_NPC,1)
AND
DB_DAF_ZandalorHouseGuard(_NPC)
THEN
Boolean("DB_SceneInterrupted");
DialogRequestStop("DAF_AD_DruidTrapScene",CHARACTER_DAF_Human_ZandalorHouse1);

IF
DialogEnded(_,_ID)
AND
DB_DialogNPCs(_Id,_NPC,1)
AND
DB_DAF_ZandalorHouseGuard(_NPC)
THEN
TimerLaunch("DAF_RestartDruidTrapScene",6000);

IF
TimerFinished("DAF_RestartDruidTrapScene")
AND
NOT DAF_War(1)
THEN
NOT Boolean("DB_SceneInterrupted");
DialogStartFourSpeakerDialog("DAF_AD_DruidTrapScene",CHARACTER_DAF_Human_ZandalorHouse1,CHARACTER_DAF_Human_ZandalorHouse2,CHARACTER_DAF_Orc_ZandalorHouse1,CHARACTER_DAF_Orc_ZandalorHouse2);
CharacterLookAtCharacter(CHARACTER_DAF_Human_ZandalorHouse1,CHARACTER_DAF_Orc_ZandalorHouse1);
CharacterLookAtCharacter(CHARACTER_DAF_Human_ZandalorHouse2,CHARACTER_DAF_Orc_ZandalorHouse2);
CharacterLookAtCharacter(CHARACTER_DAF_Orc_ZandalorHouse1,CHARACTER_DAF_Human_ZandalorHouse1);
CharacterLookAtCharacter(CHARACTER_DAF_Orc_ZandalorHouse2,CHARACTER_DAF_Human_ZandalorHouse2);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DAF_DruidScene)
AND
NOT DAF_War(1)
THEN
DialogStartFourSpeakerDialog("DAF_AD_DruidTrapScene",CHARACTER_DAF_Human_ZandalorHouse1,CHARACTER_DAF_Human_ZandalorHouse2,CHARACTER_DAF_Orc_ZandalorHouse1,CHARACTER_DAF_Orc_ZandalorHouse2);

IF
AutomatedDialogEnded("DAF_AD_DruidTrapScene",_)
AND
NOT Boolean("DB_SceneInterrupted")
THEN
CharacterDisplayText(CHARACTER_DAF_Human_ZandalorHouse1,"DAF_DruidScenePaper");
CharacterDisplayText(CHARACTER_DAF_Human_ZandalorHouse2,"DAF_DruidSceneScissors");
CharacterDisplayText(CHARACTER_DAF_Orc_ZandalorHouse1,"DAF_DruidSceneScissors");
CharacterDisplayText(CHARACTER_DAF_Orc_ZandalorHouse2,"DAF_DruidSceneScissors");

IF
CharacterDisplayTextEnded(CHARACTER_DAF_Human_ZandalorHouse1,"DAF_DruidScenePaper")
AND
NOT Boolean("DB_SceneInterrupted")
THEN
CharacterPlayAnimation(CHARACTER_DAF_Human_ZandalorHouse1,"Sigh_01","");
TimerLaunch("DAF_DruidTrap1",4000);
CharacterLookAtCharacter(CHARACTER_DAF_Human_ZandalorHouse2,CHARACTER_DAF_Human_ZandalorHouse1,0);
CharacterLookAtCharacter(CHARACTER_DAF_Orc_ZandalorHouse1,CHARACTER_DAF_Human_ZandalorHouse1,0);
CharacterLookAtCharacter(CHARACTER_DAF_Orc_ZandalorHouse2,CHARACTER_DAF_Human_ZandalorHouse1,0);

IF
TimerFinished("DAF_DruidTrap1")
AND
NOT Boolean("DB_SceneInterrupted")
THEN
ProcCharacterMoveToTrigger(CHARACTER_DAF_Human_ZandalorHouse1,TRIGGER_DAF_DruidTrap,0,"DAF_DruidTrapExplode");

IF
CharacterEvent(CHARACTER_DAF_Human_ZandalorHouse1,"DAF_DruidTrapExplode")
AND
CharacterGetPosition(CHARACTER_DAF_Human_ZandalorHouse1,_X,_Y,_Z)
THEN
CharacterSetOnStage(CHARACTER_DAF_Human_ZandalorHouse1,0);
NOT DB_DAF_BO_Human(CHARACTER_DAF_Human_ZandalorHouse1);
ItemSetOnstage(ITEM_DAF_DruidTrapCorpse,1);
ItemDragToTrigger(ITEM_DAF_DruidTrapCorpse,TRIGGER_DAF_DruidCorpse);
PlayEffectAtPosition("FX_Skills_Fire_Fireball_Impact_A",_X,_Y,_Z);
TimerLaunch("DAF_FlyingBody",700);
TimerLaunch("DAF_FlyingBody2",4000);

IF
TimerFinished("DAF_FlyingBody")
THEN
DialogRequestStopForCharacter(CHARACTER_DAF_Human_ZandalorHouse2);
CharacterPlayAnimation(CHARACTER_DAF_Human_ZandalorHouse2,"knockdown_fall","knockdown_fall");

IF
CharacterEvent(CHARACTER_DAF_Human_ZandalorHouse2,"knockdown_fall")
THEN
CharacterPlayAnimation(CHARACTER_DAF_Human_ZandalorHouse2,"knockdown_getup","");

IF
TimerFinished("DAF_FlyingBody2")
THEN
CharacterDisplayText(CHARACTER_DAF_Orc_ZandalorHouse1,"DAF_DruidPlanB");
GoalCompleted;

IF
DAF_War(1)
THEN
GoalCompleted;


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
