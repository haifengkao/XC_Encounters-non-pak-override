Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_OneShotPlayerTrigger(TRIGGER_DAF_FamilyHouseTriggerAD);
ProcTriggerRegisterForPlayers(TRIGGER_DAF_EnteredVillage);
DB_Dialogs(CHARACTER_DAF_BO_George,"DAF_BO_George");
DB_Dialogs(CHARACTER_DAF_BO_FamilyHouseGuard_01,"DAF_BO_FamilyHouseGuard_01");
DB_Dialogs(CHARACTER_DAF_BO_FamilyHouseGuard_02,"DAF_BO_FamilyHouseGuard_02");
DB_Dialogs(CHARACTER_DAF_Orc_ZandalorHouse1,"DAF_Orc_ZandalorHouse1");
DB_Dialogs(CHARACTER_DAF_Orc_ZandalorHouse2,"DAF_Orc_ZandalorHouse2");
DB_Dialogs(CHARACTER_DAF_Human_ZandalorHouse1,"DAF_Human_ZandalorHouse1");
DB_Dialogs(CHARACTER_DAF_Human_ZandalorHouse2,"DAF_Human_ZandalorHouse2");

DB_BigTrader(CHARACTER_DAF_Arhu);
DB_BigTrader(CHARACTER_DAF_Arhu_Cat);
DB_BigTrader(CHARACTER_DAF_EquipmentTrader);
DB_BigTrader(CHARACTER_DAF_BO_OrcTrader);
DB_BigTrader(CHARACTER_DAF_MagicTrader);
DB_BigTrader(CHARACTER_DAF_Skeleton_Trader);
KBSECTION
IF
DialogCharacterEventSet("DAF_BOAllHostile",_,_DialogInstance)
THEN
DAF_SetBOVillageHostile(_DialogInstance);

IF
DialogEnded(_,_DialogInstance)
AND
DAF_SetBOVillageHostile(_DialogInstance)
THEN
NOT DAF_SetBOVillageHostile(_DialogInstance);
SetRelationFactionToPlayers("DAF_VillageBO_Orc",0);
SetRelationFactionToPlayers("DAF_VillageBO_Human",0);
DAF_War(1);

PROC
Proc_RatcatcherLeavesTown()
THEN
ProcTriggerUnregisterForPlayers(TRIGGER_DAF_FamilyHouseTriggerAD);
CharacterSetVarItem(CHARACTER_DAF_BO_FamilyHouseGuard_01,"Seat",ITEM_DAF_Bench_Houseguards);
CharacterSetVarItem(CHARACTER_DAF_BO_FamilyHouseGuard_02,"Seat",ITEM_DAF_Bench_Houseguards);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTER_DAF_BO_FamilyHouseGuard_01);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTER_DAF_BO_FamilyHouseGuard_02);
ClearDefaultDialog(CHARACTER_DAF_BO_FamilyHouseGuard_01);
ClearDefaultDialog(CHARACTER_DAF_BO_FamilyHouseGuard_02);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DAF_FamilyHouseTriggerAD)
THEN
DialogStartTwoSpeakerDialog("DAF_AD_FamilyHouseGuards", CHARACTER_DAF_BO_FamilyHouseGuard_01, CHARACTER_DAF_BO_FamilyHouseGuard_02);

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_DAF_EnteredVillage)
THEN
QuestAdd("DAF_HuntersEdge");
QuestUpdate("DAF_HuntersEdge","Inside");
QuestClose("DAF_HuntersEdge");

IF
CharacterEnteredTrigger(CHARACTER_Madoc,TRIGGER_DAF_EnteredVillage)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_HuntersEdgeEntrance",CHARACTER_Madoc);

IF
AutomatedDialogEnded("DAF_AD_FamilyHouseGuards",_)
THEN
CharacterSetScriptframe(CHARACTER_DAF_BO_FamilyHouseGuard_01,"DialogDone");
CharacterSetScriptframe(CHARACTER_DAF_BO_FamilyHouseGuard_02,"DialogDone");

IF
DAF_War(1)
THEN
QuestClose("DAF_DrunkOrcs");
QuestClose("DAF_ArmoryKey");
QuestClose("DAF_Pondering_Skeleton");
QuestClose("DAF_Lovers");
QuestClose("DAF_BackAlley_Trader");
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
