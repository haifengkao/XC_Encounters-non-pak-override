Version 1
SubGoalCombiner SGC_AND
INITSECTION
ItemLock(ITEM_LUC_M_TempleDoor,"storylock");
CharacterDie(CHARACTER_LUC_M_LeandraFight_DK,0);
CharacterDie(CHARACTER_LUC_M_LeandraFight_DK2,0);
CharacterDie(CHARACTER_LUC_M_LeandraFight_DK3,0);
CharacterSetOnStage(CHARACTER_LUC_M_Leandra,0);
CharacterSetVisible(CHARACTER_LUC_M_LeandraDialogHack,0);
CharacterSetCanFight(CHARACTER_LUC_M_LeandraDialogHack,0);
CharacterSetOnStage(CHARACTER_LUC_M_LeandraDialogHack,0);

DB_OneShotPlayerTrigger(TRIGGER_LUC_M_DiscoverDoor);
DB_OneShotPlayerTrigger(TRIGGER_LUC_M_LeandraFight);

DB_PlayerComment_Trigger(TRIGGER_LUC_BoxEnterLowerMines1,"LUC_M_CommentEnterLowerMines");
DB_PlayerComments("LUC_M_CommentEnterLowerMines","LUC_M_CommentEnterLowerMines_1_1",1,1);
DB_PlayerComments("LUC_M_CommentEnterLowerMines","LUC_M_CommentEnterLowerMines_2_1",2,1);
DB_PlayerComments("LUC_M_CommentEnterLowerMines","LUC_M_CommentEnterLowerMines_2_2",2,2);

DB_PlayerComment_Trigger(TRIGGER_LUC_M_CommentDKFactory,"LUC_M_CommentDKFactory");
DB_PlayerComments("LUC_M_CommentDKFactory","LUC_M_CommentDKFactory_1_1",1,1);
DB_PlayerComments("LUC_M_CommentDKFactory","LUC_M_CommentDKFactory_2_1",2,1);
DB_PlayerComments("LUC_M_CommentDKFactory","LUC_M_CommentDKFactory_2_2",2,2);

DB_PlayerComments("LUC_M_CommentAfterLeandra","LUC_M_CommentAfterLeandra_1_1",1,1);
DB_PlayerComments("LUC_M_CommentAfterLeandra","LUC_M_CommentAfterLeandra_2_1",2,1);
DB_PlayerComments("LUC_M_CommentAfterLeandra","LUC_M_CommentAfterLeandra_2_2",2,2);
DB_PlayerComments("LUC_M_CommentAfterLeandra","LUC_M_CommentAfterLeandra_2_3",2,3);
DB_PlayerComments("LUC_M_CommentAfterLeandra","LUC_M_CommentAfterLeandra_2_4",2,4);

DB_PlayerComments("LUC_M_CommentDiscoverDoor","LUC_M_CommentDiscoverDoor_1_1",1,1);
DB_PlayerComments("LUC_M_CommentDiscoverDoor","LUC_M_CommentDiscoverDoor_2_1",2,1);
DB_PlayerComments("LUC_M_CommentDiscoverDoor","LUC_M_CommentDiscoverDoor_2_2",2,2);

DB_PlayerComments("LUC_M_CommentBlueprint","LUC_M_CommentBlueprint_1_1",1,1);
DB_PlayerComments("LUC_M_CommentBlueprint","LUC_M_CommentBlueprint_2_1",2,1);
DB_PlayerComments("LUC_M_CommentBlueprint","LUC_M_CommentBlueprint_2_2",2,2);

DB_PlayerComments("LUC_M_CommentAfterFight","LUC_M_CommentAfterFight_1_1",1,1);
DB_PlayerComments("LUC_M_CommentAfterFight","LUC_M_CommentAfterFight_2_1",2,1);
DB_PlayerComments("LUC_M_CommentAfterFight","LUC_M_CommentAfterFight_2_2",2,2);

StackTrigger("LUC_M_Stunpoints",TRIGGER_LUC_M_Stunpoint3);
StackTrigger("LUC_M_Stunpoints",TRIGGER_LUC_M_Stunpoint2);
StackTrigger("LUC_M_Stunpoints",TRIGGER_LUC_M_Stunpoint1);

DB_BrokenDetonator(ITEM_LUC_M_PUZ_Detonator_A_000);
DB_BrokenDetonator(ITEM_LUC_M_PUZ_Detonator_A_001);

ItemToContainer(ITEM_LUC_M_DKCorpse_Ore1,ITEM_LUC_M_DKCorpse1,1);
ItemToContainer(ITEM_LUC_M_DKCorpse_Ore2,ITEM_LUC_M_DKCorpse2,1);
ItemToContainer(ITEM_LUC_M_DKCorpse_Ore3,ITEM_LUC_M_DKCorpse3,1);
KBSECTION
IF
CharacterEnteredRegion(_Player,"LUC_Mines")
AND
_Player.DB_IsPlayer()
THEN
QuestAdd("LUC_M_Temple");
QuestUpdate("LUC_M_Temple","StartWander");

IF
CharacterUsedItem(_Character,_Item)
AND
DB_BrokenDetonator(_Item)
THEN
CharacterDisplayText(_Character,"GEN_Broken");

IF
GlobalEventSet("LUC_M_PasswordKnown")
THEN
QuestUpdate("LUC_M_Temple","PasswordFound");

IF
CharacterUsedItem(_Character,ITEM_LUC_M_TempleDoor)
AND 
NOT Boolean("LUC_M_TempleDoorUnlocked")
THEN
ProcDoTwoSpeakerItemDialog("LUC_M_TempleDoor",_Character,ITEM_LUC_M_TempleDoor);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_LUC_M_DiscoverDoor)
THEN
QuestUpdate("LUC_M_Temple","DoorFound");
Launch_PlayerComment(_Player,"LUC_M_CommentDiscoverDoor");

IF
DialogEnded("LUC_M_TempleDoor",_)
AND
DB_GlobalEvent("LUC_M_TempleDoor_UnlockedWithKeyword")
THEN
ItemUnLock(ITEM_LUC_M_TempleDoor);
ItemOpen(ITEM_LUC_M_TempleDoor);
Boolean("LUC_M_TempleDoorUnlocked");
QuestUpdate("LUC_M_Temple","DoorOpen");


//BOSS FIGHT WITH LEANDRA

PROC
ProcOneShotTriggerEntered((CHARACTER)_Character,TRIGGER_LUC_M_LeandraFight)
AND
CharacterGetPosition(CHARACTER_LUC_M_Leandra,_X,_Y,_Z)
AND
QryGetValidDialogSpeaker(_Character)
AND
DB_DialogSpeaker(_ActualSpeaker)
THEN
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PlayEffectAtPosition("FX_GP_CameraShake_A",_X,_Y,_Z);
CharacterSetOnStage(CHARACTER_LUC_M_Leandra,1);
CharacterSetOnStage(CHARACTER_LUC_M_LeandraDialogHack,1);
ProcDoThreeSpeakerDialog("LUC_M_Leandra",CHARACTER_LUC_M_Leandra,CHARACTER_LUC_M_LeandraDialogHack,_ActualSpeaker);
DB_LUC_M_Frozen(_Character);
Proc_LUC_M_FreezeParty();
CharacterDie(CHARACTER_LUC_M_DK1,0);
CharacterDie(CHARACTER_LUC_M_DK2,0);
CharacterDie(CHARACTER_LUC_M_DK3,0);
CharacterDie(CHARACTER_LUC_M_DK4,0);

PROC
Proc_LUC_M_FreezeParty()
AND
_Character.DB_IsPlayer()
THEN
PlayEffectAtCharacter("FX_Skills_Air_Storm_Impact_A",_Character);
CharacterFreeze(_Character);
CharacterApplyStatus(_Character,"STUNNED",-1.0,1);

IF
GlobalEventSet("LUC_M_TeleportParty")
AND
_Character.DB_IsPlayer()
AND
NOT DB_LUC_M_Frozen(_Character)
THEN
Proc_LUC_Freeze(_Character);

PROC
Proc_LUC_Freeze((CHARACTER)_Character)
AND
TopOfStackTrigger("LUC_M_Stunpoints",_Trigger)
THEN
DB_LUC_M_Frozen(_Character);
PopStack("LUC_M_Stunpoints");
CharacterTeleportToTrigger(_Character,_Trigger,"");
CharacterPlayEffect(_Character,"FX_GP_TeleportSmoke_A");

PROC
Proc_UnfreezeAll()
AND
_Character.DB_IsPlayer()
THEN
CharacterUnfreeze(_Character);
CharacterRemoveStatus(_Character,"STUNNED");

IF
DialogEnded("LUC_M_Leandra",_)
THEN
CharacterSetOnStage(CHARACTER_LUC_M_LeandraDialogHack,0);
DialogStartOneSpeakerDialog("LUC_M_AD_Leandra_Starstone",CHARACTER_LUC_M_Leandra);

IF
GlobalEventSet("LUC_M_RezDKs")
THEN
TimerLaunch("LUC_M_RezDKs",1000);

IF
TimerFinished("LUC_M_RezDKs")
THEN
CharacterResurrect(CHARACTER_LUC_M_LeandraFight_DK);
PlayEffectAtCharacter("FX_Skills_Air_Storm_Impact_A",CHARACTER_LUC_M_LeandraFight_DK);
TimerLaunch("CHARACTER_LUC_M_LeandraFight_DK2",500);
TimerLaunch("CHARACTER_LUC_M_LeandraFight_DK3",1000);

IF
TimerFinished("CHARACTER_LUC_M_LeandraFight_DK2")
THEN
CharacterResurrect(CHARACTER_LUC_M_LeandraFight_DK2);
PlayEffectAtCharacter("FX_Skills_Air_Storm_Impact_A",CHARACTER_LUC_M_LeandraFight_DK2);

IF
TimerFinished("CHARACTER_LUC_M_LeandraFight_DK3")
THEN
CharacterResurrect(CHARACTER_LUC_M_LeandraFight_DK3);
PlayEffectAtCharacter("FX_Skills_Air_Storm_Impact_A",CHARACTER_LUC_M_LeandraFight_DK3);

IF
AutomatedDialogEnded("LUC_M_AD_Leandra_Starstone",_)
THEN
CharacterPlayAnimation(CHARACTER_LUC_M_Leandra,"skill_purifyingfire_cast","EndOfFight");
CharacterPlayEffect(CHARACTER_LUC_M_Leandra,"FX_Skills_Fire_PurifyingFire_Cast_A");

IF
CharacterEvent(CHARACTER_LUC_M_Leandra,"EndOfFight")
THEN
CharacterPlayEffect(CHARACTER_LUC_M_Leandra,"FX_GP_TeleportSmoke_A");
TimerLaunch("LUC_M_LeandraResurrect6",500);
TriggerCreatePuddle(TRIGGER_LUC_M_LavaSeed1,"Lava",8000,8000,20,40,0.1);
TriggerCreatePuddle(TRIGGER_LUC_M_LavaSeed2,"Lava",6000,6000,20,40,0.1);
TriggerCreatePuddle(TRIGGER_LUC_M_LavaSeed3,"Lava",5000,5000,20,40,0.1);
TriggerCreatePuddle(TRIGGER_LUC_M_LavaSeed4,"Lava",5000,5000,10,20,0.1);
TriggerCreatePuddle(TRIGGER_LUC_M_LavaSeed5,"Lava",5000,5000,10,20,0.1);
TriggerSetAtmosphere("2b37489f-2b08-4c19-b737-1aab885554fe","Mine_Fallingdust_CamFX");
QuestClose("LUC_M_CaveIn");
QuestClose("LUC_MineFuse");
QuestClose("LUC_M_RogueDK");
LockWaypoint("WAYP_Luculla_Mines");
LockWaypoint("WAYP_Luculla_Mines2");
ItemSetCanInteract(ITEM_WAYP_Luculla_Mines,0);
ItemSetCanInteract(ITEM_WAYP_Luculla_Mines2,0);

IF
TimerFinished("LUC_M_LeandraResurrect6")
AND
CharacterGetClosestPlayer(CHARACTER_LUC_M_Leandra,_Character)
AND
NOT DB_OnlyOnce("LUC_M_CommentAfterFight")
THEN
DB_OnlyOnce("LUC_M_CommentAfterFight");
Launch_PlayerComment(_Character,"LUC_M_CommentAfterFight");
CharacterSetOnStage(CHARACTER_LUC_M_Leandra,0);
Proc_UnfreezeAll();
ProcSetRelationToPlayers(CHARACTER_LUC_M_LeandraFight_DK,0);
ProcSetRelationToPlayers(CHARACTER_LUC_M_LeandraFight_DK2,0);
ProcSetRelationToPlayers(CHARACTER_LUC_M_LeandraFight_DK3,0);
CharacterApplyStatus(CHARACTER_LUC_M_LeandraFight_DK,"INVULNERABLE",-1.0,1);
CharacterApplyStatus(CHARACTER_LUC_M_LeandraFight_DK2,"INVULNERABLE",-1.0,1);
CharacterApplyStatus(CHARACTER_LUC_M_LeandraFight_DK3,"INVULNERABLE",-1.0,1);
QuestUpdate("LUC_M_Temple","Boss");

IF
CharacterUsedItem(_Player, ITEM_LUC_M_WandBlueprint)
AND
NOT DB_Companion(_Player)
AND
DB_OnlyOnce("LUC_M_WandBlueprint")
THEN
CharacterDisplayText(_Player, "LUC_M_CommentBlueprint_After");

IF
CharacterUsedItem(_Player, ITEM_LUC_M_WandBlueprint)
AND
NOT DB_Companion(_Player)
AND
NOT DB_OnlyOnce("LUC_M_WandBlueprint")
THEN
DB_OnlyOnce("LUC_M_WandBlueprint");
Launch_PlayerComment(_Player, "LUC_M_CommentBlueprint");

IF
CharacterUsedItem(_Player, ITEM_LUC_M_WandBlueprint)
AND
DB_Companion(_Player)
AND
_Player != CHARACTER_Wolgraff
THEN
CharacterDisplayText(_Player, "LUC_M_CommentBlueprint_After");

IF
CharacterUsedItem(_Player, ITEM_LUC_M_WandBlueprint)
AND
DB_Companion(_Player)
AND
_Player == CHARACTER_Wolgraff
AND
CharacterGetVarInteger(CHARACTER_Wolgraff,"VoiceBack",1)
THEN
CharacterDisplayText(_Player, "LUC_M_CommentBlueprint_After");

IF
CharacterUsedItem(_Player, ITEM_LUC_M_WandBlueprint)
AND
DB_Companion(_Player)
AND
_Player == CHARACTER_Wolgraff
AND
CharacterGetVarInteger(CHARACTER_Wolgraff,"VoiceBack",0)
THEN
CharacterDisplayText(_Player, "LUC_M_CommentBlueprint_After_WolgraffNarrator");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
