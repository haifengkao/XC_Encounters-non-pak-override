Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetDefaultThreatenedDialog(CHARACTER_DAF_Grutilda, "Default_Threatened_EVIL");

DB_IsNotMessingAround(CHARACTER_DAF_Grutilda);

//REGION Databases
DB_DAF_BO_Human(CHARACTER_DAF_BO_ArmoryKeyGuard);
DB_DAF_BO_Human(CHARACTER_DAF_BO_FamilyHouseGuard_01);
DB_DAF_BO_Human(CHARACTER_DAF_BO_FamilyHouseGuard_02);
DB_DAF_BO_Human(CHARACTER_DAF_BO_GarrickGuard_01);
DB_DAF_BO_Human(CHARACTER_DAF_BO_GarrickGuard_02);
DB_DAF_BO_Human(CHARACTER_DAF_BO_GarrickGuard_03);
DB_DAF_BO_Human(CHARACTER_DAF_HumanGuard1);
DB_DAF_BO_Human(CHARACTER_DAF_HumanGuard2);
DB_DAF_BO_Human(CHARACTER_DAF_BO_GateGuard1);
DB_DAF_BO_Human(CHARACTER_DAF_Human_Lover);
DB_DAF_BO_Human(CHARACTER_DAF_Campfire_Human);
DB_DAF_BO_Human(CHARACTER_DAF_BO_Human_Male_A_003);
DB_DAF_BO_Human(CHARACTER_DAF_BO_PrisonGuard1);
DB_DAF_BO_Human(CHARACTER_DAF_Leonard);
DB_DAF_BO_Human(CHARACTER_DAF_BO_GateGuard2);
DB_DAF_BO_Human(CHARACTER_DAF_BO_GateGuard3);
DB_DAF_BO_Human(CHARACTER_DAF_BeggingPriest);
DB_DAF_BO_Human(CHARACTER_DAF_BO_Human_Patrol);
DB_DAF_BO_Human(CHARACTER_DAF_Human_ZandalorHouse1);
DB_DAF_BO_Human(CHARACTER_DAF_Human_ZandalorHouse2);
DB_DAF_BO_Human(CHARACTER_DAF_WarehouseGuard);
DB_DAF_BO_Human(CHARACTER_DAF_WarehouseGuard2);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_ArmoryKeyOrc);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_Excecutioner);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_OrcTrader);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_ScoutOrc1);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_ScoutOrc2);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_TavernOrc1);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_TavernOrc2);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_TavernOrc3);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_TavernOrc4);
DB_DAF_BO_Orc(CHARACTER_DAF_Grutilda);
DB_DAF_BO_Orc(CHARACTER_DAF_OrcPoet);
DB_DAF_BO_Orc(CHARACTER_DAF_Orc_Male_A_000);
DB_DAF_BO_Orc(CHARACTER_DAF_Orc_Lover);
DB_DAF_BO_Orc(CHARACTER_DAF_Orc_ZandalorHouse1);
DB_DAF_BO_Orc(CHARACTER_DAF_Orc_ZandalorHouse2);
DB_DAF_BO_Orc(CHARACTER_DAF_Campfire_Orc);
DB_DAF_BO_Orc(CHARACTER_DAF_EquipmentTrader);
DB_DAF_BO_Orc(CHARACTER_DAF_MagicTrader);
DB_DAF_BO_Orc(CHARACTER_DAF_Ratcatcher);
DB_DAF_BO_Orc(CHARACTER_DAF_BO_George);
DB_DAF_BO_Orc(CHARACTER_DAF_Knight_Guard1);
DB_DAF_BO_Orc(CHARACTER_DAF_Knight_Guard2);
DB_DAF_BO_Orc(CHARACTER_DAF_Knight_Guard3);
DB_DAF_BO_Orc(CHARACTER_DAF_Knight_Guard4);
DB_DAF_BO_Orc(CHARACTER_DAF_Knight_Guard5);
DB_DAF_HumanWarPositions(CHARACTER_DAF_BO_GarrickGuard_01,TRIGGER_DAF_War_Poker1);
DB_DAF_HumanWarPositions(CHARACTER_DAF_BO_GarrickGuard_02,TRIGGER_DAF_War_Poker2);
DB_DAF_HumanWarPositions(CHARACTER_DAF_BO_GarrickGuard_03,TRIGGER_DAF_War_Poker3);
DB_DAF_HumanWarPositions(CHARACTER_DAF_BO_PrisonGuard1,TRIGGER_DAF_War_CellarGuard);
DB_DAF_HumanWarPositions(CHARACTER_DAF_HumanGuard1,TRIGGER_DAF_War_HumanGuard1);
DB_DAF_HumanWarPositions(CHARACTER_DAF_HumanGuard2,TRIGGER_DAF_War_HumanGuard2);
DB_DAF_OrcWarPositions(CHARACTER_DAF_BO_ScoutOrc1,TRIGGER_DAF_War_Scout1);
DB_DAF_OrcWarPositions(CHARACTER_DAF_BO_ScoutOrc2,TRIGGER_DAF_War_Scout2);
DB_DAF_OrcWarPositions(CHARACTER_DAF_BO_TavernOrc1,TRIGGER_DAF_War_Tavern1);
DB_DAF_OrcWarPositions(CHARACTER_DAF_BO_TavernOrc2,TRIGGER_DAF_War_Tavern2);
DB_DAF_OrcWarPositions(CHARACTER_DAF_BO_TavernOrc3,TRIGGER_DAF_War_Tavern3);
DB_DAF_OrcWarPositions(CHARACTER_DAF_BO_TavernOrc4,TRIGGER_DAF_War_Tavern4);
DB_DAF_DreadOrcs(CHARACTER_DAF_Grutilda,CHARACTER_DAF_DreadOrc_Grutilda);
DB_DAF_DreadOrcs(CHARACTER_DAF_BO_ScoutOrc1,CHARACTER_DAF_DreadOrc_Male1);
DB_DAF_DreadOrcs(CHARACTER_DAF_BO_ScoutOrc2,CHARACTER_DAF_DreadOrc_Male2);
DB_DAF_DreadOrcs(CHARACTER_DAF_BO_TavernOrc1,CHARACTER_DAF_DreadOrc_Male3);
DB_DAF_DreadOrcs(CHARACTER_DAF_BO_TavernOrc2,CHARACTER_DAF_DreadOrc_Male4);
DB_DAF_DreadOrcs(CHARACTER_DAF_BO_TavernOrc3,CHARACTER_DAF_DreadOrc_Male5);
DB_DAF_DreadOrcs(CHARACTER_DAF_BO_TavernOrc4,CHARACTER_DAF_DreadOrc_Male6);
DB_DAF_WarExtras(CHARACTER_DAF_Ratcatcher,TRIGGER_DAF_War_Ratcatcher);
DB_DAF_WarExtras(CHARACTER_DAF_BO_Excecutioner,TRIGGER_DAF_War_Exec);
DB_DAF_WarExtras(CHARACTER_DAF_BeggingPriest,TRIGGER_DAF_War_Priestess);
DB_DAF_WarExtras(CHARACTER_DAF_EquipmentTrader,TRIGGER_DAF_War_Trader);
DB_DAF_WarExtras(CHARACTER_DAF_BO_OrcTrader,TRIGGER_DAF_War_BackAlleyTrader);
DB_DAF_WarExtras(CHARACTER_DAF_BO_Human_Male_A_003,TRIGGER_DAF_War_DruidHouse);
//END_REGION

//REGION Comments zone
DB_PlayerComments("DAF_BO_CommentKilledAllOrcs","DAF_BO_CommentKilledAllOrcs_1_1",1,1);
DB_PlayerComments("DAF_BO_CommentKilledAllOrcs","DAF_BO_CommentKilledAllOrcs_2_1",2,1);
DB_PlayerComments("DAF_BO_CommentKilledAllOrcs","DAF_BO_CommentKilledAllOrcs_2_2",2,2);
DB_PlayerComments("DAF_BO_CommentKilledAllOrcsNotByPlayer","DAF_BO_CommentKilledAllOrcsNotByPlayer_1_1",1,1);
DB_PlayerComments("DAF_BO_CommentKilledAllOrcsNotByPlayer","DAF_BO_CommentKilledAllOrcsNotByPlayer_2_1",2,1);
DB_PlayerComments("DAF_BO_CommentKilledAllOrcsNotByPlayer","DAF_BO_CommentKilledAllOrcsNotByPlayer_2_2",2,2);
DB_PlayerComments("DAF_Comment_TuskPickedUp","DAF_Comment_TuskPickedUp_1_1",1,1);
DB_PlayerComments("DAF_Comment_TuskPickedUp","DAF_Comment_TuskPickedUp_2_1",2,1);
DB_PlayerComments("DAF_Comment_TuskPickedUp","DAF_Comment_TuskPickedUp_2_2",2,2);
DB_PlayerComments("DAF_Comment_UnknownTuskPickedUp","DAF_Comment_UnknownTuskPickedUp_1_1",1,1);
DB_PlayerComments("DAF_Comment_UnknownTuskPickedUp","DAF_Comment_UnknownTuskPickedUp_2_1",2,1);
DB_PlayerComments("DAF_Comment_UnknownTuskPickedUp","DAF_Comment_UnknownTuskPickedUp_2_2",2,2);
DB_PlayerComments("DAF_CommentFoundShaft","DAF_CommentFoundShaft_1_1",1,1);
DB_PlayerComments("DAF_CommentFoundShaft","DAF_CommentFoundShaft_1_2",1,2);
DB_PlayerComments("DAF_CommentFoundShaft","DAF_CommentFoundShaft_2_1",2,1);
DB_PlayerComments("DAF_CommentFoundShaft","DAF_CommentFoundShaft_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_DAF_Area_Comment_Shaft,"DAF_CommentFoundShaft");
//END_REGION

//REGION Counter
KillCounter("DAF_BO_Orc",25);
PROC_DAF_InitCounterOrc();
//END_REGION

ItemToContainer(ITEM_DFD_KT_GarrickLetter,ITEM_DFD_KT_GarrickCorpse,-1);
ItemToContainer(ITEM_DAF_OrcTusk,ITEM_DFD_KT_GarrickCorpse,-1);
KBSECTION
IF
DB_DAF_BO_Human(_Character)
THEN
DB_NoRunAway(_Character);
NoReflectionDialogOnKill(_Character);
DB_CustomAttackDialog(_Character,"HuntersEdgeImmaculate_I_attack_you");
DB_CustomWarningDialog(_Character,"HuntersEdgeImmaculate_please_dont_attack");

IF
DB_DAF_BO_Orc(_Character)
THEN
DB_NoRunAway(_Character);
DB_CustomAttackDialog(_Character,"HuntersEdgeOrc_I_attack_you");
DB_CustomWarningDialog(_Character,"HuntersEdgeOrc_please_dont_attack");
NoReflectionDialogOnKill(_Character);

IF
DB_DAF_DreadOrcs(_,_Character)
THEN
DB_NoRunAway(_Character);
DB_CustomAttackDialog(_Character,"HuntersEdgeOrc_I_attack_you");
DB_CustomWarningDialog(_Character,"HuntersEdgeOrc_please_dont_attack");
NoReflectionDialogOnKill(_Character);

IF
DAF_War(1)
THEN
ProcTriggerUnregisterForPlayers(TRIGGER_DAF_Area_Comment_Shaft);

IF
TextEventSet("war")
THEN
ItemToCharacter(ITEM_DAF_OrcTusk,CHARACTER_Player1);

//REGION Procedures

IF
CharacterDied(_Human)
AND
DB_DAF_BO_Human(_Human)
THEN
NOT DB_DAF_BO_Human(_Human);

PROC
PROC_DAF_BO_ChangeHumanDialog()
AND
DB_DAF_BO_Human(_Human)
THEN
ProcRemoveAllDialogEntriesForSpeaker(_Human);
DB_Dialogs(_Human,"DAF_BO_WarHuman");
//END_REGION

//REGION Unclean way to catch the elemental damage / human soldier killing the last orc
IF
CharacterAttackedByCharacter(_Badguy,_New)
AND
DB_DAF_BO_Orc(_Badguy)
AND
NOT DB_KnowAttackerOrc(_)
THEN
DB_KnowAttackerOrc(_New);

IF
CharacterAttackedByCharacter(_Badguy,_New)
AND
DB_DAF_BO_Orc(_Badguy)
AND
DB_KnowAttackerOrc(_Old)
THEN
NOT DB_KnowAttackerOrc(_Old);
DB_KnowAttackerOrc(_New);
//END_REGION

//REGION Starting of the war
IF
CharacterUsedItem(_,ITEM_DAF_Airvent)
THEN
ItemDisplayText(ITEM_DAF_Airvent,"DAF_Airvent");
QuestUpdate("DAF_War","Vent");

IF
ItemAddedToCharacter(ITEM_DAF_OrcTusk,_Character)
AND
_Character.DB_IsPlayer()
AND
NOT DB_OnlyOnce("ITEM_DAF_OrcTusk")
AND
DB_GlobalEvent("DAF_BloodstoneQuestStarted")
THEN
DB_OnlyOnce("ITEM_DAF_OrcTusk");
Launch_PlayerComment(_Character,"DAF_Comment_TuskPickedUp");
QuestUpdate("DAF_War","Body");

IF
ItemAddedToCharacter(ITEM_DAF_OrcTusk,_Character)
AND
_Character.DB_IsPlayer()
AND
NOT DB_OnlyOnce("ITEM_DAF_OrcTusk")
AND
NOT DB_GlobalEvent("DAF_BloodstoneQuestStarted")
THEN
DB_OnlyOnce("ITEM_DAF_OrcTusk");
Launch_PlayerComment(_Character,"DAF_Comment_UnknownTuskPickedUp");
QuestAdd("DAF_War");
QuestUpdate("DAF_War","StartBody");

IF
DialogStartRequested(CHARACTER_DAF_BO_George,_Player)
AND
ItemIsInPartyInventory(ITEM_DAF_OrcTusk,_Player,0,1)
THEN
GlobalSetEvent("DAF_GeorgeTuskFound");

IF
DialogEnded("DAF_BO_George",_)
THEN
GlobalClearEvent("DAF_GeorgeTuskFound");

IF
DialogStartRequested(CHARACTER_DAF_Leonard,_Player)
AND
ItemIsInPartyInventory(ITEM_DAF_OrcTusk,_Player,0,1)
THEN
GlobalSetEvent("DAF_LeonardTuskFound");

IF
DialogEnded("DAF_Leonard",_)
THEN
GlobalClearEvent("DAF_LeonardTuskFound");

IF
DialogCharacterEventSet("DAF_TakeGeorgeTusk",_Player,_)
AND
ItemIsInPartyInventory(ITEM_DAF_OrcTusk,_Player,1,1)
THEN
NOOP(1);

IF
DialogEnded("DAF_Leonard",_)
AND
DB_GlobalEvent("DAF_LeonardKnowsTruth")
THEN
AutoSave();
ProcCharacterMoveToTrigger(CHARACTER_DAF_Leonard,TRIGGER_DAF_Warehouse_StandOnLid,1,"DAF_LeonardRallyTroops");
ProcRemoveDialogEntryForSpeaker(CHARACTER_DAF_Leonard,"DAF_Leonard");
CharacterSetHasDialog(CHARACTER_DAF_Leonard,0);
CharacterSetHasDefaultDialog(CHARACTER_DAF_Leonard,0);

IF
CharacterAttackedByCharacter(CHARACTER_DAF_Grutilda, _)
AND
DB_GlobalEvent("DAF_LeonardKnowsTruth")
THEN
CharacterPurgeQueue(CHARACTER_DAF_Leonard);
CharacterPurgeQueue(CHARACTER_DAF_Grutilda);
ProcClearStoryMove(CHARACTER_DAF_Leonard);
ProcClearStoryMove(CHARACTER_DAF_Grutilda);
ProcTransformToDreadOrcs();
PROC_DAF_StartTheWar();

IF
CharacterEvent(CHARACTER_DAF_Leonard,"DAF_LeonardRallyTroops")
THEN
CharacterDisplayText(CHARACTER_DAF_Leonard,"DAF_LeonardRallyTroops");
TimerLaunch("DAF_LeonardRunOutside",2000);

IF
TimerFinished("DAF_LeonardRunOutside")
THEN
ProcCharacterMoveToTrigger(CHARACTER_DAF_Leonard,TRIGGER_DAF_War_Leonard,1,"DAF_LeonardCallGrutilda");
CharacterLookAtCharacter(CHARACTER_DAF_MagicTrader,CHARACTER_DAF_Leonard);
TimerLaunch("DAF_HumansWarpositions",3000);

IF
TimerFinished("DAF_HumansWarpositions")
AND
DB_DAF_HumanWarPositions(_Character,_Trigger)
THEN
CharacterFlushQueue(_Character);
DialogRequestStopForCharacter(_Character);
ProcRemoveAllDialogEntriesForSpeaker(_Character);
CharacterSetHasDialog(_Character,0);
CharacterSetHasDefaultDialog(_Character,0);
CharacterSetEvent(_Character,"prewar");
ProcCharacterMoveToTrigger(_Character,_Trigger,1,"arrivedatwarpos");

IF
CharacterEvent(_Character,"arrivedatwarpos")
AND
DB_DAF_HumanWarPositions(_Character,_Trigger)
THEN
CharacterLookFromTrigger(_Character,_Trigger,1);

IF
CharacterEvent(CHARACTER_DAF_Leonard,"DAF_LeonardCallGrutilda")
AND
DB_Dead(CHARACTER_DAF_Grutilda)
THEN
CharacterDisplayText(CHARACTER_DAF_Leonard,"DAF_WarGrutildaDead");
CharacterLookFromTrigger(CHARACTER_DAF_Leonard,TRIGGER_DAF_War_Leonard,0);
PROC_DAF_StartTheWar();


IF
CharacterEvent(CHARACTER_DAF_Leonard,"DAF_LeonardCallGrutilda")
AND
NOT DB_Dead(CHARACTER_DAF_Grutilda)
THEN
CharacterLookAtCharacter(CHARACTER_DAF_Leonard,CHARACTER_DAF_Grutilda);
DialogStartOneSpeakerDialog("DAF_AD_CallingGrutilda",CHARACTER_DAF_Leonard);
DialogRequestStopForCharacter(CHARACTER_DAF_Grutilda);
CharacterPurgeQueue(CHARACTER_DAF_Grutilda);
ProcCharacterMoveToTrigger(CHARACTER_DAF_Grutilda,TRIGGER_DAF_War_Grutilda,0,"DAF_GrutildaArrived");
ProcRemoveAllDialogEntriesForSpeaker(CHARACTER_DAF_Grutilda);
CharacterSetHasDialog(CHARACTER_DAF_Grutilda,0);
CharacterSetHasDefaultDialog(CHARACTER_DAF_Grutilda,0);

IF
CharacterEvent(CHARACTER_DAF_Grutilda,"DAF_GrutildaArrived")
THEN
CharacterLookFromTrigger(CHARACTER_DAF_Grutilda,TRIGGER_DAF_War_Grutilda,0);
DialogRequestStopForCharacter(CHARACTER_DAF_Leonard);
DialogStartTwoSpeakerDialog("DAF_AD_LeonardTellsTruthToGrutilda",CHARACTER_DAF_Leonard,CHARACTER_DAF_Grutilda);
Proc_DAF_OrcWarPositions();

PROC
Proc_DAF_OrcWarPositions()
AND
DB_DAF_OrcWarPositions(_Character,_Trigger)
THEN
CharacterFlushQueue(_Character);
DialogRequestStopForCharacter(_Character);
CharacterSetEvent(_Character,"prewar");
ProcRemoveAllDialogEntriesForSpeaker(_Character);
CharacterSetHasDialog(_Character,0);
CharacterSetHasDefaultDialog(_Character,0);
ProcCharacterMoveToTrigger(_Character,_Trigger,1,"arrivedatwarpos");

IF
CharacterEvent(_Character,"arrivedatwarpos")
AND
DB_DAF_OrcWarPositions(_Character,_Trigger)
THEN
CharacterLookFromTrigger(_Character,_Trigger,1);

IF
AutomatedDialogEnded("DAF_AD_LeonardTellsTruthToGrutilda", _ID)
AND
DB_CurrentLevel("DarkForest")
AND
NOT DAF_War(1)
THEN
ProcTransformToDreadOrcs();
TimerLaunch("TransformingToDreadOrcs",500);

IF
TimerFinished("TransformingToDreadOrcs")
THEN
CharacterAttackCharacter(CHARACTER_DAF_DreadOrc_Grutilda, CHARACTER_DAF_Leonard);

IF
CharacterAttackedByCharacter(CHARACTER_DAF_Leonard,CHARACTER_DAF_DreadOrc_Grutilda)
AND
NOT DAF_War(1)
THEN
CharacterDie(CHARACTER_DAF_Leonard,0);
PROC_DAF_StartTheWar();

PROC
PROC_DAF_StartTheWar()
THEN
CharacterSetRelationFactionToFaction("DAF_VillageBO_Human","DAF_VillageBO_Orc",0);
CharacterSetRelationFactionToFaction("DAF_VillageBO_Orc","DAF_VillageBO_Human",0);
SetRelationFactionToPlayers("DAF_VillageBO_Orc",0);
CharacterSetRelationFactionToFaction("DAF_VillageBO_Orc","DAF_VillageBO_GateGuard",0);
CharacterSetRelationFactionToFaction("DAF_VillageBO_GateGuard","DAF_VillageBO_Orc",0);
PROC_DAF_BO_ChangeHumanDialog();
ProcSetAllDAFVillageToWar();
DAF_War(1);
QuestUpdate("DAF_War","War");
ProcMoveWarExtras();
GlobalSetEvent("DAF_WarIsOn");

PROC 
ProcMoveWarExtras()
AND
DB_DAF_WarExtras(_Character,_Trigger)
THEN
CharacterMoveToTrigger(_Character,_Trigger,1,"");

PROC
ProcSetAllDAFVillageToWar()
AND
DB_DAF_BO_Human(_Character)
THEN
CharacterSetEvent(_Character,"war");

PROC
ProcSetAllDAFVillageToWar()
AND
DB_DAF_BO_Orc(_Character)
THEN
CharacterSetEvent(_Character,"war");

IF
DAF_War(1)
AND
NOT DB_Dead(CHARACTER_DAF_Hershel)
THEN
DialogRequestStopForCharacter(CHARACTER_DAF_Hershel);
CharacterSetHasDialog(CHARACTER_DAF_Hershel, 0);
CharacterDisappearOutOfSight(CHARACTER_DAF_Hershel,0, 1, "", 1);

IF
CharacterDied(_Orc)
AND
DB_DAF_BO_Orc(_Orc)
THEN
NOT DB_DAF_BO_Orc(_Orc);
Proc_CheckEndOfDAFWar();

//END_REGION
//REGION Dread Orc Transformation
IF
DB_DAF_DreadOrcs(_,_DreadOrc)
THEN
CharacterSetOnStage(_DreadOrc,0);

PROC
ProcTransformToDreadOrcs()
AND
DB_DAF_DreadOrcs(_Orig,_Dread)
AND
CharacterGetPosition(_Orig,_X,_Y,_Z)
THEN
NOT DB_DAF_BO_Orc(_Orig);
DB_DAF_BO_Orc(_Dread);
Poof(_Orig);
CharacterSetOnStage(_Dread,1);
CharacterTeleportToPosition(_Dread,_X,_Y,_Z,"");
PlayEffectAtPosition("FX_Skills_Air_Storm_Impact_A",_X,_Y,_Z);
NOT DB_DAF_DreadOrcs(_Orig,_Dread);
QuestUpdate("DAF_War","Transformation");
//END_REGION
//REGION Ending of the war
PROC
Proc_CheckEndOfDAFWar()
AND
NOT DB_DAF_BO_Orc(_)
AND
DAF_War(1)
AND
DB_KnowAttackerOrc(_Buddy)
AND
NOT _Buddy.DB_IsPlayer()
AND
CharacterGetClosestPlayer(_Buddy,_Player)
THEN
Launch_PlayerComment(_Player,"DAF_BO_CommentKilledAllOrcsNotByPlayer");
GlobalSetEvent("DAF_BO_WarOver");
ProcDAFWarDone();
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_AfterWar",_Player);

PROC
Proc_CheckEndOfDAFWar()
AND
NOT DB_DAF_BO_Orc(_)
AND
DAF_War(1)
AND
DB_KnowAttackerOrc(_Player)
AND
_Player.DB_IsPlayer()
THEN
Launch_PlayerComment(_Player,"DAF_BO_CommentKilledAllOrcs");
GlobalSetEvent("DAF_BO_WarOver");
ProcDAFWarDone();
QuestUpdate("HOM_Starstones", "OnCausingWarInHuntersEdge");
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_AfterWar",_Player);

PROC	
ProcDAFWarDone()
AND
NOT DB_DAF_BO_Human(_)
THEN
QuestUpdate("DAF_War","FinishPlayer");
QuestClose("DAF_War");
//END_REGION

//REGION Aftermath
IF
	GlobalEventSet("DAF_BO_HumanLeaving")
THEN
	CHARACTER_DAF_Leonard.boLeaveVillage();

IF 
	DialogEnded("DAF_BO_WarHuman",_)
AND
	CHARACTER_DAF_Leonard.boLeaveVillage()
AND
	DB_DAF_BO_Human(_Human)
THEN
	ItemUnlockAndOpen(ITEM_DAF_DoorVillageEntrance1);
	CharacterSetHasDialog(_Human,0);
	CharacterSetHasDefaultDialog(_Human,0);
	ProcCharacterMoveToTrigger(_Human,TRIGGER_DAF_BO_HumanLeaving,1,"DAF_BO_GoingAway");
	QuestUpdate("DAF_War","FinishHuman");
	QuestClose("DAF_War");

IF
	CharacterEvent(_Human,"DAF_BO_GoingAway")
THEN
	CharacterDisappearOutOfSight(_Human,180,1,"",1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
