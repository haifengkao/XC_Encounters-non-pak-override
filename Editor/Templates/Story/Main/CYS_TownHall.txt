Version 1
SubGoalCombiner SGC_AND
INITSECTION
TriggerRegisterForCharacter(TRIGGER_CYS_Area_CecilLibrary,CHARACTER_CYS_Charlene);
SetDefaultThreatenedDialog(CHARACTER_CYS_OrcLibrarian, "Default_Threatened");
KBSECTION
//REGION CECIL GOES UPSTAIRS
IF
DialogEnded("CYS_Cecil",_)
AND
CharacterGetVarInteger(CHARACTER_CYS_Cecil,"goUpstairs",1)
THEN
CharacterSetVarInteger(CHARACTER_CYS_Cecil,"goUpstairs",0);
CharacterSetHasDialog(CHARACTER_CYS_Cecil,0);
ProcCharacterMoveToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_Point_Cecil_GoDown,0,"CYS_CecilIsUp");

IF
CharacterEvent(CHARACTER_CYS_Cecil,"CYS_CecilIsUp")
THEN
CharacterTeleportToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_PointCecilLibrary,"");
PROC_CYS_CheckVictoriaDead();
//END_REGION

//REGION CECIL SHOWS LIBRARY
PROC
PROC_CYS_CheckVictoriaDead()
AND
NOT DB_Dead(CHARACTER_CYS_OrcLibrarian)
THEN
CHARACTER_CYS_Cecil.ShowLibrary();
DB_OneShotPlayerTrigger(TRIGGER_CYS_Area_CecilLibrary);

IF
CharacterLeftTrigger(_Char,TRIGGER_CYS_REGION_Townhall_Office)
AND
NOT DB_InRegion(_,TRIGGER_CYS_REGION_Townhall_Office)
AND
CHARACTER_CYS_Cecil.ShowLibrary()
THEN
TimerLaunch("CYS_CheckLibraryScene",100);

IF
TimerFinished("CYS_CheckLibraryScene")
AND
NOT DB_InRegion(_,TRIGGER_CYS_REGION_Townhall_1)
THEN
//noone there in the office or upstairs, cancel the scene
ProcTriggerUnregisterForPlayers(TRIGGER_CYS_Area_CecilLibrary);
NOT DB_OneShotPlayerTrigger(TRIGGER_CYS_Area_CecilLibrary);
NOT CHARACTER_CYS_Cecil.ShowLibrary();
ProcCharacterMoveToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_Point_Cecil_GoUp,0,"CYS_CecilIsDown");

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_CYS_Area_CecilLibrary)
AND
NOT DB_CYSCecilLibraryPlayer(_)
THEN
NOT CHARACTER_CYS_Cecil.ShowLibrary();
DB_CYSCecilLibraryPlayer(_Player);
TimerLaunch("CYSCecilDoSpeech",500);

IF
TimerFinished("CYSCecilDoSpeech")
AND
DB_CYSCecilLibraryPlayer(_Player)
THEN
NOT DB_CYSCecilLibraryPlayer(_Player);
PROC_MendatoryDialog("CYS_CecilLibrary", CHARACTER_CYS_Cecil, _Player);

IF
DialogEnded("CYS_CecilLibrary",_)
THEN
ProcCharacterMoveToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_Point_Cecil_GoUp,0,"CYS_CecilIsDown");

IF
CharacterEvent(CHARACTER_CYS_Cecil,"CYS_CecilIsDown")
THEN
NOT CHARACTER_CYS_Cecil.ShowLibrary();
CharacterTeleportToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_Point_Cecil_GoDown,"");
ProcCharacterMoveToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_PointCecil1,0,"CYS_CecilOffice");

IF
CharacterEvent(CHARACTER_CYS_Cecil,"CYS_CecilOffice")
THEN
CharacterSetHasDialog(CHARACTER_CYS_Cecil,1);
//END_REGION

//REGION CECIL SEES VICTORIA DEAD
PROC
PROC_CYS_CheckVictoriaDead()
AND
DB_Dead(CHARACTER_CYS_OrcLibrarian)
THEN
TriggerUnregisterForCharacter(TRIGGER_CYS_Area_CecilLibrary,CHARACTER_CYS_Charlene);
CharacterSetHasDialog(CHARACTER_CYS_Cecil,1);
CharacterSetVarFixedString(CHARACTER_CYS_Cecil,"currentState","State_VictoriaDead");
CHARACTER_CYS_Cecil.Mourning();
CharacterSetVarInteger(CHARACTER_CYS_Cecil,"CYS_CecilSawVictoriaDead",1);
//END_REGION

//REGION CHARLENE SEES VICTORIA DEAD
IF
CharacterEnteredTrigger(CHARACTER_CYS_Charlene,TRIGGER_CYS_Area_CecilLibrary)
AND
DB_Dead(CHARACTER_CYS_OrcLibrarian)
AND
CharacterGetVarInteger(CHARACTER_CYS_Cecil, "goSeeVictoria", 1)
THEN
TriggerUnregisterForCharacter(TRIGGER_CYS_Area_CecilLibrary,CHARACTER_CYS_Charlene);
CharacterSetVarFixedString(CHARACTER_CYS_Charlene,"currentState","State_VictoriaDead");


IF
CharacterEnteredTrigger(CHARACTER_CYS_Charlene,TRIGGER_CYS_Area_CecilLibrary)
AND
DB_Dead(CHARACTER_CYS_OrcLibrarian)
AND
CharacterGetVarInteger(CHARACTER_CYS_Cecil, "goSeeVictoria", 0)
THEN
CharacterSetVarInteger(CHARACTER_CYS_Cecil, "goSeeVictoria", 1);
TriggerUnregisterForCharacter(TRIGGER_CYS_Area_CecilLibrary,CHARACTER_CYS_Charlene);
ProcCharacterMoveToTrigger(CHARACTER_CYS_Charlene,TRIGGER_CYS_Point_Cecil_GoUp,1,"CYS_CharleneUpstairs");

IF
CharacterEvent(CHARACTER_CYS_Charlene,"CYS_CharleneUpstairs")
THEN
CharacterTeleportToTrigger(CHARACTER_CYS_Charlene,TRIGGER_CYS_Point_Cecil_GoDown,"");
CharacterMoveToCharacter(CHARACTER_CYS_Charlene,CHARACTER_CYS_Cecil,1,"CYS_CharleneAtCecil",0);

IF
CharacterEvent(CHARACTER_CYS_Charlene,"CYS_CharleneAtCecil")
THEN
DialogStartTwoSpeakerDialog("CYS_AD_CharleneCecil",CHARACTER_CYS_Charlene,CHARACTER_CYS_Cecil);

IF
AutomatedDialogEnded("CYS_AD_CharleneCecil",_)
THEN
ProcCharacterMoveToTrigger(CHARACTER_CYS_Cecil,TRIGGER_CYS_Point_Cecil_GoDown,1,"CYS_CecilIsUp");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
