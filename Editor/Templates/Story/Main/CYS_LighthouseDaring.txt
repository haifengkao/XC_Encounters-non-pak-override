Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_CYS_LighthouseDare1,CHARACTER_CYS_LighthouseDare2,"CYS_LighthouseDaring");
DB_Dialogs(CHARACTER_CYS_Aureus,"CYS_Aureus");

CHARACTER_CYS_LighthouseDare1.FirstTime();
DB_LighthouseGuard(CHARACTER_CYS_LighthouseDare1,TRIGGER_CYS_PointLighthouseDaring3,TRIGGER_CYS_PointAureusMeeting1);
DB_LighthouseGuard(CHARACTER_CYS_LighthouseDare2,TRIGGER_CYS_PointLighthouseDaring4,TRIGGER_CYS_PointAureusMeeting2);

ItemToContainer(ITEM_CYS_LegionOrdersLighthouse,ITEM_CYS_CorpseLegionaireWithOrders,-1);


DB_PlayerComments("CYS_FoundKnownLighthouseLegion","CYS_FoundKnownLighthouseLegion_1_1",1,1);
DB_PlayerComments("CYS_FoundKnownLighthouseLegion","CYS_FoundKnownLighthouseLegion_2_1",2,1);
DB_PlayerComments("CYS_FoundKnownLighthouseLegion","CYS_FoundKnownLighthouseLegion_2_2",2,2);

DB_PlayerComments("CYS_FoundUnknownLighthouseLegion","CYS_FoundUnknownLighthouseLegion_1_1",1,1);
DB_PlayerComments("CYS_FoundUnknownLighthouseLegion","CYS_FoundUnknownLighthouseLegion_2_1",2,1);
DB_PlayerComments("CYS_FoundUnknownLighthouseLegion","CYS_FoundUnknownLighthouseLegion_2_2",2,2);

db_LighthouseLegionSecondScene(TRIGGER_CYS_Area_SecondLighthouseSoldiersScene_1);
db_LighthouseLegionSecondScene(TRIGGER_CYS_Area_SecondLighthouseSoldiersScene_2);
ProcTriggerRegisterForPlayers(TRIGGER_CYS_Area_SecondLighthouseSoldiersScene_1);
ProcTriggerRegisterForPlayers(TRIGGER_CYS_Area_SecondLighthouseSoldiersScene_2);

DB_PlayerComments("CYS_LighthouseKnowAboutLegionSecondScene","CYS_LighthouseKnowAboutLegionSecondScene_1_1",1,1);
DB_PlayerComments("CYS_LighthouseKnowAboutLegionSecondScene","CYS_LighthouseKnowAboutLegionSecondScene_2_1",2,1);
DB_PlayerComments("CYS_LighthouseKnowAboutLegionSecondScene","CYS_LighthouseKnowAboutLegionSecondScene_2_2",2,2);
DB_PlayerComments("CYS_LighthouseNotKnowAboutLegionSecondScene","CYS_LighthouseNotKnowAboutLegionSecondScene_1_1",1,1);
DB_PlayerComments("CYS_LighthouseNotKnowAboutLegionSecondScene","CYS_LighthouseNotKnowAboutLegionSecondScene_2_1",2,1);
DB_PlayerComments("CYS_LighthouseNotKnowAboutLegionSecondScene","CYS_LighthouseNotKnowAboutLegionSecondScene_2_2",2,2);

DB_PlayerComment_Trigger(TRIGGER_CYS_Area_ThirdLighthouseSoldiersScene_1,"CYS_ThirdLighthouseSoldiersScene");

DB_PlayerComments("CYS_ThirdLighthouseSoldiersScene","CYS_ThirdLighthouseSoldiersScene_1_1",1,1);
DB_PlayerComments("CYS_ThirdLighthouseSoldiersScene","CYS_ThirdLighthouseSoldiersScene_2_1",2,1);
DB_PlayerComments("CYS_ThirdLighthouseSoldiersScene","CYS_ThirdLighthouseSoldiersScene_2_2",2,2);
KBSECTION
//REGION THE WAY TO THE LIGHTHOUSE

//REGION POSSIBLE FIRST LEGION BODIES WITH ORDERS
IF
GameBookInterfaceClosed(ITEM_CYS_LegionOrdersLighthouse,_Player)
AND
_Player.DB_IsPlayer()
AND
NOT ITEM_CYS_LegionOrdersLighthouse.HasAlreadyBeenCommentedOn()
THEN
ITEM_CYS_LegionOrdersLighthouse.HasAlreadyBeenCommentedOn();
PROC_CYS_FoundLegionOrdersLighthouse(_Player);

PROC
PROC_CYS_FoundLegionOrdersLighthouse((CHARACTER)_Player)
AND
CharacterGetVarInteger(CHARACTER_CYS_Selenia,"CYS_ToldAboutLightHouseExpedition",1)
THEN
Launch_PlayerComment(_Player,"CYS_FoundKnownLighthouseLegion");
QuestUpdate("CYS_LightHouseGuards","Update2");

PROC
PROC_CYS_FoundLegionOrdersLighthouse((CHARACTER)_Player)
AND
CharacterGetVarInteger(CHARACTER_CYS_Selenia,"CYS_ToldAboutLightHouseExpedition",0)
THEN
CharacterSetVarInteger(CHARACTER_CYS_Selenia,"CYS_ToldAboutLightHouseExpedition",1);
Launch_PlayerComment(_Player,"CYS_FoundUnknownLighthouseLegion");
QuestAdd("CYS_LightHouseGuards");
QuestUpdate("CYS_LightHouseGuards","Update3");
//END_REGION

//REGION POSSIBLE SECOND LEGION BODIES PLAYERCOMMENT
PROC
Proc_CYS_ClearLighthouseLegionSecondSceneTriggers()
AND
db_LighthouseLegionSecondScene(_Trigger)
THEN
ProcTriggerUnregisterForPlayers(_Trigger);

IF
CharacterEnteredTrigger(_Player,_Trigger)
AND
db_LighthouseLegionSecondScene(_Trigger)
AND
CharacterGetVarInteger(CHARACTER_CYS_Selenia,"CYS_ToldAboutLightHouseExpedition",1)
THEN
Launch_PlayerComment(_Player,"CYS_LighthouseKnowAboutLegionSecondScene");
Proc_CYS_ClearLighthouseLegionSecondSceneTriggers();

IF
CharacterEnteredTrigger(_Player,_Trigger)
AND
db_LighthouseLegionSecondScene(_Trigger)
AND
NOT CharacterGetVarInteger(CHARACTER_CYS_Selenia,"CYS_ToldAboutLightHouseExpedition",1)
THEN
CharacterSetVarInteger(CHARACTER_CYS_Selenia,"CYS_ToldAboutLightHouseExpedition",1);
Launch_PlayerComment(_Player,"CYS_LighthouseKnowAboutLegionSecondScene");
Proc_CYS_ClearLighthouseLegionSecondSceneTriggers();
//END_REGION
//END_REGION

//REGION Skeleton Boss Fight And Using Key
IF
CharacterDied(CHARACTER_CYS_LighthouseSkeletonBoss)
THEN
ItemUnLock(ITEM_CYS_LighthouseDoor);
//END_REGION

//REGION MEETING AFTER LIGHTHOUSE
IF
DialogEnded("CYS_SamsonGhost",_)
AND
NOT TRIGGER_CYS_AreaLighthouseDaring.Activated()
THEN
TRIGGER_CYS_AreaLighthouseDaring.Activated();
GlobalSetEvent("CYS_WentToLighthouse");
DB_OneShot_DialogTrigger(TRIGGER_CYS_AreaLighthouseDaring,"CYS_LighthouseDaring",CHARACTER_CYS_LighthouseDare1,CHARACTER_CYS_LighthouseDare2);
QuestUpdate("CYS_LightHouseGuards","Update5");
//END_REGION

//REGION TELL THE GUARDS
IF
GlobalEventSet("CYS_TellLighthouseGuards")
THEN
CHARACTER_CYS_LighthouseDare1.GoesToSelenia();

IF
DialogEnded("CYS_LighthouseDaring",_)
AND
CHARACTER_CYS_LighthouseDare1.GoesToSelenia()
THEN
NOT CHARACTER_CYS_LighthouseDare1.GoesToSelenia();
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare1,0);
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare2,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_LighthouseDare1,"CYS_LighthouseDaring");
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Selenia,"CYS_Selenia");
CharacterDisappearOutOfSight(CHARACTER_CYS_LighthouseDare1,90,1,"GoesToSelenia",1);
CharacterDisappearOutOfSight(CHARACTER_CYS_LighthouseDare2,90,1,"GoesToSelenia",1);

IF
CharacterEvent(_LighthouseGuard,"GoesToSelenia")
AND
DB_LighthouseGuard(_LighthouseGuard,_,_Trigger)
THEN
_LighthouseGuard.CYS_LighthouseGuardAtSelenia();
CharacterAppearAtTrigger(_LighthouseGuard,_Trigger,0);
CharacterSetVarFixedString(_LighthouseGuard,"currentState","State_WaitWithSelenia");

IF
CHARACTER_CYS_LighthouseDare1.CYS_LighthouseGuardAtSelenia()
AND
CHARACTER_CYS_LighthouseDare2.CYS_LighthouseGuardAtSelenia()
THEN
DB_Dialogs(CHARACTER_CYS_LighthouseDare1,CHARACTER_CYS_LighthouseDare2,CHARACTER_CYS_Selenia,"CYS_AureusGuards");
//END_REGION

//REGION DONT TELL THE GUARDS
IF
GlobalEventSet("CYS_DontTellLighthouseGuards")
THEN
CHARACTER_CYS_LighthouseDare1.GoesToLighthouse();

IF
DialogEnded("CYS_LighthouseDaring",_)
AND
CHARACTER_CYS_LighthouseDare1.GoesToLighthouse()
THEN
NOT CHARACTER_CYS_LighthouseDare1.GoesToLighthouse();
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare1,0);
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare2,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_LighthouseDare1,"CYS_LighthouseDaring");
ProcCharacterMoveToTrigger(CHARACTER_CYS_LighthouseDare1,TRIGGER_CYS_PointLighthouseDaring3,1,"GoesToLighthouse");
ProcCharacterMoveToTrigger(CHARACTER_CYS_LighthouseDare2,TRIGGER_CYS_PointLighthouseDaring4,1,"GoesToLighthouse");
QuestClose("CYS_LightHouseGuards");

IF
CharacterEvent(_Character,"GoesToLighthouse")
THEN
ItemOpen(ITEM_CYS_LighthouseDoor);
ProcCharacterMoveToTrigger(_Character,TRIGGER_CYS_Point_Lighthouse_Up,1,"GoesToLighthouse2");

IF
CharacterEvent(_Character,"GoesToLighthouse2")
THEN
CharacterSetOnStage(_Character,0);
_Character.hidden();

IF
CHARACTER_CYS_LighthouseDare1.hidden()
AND
CHARACTER_CYS_LighthouseDare2.hidden()
THEN
DB_OneShotPlayerTrigger(TRIGGER_CYS_AreaLighthouseGuards);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_CYS_AreaLighthouseGuards)
THEN
CharacterSetOnStage(CHARACTER_CYS_LighthouseDare1,1);
CharacterSetOnStage(CHARACTER_CYS_LighthouseDare2,1);
ProcCharacterMoveToTrigger(CHARACTER_CYS_LighthouseDare1,TRIGGER_CYS_PointLighthouseDaring3,1,"GoesToLighthouse3");
ProcCharacterMoveToTrigger(CHARACTER_CYS_LighthouseDare2,TRIGGER_CYS_PointLighthouseDaring4,1,"GoesToLighthouse3");
CharacterDisplayText(CHARACTER_CYS_LighthouseDare1,"CYS_Lighthouse_1");
CharacterDisplayText(CHARACTER_CYS_LighthouseDare2,"CYS_Lighthouse_2");

IF
CharacterEvent(_Character,"GoesToLighthouse3")
THEN
CharacterDisappearOutOfSight(_Character,100,1,"",1);
//END_REGION

//REGION MEETING WITH Selenia
IF
GlobalEventSet("CYS_HelpLighthouseGuards")
THEN
CHARACTER_CYS_LighthouseDare1.isRewarded();

IF
GlobalEventSet("CYS_AccuseLighthouseGuards")
THEN
CHARACTER_CYS_LighthouseDare1.isPunished();

IF
DialogEnded("CYS_AureusGuards",_)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_LighthouseDare1,"CYS_AureusGuards");
DB_Dialogs(CHARACTER_CYS_Selenia,"CYS_Selenia");
PROC_CYS_CheckLighthouseGuards();

PROC
PROC_CYS_CheckLighthouseGuards()
AND
CHARACTER_CYS_LighthouseDare1.isRewarded()
THEN
NOT CHARACTER_CYS_LighthouseDare1.isRewarded();
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare1,0);
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare2,0);
CharacterDisappearOutOfSight(CHARACTER_CYS_LighthouseDare1,270,1,"",1);
CharacterDisappearOutOfSight(CHARACTER_CYS_LighthouseDare2,270,1,"",1);
GoalCompleted;

PROC
PROC_CYS_CheckLighthouseGuards()
AND
CHARACTER_CYS_LighthouseDare1.isPunished()
THEN
NOT CHARACTER_CYS_LighthouseDare1.isPunished();
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare1,0);
CharacterSetHasDialog(CHARACTER_CYS_LighthouseDare2,0);
ProcCharacterMoveToTrigger(CHARACTER_CYS_LighthouseDare1,TRIGGER_CYS_Point_CookKitchen_Up,1,"CYS_LighthouseGuardInKitchen");
ProcCharacterMoveToTrigger(CHARACTER_CYS_LighthouseDare2,TRIGGER_CYS_Point_CookKitchen_Up,1,"CYS_LighthouseGuardInKitchen");

IF
CharacterEvent(CHARACTER_CYS_LighthouseDare1,"CYS_LighthouseGuardInKitchen")
THEN
DB_Dialogs(CHARACTER_CYS_LighthouseDare1, "CYS_Mallius");

IF
CharacterEvent(CHARACTER_CYS_LighthouseDare2,"CYS_LighthouseGuardInKitchen")
THEN
DB_Dialogs(CHARACTER_CYS_LighthouseDare2, "CYS_Lucia");

IF
CharacterEvent(_Character,"CYS_LighthouseGuardInKitchen")
THEN
CharacterTeleportToTrigger(_Character,TRIGGER_CYS_Point_CookKitchen_Down,"");
CharacterSetHasDialog(_Character,1);
CharacterSetVarFixedString(_Character,"currentState","State_AfterSelenia");
//END_REGION
EXITSECTION
NOT DB_LighthouseGuard(CHARACTER_CYS_LighthouseDare1,TRIGGER_CYS_PointLighthouseDaring3,TRIGGER_CYS_PointAureusMeeting1);
NOT DB_LighthouseGuard(CHARACTER_CYS_LighthouseDare2,TRIGGER_CYS_PointLighthouseDaring4,TRIGGER_CYS_PointAureusMeeting2);
ENDEXITSECTION
ParentTargetEdge "DOSStart"
