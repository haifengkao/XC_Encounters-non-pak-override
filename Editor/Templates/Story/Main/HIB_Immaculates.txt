Version 1
SubGoalCombiner SGC_AND
INITSECTION
ItemToCharacter(ITEM_HIB_ImmaculateCharmer_Wand,CHARACTER_HIB_ImmaculateCharmer2);

DB_OneShot_DialogTrigger_NewSystem(TRIGGER_HIB_Area_ImmaculatesInfo,"HIB_ImmaculateInfo_1",CHARACTER_HIB_ImmaculateInfo1);
DB_HIB_ImmaculatesGroups("HIB_ImmaculateInfo_1",CHARACTER_HIB_ImmaculateInfo1,"HIB_ImmaculateInfo_1");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateInfo_1",CHARACTER_HIB_ImmaculateInfo2,"HIB_Immaculates");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateInfo_1",CHARACTER_HIB_ImmaculateInfo3,"HIB_Immaculates");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateInfo_1",CHARACTER_HIB_ImmaculateInfo4,"HIB_Immaculates");

DB_OneShot_DialogTrigger_NewSystem(TRIGGER_HIB_Area_ImmaculatesBarter,"HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter1);
DB_HIB_ImmaculatesGroups("HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter1,"HIB_ImmaculateGood1");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter2,"HIB_ImmaculateGood2");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter3,"HIB_Immaculates");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter4,"HIB_Immaculates");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter5,"HIB_Immaculates");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateGood1",CHARACTER_HIB_ImmaculateBarter6,"");
DB_IsNotMessingAround(CHARACTER_HIB_ImmaculateBarter6);

DB_OneShot_DialogTrigger_NewSystem(TRIGGER_HIB_Area_ImmaculatesCharmer,"HIB_ImmaculateCharmer2",CHARACTER_HIB_ImmaculateCharmer2);
DB_HIB_ImmaculatesGroups("HIB_ImmaculateCharmer2",CHARACTER_HIB_ImmaculateCharmer1,"HIB_Immaculates");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateCharmer2",CHARACTER_HIB_ImmaculateCharmer2,"HIB_ImmaculateCharmer2");
DB_HIB_ImmaculatesGroups("HIB_ImmaculateCharmer2",CHARACTER_HIB_ShamblingMound1,"");

PROC_HIB_ImmaculatesInit();
KBSECTION
//REGION GENERAL
PROC
PROC_HIB_ImmaculatesInit()
AND
DB_HIB_ImmaculatesGroups(_,_Immaculate,_)
AND
_Immaculate != CHARACTER_HIB_ShamblingMound1
AND
_Immaculate != CHARACTER_HIB_ImmaculateBarter6
THEN
DB_IsNotMessingAround(_Immaculate);

PROC
PROC_HIB_ImmaculatesInit()
AND
DB_OneShot_DialogTrigger_NewSystem(_Trigger,_GroupID,_)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_)
AND
_Immaculate != CHARACTER_HIB_ShamblingMound1
THEN
SneakTriggerSpotter(_Trigger,_Immaculate);

IF
CharacterEnteredCombat(_Immaculate,_)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_)
AND
DB_OneShot_DialogTrigger_NewSystem(_Trigger,_GroupID,_Leader)
THEN
NOT DB_OneShot_DialogTrigger_NewSystem(_Trigger,_GroupID,_Leader);

IF
DialogCharacterEventSet("HIB_ImmaculatesGoFriendly",_Immaculate,_ID)
AND
DB_DialogNPCs(_ID,_Immaculate,1)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_Dialog)
THEN
CharacterSetVarInteger(_Immaculate,"HIB_ImmaculatesGoFriendly",0);
PROC_HIB_ImmaculatesGoFriendly(_GroupID);

PROC
PROC_HIB_ImmaculatesGoFriendly((STRING)_GroupID)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_Dialog)
AND
_Dialog!=""
THEN
DB_Dialogs(_Immaculate,_Dialog);

IF
DialogEnded(_Dialog,_ID)
AND
DB_DialogNPCs(_ID,_Immaculate,1)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_Dialog)
AND
CharacterGetVarInteger(_Immaculate,"HIB_ImmaculatesGoHostile",1)
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
CharacterSetVarInteger(_Immaculate,"HIB_ImmaculatesGoHostile",0);
PROC_HIB_ImmaculatesGoHostile(_GroupID);
PROC_HIB_ImmaculatesMoveTowardsPlayer(_Player,_GroupID);

PROC
PROC_HIB_ImmaculatesMoveTowardsPlayer((CHARACTER)_Player,(STRING)_GroupID)
AND
CharacterGetPosition(_Player,_X,_Y,_Z)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_)
THEN
CharacterMoveToPosition(_Immaculate,_X,_Y,_Z,1,"");

IF
CharacterEnteredCombat(_Immaculate,_)
AND
DB_HIB_ImmaculatesGroups(_,_Immaculate,_)
THEN
CharacterPurgeQueue(_Immaculate);


PROC
PROC_HIB_ImmaculatesGoHostile((STRING)_GroupID)
AND
DB_HIB_ImmaculatesGroups(_GroupID,_Immaculate,_)
THEN
DialogRequestStopForCharacter(_Immaculate);
CharacterSetFaction(_Immaculate,"HIB_Immaculates");
//END_REGION

//REGION BARTER
IF
GlobalEventSet("HIB_ImmaculatesBarterAccepted")
THEN
DB_BigTrader(CHARACTER_HIB_ImmaculateBarter2);
//END_REGION

//REGION CHARMER
IF
DialogEnded("HIB_ImmaculateCharmer2",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_ImmaculateCharmer2,"HIB_ImmaculateCharmersLeave",1)
THEN
CharacterSetVarInteger(CHARACTER_HIB_ImmaculateCharmer2,"HIB_ImmaculateCharmersLeave",0);
CharacterSetHasDialog(CHARACTER_HIB_ImmaculateCharmer1,0);
CharacterSetHasDialog(CHARACTER_HIB_ImmaculateCharmer2,0);
ProcCharacterMoveToTrigger(CHARACTER_HIB_ImmaculateCharmer1,TRIGGER_HIB_Point_ImmaculateCharmer1,0,"HIB_ImmaculateCharmerAtCamp");
ProcCharacterMoveToTrigger(CHARACTER_HIB_ImmaculateCharmer2,TRIGGER_HIB_Point_ImmaculateCharmer2,0,"HIB_ImmaculateCharmerAtCamp");
ProcCharacterMoveToTrigger(CHARACTER_HIB_ShamblingMound1,TRIGGER_HIB_Point_ShamblingMound2,1,"");
CharacterSetVarTrigger(CHARACTER_HIB_ShamblingMound1,"Anchor",TRIGGER_HIB_Point_ShamblingMound2);

IF
CharacterEvent(_Immaculate,"HIB_ImmaculateCharmerAtCamp")
THEN
CharacterSetVarFixedString(_Immaculate,"currentState","State_AtCamp");
CharacterSetHasDialog(_Immaculate,1);

IF
CharacterDied(CHARACTER_HIB_ShamblingMound1)
AND
DB_HIB_ImmaculatesGroups("HIB_ImmaculateCharmer2",_Immaculate,_)
THEN
PROC_HIB_ImmaculatesGoHostile("HIB_ImmaculateCharmer2");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
