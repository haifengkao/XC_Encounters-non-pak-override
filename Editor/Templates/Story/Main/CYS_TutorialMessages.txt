Version 1
SubGoalCombiner SGC_AND
INITSECTION
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_Drag);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_Camera);
TriggerRegisterForPlayers(TRIGGER_CYS_Box_TutSaveLoad);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_FireSurface);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_PoisonFire);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_GasPit);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_SneakAttack);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_PressurePlates);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_PartySplit);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_PartyJoin);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_Portrait);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_Journal);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_Map);
TriggerRegisterForPlayers(TRIGGER_CYS_TUT_SelectionActiveSearch);

DB_PlayerComments("CYS_Tut_CommentTraps","CYS_Tut_CommentTraps_1_1",1,1);
DB_PlayerComments("CYS_Tut_CommentTraps","CYS_Tut_CommentTraps_2_1",2,1);
DB_PlayerComments("CYS_Tut_CommentTraps","CYS_Tut_CommentTraps_2_2",2,2);

DB_PlayerComments("CYS_Tut_CommentEnd","CYS_Tut_CommentEnd_1_1",1,1);
DB_PlayerComments("CYS_Tut_CommentEnd","CYS_Tut_CommentEnd_2_1",2,1);
DB_PlayerComments("CYS_Tut_CommentEnd","CYS_Tut_CommentEnd_2_2",2,2);

DB_PlayerComment_Trigger(TRIGGER_CYS_BoxCommentEndOfTut,"CYS_Tut_CommentEnd");

DB_TutLockedChest(ITEM_CYS_Tut_ChestWithKey,ITEM_CYS_TUT_ChestKey);
DB_TutLockedChest(ITEM_CYS_TUT_RichChest,ITEM_CYS_Tut_RichChestKey);

DB_PlayerComments("CYS_Tut_Durability","CYS_Tut_Durability_1_1",1,1);
DB_PlayerComments("CYS_Tut_Durability","CYS_Tut_Durability_2_1",2,1);

DB_TUT_InitialLevel("Cyseal");

DB_TutorialScroll(ITEM_Cys_TUT_Scroll_Fireball);
DB_TutorialScroll(ITEM_Cys_TUT_Scroll_Fireball2);
DB_TutorialScroll(ITEM_Cys_TUT_Scroll_Rain);

DB_Tutorial_Arrowshaft(ITEM_Cys_TUT_ArrowShaft1);
DB_Tutorial_Arrowshaft(ITEM_Cys_TUT_ArrowShaft2);
KBSECTION
//REGION Combat Popup
IF				
CharacterTurnStarted(_Player)
AND
_Player.DB_IsPlayer()
THEN
Proc_CharCountHelper(_Player,"CombatTurnCounter");

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn>0
THEN
CharacterSendGlobalCombatCounter(_Player,_Turn);

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 1
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat");

IF
TutorialBoxClosed(_Player,"TUT_Combat")
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat_TurnOrder");

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 3
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat_TargetCycle");

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 4
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat_TacticalView");

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 5
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat_Guard");


//END_REGION 
IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_Journal)
THEN
PROC_CheckPlayTut(_Player,"TUT_QuestLog");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_Map)
THEN
PROC_CheckPlayTut(_Player,"TUT_Map");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_Drag)
THEN
PROC_CheckPlayTut(_Player,"TUT_Drag");

IF
CharacterEnteredTrigger((CHARACTER)_Player,_Trigger)
AND
DB_Subregion(_Trigger,_,_)
THEN
PROC_CheckPlayTut(_Player,"TUT_SelectionCursorMode_Controller");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_Camera)
THEN
PROC_CheckPlayTut(_Player,"TUT_Camera");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_Box_TutSaveLoad)
THEN
PROC_CheckPlayTut(_Player,"TUT_SaveLoad");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_Portrait)
AND
DB_SplitDone(1)
THEN
PROC_CheckPlayTut(_Player,"TUT_CompanionSwitch");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_GasPit)
THEN
PROC_CheckPlayTut(_Player,"TUT_GasPit");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_FireSurface)
THEN
PROC_CheckPlayTut(_Player,"TUT_FireSurface");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_SneakAttack)
AND
NOT DB_Combat(_Player,_)
THEN
PROC_CheckPlayTut(_Player,"TUT_SneakAttack");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_PressurePlates)
THEN
PROC_CheckPlayTut(_Player,"TUT_PressurePlates");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_PartySplit)
AND
CharacterGetOtherPlayer(_Player, _Other)
AND
CharacterGetReservedPeerID(_Player, _ID)
AND
CharacterGetReservedPeerID(_Other, _ID2)
AND
_ID == _ID2
THEN
DB_SplitDone(1);
PROC_CheckPlayTut(_Player,"TUT_CompanionSplit");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_PartyJoin)
AND
CharacterGetOtherPlayer(_Player, _Other)
AND
CharacterGetReservedPeerID(_Player, _ID)
AND
CharacterGetReservedPeerID(_Other, _ID2)
AND
_ID == _ID2
THEN
PROC_CheckPlayTut(_Player,"TUT_CompanionJoin");

IF
CharacterUsedItem(_Player,_Chest)
AND
DB_TutLockedChest(_Chest,_)
AND
ItemIsLocked(_Chest,1)
THEN
PROC_CheckPlayTut(_Player,"TUT_Locked_UseKey");

IF
CharacterUsedItem(_Player,ITEM_CYS_Tut_ChestBreakable)
AND
ItemIsLocked(ITEM_CYS_Tut_ChestBreakable,1)
THEN
PROC_CheckPlayTut(_Player,"TUT_Locked_Destroy");

IF
CharacterUsedItem(_Player,ITEM_CYS_Tut_ChestLockpick)
AND
ItemIsLocked(ITEM_CYS_Tut_ChestLockpick,1)
THEN
PROC_CheckPlayTut(_Player,"TUT_Locked_UseLockpick");

IF
CharacterEnteredTrigger((CHARACTER)_Player,TRIGGER_CYS_TUT_PoisonFire)
THEN
PROC_CheckPlayTut(_Player,"TUT_PoisonFire");

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_Tut_CommentSneak)
THEN
PROC_CheckPlayTut(_Player,"TUT_Sneak");

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_Tut_CommentSneak)
AND
QueryOnlyOnce("CYS_Tut_CommentSneak")
THEN
Launch_PlayerComment(_Player,"CYS_Tut_CommentSneak");

IF
CharacterUsedItemTemplate(_Player,_,_Locked)
AND
ItemHandleIsLocked(_Locked,1)
THEN
PROC_CheckPlayTut(_Player,"TUT_Locked");

IF
CharacterUsedItem(_Player,ITEM_CYS_TutDoorRoom3)
AND
ItemIsLocked(ITEM_CYS_TutDoorRoom3,1)
THEN
PROC_CheckPlayTut(_Player,"TUT_LockedDoor");


//REGION Mine Region

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_Tut_CommentDungeonEntrance)
THEN
PROC_CheckPlayTut(_Player,"TUT_Mine");

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_Tut_CommentDungeonEntrance)
AND
QueryOnlyOnce("CYS_Tut_CommentDungeonEntrance")
THEN
Launch_PlayerComment(_Player,"CYS_Tut_CommentDungeonEntrance");

//END_REGION

//REGION Destroy door 

IF
CharacterUsedItem(_Player,ITEM_CYS_TUT_BreakableDoor)
THEN
PROC_CheckPlayTut(_Player,"TUT_WoodenDoor");

IF
ItemDestroyed(ITEM_CYS_TUT_BreakableDoor)
AND
DB_IsPlayer(_Player)
THEN
CompleteTutorial(_Player,"TUT_WoodenDoor");

//END_REGION

//REGION door lever 

IF
CharacterItemEvent(_Player,_,"TUT_UsedDoorLever")
AND
NOT DB_OnlyOnce("CYS_TUT_OpenDoor")
THEN
DB_OnlyOnce("CYS_TUT_OpenDoor");
ItemUnlockAndOpen(ITEM_CYS_TutDoorRoom3);

//END_REGION 

IF
CharacterDestroyedItem(_Player,ITEM_CYS_TUT_BreakableDoor)
THEN
Launch_PlayerComment(_Player,"CYS_Tut_Durability");

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_TUT_SelectionActiveSearch)
THEN
PROC_CheckPlayTut(_Player,"TUT_SelectionActiveSearch_Controller");

IF
ItemAddedToCharacter(_TutoScroll,_Player)
AND
DB_TutorialScroll(_TutoScroll)
THEN
PROC_CheckPlayTut(_Player,"TUT_UseScroll");

IF
ItemAddedToCharacter(ITEM_Cys_TUT_ArrowHead,_Player)
THEN
Proc_CheckCraftingComponentsTuto(_Player);

IF
ItemAddedToCharacter(_TutoArrowShaft,_Player)
AND
DB_Tutorial_Arrowshaft(_TutoArrowShaft)
THEN
Proc_CheckCraftingComponentsTuto(_Player);

PROC
Proc_CheckCraftingComponentsTuto((CHARACTER)_Player)
AND
ItemIsInCharacterInventory(ITEM_Cys_TUT_ArrowHead,_Player,1)
AND
DB_Tutorial_Arrowshaft(_TutoArrowShaft)
AND
ItemIsInCharacterInventory(_TutoArrowShaft,_Player,1)
THEN
PROC_CheckPlayTut(_Player,"TUT_ItemCombo");

IF
DialogStarted("CYS_DD_BibiusJunius",_Inst)
AND
DB_DialogPlayers(_Inst,_Player1,1)
AND
DB_DialogPlayers(_Inst,_Player2,2)
THEN
PROC_CheckPlayTut(_Player1,"TUT_DualDialogCIR");
PROC_CheckPlayTut(_Player2,"TUT_DualDialogCIR");

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
