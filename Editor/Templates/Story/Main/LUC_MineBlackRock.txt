Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_PlayerComments("LUC_M_CommentBlackrockOreInfected","LUC_M_CommentBlackrockOreInfected_1_1",1,1);
DB_PlayerComments("LUC_M_CommentBlackrockOreInfected","LUC_M_CommentBlackrockOreInfected_2_1",2,1);
DB_PlayerComments("LUC_M_CommentBlackrockOreInfected","LUC_M_CommentBlackrockOreInfected_2_2",2,2);

DB_PlayerComments("LUC_M_CommentBlackrockAbility","LUC_M_CommentBlackrockAbility_1_1",1,1);
DB_PlayerComments("LUC_M_CommentBlackrockAbility","LUC_M_CommentBlackrockAbility_2_1",2,1);
DB_PlayerComments("LUC_M_CommentBlackrockAbility","LUC_M_CommentBlackrockAbility_2_2",2,2);

DB_PlayerComments("LUC_M_CommentBlackrockCage","LUC_M_CommentBlackrockCage_1_1",1,1);
DB_PlayerComments("LUC_M_CommentBlackrockCage","LUC_M_CommentBlackrockCage_2_1",2,1);
DB_PlayerComments("LUC_M_CommentBlackrockCage","LUC_M_CommentBlackrockCage_2_2",2,2);

ItemToCharacter(ITEM_LUC_BloodStoneCage,CHARACTER_LUC_Overseer);

DB_BlackRockRootTemplate("LOOT_Ore_BlackRock_A_b9db80ea-0844-4b89-8074-a036b5034ab8",1);
DB_BlackRockRootTemplate("LOOT_OreBar_A_BlackRock_A_e034ce03-2e35-4c1b-b47c-61385da84f22",1);
DB_BlackRockRootTemplate("WPN_Blackrock_2H_Axe_A_d674f267-f7f5-42a8-b8a5-9638d219825c",0);
DB_BlackRockRootTemplate("WPN_Blackrock_2H_Hammer_A_07de2e3e-28a4-45e4-b74d-11ef83bc468b",0);
DB_BlackRockRootTemplate("WPN_Blackrock_2H_Spear_A_5b4fb7b4-cb2f-438e-9738-3f19f87ade79",0);
DB_BlackRockRootTemplate("WPN_Blackrock_2H_Sword_A_5204df98-54c7-488d-a598-209873b935eb",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Axe_A_4c2d0965-5f71-4d62-8e12-a75a37dd62d2",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Bow_A_d4356349-497e-4be6-a7a9-2c7c9da36414",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Crossbow_A_f051be26-b6c2-46ab-99ed-38c37bdb2f9c",0);
DB_BlackRockRootTemplate("WPN_Blackrock_DaggerPush_A_f07caeb5-6ee9-4606-9dcd-2fbace3eb081",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Hammer_A_dece3169-6659-43a7-abac-2fac6708fa8f",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Scythe_A_cbb31cab-dc07-4268-abb2-bc221634de12",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Shield_A_5281d6f3-4b37-4284-b03f-b1034f3bc69a",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Shield_B_910caf06-8273-4426-9ab6-776a541e9951",0);
DB_BlackRockRootTemplate("WPN_Blackrock_Sword_A_feaa87ca-e6ea-40ce-a097-a63fa7634c10",0);
DB_BlackRockRootTemplate("TOOL_Wand_ShamblingMound_A_2b27e99b-44ac-4e91-952e-46f8b56117a6",0);

CharacterLockAbility(CHARACTER_Player1,"Blackrock");
CharacterLockAbility(CHARACTER_Player2,"Blackrock");

//REGION Blackrock immunities for important NPCs
CharacterAddAbility(CHARACTER_CYS_Zixzax,"Blackrock",1);
CharacterAddAbility(CHARACTER_HOM_Astarte,"Blackrock",1);
CharacterAddAbility(CHARACTER_HOM_Shrike,"Blackrock",1);
CharacterAddAbility(CHARACTER_LUC_Overseer,"Blackrock",1);
//END_REGION
KBSECTION
IF
CharacterItemHandleEvent(_Player,_Orevein,"MinespotUsed")
THEN
DB_Mining_Orevein(_Player,_Orevein);
CharTimer(_Player,"MineSafetyTimer",20000);

IF
CharTimerFinished(_Player,"MineSafetyTimer")
AND
DB_Mining_Orevein(_Player,_Orevein)
THEN
ItemHandleSetEvent(_Orevein,"MineFailed");
NOT DB_Mining_Orevein(_Player,_Orevein);
CharacterUnfreeze(_Player);

IF
CharacterItemHandleEvent(_Player,_Orevein,"MinespotUsed")
AND
ItemTemplateIsInPartyInventory(_Player,"WPN_Undead_Pickaxe_A_0f11d7e7-4e0b-4921-aa63-f8c6511ac631",0,0)
THEN
ItemHandleSetEvent(_Orevein,"MineFailed");
NOT DB_Mining_Orevein(_Player,_Orevein);
CharTimerCancel(_Player,"MineSafetyTimer");
CharacterDisplayText(_Player,"GLO_PickaxeRequired");

IF
CharacterItemHandleEvent(_Player,_Orevein,"MinespotUsed")
AND
ItemTemplateIsInPartyInventory(_Player,"WPN_Undead_Pickaxe_A_0f11d7e7-4e0b-4921-aa63-f8c6511ac631",1,_Count)
AND
_Count > 0
AND
GetItemhandleForItemTemplateInInventory(_Player,"WPN_Undead_Pickaxe_A_0f11d7e7-4e0b-4921-aa63-f8c6511ac631",_PickaxeHandle)
THEN
CharacterSetFightMode(_Player,0,1);
CharacterFreeze(_Player);
CharacterPlayAnimation(_Player,"use_mine","mineore");

IF
CharacterEvent(_Player,"mineore")
AND
DB_Mining_Orevein(_Player,_Orevein)
AND
ItemHandleGetVarString(_Orevein,"Ore",_OreItemTemplate)
THEN
ItemTemplateAddToCharacter(_OreItemTemplate,_Player,1);
ItemHandleSetEvent(_Orevein,"MineCompletedSuccesfully");
NOT DB_Mining_Orevein(_Player,_Orevein);
CharTimerCancel(_Player,"MineSafetyTimer");
CharacterUnfreeze(_Player);

IF
ItemTemplateAddedToCharacter(_BlackRockRootTemplate,_ItemHandle,_Character)
AND
DB_BlackRockRootTemplate(_BlackRockRootTemplate,_ShowComment)
THEN
Proc_BlackOreReceived(_ItemHandle,_Character,_ShowComment);

PROC
Proc_BlackOreReceived((INTEGER)_ItemHandle,(CHARACTER)_Character,(INTEGER)_ShowComment)
AND
CharacterGetAbility(_Character,"Blackrock",_Score)
AND
_Score > 0
THEN
OreHandled(_ItemHandle);
Proc_BlackOreComment(_Character,"LUC_M_CommentBlackrockAbility",_ShowComment);

PROC
Proc_BlackOreReceived((INTEGER)_ItemHandle,(CHARACTER)_Character,(INTEGER)_ShowComment)
AND
NOT OreHandled(_ItemHandle)
AND
ItemIsInPartyInventory(ITEM_LUC_BloodStoneCage,_Character,1,1)
THEN
OreHandled(_ItemHandle);
ItemHandleToContainer(_ItemHandle,ITEM_LUC_BloodStoneCage,-1);
Proc_BlackOreComment(_Character,"LUC_M_CommentBlackrockCage",_ShowComment);

PROC
Proc_BlackOreReceived((INTEGER)_ItemHandle,(CHARACTER)_Character,(INTEGER)_ShowComment)
AND
NOT OreHandled(_ItemHandle)
THEN
CharacterApplyStatus(_Character,"BLACKROCKED",-1.0);
Proc_BlackOreComment(_Character,"LUC_M_CommentBlackrockOreInfected",_ShowComment);

PROC
Proc_BlackOreReceived((INTEGER)_ItemHandle,(CHARACTER)_Character,(INTEGER)_ShowComment)
THEN
NOT OreHandled(_ItemHandle);

PROC
Proc_BlackOreComment((CHARACTER)_Character,(STRING)_String,1)
THEN
Launch_PlayerComment(_Character,_String);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
