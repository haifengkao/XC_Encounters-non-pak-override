Version 1
SubGoalCombiner SGC_AND
INITSECTION
CharacterSetVarInteger(CHARACTER_DAF_AnguishDemon,"IgnoreDialogChecks",0);
SneakTriggerSpotter(TRIGGER_DAF_AnguishSneak,CHARACTER_DAF_AnguishDemon);
CannotTrade(CHARACTER_DAF_ForestSpiritApprentice);
DB_IsNotMessingAround(CHARACTER_DAF_ForestSpiritApprentice);
DB_Dialogs(CHARACTER_DAF_ForestSpiritApprentice,"DAF_ForestSpiritApprentice");
DB_Dialogs(CHARACTER_DAF_ForestSpirit,"DAF_ForestSpirit");
DB_NoDefaultBehavior(CHARACTER_DAF_ForestSpirit);
//DB_Dialogs(CHARACTER_DAF_AnguishDemon,"DAF_AnguishDemon"); DO NOT GIVE AnguishDemon a dialog or defaultdialog through the normal system. THere are some exceptions so I handle dialog starting myself. -G
CharacterSetHasDialog(CHARACTER_DAF_AnguishDemon,1);
DB_NoRunAway(CHARACTER_DAF_AnguishDemon);


DB_AD_Dialog(CHARACTER_DAF_Anguish_PrisonWarrior,"GEN_AD_Ellipsis");
DB_AD_Dialog(CHARACTER_DAF_Anguish_Cyclops_Floater1,"GEN_AD_Ellipsis");
DB_AD_Dialog(CHARACTER_DAF_Anguish_Cyclops_Floater2,"GEN_AD_Ellipsis");
DB_AD_Dialog(CHARACTER_DAF_Anguish_Cyclops_Floater3,"GEN_AD_Ellipsis");
DB_AD_Dialog(CHARACTER_DAF_Anguish_Cyclops_Floater4,"GEN_AD_Ellipsis");
DB_AD_Dialog(CHARACTER_DAF_Anguish_Cyclops_Floater5,"GEN_AD_Ellipsis");
DB_AD_Dialog(CHARACTER_DAF_Anguish_Cyclops_Floater6,"GEN_AD_Ellipsis");

DB_DisableAttackReaction(CHARACTER_DAF_Anguish_Cyclops_Floater1);
DB_DisableAttackReaction(CHARACTER_DAF_Anguish_Cyclops_Floater2);
DB_DisableAttackReaction(CHARACTER_DAF_Anguish_Cyclops_Floater3);
DB_DisableAttackReaction(CHARACTER_DAF_Anguish_Cyclops_Floater4);
DB_DisableAttackReaction(CHARACTER_DAF_Anguish_Cyclops_Floater5);
DB_DisableAttackReaction(CHARACTER_DAF_Anguish_Cyclops_Floater6);
DB_DisableAttackReaction(CHARACTER_DAF_Anguish_PrisonWarrior);

NeverThreatenedDialog(CHARACTER_DAF_Anguish_PrisonWarrior);
NeverThreatenedDialog(CHARACTER_DAF_AnguishDemon);

ItemToCharacter(ITEM_DAF_TempleRune,CHARACTER_DAF_AnguishDemon);
ItemToCharacter(ITEM_DAF_AnguishKey,CHARACTER_DAF_AnguishDemon);
ItemSetOwner(ITEM_DAF_TempleDoor,CHARACTER_LUC_PickAxeHolderHack);
ItemSetOnlyOwnerCanUse(ITEM_DAF_TempleDoor,1);
DB_NoForbiddenText(ITEM_DAF_TempleDoor);

DB_OneShotPlayerTrigger(TRIGGER_DAF_AnguishDemon_AD);

//REGION Anguish Demon Fight Database
DB_AnguishDemon_TormentedSlaves(CHARACTER_DAF_AnguishDemon_Slave_01,TRIGGER_DAF_AnguishDemon_Trigger_Fire,"Fire");
DB_AnguishDemon_TormentedSlaves(CHARACTER_DAF_AnguishDemon_Slave_02,TRIGGER_DAF_AnguishDemon_Trigger_Ice,"Ice");
DB_AnguishDemon_TormentedSlaves(CHARACTER_DAF_AnguishDemon_Slave_03,TRIGGER_DAF_AnguishDemon_Trigger_Air,"Electrified");
DB_AnguishDemon_TormentedSlaves(CHARACTER_DAF_AnguishDemon_Slave_04,TRIGGER_DAF_AnguishDemon_Trigger_Earth,"Ooze");
//END_REGION

CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Initial_FemaleCaster,0);	//added by Ed on August 6, 2014
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Initial_Warrior_01,0);	//added by Ed on August 6, 2014
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Initial_Warrior_02,0);	//added by Ed on August 6, 2014
PROC_DAF_AnguishDemonFightInit();
DB_NoCharacterCompanionReplace(CHARACTER_DAF_AnguishDemon);
KBSECTION
IF
GlobalEventSet("Showswamphutmarker")
THEN
ProcShowMarker("DAF_Swamphut");


//REGION Temple Rune
IF
CharacterUsedItemFailed(_Character,ITEM_DAF_TempleDoor)
AND
ItemIsInPartyInventory(ITEM_DAF_TempleRune,_Character,0,0)
THEN
ItemPlaySound(ITEM_DAF_TempleDoor,"Item_Door_Locked");
CharacterDisplayText(_Character,"DAF_TempleRuneMissing");

IF
CharacterUsedItemFailed(_Character,ITEM_DAF_TempleDoor)
AND
ItemIsInPartyInventory(ITEM_DAF_TempleRune,_Character,1,1)
THEN
CharacterDisplayText(_Character,"DAF_TempleRuneUsed");
ItemSetOnlyOwnerCanUse(ITEM_DAF_TempleDoor,0);

//END_REGION

//REGION Anguish Demon Intro Scene
PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DAF_AnguishDemon_AD)
THEN
DialogStartTwoSpeakerDialog("DAF_AD_DemonForestSpirit",CHARACTER_DAF_AnguishDemon,CHARACTER_DAF_ForestSpirit);

IF
AutomatedDialogEnded("DAF_AD_DemonForestSpirit",_)
THEN
CharacterSetVarInteger(CHARACTER_DAF_AnguishDemon,"Active",1);
//END_REGION

//REGION Anguish Demon Dialog
PROC
ProcCharInTriggerSpotted((CHARACTER)_Player,TRIGGER_DAF_AnguishSneak)
THEN
DialogRequestStopForCharacter(CHARACTER_DAF_AnguishDemon);
ProcAnguishDialogRequest(_Player);

IF
DialogStartRequested(CHARACTER_DAF_AnguishDemon,_Character)
THEN
ProcAnguishDialogRequest(_Character);

PROC
ProcAnguishDialogRequest((CHARACTER)_Character)
THEN
NOT DAF_AnguishDialogHandled(1);

PROC
ProcAnguishDialogRequest((CHARACTER)_Character)
AND
DB_GlobalEvent("DAF_AnguishDemon_DealComplete")
THEN
DAF_AnguishDialogHandled(1);

PROC
ProcAnguishDialogRequest((CHARACTER)_Character)
AND
DB_GlobalEvent("DAF_JahanOffered")
THEN
DAF_AnguishDialogHandled(1);

PROC
ProcAnguishDialogRequest((CHARACTER)_Companion)
AND
DB_Companion(_Companion)
AND
_Companion != CHARACTER_Jahan
AND
NOT DAF_AnguishDialogHandled(1)
THEN
DialogStartTwoSpeakerDialog("Default_Companion",CHARACTER_DAF_AnguishDemon, _Companion);
DAF_AnguishDialogHandled(1);

PROC
ProcAnguishDialogRequest(CHARACTER_Jahan)
AND
CharacterGetClosestAlivePlayer(CHARACTER_Jahan, _Character)
AND
NOT DAF_AnguishDialogHandled(1)
THEN
ProcCheckPartyForTrait("Egotistical",_Character,"Anguish_PartyHasEgotisticalTrait");
ProcCheckPartyForTrait("Heartless",_Character,"Anguish_PartyHasHeartlessTrait");
GlobalSetEvent("Anguish_JahanPresent");
PROC_DoCompanionNemesisDialog("DAF_AnguishDemon",CHARACTER_DAF_AnguishDemon,CHARACTER_Jahan);
QuestAdd("JAH_Demon");
QuestUpdate("JAH_Demon","Start");
QuestUpdate("JAH_Demon","Demon");
DAF_AnguishDialogHandled(1);

PROC
ProcAnguishDialogRequest((CHARACTER)_Character)
AND
NOT DAF_AnguishDialogHandled(1)
AND
CharacterGetDistanceToCharacter(_Character,CHARACTER_Jahan,_Dist)
AND
_Dist < 10.0
THEN
ProcCheckPartyForTrait("Egotistical",_Character,"Anguish_PartyHasEgotisticalTrait");
ProcCheckPartyForTrait("Heartless",_Character,"Anguish_PartyHasHeartlessTrait");
GlobalSetEvent("Anguish_JahanPresent");
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon,_Character,1);
CharacterLookAtCharacter(_Character,CHARACTER_DAF_AnguishDemon,1);
CharacterLookAtCharacter(CHARACTER_Jahan,CHARACTER_DAF_AnguishDemon,1);
DialogStartThreeSpeakerDialog("DAF_AnguishDemon",CHARACTER_DAF_AnguishDemon,_Character,CHARACTER_Jahan);
QuestAdd("JAH_Demon");
QuestUpdate("JAH_Demon","Start");
QuestUpdate("JAH_Demon","Demon");
DAF_AnguishDialogHandled(1);

PROC
ProcAnguishDialogRequest((CHARACTER)_Character)
AND
NOT DAF_AnguishDialogHandled(1)
THEN
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon,_Character,1);
CharacterLookAtCharacter(_Character,CHARACTER_DAF_AnguishDemon,1);
DialogStartTwoSpeakerDialog("DAF_AnguishDemon",CHARACTER_DAF_AnguishDemon,_Character);
QuestUpdate("JAH_Demon","Demon");

IF
DialogEnded("DAF_AnguishDemon",_)
THEN
GlobalClearEvent("Anguish_JahanPresent");
NOT DB_QueuedDialogNemesis("DAF_AnguishDemon",CHARACTER_DAF_AnguishDemon,CHARACTER_Jahan);

IF
GlobalEventSet("JAH_Lobotomized")
AND
CharacterAddToCharacterCreation(CHARACTER_Jahan,1,_)
THEN
ProcCancelAllCompanionDialogs(CHARACTER_Jahan);
QuestUpdate("JAH_Demon","DealDemon");
QuestClose("JAH_Demon");
DB_NoDefaultBehavior(CHARACTER_Jahan);
CharacterPlayEffect(CHARACTER_Jahan,"FX_GP_StarStone_Up_A");
IgnoreCompanionDialogsAndComments(CHARACTER_Jahan);
CharacterLevelUp(CHARACTER_Jahan);

IF
DialogEnded("DAF_AnguishDemon",_)
AND
DB_GlobalEvent("DAF_AnguishAggro")
AND
NOT DB_OnlyOnce("ProcTransformAnguish")
THEN
DB_OnlyOnce("ProcTransformAnguish");
ProcTransformAnguish();

IF
CharacterEnteredCombat(CHARACTER_DAF_AnguishDemon,_)
AND
NOT DB_OnlyOnce("ProcTransformAnguish")
THEN
DB_OnlyOnce("ProcTransformAnguish");
NOT DB_QueuedDialogNemesis("DAF_AnguishDemon",CHARACTER_DAF_AnguishDemon,CHARACTER_Jahan);
ProcTransformAnguish();

IF
ItemOpened(ITEM_DAF_ForestSpirit_Prisondoor)
AND
NOT DB_OnlyOnce("ProcTransformAnguish")
THEN
DB_OnlyOnce("ProcTransformAnguish");
ProcTransformAnguish();

IF
CharacterDied(CHARACTER_DAF_AnguishDemon_Fighter)
THEN
UnlockAchievement("quest40");

IF
CharacterDied(CHARACTER_DAF_AnguishDemon_Fighter)	//Edouard - replaced with actual combat character (the one that will be killed by players)
THEN
GlobalSetEvent("DAF_AnguishDemonGone");
QuestUpdate("DAF_Zandalor","KillDemon");
QuestUpdate("JAH_Demon","KillDemon");
QuestClose("JAH_Demon");
ProcClearForbiddenArea("DAF_Swamphut");
ProcCancelOneCompanionDialog(CHARACTER_Jahan,"JAH_Truth");

IF
CharacterDied(CHARACTER_DAF_AnguishDemon_Fighter)
AND
DB_AnguishDemon_TormentedSlaves(_Slave,_Trigger,_Surface)
THEN
NOT DB_AnguishDemon_TormentedSlaves(_Slave,_Trigger,_Surface);

IF
CharacterDied(CHARACTER_DAF_ImmaFort_DemonBoss)
THEN
GlobalSetEvent("DAF_FortDemonDied");

IF
DialogEnded("DAF_AnguishDemon",_ID)
AND
DB_GlobalEvent("DAF_AnguishDemon_DealComplete")
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
ItemToCharacter(ITEM_DAF_TempleRune,_Player);
DB_AD_Dialog(CHARACTER_DAF_AnguishDemon,"DAF_AD_AnguishDemon_AfterDeal");

IF
DialogEnded("DAF_AnguishDemon",_ID)
AND
DB_GlobalEvent("DAF_JahanOffered")
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
PROC_LoopEffectAtCharacter("FX_GP_Cyclops_Floater_A",CHARACTER_Jahan,"Jahan_Floater","__ANY__","Bip001 Head");
ItemToCharacter(ITEM_DAF_TempleRune,_Player);
DB_AD_Dialog(CHARACTER_DAF_AnguishDemon,"DAF_AD_AnguishDemon_AfterDeal");
OnCompanionDismissed(CHARACTER_Jahan);
GlobalSetEvent("JahanDismissed");
ProcCancelAllCompanionDialogs(CHARACTER_Jahan);
NOT DB_Companion(CHARACTER_Jahan);
CharacterDetachFromGroup(CHARACTER_Jahan);
CharacterMakeNPC(CHARACTER_Jahan);
CharacterRemoveFromParty(CHARACTER_Jahan);
CharacterSetCanFight(CHARACTER_Jahan,0);
QuestUpdate("DAF_Zandalor","OfferJahan");
QuestUpdate("JAH_Demon","OfferJahan");
QuestClose("JAH_Demon");
DB_NoDefaultBehavior(CHARACTER_Jahan);
TimerLaunch("AnguishTeleportJahan",2000);
CharacterRemoveStatus(CHARACTER_Jahan,"UNSHEATHED");
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon,CHARACTER_Jahan,1);
CharacterMoveAllItemsToCharacter(CHARACTER_Jahan,CHARACTER_DAF_AnguishDemon);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTER_Jahan);
DB_Dialogs(CHARACTER_Jahan,"GEN_AD_Ellipsis");
PROC_END_RemoveCompInDBRoomB(CHARACTER_Jahan);
NOT DB_CompanionRecruited(CHARACTER_Jahan);

IF
TimerFinished("AnguishTeleportJahan")
THEN
Poof(CHARACTER_Jahan);
CharacterTeleportToTrigger(CHARACTER_Jahan,TRIGGER_DAF_Jahan_Prison,"AnguishJahanInPrison");

IF
CharacterEvent(CHARACTER_Jahan,"AnguishJahanInPrison")
THEN
Foop(CHARACTER_Jahan);
CharacterMoveToTrigger(CHARACTER_DAF_AnguishDemon,TRIGGER_DAF_AnguishDemon_Cyclops,0,"ArrivedAtJahanCage");

IF
CharacterEvent(CHARACTER_DAF_AnguishDemon,"ArrivedAtJahanCage")
THEN
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon,CHARACTER_Jahan);
DialogStartTwoSpeakerDialog("DAF_AD_DemonJahan",CHARACTER_DAF_AnguishDemon,CHARACTER_Jahan);
ProcDefinePartyDialog("DAF_DD_AfterJahclops");
//END_REGION

//REGION Anguish Demon Fight
PROC
PROC_DAF_AnguishDemonFightInit()
AND
DB_AnguishDemon_TormentedSlaves(_Slave,_,_)						
THEN
CharacterSetOnStage(_Slave,0);
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Fighter,0);	
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_FemaleCaster,0);	
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Fighter,0);	
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Mage,0);	
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Warrior,0);

PROC
ProcTransformAnguish()
AND
CharacterGetPosition(CHARACTER_DAF_AnguishDemon,_X,_Y,_Z)
THEN
Poof(CHARACTER_DAF_AnguishDemon);
CharacterTeleportToPosition(CHARACTER_DAF_AnguishDemon_Fighter,_X,_Y,_Z,"");
PlayEffectAtCharacter("FX_Creatures_Undead_Skeleton_Fire_A_Death",CHARACTER_DAF_AnguishDemon_Fighter);
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Fighter,1);
CharacterMoveAllItemsToCharacter(CHARACTER_DAF_AnguishDemon,CHARACTER_DAF_AnguishDemon_Fighter,0);
Foop(CHARACTER_DAF_AnguishDemon_Cyclops_Initial_FemaleCaster);	//added by Ed on August 6, 2014
Foop(CHARACTER_DAF_AnguishDemon_Cyclops_Initial_Warrior_01);	//added by Ed on August 6, 2014
Foop(CHARACTER_DAF_AnguishDemon_Cyclops_Initial_Warrior_02);	//added by Ed on August 6, 2014

PROC
ProcTransformAnguish()
AND
DB_AnguishDemon_TormentedSlaves(_Slave,_Trigger,_Surface)
THEN
CharacterSetOnStage(_Slave,1);
PlayEffectAtCharacter("FX_GP_TeleportSmoke_A",_Slave);
TriggerCreateSurface(_Trigger,_Surface,1.2);	

IF
CharacterEvent(CHARACTER_DAF_AnguishDemon_Fighter,"DAF_AnguishDemon_CyclopsAppears_Female_Event")
THEN
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_FemaleCaster,1);
CharacterApplyStatus(CHARACTER_DAF_AnguishDemon_Cyclops_FemaleCaster,"SUMMONING",-1.0,1);
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon_Fighter,CHARACTER_DAF_AnguishDemon_Cyclops_FemaleCaster,0);
PlayEffectAtCharacter("FX_Creatures_Undead_Skeleton_Fire_A_Death",CHARACTER_DAF_AnguishDemon_Cyclops_FemaleCaster);

IF
CharacterEvent(CHARACTER_DAF_AnguishDemon_Fighter,"DAF_AnguishDemon_CyclopsAppears_Fighter_Event")
THEN
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Fighter,1);
CharacterApplyStatus(CHARACTER_DAF_AnguishDemon_Cyclops_Fighter,"SUMMONING",-1.0,1);
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon_Fighter,CHARACTER_DAF_AnguishDemon_Cyclops_Fighter,0);
PlayEffectAtCharacter("FX_Creatures_Undead_Skeleton_Fire_A_Death",CHARACTER_DAF_AnguishDemon_Cyclops_Fighter);

IF
CharacterEvent(CHARACTER_DAF_AnguishDemon_Fighter,"DAF_AnguishDemon_CyclopsAppears_Wizard_Event")
THEN
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Mage,1);
CharacterApplyStatus(CHARACTER_DAF_AnguishDemon_Cyclops_Mage,"SUMMONING",-1.0,1);
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon_Fighter,CHARACTER_DAF_AnguishDemon_Cyclops_Mage,0);
PlayEffectAtCharacter("FX_Creatures_Undead_Skeleton_Fire_A_Death",CHARACTER_DAF_AnguishDemon_Cyclops_Mage);

IF
CharacterEvent(CHARACTER_DAF_AnguishDemon_Fighter,"DAF_AnguishDemon_CyclopsAppears_Warrior_Event")
THEN
CharacterSetOnStage(CHARACTER_DAF_AnguishDemon_Cyclops_Warrior,1);
CharacterApplyStatus(CHARACTER_DAF_AnguishDemon_Cyclops_Warrior,"SUMMONING",-1.0,1);
CharacterLookAtCharacter(CHARACTER_DAF_AnguishDemon_Fighter,CHARACTER_DAF_AnguishDemon_Cyclops_Warrior,0);
PlayEffectAtCharacter("FX_Creatures_Undead_Skeleton_Fire_A_Death",CHARACTER_DAF_AnguishDemon_Cyclops_Warrior);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
