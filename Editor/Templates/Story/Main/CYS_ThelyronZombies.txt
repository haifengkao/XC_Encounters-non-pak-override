Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_CYS_Blossius,"CYS_Blossius");
DB_Dialogs(CHARACTER_CYS_Livia,"CYS_Livia");
DB_Dialogs(CHARACTER_LUC_Marisa,"LUC_Marisa");

DB_OneShot_DialogTrigger(TRIGGER_CYS_BoxGraveyardGuards1,"CYS_Blossius",CHARACTER_CYS_Blossius);
DB_OneShotPlayerTrigger(TRIGGER_CYS_BoxChurchBypassTrigger);
ItemToCharacter(ITEM_CYS_BlossiusWill,CHARACTER_CYS_Blossius);
ItemToCharacter(ITEM_LUC_MarisaTreasure,CHARACTER_LUC_Marisa);
DB_ItemGivesPartyDialog(ITEM_CYS_BlossiusWill,"CYS_DD_BlossiusWill");
KBSECTION
//REGION SKELETON GUARDS
PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_CYS_BoxChurchBypassTrigger)
THEN
RemoveOneShotDialog(TRIGGER_CYS_BoxChurchBypassTrigger);

IF
DialogCharacterEventSet("CYS_GetBlossiusWill",_Player,_)
THEN
ItemToCharacter(ITEM_CYS_BlossiusWill,_Player);

IF
ItemAddedToCharacter(ITEM_CYS_BlossiusWill,_Player)
AND
_Player.DB_IsPlayer()
THEN
GlobalSetEvent("CYS_HasBlossiusWill");

IF
ItemRemovedFromCharacter(ITEM_CYS_BlossiusWill,_Player)
AND
_Player.DB_IsPlayer()
THEN
GlobalClearEvent("CYS_HasBlossiusWill");

IF
ItemTemplateCombinedWithItemTemplate("LOOT_Ink_Pot_A_Quill_A_7c9e8ca5-de93-4e43-be83-2cb6a9022c2f","Quest_CYS_BlossiusWill_48ec9368-3530-4ecb-9ba1-86ece3b26ec9",_Player,_)
THEN
QuestUpdate("CYS_BlossiusWill","Altered");

IF
DialogCharacterEventSet("LUC_GaveMarisaWill",_Player,_)
AND
ItemIsInPartyInventory(ITEM_CYS_BlossiusWill,_Player,1,1)
THEN
ItemToCharacter(ITEM_CYS_BlossiusWill,CHARACTER_LUC_Marisa);

IF
DialogCharacterEventSet("LUC_GaveMarisaAlteredWill",_Player,_)
AND
ItemTemplateIsInPartyInventory(_Player,"Quest_CYS_BlossiusWillAltered_c42d53c3-4293-4cb0-b42a-dcedf6c30f29",1,1)
THEN
ItemToCharacter(ITEM_LUC_MarisaTreasure,_Player);
ItemTemplateRemoveFromCharacter("Quest_CYS_BlossiusWillAltered_c42d53c3-4293-4cb0-b42a-dcedf6c30f29",_Player,1);
//END_REGION

IF
TextEventSet("quill")
THEN
ItemTemplateAddToCharacter("LOOT_Ink_Pot_A_Quill_A_7c9e8ca5-de93-4e43-be83-2cb6a9022c2f",CHARACTER_Player1,2);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
