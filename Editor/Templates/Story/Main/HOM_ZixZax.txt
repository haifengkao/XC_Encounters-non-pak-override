Version 1
SubGoalCombiner SGC_AND
INITSECTION
CharacterTeleportToTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_ZixZax,"");
DB_Dialogs(CHARACTER_CYS_Zixzax,"HOM_ZixZax_Intro1");
DB_NoHostileDialog(CHARACTER_CYS_Zixzax);

DB_PlayerComments("HOM_Comment_Zixzax", "HOM_Comment_Zixzax_2_1", 2,1);
DB_PlayerComments("HOM_Comment_Zixzax", "HOM_Comment_Zixzax_2_2", 2,2);

DB_PlayerComments("HOM_Comment_FirstTimeInHomestead", "HOM_Comment_FirstTimeInHomestead_2_1", 2,1);
DB_PlayerComments("HOM_Comment_FirstTimeInHomestead", "HOM_Comment_FirstTimeInHomestead_2_2", 2,2);

DB_PlayerComments("HOM_Comment_HallwayEntry", "HOM_Comment_HallwayEntry_1_1",1,1);
DB_PlayerComments("HOM_Comment_HallwayEntry", "HOM_Comment_HallwayEntry_2_1",2,1);
DB_PlayerComments("HOM_Comment_HallwayEntry", "HOM_Comment_HallwayEntry_2_2",2,2);

DB_PlayerComments("HOM_Comment_AfterWeaver", "HOM_Comment_AfterWeaver_1_1",1,1);
DB_PlayerComments("HOM_Comment_AfterWeaver", "HOM_Comment_AfterWeaver_2_1",2,1);
DB_PlayerComments("HOM_Comment_AfterWeaver", "HOM_Comment_AfterWeaver_2_2",2,2);

DB_PyramidManual(CHARACTER_Player1,ITEM_HOM_PyramidInstructions1);
DB_PyramidManual(CHARACTER_Player2,ITEM_HOM_PyramidInstructions2);

DB_CustomCompanionDialog(CHARACTER_HOM_Weaver,"HOM_WeaverCompanion");

DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_StatueLighter1,ITEM_HOM_Observatory_Brazier1);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_StatueLighter2,ITEM_HOM_Observatory_Brazier2);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_StatueLighter3,ITEM_HOM_Observatory_Brazier3);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_StatueLighter4,ITEM_HOM_Observatory_Brazier4);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter2,ITEM_HOM_Observatory_Fire3);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter2,ITEM_HOM_Observatory_Fire5);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter3,ITEM_HOM_Observatory_Fire4);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter3,ITEM_HOM_Observatory_Fire10);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter4,ITEM_HOM_Observatory_Fire6);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter4,ITEM_HOM_Observatory_Fire7);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter5,ITEM_HOM_Observatory_Fire8);
DB_HOM_ProximityFire(TRIGGER_HOM_Observatory_FireLighter5,ITEM_HOM_Observatory_Fire9);

DB_OneshotPlayerTrigger(TRIGGER_HOM_Observatory_Zixzax_AD);

CannotTrade(CHARACTER_CYS_Zixzax);
KBSECTION
IF
TextEventSet("zap")
THEN
CharacterTeleportPartyToTrigger(TRIGGER_HOM_Players_FirstTime,"");

IF
TextEventSet("metzz")
THEN
ProcMetZZ();

IF
DB_PyramidManual(_,_Item)
THEN
ItemToCharacter(_Item,CHARACTER_HOM_AltarHack);

PROC
ProcMetZZ()
THEN
NOT DB_NumStarStonesFound(0);
DB_NumStarStonesFound(1);
DB_OnlyOnce("TeleportToHomestead");
DB_OnlyOnce("playersarrivedinhomestead");
QuestAdd("HOM_Starstones");
QuestUpdate("HOM_Starstones","Start");
DB_OnlyOnce("zixzaxintro1");
GlobalSetEvent("UsedHOMTelescope");
DB_HOM_UnlockTeleporter(ITEM_HOM_TeleportFromObservatory_Inactive);
DB_OnlyOnce("zixzaxintro3");
NOT DB_OneShotPlayerTrigger(TRIGGER_HOM_BoxZixzaxTeleportComment);
ProcTriggerUnregisterForPlayers(TRIGGER_HOM_BoxZixzaxTeleportComment);
GlobalSetEvent("HOM_ZixZaxIntro_TeleportToEntranceHall");
CharacterTeleportToTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_TeleportReturnFromObservatory,"zixzaxintro4");
DB_OnlyOnce("PyramidsAndKeyGiven");
ItemToCharacter(ITEM_TeleportPyramidA,CHARACTER_Player1);
QuestAdd("CYS_Pyramids");
QuestUpdate("CYS_Pyramids","Update1");
QuestClose("CYS_Pyramids");
DB_TeleporterPyramidUnlocked(ITEM_TeleportPyramidA);
GlobalSetEvent("CYS_HomesteadKeyGiven");
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Zixzax,"HOM_ZixZax_Intro2");
DB_Dialogs(CHARACTER_CYS_Zixzax,"HOM_ZixZax");
DB_PlayerComments("GLO_CommentZapped","GLO_CommentZapped_1_1",1,1);
DB_PlayerComments("GLO_CommentZapped","GLO_CommentZapped_2_1",2,1);
DB_PlayerComments("GLO_CommentZapped","GLO_CommentZapped_2_2",2,2);
QuestUpdate("HOM_Starstones","Key");

PROC
Proc_RememberLastDepletedStarStone((INTEGER)_NewItemHandle)
AND
DB_LastDepletedStarStone(_OldItemHandle)
THEN
NOT DB_LastDepletedStarStone(_OldItemHandle);

PROC
Proc_RememberLastDepletedStarStone((INTEGER)_NewItemHandle)
THEN
DB_LastDepletedStarStone(_NewItemHandle);

//REGION Teleporting players to homestead after first zapped by starstone
PROC
Proc_TeleportToHomesteadTheFirstTimePlayersAreZapped()
AND
NOT DB_OnlyOnce("TeleportToHomestead")
THEN
DB_OnlyOnce("TeleportToHomestead");
ProcCheckPlayerForTeleportToHomestead(CHARACTER_Player1);
ProcCheckPlayerForTeleportToHomestead(CHARACTER_Player2);

PROC
ProcCheckPlayerForTeleportToHomestead((CHARACTER)_Player)
AND
_Player.DB_IsInDialog()
AND
CharacterGetOtherPlayer(_Player,_Player2)
THEN
DB_HOM_DialogPreventingTP(_Player);
OpenMessageBox(_Player2,"HOM_WaitingForDialog");

PROC
ProcCheckPlayerForTeleportToHomestead((CHARACTER)_Player)
AND
NOT _Player.DB_IsInDialog()
THEN
ProcReadyPlayerToGoToHomestead(_Player);

IF
CharTimerFinished(_Player,"ApplyStarStoneEffect")
THEN
CharacterApplyStatus(_Player,"KNOCKED_DOWN",-1.0,1);
CharacterFreeze(_Player);

IF
DialogEnded(_,_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
DB_HOM_DialogPreventingTP(_Player)
AND
NOT _Player.DB_IsInDialog()
THEN
ProcReadyPlayerToGoToHomestead(_Player);

PROC
ProcReadyPlayerToGoToHomestead((CHARACTER)_Player)
THEN
CharTimer(_Player,"ApplyStarStoneEffect",400);
NOT DB_HOM_DialogPreventingTP(_Player);
DB_HOM_ReadyToTP(_Player);

IF
DialogEnded(_,_ID)
AND
DB_DialogPlayers(_ID,_Player,2)
AND
DB_HOM_DialogPreventingTP(_Player)
AND
NOT _Player.DB_IsInDialog()
THEN
ProcReadyPlayerToGoToHomestead(_Player);

IF
DB_HOM_ReadyToTP(CHARACTER_Player1)
AND
DB_HOM_ReadyToTP(CHARACTER_Player2)
THEN
ProcStopCompanionDialogs();
ProcKnockDownCompanionZap();
TimerLaunch("TransportPlayersToHomestead",1500);

PROC
ProcKnockDownCompanionZap()
AND
DB_Companion(_Comp)
THEN
ProcReadyPlayerToGoToHomestead(_Comp);

IF
TimerFinished("TransportPlayersToHomestead")
THEN
CharacterUnfreeze(CHARACTER_Player1);
CharacterUnfreeze(CHARACTER_Player2);
ProcUnfreezeComp();
ProcFreeGuardsForPlayer(CHARACTER_Player1);
ProcFreeGuardsForPlayer(CHARACTER_Player2);
CharacterTeleportPartyToTrigger(TRIGGER_HOM_Players_FirstTime,"");
NOT DB_HOM_ReadyToTP(CHARACTER_Player1);
NOT DB_HOM_ReadyToTP(CHARACTER_Player2);

PROC
ProcUnfreezeComp()
AND
DB_Companion(_Comp)
THEN
CharacterUnfreeze(_Comp);

PROC
ProcRemoveKnockedDownComp()
AND
DB_Companion(_Comp)
THEN
CharacterRemoveStatus(_Comp,"KNOCKED_DOWN");

//END_REGION

//REGION Homestead Intro - Observatory
IF
RegionStarted("Homestead")
AND
NOT DB_OnlyOnce("playersarrivedinhomestead")
THEN
CharacterTeleportToTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_ZixZax,"");
CharacterLookFromTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_ZixZax,1);
ProcStopAffectionDialog();
ProcCancelDualDialogs();
ProcCancelAllCompanionDialogsForAllCompanions();
DB_OnlyOnce("playersarrivedinhomestead");
TimerLaunch("playersarrivedinhomestead2",2000);

IF
TimerFinished("playersarrivedinhomestead2")
THEN
CharacterRemoveStatus(CHARACTER_Player1,"KNOCKED_DOWN");
CharacterRemoveStatus(CHARACTER_Player2,"KNOCKED_DOWN");
ProcRemoveKnockedDownComp();
//PROC_MendatoryDialogThreeSpeaker("HOM_ZixZax_Intro1",CHARACTER_CYS_Zixzax,CHARACTER_Player1,CHARACTER_Player2);
Launch_PlayerComment(CHARACTER_Player1,"HOM_Comment_FirstTimeInHomestead");
TimerLaunch("HOM_ObservatoryLights1",2000);

IF
TimerFinished("HOM_ObservatoryLights1")
THEN
ItemSetEvent(ITEM_HOM_Observatory_Center1,"StartFire");
ItemSetEvent(ITEM_HOM_Observatory_Center2,"StartFire");
ItemSetEvent(ITEM_HOM_Observatory_Center3,"StartFire");
ItemSetEvent(ITEM_HOM_Observatory_Center4,"StartFire");
TimerLaunch("HOM_ObservatoryLights2",1000);

IF
TimerFinished("HOM_ObservatoryLights2")
THEN
ItemSetEvent(ITEM_HOM_Observatory_Center5,"StartFire");
ItemSetEvent(ITEM_HOM_Observatory_Center6,"StartFire");
ItemSetEvent(ITEM_HOM_Observatory_Center7,"StartFire");
ItemSetEvent(ITEM_HOM_Observatory_Center8,"StartFire");
TimerLaunch("HOM_ObservatoryLights3",1000);

IF
TimerFinished("HOM_ObservatoryLights3")
THEN
ItemSetEvent(ITEM_HOM_Observatory_Center9,"StartFire");

IF
CharacterEnteredTrigger(_,TRIGGER_HOM_Observatory_Zixzax_AD)
AND
NOT DB_IsInDialog(CHARACTER_CYS_Zixzax)
THEN
CharacterDisplayText(CHARACTER_CYS_Zixzax,"HOM_Zixzax_Telescope");

IF
DialogEnded("HOM_ZixZax_Intro1",_)
THEN
Launch_PlayerComment(CHARACTER_Player1,"HOM_Comment_Zixzax");
QuestAdd("HOM_Starstones");
QuestUpdate("HOM_Starstones","Start");
NOT DB_StarStoneRoomQueue(1);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Zixzax,"HOM_ZixZax_Intro1");
DB_Dialogs(CHARACTER_CYS_Zixzax,"HOM_ZixZax_Intro2");
DB_OnlyOnce("HOM_HadFirstZZConvo");
CharacterMoveToTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_ZZ_AfterTelescope,0,"HOM_ZZ_AfterTelescope");

IF
CharacterEvent(CHARACTER_CYS_Zixzax,"HOM_ZZ_AfterTelescope")
THEN
CharacterLookFromTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_ZZ_AfterTelescope,0);

IF
CharacterUsedItem(_Character,ITEM_HOM_Telescope)
AND
NOT DB_OnlyOnce("HOM_HadFirstZZConvo")
AND
NOT DB_IsInDialog(CHARACTER_CYS_Zixzax)
THEN
PROC_MendatoryDialog("HOM_ZixZax_Intro1",CHARACTER_CYS_Zixzax,_Character);

IF
CharacterUsedItem(_Character,ITEM_HOM_Telescope)
AND
NOT DB_OnlyOnce("HOM_HadFirstZZConvo")
AND
DB_IsInDialog(CHARACTER_CYS_Zixzax)
THEN
CharacterDisplayText(_Character, "GLOBAL_CannotUseNow");

IF
DialogStarted("HOM_Zixzax_Intro2",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
CharacterLookAtCharacter(CHARACTER_CYS_Zixzax,_Player);
CharacterLookAtCharacter(_Player,CHARACTER_CYS_Zixzax);

IF
DialogEnded("HOM_Zixzax_Intro2",_)
THEN
RemoveOneShotDialog(TRIGGER_HOM_BoxObservatoryExit);
ItemSetCanInteract(ITEM_HOM_TeleportFromObservatory,1);

IF
CharacterUsedItem(_Character,ITEM_HOM_Telescope)
AND
DB_OnlyOnce("HOM_HadFirstZZConvo")
AND
NOT _Character.MoviePlaying()
THEN
_Character.MoviePlaying();
MoviePlay(_Character,"Telescope_BlackHoleView_A.bik");
GlobalSetEvent("UsedHOMTelescope");

IF
MovieFinished("Telescope_BlackHoleView_A.bik")
AND
_Character.MoviePlaying()
THEN
NOT _Character.MoviePlaying();

IF
MovieFinished("Telescope_BlackHoleView_A.bik")
AND
NOT DB_OnlyOnce("zixzaxintro1")
THEN
DB_OnlyOnce("zixzaxintro1");
TimerLaunch("HOM_ZZ_AfterTelescopeMovie",1500);
QuestUpdate("HOM_Starstones","Telescope");

IF
TimerFinished("HOM_ZZ_AfterTelescopeMovie")
THEN
DialogStartOneSpeakerDialog("HOM_ZZ_AfterTelescope",CHARACTER_CYS_Zixzax);
PROC_Transform_HOM_Teleporter(ITEM_HOM_TeleportFromObservatory_Inactive);
ItemSetCanInteract(ITEM_HOM_TeleportFromObservatory,0);

IF
AutomatedDialogEnded("HOM_ZZ_AfterTelescope",_)
THEN
DB_OneShot_DialogTrigger(TRIGGER_HOM_BoxObservatoryExit,"HOM_Zixzax_Intro2",CHARACTER_CYS_Zixzax);
ProcCharacterMoveToItem(CHARACTER_CYS_Zixzax,ITEM_HOM_TeleportFromObservatory_Inactive,1,"zixzaxintro1");

IF
CharacterEvent(CHARACTER_CYS_Zixzax,"zixzaxintro1")
THEN
CharacterPlayAnimation(CHARACTER_CYS_Zixzax,"Talk_Happy","zixzaxintro2");

IF
CharacterEvent(CHARACTER_CYS_Zixzax,"zixzaxintro2")
AND
NOT CHARACTER_CYS_Zixzax.DB_IsInDialog()
THEN
CharacterDisplayText(CHARACTER_CYS_Zixzax,"HOM_ZixZax_DiscoversPortal");

IF
DialogEnded("HOM_ZixZax_Intro2",_ID)
THEN
DB_OneShotPlayerTrigger(TRIGGER_HOM_BoxZixzaxTeleportComment);

PROC
ProcOneShotTriggerEntered(_,TRIGGER_HOM_BoxZixzaxTeleportComment)
THEN
TimerLaunch("HOM_ZixzaxPortal",1000);

IF
TimerFinished("HOM_ZixzaxPortal")
THEN
CharacterDisplayText(CHARACTER_CYS_Zixzax,"HOM_Zixzax_AtPortal");

IF
CharacterUsedItem(_,ITEM_HOM_TeleportFromObservatory)
AND
NOT DB_OnlyOnce("zixzaxintro3")
THEN
DB_OnlyOnce("zixzaxintro3");
NOT DB_OneShotPlayerTrigger(TRIGGER_HOM_BoxZixzaxTeleportComment);
ProcTriggerUnregisterForPlayers(TRIGGER_HOM_BoxZixzaxTeleportComment);
GlobalSetEvent("HOM_ZixZaxIntro_TeleportToEntranceHall");
DialogRequestStopForCharacter(CHARACTER_CYS_Zixzax);
CharacterTeleportToTrigger(CHARACTER_CYS_Zixzax,TRIGGER_HOM_TeleportReturnFromObservatory,"zixzaxintro4");
//END_REGION

//REGION Homestead intro - Weaver
IF
CharacterEvent(CHARACTER_CYS_Zixzax,"zixzaxintro4")
THEN
DB_Subregion(TRIGGER_HOM_REGION_Observatory,"SUBREGION_HOM_Observatory",0);
CharacterDisplayText(CHARACTER_CYS_Zixzax,"HOM_ZixZax_ThroughPortal");
CharacterMoveToCharacter(CHARACTER_CYS_Zixzax,CHARACTER_HOM_Weaver,1,"zixzaxintro7",0);

IF
CharacterDisplayTextEnded(CHARACTER_CYS_Zixzax,"HOM_ZixZax_ThroughPortal")
AND
CharacterGetClosestPlayer(CHARACTER_CYS_Zixzax,_Player)
AND
DB_CurrentLevel("Homestead")
THEN
Launch_PlayerComment(_Player, "HOM_Comment_HallwayEntry");

IF
CharacterEvent(CHARACTER_CYS_Zixzax,"zixzaxintro7")
THEN
CharacterLookAtCharacter(CHARACTER_CYS_Zixzax,CHARACTER_HOM_Weaver);
CharacterLookAtCharacter(CHARACTER_HOM_Weaver,CHARACTER_CYS_Zixzax);
DB_OneShot_DialogTrigger(TRIGGER_HOM_BoxZixzaxWeaverReminder,"HOM_ZixzaxWeaver",CHARACTER_CYS_Zixzax,CHARACTER_HOM_Weaver);

IF
DialogStarted("HOM_ZixzaxWeaver",_)
THEN
CharacterLookAtCharacter(CHARACTER_CYS_Zixzax,CHARACTER_HOM_Weaver);
CharacterLookAtCharacter(CHARACTER_HOM_Weaver,CHARACTER_CYS_Zixzax);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Zixzax,"HOM_ZixZax_Intro2");
QuestUpdate("HOM_Starstones","Weaver");
QuestUpdate("HOM_Starstones","NotOnTapestry");

IF
DialogEnded("HOM_ZixzaxWeaver",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
ProcDefinePartyDialog("HOM_PD_AfterZixzaxWeaver");
CharacterLookAtCharacter(CHARACTER_CYS_Zixzax,_Player);
DB_Dialogs(CHARACTER_CYS_Zixzax,"HOM_ZixZax");
CharacterSetVarInteger(CHARACTER_CYS_Zixzax, "AfterIntro", 1);

//END_REGION

IF
DialogEnded("HOM_ZixZax",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
AND
NOT DB_OnlyOnce("PyramidsAndKeyGiven")
THEN
DB_OnlyOnce("PyramidsAndKeyGiven");
CharacterLookAtCharacter(CHARACTER_CYS_Zixzax,CHARACTER_HOM_Weaver);
GlobalSetEvent("CYS_HomesteadKeyGiven");
DB_PlayerComments("GLO_CommentZapped","GLO_CommentZapped_1_1",1,1);
DB_PlayerComments("GLO_CommentZapped","GLO_CommentZapped_2_1",2,1);
DB_PlayerComments("GLO_CommentZapped","GLO_CommentZapped_2_2",2,2);
QuestUpdate("HOM_Starstones","Key");
CharacterSetVarItem(CHARACTER_CYS_Zixzax,"Seat",ITEM_HOM_Chair_Zixzax);
CharacterSetVarInteger(CHARACTER_CYS_Zixzax, "AfterIntro", 0);

IF
DB_OnlyOnce("PyramidsAndKeyGiven")
AND
_Player.DB_IsPlayer()
THEN
PROC_CheckPlayTutWithDelay(_Player,"TUT_Homestead",2000);

PROC
ProcGivePyramidManuals()
AND
DB_PyramidManual(_Player,_Item)
THEN
ItemToCharacter(_Item,_Player);
NOT DB_PyramidManual(_Player,_Item);

IF
DB_HOM_ProximityFire(_Trigger,_Item)
THEN
ProcTriggerRegisterForPlayers(_Trigger);

IF
CharacterEnteredTrigger(_,_Trigger)
AND
DB_HOM_ProximityFire(_Trigger,_Item)
THEN
ProcTriggerUnregisterForPlayers(_Trigger);
NOT DB_HOM_ProximityFire(_Trigger,_Item);
ItemSetEvent(_Item,"StartFire");

IF
DialogEnded("HOM_Zixzax_HallOfHeroes",_)
THEN
QuestUpdate("HOM_Starstones","ReturnToHOM");

IF
GlobalEventSet("HOM_UnlockAchievment33")
THEN
UnlockAchievement("quest33");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
