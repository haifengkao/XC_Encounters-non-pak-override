Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_Madoc,"CYS_Madoc");
ProcTriggerRegisterForPlayers(TRIGGER_CYS_GraveyardEnter);
ProcTriggerRegisterForPlayers(TRIGGER_CYS_BillehGahr);
ProcTriggerRegisterForPlayers(TRIGGER_CYS_BertiasPen);
ProcTriggerRegisterForPlayers(TRIGGER_CYS_TrainingGrounds);
DB_NoRunAway(CHARACTER_Madoc);
TriggerRegisterForCharacter(TRIGGER_CYS_MAD_ExitTavern,CHARACTER_Madoc);
DB_PlayerComments("CYS_Comment_MAD_ExitTavern", "CYS_Comment_MAD_ExitTavern_1_1", 1,1);
DB_PlayerComments("CYS_Comment_MAD_ExitTavern", "CYS_Comment_MAD_ExitTavern_1_2", 1,2);
DB_PlayerComments("CYS_Comment_MAD_ExitTavern", "CYS_Comment_MAD_ExitTavern_2_1", 2,1);
DB_PlayerComments("CYS_Comment_MAD_ExitTavern", "CYS_Comment_MAD_ExitTavern_2_2", 2,2);
DB_PlayerComments("CYS_Comment_MAD_ExitTavern", "CYS_Comment_MAD_ExitTavern_2_3", 2,3);

DB_DismissEvent(CHARACTER_Madoc,"MadocDismissed");
DB_CustomWarningDialog(CHARACTER_Madoc,"Madoc_please_Dont_attack");
DB_AttackDialog(CHARACTER_Madoc,"Companion_Madoc_Attacks");

DB_MAD_Tolerance(-5);
KBSECTION
//REGION Party join/leave
IF
DialogCharacterEventSet("Madoc_Recruited",_Player,_)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_Madoc,"CYS_Madoc");
DB_Dialogs(CHARACTER_Madoc,"MAD_Default");
DB_CompanionRecruited(CHARACTER_Madoc);
CharacterMakeCompanion(CHARACTER_Madoc,_Player);
Proc_LimitAbilitiesForCompanion(CHARACTER_Madoc);
CharacterSetEvent(CHARACTER_Madoc,"Hired");
DB_Companion(CHARACTER_Madoc);
ProcSetPartyFullIfLoneWolf();

IF
DialogEnded("MAD_Default",_)
AND 
DB_GlobalEvent("MadocDismissed")
AND
DB_Companion(CHARACTER_Madoc)
THEN
ProcDismissMadoc();

IF
DialogStarted(_,_Inst)
AND
DB_Companion(CHARACTER_Madoc)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
CharacterGetDistanceToCharacter(_Player,CHARACTER_Madoc,_Dist)
AND
_Dist<10
THEN
DB_MadoraCheck(_Player);
GlobalSetEvent("GLO_Madora_Near");

IF
DB_MadoraCheck(_Player)
AND
NOT _Player.DB_IsInDialog()
THEN
NOT DB_MadoraCheck(_Player);
GlobalClearEvent("GLO_Madora_Near");

PROC
ProcDoDismissOfCompanion(CHARACTER_Madoc)
THEN
ProcDismissMadoc();

PROC
ProcDismissMadoc()
AND
DB_Companion(CHARACTER_Madoc)
THEN
OnCompanionDismissed(CHARACTER_Madoc);
ProcHACKFixCompanionReputation(CHARACTER_Madoc);

PROC
ProcDismissMadoc()
AND
NOT DB_GlobalEvent("HallOfHeroesUnlocked")
AND
CharacterGetPosition(CHARACTER_Madoc,_X,_Y,_Z)
AND
DB_Companion(CHARACTER_Madoc)
THEN
ProcCancelAllCompanionDialogs(CHARACTER_Madoc);
NOT DB_Companion(CHARACTER_Madoc);
CharacterDetachFromGroup(CHARACTER_Madoc);
CharacterMakeNPC(CHARACTER_Madoc);
CharacterRemoveFromParty(CHARACTER_Madoc);
CharacterTeleportToTrigger(CHARACTER_Madoc,TRIGGER_CYS_Tavern_Madoc,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PROC_END_RemoveCompInDBRoomB(CHARACTER_Madoc);

PROC
ProcDismissMadoc()
AND
DB_GlobalEvent("HallOfHeroesUnlocked")
AND
CharacterGetPosition(CHARACTER_Madoc,_X,_Y,_Z)
AND
DB_Companion(CHARACTER_Madoc)
THEN
NOT DB_Companion(CHARACTER_Madoc);
CharacterDetachFromGroup(CHARACTER_Madoc);
CharacterMakeNPC(CHARACTER_Madoc);
CharacterRemoveFromParty(CHARACTER_Madoc);
CharacterTeleportToTrigger(CHARACTER_Madoc,TRIGGER_HOM_HoH_Madoc_Neutral,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PROC_END_RemoveCompInDBRoomB(CHARACTER_Madoc);

IF
DialogCharacterEventSet("MadocRejoins",_Player,_ID)
THEN
DB_Companion(CHARACTER_Madoc);
CharacterMakeCompanion(CHARACTER_Madoc,_Player);
DialogClearCharacterEvent("MadocRejoins",_Player,_ID);
ProcSetPartyFullIfLoneWolf();

IF
GlobalEventSet("HallOfHeroesUnlocked")
AND
DB_GlobalEvent("MadocDismissed")
THEN
CharacterTeleportToTrigger(CHARACTER_Madoc,TRIGGER_HOM_HoH_Madoc_Neutral,"");
//END_REGION

//REGION Tolerance
IF
GlobalEventSet("MAD_PlusTolerance")
AND
DB_MAD_Tolerance(_OldVal)
AND
IntegerSum(_OldVal,1,_NewVal)
THEN
NOT DB_MAD_Tolerance(_OldVal);
DB_MAD_Tolerance(_NewVal);
GlobalClearEvent("MAD_PlusTolerance");
PROC_CheckIfMadoraIsTolerant(_NewVal);

IF
GlobalEventSet("MAD_MinTolerance")
AND
DB_MAD_Tolerance(_OldVal)
AND
IntegerSum(_OldVal,-1,_NewVal)
THEN
NOT DB_MAD_Tolerance(_OldVal);
DB_MAD_Tolerance(_NewVal);
GlobalClearEvent("MAD_MinTolerance");
GlobalClearEvent("MAD_Tolerant");
PROC_CheckIfMadoraIsTolerant(_NewVal);

IF
TextEventSet("MadTol")
THEN
GlobalSetEvent("MAD_PlusTolerance");

PROC
PROC_CheckIfMadoraIsTolerant((INTEGER)_NewVal)
AND
_NewVal >= 0
THEN
GlobalSetEvent("MAD_Tolerant");
//END_REGION

//REGION Companion comments & dialogs
IF
GlobalEventSet("CYS_ArrestEsmeralda")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_ArrestEsme",CHARACTER_CYS_Aureus);

IF
GlobalEventSet("CYS_AureusFreeEsme")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_FreeEsme",CHARACTER_CYS_Aureus);

IF
GlobalEventSet("CYS_ActuallyKilledVictoria")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_KilledVictoria",CHARACTER_CYS_Eglandaer);

IF
DialogEnded("CYS_GreavingOrc",_)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_GreavingOrc",CHARACTER_CYS_GreavingOrc);

IF
GlobalEventSet("CYS_BertiaKeepGold")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_BagOfGold",CHARACTER_CYS_Bertia);

IF
CharacterDied(CHARACTER_CYS_NorthCaveSkeleton4)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_ArhuRobot",CHARACTER_CYS_NorthCaveSkeleton4);

IF
DialogEnded("CYS_Zapper",_Instance)
AND
DB_DialogPlayers(_Instance,_Player,1)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_Zapper",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_GraveyardEnter)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_Graveyard",_Player);

IF
CharacterLeftTrigger(_Player,TRIGGER_CYS_BillehGahr)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_BillehGahr",_Player);

IF
DialogEnded("CYS_Jake",_)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_ZombieJake",CHARACTER_CYS_ZombieJake);

IF
DialogEnded("CYS_Reginald",_Instance)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_Nick",CHARACTER_CYS_Reginald);

IF
DialogEnded("LUC_FortuneTeller",_Instance)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_FortuneTeller",CHARACTER_LUC_FortuneTeller);

IF
AutomatedDialogEnded("HIB_AD_IcaraZixzax",_Instance)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_LeaveIcara",CHARACTER_HIB_Icara);

IF
DialogEnded("HIB_Staff",_Instance)
AND
ItemGetClosestPlayer(ITEM_HIB_ElementalStaff,_Player)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_BoreasStaff",_Player);

IF
DialogEnded("CYS_OrcRaidGuards",_)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_HarbourGuardTalk",CHARACTER_CYS_RaidGuard1);

IF
CharacterEnteredTrigger(CHARACTER_Madoc,TRIGGER_CYS_TrainingGrounds)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_TrainingGrounds",CHARACTER_Madoc);

IF
DialogEnded("CYS_Cecil",_)
AND
DB_GlobalEvent("CYS_StaffOfPergamon")
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_Staff",CHARACTER_CYS_Cecil);

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_BertiasPen)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_Bertia",_Player);

IF
CharacterEnteredTrigger(CHARACTER_Madoc,TRIGGER_CYS_MAD_ExitTavern)
AND
DB_Companion(CHARACTER_Madoc)
AND
CharacterGetClosestAlivePlayer(CHARACTER_Madoc,_Player)
AND
CharacterGetDistanceToCharacter(CHARACTER_Madoc,_Player,_Dist)
THEN
DB_PlayerComments_Speaker("CYS_Comment_MAD_ExitTavern",CHARACTER_Madoc,_Player);
Check_PlayerComments_Distance("CYS_Comment_MAD_ExitTavern",CHARACTER_Madoc,_Dist);

IF
DialogEnded("CYS_UnsinkableSam",_)
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_Maxine",CHARACTER_CYS_UnsinkableSam);

IF
CharacterDied(_SnowWarden)
AND
DB_HIB_IcePrisonWardens(_,_SnowWarden,_,_)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_Wardens",_SnowWarden);

IF
DialogEnded("LUC_Lawrence",_)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_Lawrence",CHARACTER_LUC_Lawrence);

IF
DialogEnded("LUC_WitchCave_Spirit",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_WitchLake",_Player);

IF
DialogEnded("LUC_Brandon",_ID)
AND
DB_GlobalEvent("LUC_ReceivedBloodCage")
THEN
GlobalClearEvent("LUC_ReceivedBloodCage");
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_GetBrandonQuest",CHARACTER_LUC_Overseer);

IF
GlobalEventSet("LUC_ImpSlaveStandDown")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_ToldImpReturnMaster",CHARACTER_LUC_ImpSlave);

IF
GlobalEventSet("HeardNorok")
AND
NOT DB_GlobalEvent("DAF_NorokAggro")
AND
NOT DB_GlobalEvent("DAF_NorokLeave")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_HeardAboutNorok",CHARACTER_DAF_Leonard);

IF
DialogEnded("DAF_Grutilda",_)
AND
DB_GlobalEvent("HeardNorokIsChildlike")
AND
NOT DB_GlobalEvent("DAF_NorokAggro")
AND
NOT DB_GlobalEvent("DAF_NorokLeave")
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_NorokChildlike",CHARACTER_DAF_Grutilda);

IF
CharacterDied(CHARACTER_DAF_Norok)
AND
DB_GlobalEvent("DAF_NorokLeave")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_CD_Leave",CHARACTER_DAF_Norok);

IF
DialogEnded("LUC_Bjorn",_)
THEN
GlobalSetEvent("MadoraNearBjorn");
GlobalClearEvent("MadoraNearMikas");
GlobalClearEvent("MadoraNearOrcGoblinSlave");
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_Truth_1",CHARACTER_LUC_Bjorn);

IF
DialogEnded("LUC_MikeTheMiner",_)
THEN
GlobalClearEvent("MadoraNearBjorn");
GlobalSetEvent("MadoraNearMikas");
GlobalClearEvent("MadoraNearOrcGoblinSlave");
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_Truth_1",CHARACTER_LUC_MikeTheMiner);
GlobalSetEvent("MadoraTalkedToMike");

IF
DialogEnded("LUC_OrcGoblinsSlave",_)
THEN
GlobalClearEvent("MadoraNearBjorn");
GlobalClearEvent("MadoraNearMikas");
GlobalSetEvent("MadoraNearOrcGoblinSlave");
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_Truth_1",CHARACTER_LUC_OrcGoblinsSlave);

IF
CharacterEnteredRegion(CHARACTER_Madoc,"DarkForest")
THEN
Proc_CompanionDialog(CHARACTER_Madoc,"MAD_Truth_2",CHARACTER_Madoc);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
