Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_PickOneBarrel(ITEM_DAF_PickOneBarrel_1);
DB_PickOneBarrel(ITEM_DAF_PickOneBarrel_2);
DB_PickOneBarrel(ITEM_DAF_PickOneBarrel_3);

DB_PlayerComments("DAF_Comment_PickOneBarrel","DAF_Comment_PickOneBarrel_1_1",1,1);
DB_PlayerComments("DAF_Comment_PickOneBarrel","DAF_Comment_PickOneBarrel_2_1",2,1);
DB_PlayerComments("DAF_Comment_PickOneBarrel","DAF_Comment_PickOneBarrel_2_2",2,2);

CharacterSetOnStage(CHARACTER_DAF_Bellegar_Barrels,0);
KBSECTION
IF
DB_PickOneBarrel(_Item)
THEN
ItemSetOnstage(_Item,0);

IF
TextEventSet("unlockbellegar")
AND
NOT DB_OnlyOnce("DAF_Bellegar_Encounter1")
THEN
DB_OnlyOnce("DAF_Bellegar_Encounter1");
DB_OneShotPlayerTrigger(TRIGGER_DAF_Bellegar_Barrels_Box);
CharacterSetOnStage(CHARACTER_DAF_Bellegar_Barrels,1);
ProcDAF_BarrelsOnStage(1);

IF
RegionStarted("DarkForest")
AND
DB_GlobalEvent("CYS_Bellegar_Freed")
AND
NOT DB_OnlyOnce("DAF_Bellegar_Encounter1")
AND
NOT DB_Dead(CHARACTER_CYS_Bellegar)
THEN
DB_OnlyOnce("DAF_Bellegar_Encounter1");
DB_OneShotPlayerTrigger(TRIGGER_DAF_Bellegar_Barrels_Box);
CharacterSetOnStage(CHARACTER_DAF_Bellegar_Barrels,1);
ProcDAF_BarrelsOnStage(1);

IF
CharacterDied(CHARACTER_CYS_Bellegar)
AND
DB_OnlyOnce("DAF_Bellegar_Encounter1")
THEN
ProcTriggerUnregisterForPlayers(TRIGGER_DAF_Bellegar_Barrels_Box);
CharacterSetOnStage(CHARACTER_DAF_Bellegar_Barrels,0);
ProcDAF_BarrelsOnStage(0);

PROC
ProcDAF_BarrelsOnStage((INTEGER)_Value)
AND
DB_PickOneBarrel(_Item)
THEN
ItemSetOnstage(_Item,_Value);

IF
ItemTemplateRemovedFromContainer(_,_ItemHandle,_Item)
AND
DB_PickOneBarrel(_Item)
AND
DB_OnlyOnce("DAF_PickOneBarrel")
THEN
ItemHandleDelete(_ItemHandle);

IF
ItemTemplateRemovedFromContainer(_,_,_Item)
AND
DB_PickOneBarrel(_Item)
AND
ItemGetClosestPlayer(_Item,_Player)
AND
NOT DB_OnlyOnce("DAF_PickOneBarrel")
THEN
DB_OnlyOnce("DAF_PickOneBarrel");
Proc_ClearPickOneBarrels();
Launch_PlayerComment(_Player,"DAF_Comment_PickOneBarrel");
QuestUpdate("DAF_Bellegar_Barrels","One");
QuestClose("DAF_Bellegar_Barrels");

PROC
Proc_ClearPickOneBarrels()
AND
DB_PickOneBarrel(_Item)
THEN
ContainerMoveAllItemsToCharacter(_Item,CHARACTER_LUC_PickAxeHolderHack);
ItemPlayEffect(_Item,"FX_Skills_Air_Storm_Impact_A");
ItemDestroy(_Item);

IF
ItemAttackedByCharacter(_Item,_Player,_)
AND
DB_PickOneBarrel(_Item)
THEN
DB_OnlyOnce("DAF_PickOneBarrel");
Proc_ClearPickOneBarrels();
Launch_PlayerComment(_Player,"DAF_Comment_PickOneBarrel");
QuestUpdate("DAF_Bellegar_Barrels","Zero");
QuestClose("DAF_Bellegar_Barrels");

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DAF_Bellegar_Barrels_Box)
THEN
DialogStartOneSpeakerDialog("DAF_AD_Bellegar_Barrel",CHARACTER_DAF_Bellegar_Barrels);
QuestAdd("DAF_Bellegar_Barrels");
QuestUpdate("DAF_Bellegar_Barrels","Start");

IF
AutomatedDialogEnded("DAF_AD_Bellegar_Barrel",_)
THEN
Poof(CHARACTER_DAF_Bellegar_Barrels);

IF
CharacterReceivedDamage(CHARACTER_DAF_Bellegar_Barrels)
AND
NOT DB_OnlyOnce("DAF_Bellegar_Barrels_Poof")
THEN
Poof(CHARACTER_DAF_Bellegar_Barrels);
DB_OnlyOnce("DAF_Bellegar_Barrels_Poof");

IF
CharacterAttackedByCharacter(CHARACTER_DAF_Bellegar_Barrels,_)
AND
NOT DB_OnlyOnce("DAF_Bellegar_Barrels_Poof")
THEN
Poof(CHARACTER_DAF_Bellegar_Barrels);
DB_OnlyOnce("DAF_Bellegar_Barrels_Poof");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
