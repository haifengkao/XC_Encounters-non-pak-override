Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION GATE & TRAPS
DB_PlayerComments("HIB_CommentGardenGate5","HIB_CommentGardenGate5_1_1",1,1);
DB_PlayerComments("HIB_CommentGardenGate5","HIB_CommentGardenGate5_2_1",2,1);
DB_PlayerComments("HIB_CommentGardenGate5","HIB_CommentGardenGate5_2_2",2,2);
ProcTriggerRegisterForPlayers(TRIGGER_HIB_AreaGardenGate5);
CharacterSetOnStage(CHARACTER_CYS_WilliamWell_DialogHack,0);

PROC_HIB_WishingWellLavaPitInit();
DB_HIB_WishingWellPressurePlate(ITEM_HIB_PressurePlate_WishingWellEntrance1);
DB_HIB_WishingWellPressurePlate(ITEM_HIB_PressurePlate_WishingWellEntrance2);
DB_HIB_WishingWellPressurePlate(ITEM_HIB_PressurePlate_WishingWellEntrance3);
DB_HIB_WishingWellPressurePlate(ITEM_HIB_PressurePlate_WishingWellEntrance4);

ItemSetOnstage(ITEM_HIB_PressurePlate_WishingWellEntrance4,0);
DB_ShovelArea(TRIGGER_HIB_ShovelPressurePlate,"HIB_ShovelPressurePlate_Well",ITEM_HIB_ShovelPressurePlate_WellDirt);
DB_ShovelRewardEvent("HIB_ShovelPressurePlate_Well","HIB_ShovelPressurePlate_WellRevealed");
//END_REGION

TriggerRegisterForPlayers(TRIGGER_HIB_Area_WilliamWell);

CharacterSetVisible(CHARACTER_HIB_WilliamWell_DialogHack,0);
CharacterSetCanFight(CHARACTER_HIB_WilliamWell_DialogHack,0);
CharacterSetImmortal(CHARACTER_HIB_WilliamWell_DialogHack,1);
DB_NoDefaultBehavior(CHARACTER_HIB_WilliamWell_DialogHack);
CharacterMakeStoryNpc(CHARACTER_HIB_WilliamWell_DialogHack,1);
CharacterFreeze(CHARACTER_HIB_WilliamWell_DialogHack);
KBSECTION
//REGION GATE TRAPS
IF
CharacterUsedItem(_Player,ITEM_HIB_GardenGate5)
AND
DB_InRegion(_Player,TRIGGER_HIB_AreaGardenGate5)
THEN
Launch_PlayerComment(_Player,"HIB_CommentGardenGate5");

IF
CharacterItemEvent(_Player,ITEM_HIB_GardenGate5_Lever, "HIB_WishingWellPuzzleEntrance_Lava_Off")
THEN
ItemUnlockAndOpen(ITEM_HIB_GardenGate5);
ItemUnlockAndOpen(ITEM_HIB_GardenGate5_2);
ItemUnlockAndOpen(ITEM_HIB_GardenGate5_3);
ProcTriggerUnregisterForPlayers(TRIGGER_HIB_AreaGardenGate5);
ITEM_HIB_GardenGate5_Lever.IsUnlocked();
PROC_HIB_WishingWellLavaPitClear();
GlobalSetEvent("HIB_WishingWellGateOpened");

IF
GlobalEventSet("HIB_ShovelPressurePlate_WellRevealed")
THEN
ItemSetOnstage(ITEM_HIB_PressurePlate_WishingWellEntrance4,1);

PROC
PROC_HIB_WishingWellLavaPitInit()
THEN
TriggerCreateSurface(TRIGGER_HIB_Point_WishingWellEntrance1,"Lava",5.0);

PROC
PROC_HIB_WishingWellLavaPitClear()
THEN
TriggerCreateSurface(TRIGGER_HIB_Point_WishingWellEntrance1,"None",5.5);

IF
CharacterItemEvent(_,_PressurePlate,"HIB_PressurePlate_WishingWellEntrance_On")
AND
DB_HIB_WishingWellPressurePlate(_PressurePlate)
THEN
PROC_HIB_WishingWellLavaPitCheck();

PROC
PROC_HIB_WishingWellLavaPitCheck()
AND
ItemGetVarFloat(ITEM_HIB_PressurePlate_WishingWellEntrance1,"CurrentPercentage",1.0)
AND
ItemGetVarFloat(ITEM_HIB_PressurePlate_WishingWellEntrance2,"CurrentPercentage",1.0)
AND
ItemGetVarFloat(ITEM_HIB_PressurePlate_WishingWellEntrance3,"CurrentPercentage",1.0)
AND
ItemGetVarFloat(ITEM_HIB_PressurePlate_WishingWellEntrance4,"CurrentPercentage",1.0)
THEN
PROC_HIB_WishingWellLavaPitClear();

IF
CharacterItemEvent(_,_PressurePlate,"HIB_PressurePlate_WishingWellEntrance_Off")
AND
DB_HIB_WishingWellPressurePlate(_PressurePlate)
AND
NOT ITEM_HIB_GardenGate5_Lever.IsUnlocked()
THEN
PROC_HIB_WishingWellLavaPitInit();
//END_REGION

//REGION WISHES
IF
DialogEnded("HIB_WilliamWell",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_WilliamWell_DialogHack,"HIB_WellStopsSnowstorm",1)
THEN
CharacterSetVarInteger(CHARACTER_HIB_WilliamWell_DialogHack,"HIB_WellStopsSnowstorm",0);
ItemSetCanInteract(ITEM_HIB_William,0);
ItemPlayEffect(ITEM_HIB_William,"FX_Skills_Air_Storm_Impact_A");
TimerLaunch("HIB_WellStopsSnowstorm",4000);

IF
TimerFinished("HIB_WellStopsSnowstorm")
THEN
ItemSetCanInteract(ITEM_HIB_William,1);
PROC_HIB_SnowStormOver();

IF
DialogEnded("HIB_WilliamWell",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_WilliamWell_DialogHack,"HIB_WellStopsVolcanoes",1)
THEN
CharacterSetVarInteger(CHARACTER_HIB_WilliamWell_DialogHack,"HIB_WellStopsVolcanoes",0);
ItemSetCanInteract(ITEM_HIB_William,0);
ItemPlayEffect(ITEM_HIB_William,"FX_Skills_Air_Storm_Impact_A");
TimerLaunch("HIB_WellStopsVolcanoes",4000);

IF
TimerFinished("HIB_WellStopsVolcanoes")
THEN
ItemSetCanInteract(ITEM_HIB_William,1);
ItemDestroy(ITEM_HIB_Volcano1_Invisible);
ItemDestroy(ITEM_HIB_Volcano2_Invisible);
ItemDestroy(ITEM_HIB_Volcano3_Invisible);
ItemDestroy(ITEM_HIB_Volcano4_Invisible);
ItemDestroy(ITEM_HIB_Volcano5_Invisible);
//END_REGION

//REGION HELPING WILLIAM
IF
DialogEnded("HIB_WilliamWell",_ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
NOT QuerCheckIfPlayerFarFrom(_Player)
AND
CharacterGetVarInteger(_Player,"HIB_WellSendsToCyseal",1)
THEN
CharacterSetVarInteger(_Player,"HIB_WellSendsToCyseal",0);
CharacterTeleportPartyToTrigger(TRIGGER_CYS_WalterArrival,"");

IF
DialogEnded("HIB_WilliamWell",_ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
QuerCheckIfPlayerFarFrom(_Player)
AND
CharacterGetVarInteger(_Player,"HIB_WellSendsToCyseal",1)
THEN
CharacterSetVarInteger(_Player,"HIB_WellSendsToCyseal",0);
ProcDoTwoSpeakerDialog("HIB_WilliamFailTP", CHARACTER_HIB_WilliamWell_DialogHack, _Player);

IF
ItemAddedToCharacter(ITEM_HIB_WellScroll,_Player)
AND
_Player.DB_IsPlayer()
THEN
GlobalSetEvent("HIB_HasWilliamScroll");
QuestUpdate("CYS_WishingWell","FoundSpell");

IF
ItemRemovedFromCharacter(ITEM_HIB_WellScroll,_Player)
AND
_Player.DB_IsPlayer()
THEN
GlobalClearEvent("HIB_HasWilliamScroll");

IF
DialogCharacterEventSet("HIB_TakeWilliamScroll",_Player,_)
AND
ItemIsInPartyInventory(ITEM_HIB_WellScroll,_Player,1,1)
THEN
NOOP(1);

IF
GameBookInterfaceClosed(ITEM_HIB_WellScroll,_Player)
THEN
PROC_HIB_WellCheckSpell(_Player);

PROC
PROC_HIB_WellCheckSpell((CHARACTER)_Player)
AND
NOT DB_InRegion(_Player,TRIGGER_HIB_Area_WilliamWell)
THEN
CharacterDisplayText(_Player,"HIB_WellSpellFailed");

PROC
PROC_HIB_WellCheckSpell((CHARACTER)_Player)
AND
DB_InRegion(_Player,TRIGGER_HIB_Area_WilliamWell)
THEN
TriggerUnregisterForPlayers(TRIGGER_HIB_Area_WilliamWell);
ItemSetCanInteract(ITEM_HIB_William,0);
ItemDestroy(ITEM_HIB_WellScroll);
GlobalSetEvent("HIB_WilliamGoingHome");

IF
CharacterUsedItem(_Player,ITEM_HIB_William)
AND
_Player.DB_IsPlayer()
THEN
ProcDoTwoSpeakerDialog("HIB_WilliamWell",CHARACTER_HIB_WilliamWell_DialogHack,_Player);

IF
GlobalEventSet("HIB_WilliamGoingHome")
AND
ItemGetClosestPlayer(ITEM_HIB_William,_Player)
THEN
ProcDoTwoSpeakerDialog("HIB_WilliamWell",CHARACTER_HIB_WilliamWell_DialogHack,_Player);

IF
DialogEnded("HIB_WilliamWell",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
AND
CharacterGetVarInteger(_Player,"HIB_SendsWilliamHome",1)
THEN
CharacterSetVarInteger(_Player,"HIB_SendsWilliamHome",0);
NOT DB_ItemDialogs(ITEM_HIB_William,"HIB_WilliamWell");
TimerLaunch("HIB_WellLeaves",1000);

IF
TimerFinished("HIB_WellLeaves")
THEN
PlayEffectAtItem("FX_GP_TeleportSmoke_A",ITEM_HIB_William);
CharacterSetOnStage(CHARACTER_HIB_WilliamWell_DialogHack,0);
CharacterSetOnStage(CHARACTER_CYS_WilliamWell_DialogHack,1);
ItemSetOnstage(ITEM_HIB_William,0);
//ItemSetOnstage(ITEM_CYS_WellSprout,0);
ItemSetOnstage(ITEM_CYS_WilliamWell,1);
QuestUpdate("CYS_WishingWell","EndHIB");



//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
