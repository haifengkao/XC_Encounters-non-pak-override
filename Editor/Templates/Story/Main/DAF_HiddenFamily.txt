Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_DAF_HiddenFamilyMother,"DAF_BO_HiddenFamily");
DB_Dialogs(CHARACTER_DAF_HiddenFamilyFather,"DAF_BO_HiddenFather");
DB_Dialogs(CHARACTER_DAF_HiddenFamilyDaughter,"DAF_BO_HiddenDaughter");
DB_OneShot_DialogTrigger_NewSystem(TRIGGER_DAF_Area_BO_HiddenFamilyDialog,"DAF_BO_HiddenFamily",CHARACTER_DAF_HiddenFamilyMother);

DB_DAF_HiddenFamily(CHARACTER_DAF_HiddenFamilyFather);
DB_DAF_HiddenFamily(CHARACTER_DAF_HiddenFamilyMother);
DB_DAF_HiddenFamily(CHARACTER_DAF_HiddenFamilyDaughter);
KBSECTION
//REGION Family Escape
IF
DialogEnded("DAF_BO_HiddenFamily",_)
AND
NOT DB_OnlyOnce("HiddenFamilyStartScripts")
THEN
DB_OnlyOnce("HiddenFamilyStartScripts");
CharacterSetVarItem(CHARACTER_DAF_HiddenFamilyDaughter,"Seat",ITEM_DAF_HiddenFamily_Chair);
CharacterSetVarItem(CHARACTER_DAF_HiddenFamilyMother,"Seat",ITEM_DAF_HiddenFamily_Chair2);
CharacterSetVarItem(CHARACTER_DAF_HiddenFamilyFather,"Seat",ITEM_DAF_HiddenFamily_Chair3);

IF
DialogEnded("DAF_BO_HiddenFamily",_)
AND
DB_GlobalEvent("DAF_HiddenFamilyLeaves")
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_DAF_HiddenFamilyFather,"DAF_BO_HiddenFamily");
PROC_DAF_HiddenFamilyLeaves();

IF
DialogEnded("DAF_BO_HiddenFamily",_)
AND
DB_GlobalEvent("DAF_HiddenFamilyAggro")
AND
DB_DAF_HiddenFamily(_Character)
THEN
ProcSetRelationToPlayers(_Character,0);

PROC
PROC_DAF_HiddenFamilyLeaves()
AND
DB_DAF_HiddenFamily(_FamilyMember)
THEN
CharacterSetHasDialog(_FamilyMember,0);
ProcCharacterMoveToTrigger(_FamilyMember,TRIGGER_DAF_DruidPortal_OUT,1,"DB_DAF_HiddenFamilyEscape_ReachedPortal");

IF
CharacterEvent(_FamilyMember,"DB_DAF_HiddenFamilyEscape_ReachedPortal")
THEN
CharacterTeleportToTrigger(_FamilyMember,TRIGGER_DAF_DruidPortal_IN,"DB_DAF_HiddenFamilyEscape_ReachedHouse");

IF
CharacterEvent(_FamilyMember,"DB_DAF_HiddenFamilyEscape_ReachedHouse")
THEN
ProcCharacterMoveToTrigger(_FamilyMember,TRIGGER_DAF_DruidHouse_1stFloorOUT,1,"DB_DAF_HiddenFamilyEscape_ReachedStairs");

IF
CharacterEvent(_FamilyMember,"DB_DAF_HiddenFamilyEscape_ReachedStairs")
THEN
CharacterTeleportToTrigger(_FamilyMember,TRIGGER_DAF_DruidHouse_1stFloorIN,"DB_DAF_HiddenFamilyEscape_ReachedTop");

IF
CharacterEvent(_FamilyMember,"DB_DAF_HiddenFamilyEscape_ReachedTop")
THEN
ProcCharacterMoveToTrigger(_FamilyMember,TRIGGER_WAYP_DarkForest_DruidHouse_Pos,1,"DB_DAF_HiddenFamilyEscape_ReachedWaypoint");

IF
CharacterEvent(_FamilyMember,"DB_DAF_HiddenFamilyEscape_ReachedWaypoint")
THEN
Poof(_FamilyMember);

//END_REGION


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
