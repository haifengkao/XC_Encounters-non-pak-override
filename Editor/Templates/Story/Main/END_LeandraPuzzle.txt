Version 1
SubGoalCombiner SGC_AND
INITSECTION
// NORMAL SIZE LEANDRA
DB_NoDefaultBehavior(CHARACTER_END_NegaLeandra);
CharacterSetOnStage(CHARACTER_END_NegaLeandra,0);
IsStoryNpc(CHARACTER_END_NegaLeandra);
CharacterApplyStatus(CHARACTER_END_NegaLeandra,"VOID_AURA",-1.0,1);
DB_END_LeandraVoiceNoPoof("END_RoomA_LeandraLine2");

// TELEPORTS
DB_END_RoomA_Shrines(ITEM_END_RoomA_Shrine1,TRIGGER_END_TP_RoomA_0_1,"END_LeandraPuzzle_Riddle1");
DB_END_RoomA_Shrines(ITEM_END_RoomA_Shrine2,TRIGGER_END_TP_RoomA_1_2,"END_LeandraPuzzle_Riddle2");

DB_END_RoomA_Dialogs(TRIGGER_END_TP_RoomA_0_1,"END_RoomA_Island1","END_AD_LP_Leandra_Riddle1");
DB_END_RoomA_Dialogs(TRIGGER_END_TP_RoomA_1_2,"END_RoomA_Island2","END_AD_LP_Leandra_Comment2");

END_RoomA_Shadow_Effect(ITEM_END_RoomA_Shrine1,TRIGGER_END_Point_Shrine1Effect);
END_RoomA_Shadow_Effect(ITEM_END_RoomA_Shrine2,TRIGGER_END_Point_Shrine2Effect);

// LEANDRA RAGE EFFECT
DB_RoomA_Lights(TRIGGER_END_RoomA_Light1,"END_RoomA_Light1","END_RoomA_Light2","FX_GP_StarStone_Down_B",1.5,400);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light2,"END_RoomA_Light2","END_RoomA_Light3","FX_GP_StarStone_Down_A",2.1,355);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light3,"END_RoomA_Light3","END_RoomA_Light4","FX_GP_StarStone_Down_B",1.8,415);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light4,"END_RoomA_Light4","END_RoomA_Light5","FX_GP_StarStone_Down_A",1.7,385);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light5,"END_RoomA_Light5","END_RoomA_Light6","FX_GP_StarStone_Down_B",2.2,425);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light6,"END_RoomA_Light6","END_RoomA_Light7","FX_GP_StarStone_Down_A",1.9,370);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light7,"END_RoomA_Light7","END_RoomA_Light8","FX_GP_StarStone_Down_A",2.0,400);
DB_RoomA_Lights(TRIGGER_END_RoomA_Light8,"END_RoomA_Light8","","FX_GP_StarStone_Down_B",2.5,370);

DB_RoomA_Lights2(TRIGGER_END_RoomA_Light1_2,"END_RoomA_Light1_2","END_RoomA_Light2_2","FX_GP_StarStone_Down_B",1.5,400);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light2_2,"END_RoomA_Light2_2","END_RoomA_Light3_2","FX_GP_StarStone_Down_A",2.1,355);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light3_2,"END_RoomA_Light3_2","END_RoomA_Light4_2","FX_GP_StarStone_Down_B",1.8,415);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light4_2,"END_RoomA_Light4_2","END_RoomA_Light5_2","FX_GP_StarStone_Down_A",1.7,385);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light5_2,"END_RoomA_Light5_2","END_RoomA_Light6_2","FX_GP_StarStone_Down_B",2.2,425);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light6_2,"END_RoomA_Light6_2","END_RoomA_Light7_2","FX_GP_StarStone_Down_A",1.9,370);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light7_2,"END_RoomA_Light7_2","END_RoomA_Light8_2","FX_GP_StarStone_Down_A",2.0,400);
DB_RoomA_Lights2(TRIGGER_END_RoomA_Light8_2,"END_RoomA_Light8_2","","FX_GP_StarStone_Down_B",2.5,370);

// GIANT LEANDRA
CharacterForceSynch(CHARACTER_END_RoomALeandra,1);
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage3");
DB_END_RoomA_Fear(1,"END_RoomA_LeandraAttack1","Stage_01_Rage_02");
DB_END_RoomA_Fear(2,"END_RoomA_LeandraAttack2","Stage_02_Rage_02");
DB_OneShotPlayerTrigger(TRIGGER_END_RoomA_SpawnLeandra);
DB_OneShotPlayerTrigger(TRIGGER_END_RoomA_Island1Transition);
DB_OneShotPlayerTrigger(TRIGGER_END_RoomA_Island2Transition);
DB_OneShotPlayerTrigger(TRIGGER_END_RoomA_Leandra3);
ProcTriggerRegisterForPlayers(TRIGGER_END_RoomA_Knock1);
ProcTriggerRegisterForPlayers(TRIGGER_END_RoomA_Knock2);


//ITEMS
ItemSetOnstage(ITEM_END_RoomA_Door,0);
ItemSetOnstage(ITEM_END_RoomA_Platform,0);
ItemApplyStatus(ITEM_END_RoomA_Door,"VOID_AURA",-1.0,1);
ItemApplyStatus(ITEM_END_RoomA_Shrine1,"VOID_AURA",-1.0,1);
ItemApplyStatus(ITEM_END_RoomA_Shrine2,"VOID_AURA",-1.0,1);

///DB_RoomA_LeandraGroundShake("Start","Stop");
DB_RoomA_LeandraGroundShake("END_RoomA_Spawn11","END_RoomA_Spawned11");
DB_RoomA_LeandraGroundShake("END_RoomA_Transition12","END_RoomA_TransitionEnded12");
DB_RoomA_LeandraGroundShake("END_RoomA_Spawn12","END_RoomA_Spawned12");
DB_RoomA_LeandraGroundShake("END_RoomA_Transition21","END_RoomA_TransitionEnded21");
DB_RoomA_LeandraGroundShake("END_RoomA_Spawn21","END_RoomA_Spawned21");
DB_RoomA_LeandraGroundShake("END_RoomA_Transition22","END_RoomA_TransitionEnded22");
DB_RoomA_LeandraGroundShake("END_RoomA_Spawn22","END_RoomA_Spawned22");
DB_RoomA_LeandraGroundShake("END_RoomA_Transition31","END_RoomA_TransitionEnded31");
DB_RoomA_LeandraGroundShake("END_RoomA_Spawn3","END_RoomA_Spawned3");
DB_RoomA_LeandraGroundShake("END_RoomA_Die","END_RoomA_Dead");
DB_RoomA_LeandraGroundShake("END_RoomA_Rage3","END_RoomA_Raged3");

//DB_RoomA_LeandraKnockEvents("Event",Trigger,"Dialog");
DB_RoomA_LeandraKnockEvents("END_RoomA_Rage1",TRIGGER_END_RoomA_Knock1);
DB_RoomA_LeandraKnockEvents("END_RoomA_Rage2",TRIGGER_END_RoomA_Knock2);

KBSECTION
IF
TextEventSet("rooma")
THEN
CharacterTeleportPartyToTrigger(TRIGGER_END_RoomA_Entrance,"");
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage3");


//REGION Shrines

IF
CharacterUsedItem(_Player,_Shrine)
AND
DB_END_RoomA_Shrines(_Shrine,_Trigger,_Dialog)
AND
NOT DB_END_RoomA_ActiveShrines(_Shrine)
AND
NOT DB_END_RoomA_Punishment(1)
THEN
ProcDoTwoSpeakerItemDialog(_Dialog,_Player,_Shrine);

IF
CharacterUsedItem(_,_Shrine)
AND
DB_END_RoomA_ActiveShrines(_Shrine)
AND
DB_END_RoomA_Shrines(_Shrine,_Trigger,_)
AND
DB_END_RoomA_Dialogs(_Trigger,_Event,_)
AND
DB_IsPlayer(_Player)
THEN
CharacterTeleportToTrigger(_Player,_Trigger,_Event);
CharacterPlayEffect(_Player,"FX_GP_TeleportSmoke_A");

IF
DialogItemEventSet("END_LP_ActivateShrine",_Shrine,_)
AND
DB_END_RoomA_Shrines(_Shrine,_,_)
THEN
Proc_END_RoomA_ActivateShrine(_Shrine);

PROC
Proc_END_RoomA_ActivateShrine((ITEM)_Shrine)
THEN
ItemSetCanInteract(_Shrine,0);
PlayEffectAtItem("FX_GP_StarStone_Down_A",_Shrine);
ItemTimer(_Shrine,"END_RoomA_UnlockShrine1",400);

IF
ItemTimerFinished(_Shrine,"END_RoomA_UnlockShrine1")
AND
END_RoomA_Shadow_Effect(_Shrine,_Point)
AND
TriggerGetPosition(_Point,_X,_Y,_Z)
THEN
PlayScaledEffectAtPosition("FX_Item_NegaVanishingObjects_A",0.9,_X,_Y,_Z);
ItemTimer(_Shrine,"END_RoomA_UnlockShrine2",2500);

IF
ItemTimerFinished(_Shrine,"END_RoomA_UnlockShrine2")
THEN
ItemSetCanInteract(_Shrine,1);
DB_END_RoomA_ActiveShrines(_Shrine);
PROC_LoopEffectAtItem("FX_Env_Portal_D",_Shrine,"END_RoomA_PortalEffect","END_Endgame_Main");
ItemRemoveStatus(_Shrine,"VOID_AURA");

//END_REGION

//REGION Transitions

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_SpawnLeandra)
THEN
TimerLaunch("END_RoomA_LeandraSpawnTimer", 2000);

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_SpawnLeandra)
AND
DB_IsPlayer(_Player)
THEN
CharacterApplyStatus(_Player,"KNOCKED_DOWN",2.0,-1);

IF
TimerFinished("END_RoomA_LeandraSpawnTimer")
THEN
Launch_PlayerComment(CHARACTER_Player1,CHARACTER_Player2,"END_Comment_RoomA");
TimerLaunch("END_RoomA_LeandraSpawnTimer2", 3000);

IF
TimerFinished("END_RoomA_LeandraSpawnTimer2")
THEN
TriggerSetAtmosphere("f5d8b983-ddf2-4d42-811b-e16f9583fc95","Eden_Astroid");
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage11");
CharacterPlaySound(CHARACTER_END_RoomALeandra,"Amb_Spot_EndGame_BigLeandraBreaths_start");
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Spawn11");

IF
CharacterEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Spawn11")
THEN
CharacterTeleportToTrigger(CHARACTER_END_LeandraVoice,TRIGGER_END_RoomA_LeandraVoice1,"END_RoomA_LeandraLine1");

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_Island1Transition)
THEN
CharacterSetEvent(CHARACTER_END_RoomALeandra, "END_RoomA_Transition12");

IF
CharacterEvent(CHARACTER_END_RoomALeandra,"END_RoomA_TransitionEnded12")
THEN
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage12");
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Spawn12");

PROC
Proc_END_RoomA_ActivateShrine(ITEM_END_RoomA_Shrine1)
THEN
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Transition21");

/* ======================================================================= */

IF
CharacterEvent(CHARACTER_END_RoomALeandra,"END_RoomA_TransitionEnded21")
THEN
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage21");
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Spawn21");

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_Island2Transition)
THEN
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Transition22");
CharacterTeleportToTrigger(CHARACTER_END_LeandraVoice,TRIGGER_END_RoomA_LeandraVoice2,"END_RoomA_LeandraLine4");

IF
CharacterEvent(CHARACTER_END_LeandraVoice,_Event)
THEN
DialogStartOneSpeakerDialog(_Event,CHARACTER_END_LeandraVoice);

IF
CharacterEvent(CHARACTER_END_RoomALeandra,"END_RoomA_TransitionEnded22")
THEN
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage22");
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Spawn22");

PROC
Proc_END_RoomA_ActivateShrine(ITEM_END_RoomA_Shrine2)
THEN
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Transition31");

/* ======================================================================= */

IF
CharacterEvent(CHARACTER_END_RoomALeandra,"END_RoomA_TransitionEnded31")
THEN
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage3");
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Spawn3");

//END_REGION

//REGION Leandra rage

IF
TimerFinished(_Timer)
AND
DB_RoomA_Lights(_Light,_Timer,_NewTimer,_Effect,_Scale,_Time)
AND
_NewTimer != ""
AND
TriggerGetPosition(_Light,_X,_Y,_Z)
THEN
PlayScaledEffectAtPosition(_Effect,_Scale,_X,_Y,_Z);
TimerLaunch(_NewTimer,_Time);

IF
TimerFinished(_Timer)
AND
DB_RoomA_Lights2(_Light,_Timer,_NewTimer,_Effect,_Scale,_Time)
AND
_NewTimer != ""
AND
TriggerGetPosition(_Light,_X,_Y,_Z)
THEN
PlayScaledEffectAtPosition(_Effect,_Scale,_X,_Y,_Z);
TimerLaunch(_NewTimer,_Time);

IF
DialogEnded(_Dialog,_)
AND
DB_END_RoomA_Shrines(_,_,_Dialog)
AND
GlobalGetEvent("END_LP_GoToStart2",1)
THEN
DB_END_RoomA_Punishment(1);
GlobalClearEvent("END_LP_GoToStart2");
TimerLaunch("END_RoomA_Light1_2",400);
Proc_END_RoomA_Fear(2);

IF
TimerFinished("END_RoomA_Light8")
THEN
NOT DB_END_RoomA_Punishment(1);
CharacterTeleportPartyToTrigger(TRIGGER_END_RoomA_Entrance,"");
Proc_END_RoomA_DropDownPlayer();

IF
TimerFinished("END_RoomA_Light8_2")
THEN
NOT DB_END_RoomA_Punishment(1);
CharacterTeleportPartyToTrigger(TRIGGER_END_TP_RoomA_0_1,"");
Proc_END_RoomA_DropDownPlayer();

PROC
Proc_END_RoomA_DropDownPlayer()
AND
DB_IsPlayer(_Player)
THEN
CharacterRemoveStatus(_Player,"FEAR");
CharacterApplyStatus(_Player, "KNOCKED_DOWN",2.0,1);

IF
DialogEnded(_Dialog,_)
AND
DB_END_RoomA_Shrines(_,_,_Dialog)
AND
GlobalGetEvent("END_LP_GoToStart1",1)
THEN
DB_END_RoomA_Punishment(1);
GlobalClearEvent("END_LP_GoToStart1");
TimerLaunch("END_RoomA_Light1",400);
Proc_END_RoomA_Fear(1);

PROC
Proc_END_RoomA_Fear((INTEGER)_Index)
AND
DB_END_RoomA_Fear(_Index,_Event,_Anim)
THEN
CharacterPlayAnimation(CHARACTER_END_RoomALeandra,_Anim,"");
Proc_END_RoomA_FearPlayer();

PROC
Proc_END_RoomA_FearPlayer()
AND
DB_IsPlayer(_Player)
THEN
CharacterLookAtCharacter(_Player,CHARACTER_END_RoomALeandra,1);
CharacterApplyStatus(_Player,"FEAR",-1.0,1);


//END_REGION

//REGION Leandra

IF
CharacterEvent(CHARACTER_END_RoomALeandra,_StartEvent)
AND
DB_RoomA_LeandraGroundShake(_StartEvent,_)
THEN
DB_END_RoomA_ShakeTimer(_StartEvent);
Proc_END_RoomA_ShakeCamera(_StartEvent);

IF
CharacterEvent(CHARACTER_END_RoomALeandra,_StopEvent)
AND
DB_RoomA_LeandraGroundShake(_StartEvent,_StopEvent)
THEN
NOT DB_END_RoomA_ShakeTimer(_StartEvent);

PROC
Proc_END_RoomA_ShakeCamera((STRING)_StartEvent)
AND
DB_IsPLayer(_Player)
THEN
CharacterPlayEffect(_Player,"FX_GP_CameraShake_A");

PROC
Proc_END_RoomA_ShakeCamera((STRING)_StartEvent)
AND
Random(300,_Time)
AND
IntegerSum(250,_Time,_Sum)
THEN
TimerLaunch(_StartEvent,_Sum);

IF
TimerFinished(_StartEvent)
AND
DB_END_RoomA_ShakeTimer(_StartEvent)
THEN
Proc_END_RoomA_ShakeCamera(_StartEvent);

IF
CharacterEvent(CHARACTER_END_RoomALeandra,_Event)
AND
DB_RoomA_LeandraKnockEvents(_Event,_Trigger)
AND
DB_IsPlayer(_Player)
AND
DB_InRegion(_Player,_Trigger)
THEN
CharacterApplyStatus(_Player,"KNOCKED_DOWN",2.0,1);

//END_REGION

//REGION Transition to Nega inn

PROC
ProcOneShotTriggerEntered(_Player,TRIGGER_END_RoomA_Leandra3)
THEN
CharacterSetAnimationSetOverride(CHARACTER_END_RoomALeandra,"Stage3");
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Rage3");
CharacterPlayAnimation(CHARACTER_END_RoomALeandra,"Stage_03_Rage_01","END_RoomA_Raged3");
CharTimer(_Player,"LeandraLookAt",100);

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_Leandra3)
AND
DB_IsPlayer(_Player)
THEN
CharacterApplyStatus(_Player,"KNOCKED_DOWN",-1.0,1);

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_Leandra3)
THEN
CharacterTeleportToTrigger(CHARACTER_END_NegaLeandra,TRIGGER_END_Point_LeandraVoiceRoomA3,"END_LP_RoomA_End");

IF
CharacterEvent(CHARACTER_END_NegaLeandra,"END_LP_RoomA_End")
THEN
Proc_VoidFoop(CHARACTER_END_NegaLeandra);
TimerLaunch("END_RoomA_Room3LeandraAppear",100);

IF
TimerFinished("END_RoomA_Room3LeandraAppear")
THEN
DialogStartOneSpeakerDialog("END_RoomA_LeandraLine2",CHARACTER_END_NegaLeandra);

IF
CharTimerFinished(_Player,"LeandraLookAt")
THEN
CharacterLookAtCharacter(CHARACTER_END_NegaLeandra,_Player,1);

IF
AutomatedDialogEnded("END_RoomA_LeandraLine2",_)
THEN
PlayEffectAtItem("FX_GP_StarStone_Down_B",ITEM_END_RoomA_Door);
TimerLaunch("END_RoomA_LeandraAtDoor0",150);

IF
TimerFinished("END_RoomA_LeandraAtDoor0")
THEN
PROC_LoopEffectAtTrigger("FX_Env_Door_Endgame_Portal_A",TRIGGER_END_Point_RoomAEffect,"END_RoomA_PortalEffect","END_Endgame_Main");
ItemSetOnstage(ITEM_END_RoomA_Door,1);
TimerLaunch("END_RoomA_LeandraAtDoor1",200);

IF
TimerFinished("END_RoomA_LeandraAtDoor1")
THEN
CharacterMoveToTrigger(CHARACTER_END_NegaLeandra,TRIGGER_END_Point_RoomA_LeandraExit,0,"END_RoomA_LeandraExit");

IF
CharacterEvent(CHARACTER_END_NegaLeandra,"END_RoomA_LeandraExit")
THEN
TimerLaunch("END_RoomA_LeandraAtDoor2",400);
Proc_VoidPoof(CHARACTER_END_NegaLeandra);

IF
TimerFinished("END_RoomA_LeandraAtDoor2")
THEN
Proc_VoidPoof(CHARACTER_END_NegaLeandra);

IF
TimerFinished("END_RoomA_LeandraAtDoor2")
AND
DB_IsPlayer(_Player)
THEN
CharacterRemoveStatus(_Player,"KNOCKED_DOWN");

IF
CharacterUsedItem(_Player,ITEM_END_RoomA_Door)
AND
DB_IsPlayer(_Player)
THEN
ItemSetCanInteract(ITEM_END_RoomA_Door,0);
TimerLaunch("END_RoomA_TransitionToNega",8500);
CharacterSetEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Die");
CharacterPlayAnimation(CHARACTER_END_RoomALeandra,"Stage_03_Die_01","END_RoomA_Dead");
CharacterTeleportToTrigger(CHARACTER_END_LeandraVoice,TRIGGER_END_RoomA_LeandraVoice3,"END_RoomA_LeandraLine3");
TimerLaunch("END_RoomA_TP_Effect",7500);

IF
TimerFinished("END_RoomA_TP_Effect")
AND
DB_IsPlayer(_Player)
THEN
CharacterPlayEffect(_Player,"FX_GP_TeleportSmoke_A");

IF
TimerFinished("END_RoomA_TransitionToNega")
AND
DB_IsPlayer(_Player)
THEN
CharacterTeleportToTrigger(_Player,TRIGGER_END_Nega_Entrance,"END_NegaKingsCrab_Enter");

IF
CharacterEvent(CHARACTER_END_RoomALeandra,"END_RoomA_Dead")
THEN
CharacterPlaySound(CHARACTER_END_RoomALeandra,"Amb_Spot_EndGame_BigLeandraBreaths_start");
CharacterForceSynch(CHARACTER_END_RoomALeandra,0);
CharacterSetOnStage(CHARACTER_END_RoomALeandra,0);
ProcTriggerUnregisterForPlayers(TRIGGER_END_RoomA_Knock1);
ProcTriggerUnregisterForPlayers(TRIGGER_END_RoomA_Knock2);

//END_REGION

//REGION Journal

PROC
ProcOneShotTriggerEntered(_,TRIGGER_END_RoomA_SpawnLeandra)
THEN
QuestAdd("END_NegaKingsCrab");
QuestUpdate("END_NegaKingsCrab","Start");

IF
CharacterUsedItem(_,ITEM_END_RoomA_Door)
THEN
QuestUpdate("END_NegaKingsCrab","LeandraPuzzleEscape");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
