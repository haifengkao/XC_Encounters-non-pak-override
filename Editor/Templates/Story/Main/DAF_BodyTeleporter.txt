Version 1
SubGoalCombiner SGC_AND
INITSECTION
TriggerRegisterForItems(TRIGGER_DAF_BodyTeleport_In);
ProcTriggerRegisterForPlayers(TRIGGER_DAF_BodyTeleport_In);
Proc_InitialBodiesOnBodyTeleporter();
KBSECTION
IF
ItemTemplateEnteredTrigger(_,_Handle,TRIGGER_DAF_BodyTeleport_In,_,_Mover)
AND
_Mover.DB_IsPlayer()
THEN
DB_ItemsInBodyTeleporter(_Handle);

IF
ItemTemplateLeftTrigger(_,_Handle,TRIGGER_DAF_BodyTeleport_In,_,_)
THEN
NOT DB_ItemsInBodyTeleporter(_Handle);

PROC
Proc_InitialBodiesOnBodyTeleporter()
AND
ItemGetHandle(ITEM_DAF_BodyTeleporter_Corpse1,_Handle1)
AND
ItemGetHandle(ITEM_DAF_BodyTeleporter_Corpse2,_Handle2)
AND
ItemGetHandle(ITEM_DAF_BodyTeleporter_Corpse3,_Handle3)
THEN
DB_ItemsInBodyTeleporter(_Handle1);
DB_ItemsInBodyTeleporter(_Handle2);
DB_ItemsInBodyTeleporter(_Handle3);

IF
CharacterUsedItem(_,ITEM_DAF_BodyTeleport_Switch)
AND
DB_ItemsInBodyTeleporter(_Handle)
AND
ItemHandleGetPosition(_Handle,_ItemAbsOldX,_ItemAbsOldY,_ItemAbsOldZ)
AND
TriggerGetPosition(TRIGGER_DAF_BodyTeleport_InPos,_TriggerInPosX,_TriggerInPosY,_TriggerInPosZ)
AND
RealSubtract(_ItemAbsOldX,_TriggerInPosX,_ItemRelOldX)
AND
RealSubtract(_ItemAbsOldY,_TriggerInPosY,_ItemRelOldY)
AND
RealSubtract(_ItemAbsOldZ,_TriggerInPosZ,_ItemRelOldZ)
AND
TriggerGetPosition(TRIGGER_DAF_BodyTeleport_OutPos,_TriggerOutPosX,_TriggerOutPosY,_TriggerOutPosZ)
AND
RealSum(_ItemRelOldX,_TriggerOutPosX,_ItemAbsNewX)
AND
RealSum(_ItemRelOldY,_TriggerOutPosY,_ItemAbsNewY)
AND
RealSum(_ItemRelOldZ,_TriggerOutPosZ,_ItemAbsNewZ)
THEN
NOT DB_ItemsInBodyTeleporter(_Handle);
ItemHandleToTransform(_Handle,_ItemAbsNewX,_ItemAbsNewY,_ItemAbsNewZ,0.0,0.0,0.0,-1,CHARACTER_NULL);
PlayEffectAtPosition("FX_Skills_Air_Storm_Impact_A",_ItemAbsOldX,_ItemAbsOldY,_ItemAbsOldZ);

IF
CharacterEnteredTrigger(_Character,TRIGGER_DAF_BodyTeleport_In)
THEN
_Character.InBodyTeleportTrigger();

IF
CharacterLeftTrigger(_Character,TRIGGER_DAF_BodyTeleport_In)
THEN
NOT _Character.InBodyTeleportTrigger();

IF
CharacterUsedItem(_,ITEM_DAF_BodyTeleport_Switch)
AND
_Character.InBodyTeleportTrigger()
AND
DB_Dead(_Character)
AND
CharacterGetPosition(_Character,_CharAbsOldX,_CharAbsOldY,_CharAbsOldZ)
AND
TriggerGetPosition(TRIGGER_DAF_BodyTeleport_InPos,_TriggerInPosX,_TriggerInPosY,_TriggerInPosZ)
AND
RealSubtract(_CharAbsOldX,_TriggerInPosX,_CharRelOldX)
AND
RealSubtract(_CharAbsOldY,_TriggerInPosY,_CharRelOldY)
AND
RealSubtract(_CharAbsOldZ,_TriggerInPosZ,_CharRelOldZ)
AND
TriggerGetPosition(TRIGGER_DAF_BodyTeleport_OutPos,_TriggerOutPosX,_TriggerOutPosY,_TriggerOutPosZ)
AND
RealSum(_CharRelOldX,_TriggerOutPosX,_CharAbsNewX)
AND
RealSum(_CharRelOldY,_TriggerOutPosY,_CharAbsNewY)
AND
RealSum(_CharRelOldZ,_TriggerOutPosZ,_CharAbsNewZ)
THEN
CharacterDetachFromGroup(_Character);
CharacterTeleportToPosition(_Character,_CharAbsNewX,_CharAbsNewY,_CharAbsNewZ);
PlayEffectAtPosition("FX_Skills_Air_Storm_Impact_A",_CharAbsOldX,_CharAbsOldY,_CharAbsOldZ);

IF
CharacterUsedItem(_,ITEM_DAF_BodyTeleport_Switch)
AND
_Character.InBodyTeleportTrigger()
AND
NOT DB_Dead(_Character)
THEN
Launch_PlayerComment(_Character,"DAF_Comment_BodyTeleporter");
QuestUpdate("DAF_Cassandra", "Flipswitch");

IF
CharacterUsedItem(_,ITEM_DAF_BodyTeleporter_Plate)
THEN
ItemDisplayText(ITEM_DAF_BodyTeleporter_Plate,"DAF_BodyTeleporter_Sign");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
