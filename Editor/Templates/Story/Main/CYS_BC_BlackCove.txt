Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Databases
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_000);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_001);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_002);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_003);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_004);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_005);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_006);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_007);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_008);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_009);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_010);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_011);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_012);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_013);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_014);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_015);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_016);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_017);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_018);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_019);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_020);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_021);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_022);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_023);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_024);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_025);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_026);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_027);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_028);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_029);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_030);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_031);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_032);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_033);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_034);
DB_CYS_BC_LeverMines(ITEM_CYS_BC_LeverMine_035);

DB_CYS_BC_Levers(ITEM_CYS_BC_LeverRunofthemill);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverDodgy);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverOrnated);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverAngry);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverSmirky);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverLusty);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverWary);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverPretty);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverDontTouch);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverOld);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverShiny);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverRaze);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverUnaware);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverLever);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverSquicky);
DB_CYS_BC_Levers(ITEM_CYS_BC_LeverPullMe);

DB_CYS_BC_Orc(CHARACTER_CYS_BC_Orc1);
DB_CYS_BC_Orc(CHARACTER_CYS_BC_Orc2);
DB_CYS_BC_Orc(CHARACTER_CYS_BC_Orc3);
DB_CYS_BC_Orc(CHARACTER_CYS_BC_Orc4);
DB_CYS_BC_Orc(CHARACTER_CYS_BC_Orc5);
DB_CYS_BC_Orc(CHARACTER_CYS_BC_Ward);

DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton1);
DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton2);
DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton3);
DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton4);
DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton5);
DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton6);
DB_CYS_BC_Skeleton(CHARACTER_CYS_BC_Skeleton7);

DB_CYS_BC_Skeleton_HC(CHARACTER_CYS_BC_Skeleton8_Hardcore,TRIGGER_CMBHC_CYSBC_OvUD_AdditionalEnemy_T01);
DB_CYS_BC_Skeleton_HC(CHARACTER_CYS_BC_Skeleton9_Hardcore,TRIGGER_CMBHC_CYSBC_OvUD_AdditionalEnemy_T02);
DB_CYS_BC_Skeleton_HC(CHARACTER_CYS_BC_Skeleton10_Hardcore,TRIGGER_CMBHC_CYSBC_OvUD_AdditionalEnemy_T03);
DB_CYS_BC_Skeleton_HC(CHARACTER_CYS_BC_Skeleton11_Hardcore,TRIGGER_CMBHC_CYSBC_OvUD_AdditionalEnemy_T04);

DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab1);
DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab2);
DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab3);
DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab4);
DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab5);
DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab6);
DB_CYS_BC_Crab(TRIGGER_CYS_BC_Crab7);

DB_CYS_BC_Gargoyle(ITEM_CYS_BC_Gargoyle4);
DB_CYS_BC_Gargoyle(ITEM_CYS_BC_Gargoyle5);
DB_CYS_BC_Gargoyle(ITEM_CYS_BC_Gargoyle7);
DB_CYS_BC_Gargoyle(ITEM_CYS_BC_Gargoyle8);
DB_CYS_BC_Gargoyle(ITEM_CYS_BC_Gargoyle11);
DB_CYS_BC_Gargoyle(ITEM_CYS_BC_Gargoyle14);

KillCounter("CYS_BC_OrcSkeleton", 12);
KillCounterDB(CHARACTER_CYS_BC_Orc1, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Orc2, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Orc3, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Orc4, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Orc5, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Ward, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton1, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton2, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton3, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton4, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton5, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton6, "CYS_BC_OrcSkeleton");
KillCounterDB(CHARACTER_CYS_BC_Skeleton7, "CYS_BC_OrcSkeleton");

KillCounter("CYS_BC_OrcSkeleton_Hardcore", 16);
KillCounterDB(CHARACTER_CYS_BC_Orc1, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Orc2, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Orc3, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Orc4, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Orc5, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Ward, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton1, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton2, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton3, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton4, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton5, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton6, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton7, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton8_Hardcore, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton9_Hardcore, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton10_Hardcore, "CYS_BC_OrcSkeleton_Hardcore");
KillCounterDB(CHARACTER_CYS_BC_Skeleton11_Hardcore, "CYS_BC_OrcSkeleton_Hardcore");

DB_CYS_BC_PontiusSkeleton(CHARACTER_CYS_BC_PontiusGrunt1, "CYS_BC_Pirate1", "CYS_BC_Pirate2");
DB_CYS_BC_PontiusSkeleton(CHARACTER_CYS_BC_PontiusGrunt2, "CYS_BC_Pirate2", "CYS_BC_Pirate3");
DB_CYS_BC_PontiusSkeleton(CHARACTER_CYS_BC_PontiusGrunt3, "CYS_BC_Pirate3", "CYS_BC_Pirate4");
DB_CYS_BC_PontiusSkeleton(CHARACTER_CYS_BC_PontiusGrunt4, "CYS_BC_Pirate4", "CYS_BC_Pirate5");
DB_CYS_BC_PontiusSkeleton(CHARACTER_CYS_BC_PontiusGrunt5, "CYS_BC_Pirate5", "CYS_BC_Pirate6");
//END_REGION
//REGION Initialization
DB_ShovelArea(TRIGGER_CYS_BC_ShovelPirate,"CYS_BC_ShovelPirate",ITEM_CYS_BC_ShovelPirateDirt);
DB_ShovelRewardItemAppear("CYS_BC_ShovelPirate",ITEM_CYS_BC_PirateTreasure,TRIGGER_CYS_BC_ShovelPiratePoint);
ItemToCharacter(ITEM_CYS_BC_LeverKey, CHARACTER_CYS_BC_PontiusSkeleton);
ItemToCharacter(ITEM_CYS_BC_OrcOrder, CHARACTER_CYS_BC_DeadOrc);
CharacterSetAnimationSetOverride(CHARACTER_CYS_BC_DeadOrc,"Lying_Wounded");
DB_DyingNpcOnBed(CHARACTER_CYS_BC_DeadOrc, ITEM_CYS_BC_DeadOrc_Invisibed);
ItemSetOnstage(ITEM_CYS_BC_LeverRoomHatch, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_PontiusGrunt1, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_PontiusGrunt2, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_PontiusGrunt3, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_PontiusGrunt4, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_PontiusGrunt5, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_Belligar, 0);
ItemSetOnstage(ITEM_CYS_BC_RazeDoll, 0);
ItemSetOnstage(ITEM_CYS_BC_LeverGoodChest, 0);
ItemSetOnstage(ITEM_CYS_BC_LeverTrappedChest, 0);
DB_EvilDude(CHARACTER_CYS_BC_Belligar);
ItemToCharacter(ITEM_CYS_BC_OrcShamanImmOrders,CHARACTER_CMB_WestBeachShaman);
PROC_CYS_BC_TurnOffMines();

DB_OneShotPlayerTrigger(TRIGGER_CYS_BC_StartOrcFight);
DB_OneShotPlayerTrigger(TRIGGER_CYS_BC_DeadOrComment);
DB_OneShotPlayerTrigger(TRIGGER_CYS_BC_LeverRoom);
//END_REGION
//REGION Comments
DB_PlayerComments("CYS_BC_CommentPit", "CYS_BC_CommentPit_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentPit", "CYS_BC_CommentPit_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentPit", "CYS_BC_CommentPit_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_CYS_BC_CommentGasPit,"CYS_BC_CommentPit");

DB_PlayerComments("CYS_BC_CommentDeadDragon", "CYS_BC_CommentDeadDragon_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentDeadDragon", "CYS_BC_CommentDeadDragon_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentDeadDragon", "CYS_BC_CommentDeadDragon_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_CYS_BC_Comment_DeadDragon,"CYS_BC_CommentDeadDragon");

DB_PlayerComments("CYS_BC_CommentSecondDragon", "CYS_BC_CommentSecondDragon_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentSecondDragon", "CYS_BC_CommentSecondDragon_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentSecondDragon", "CYS_BC_CommentSecondDragon_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_CYS_BC_CommentSecondDragon,"CYS_BC_CommentSecondDragon");

DB_PlayerComments("CYS_BC_CommentCrabKilled", "CYS_BC_CommentCrabKilled_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentCrabKilled", "CYS_BC_CommentCrabKilled_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentCrabKilled", "CYS_BC_CommentCrabKilled_2_2",2,2);

DB_PlayerComments("CYS_BC_CommentOrcSkeleton", "CYS_BC_CommentOrcSkeleton_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentOrcSkeleton", "CYS_BC_CommentOrcSkeleton_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentOrcSkeleton", "CYS_BC_CommentOrcSkeleton_2_2",2,2);

DB_PlayerComments("CYS_BC_CommentCrabSummoner", "CYS_BC_CommentCrabSummoner_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentCrabSummoner", "CYS_BC_CommentCrabSummoner_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentCrabSummoner", "CYS_BC_CommentCrabSummoner_2_2",2,2);

DB_PlayerComments("CYS_BC_CommentNothingLever", "CYS_BC_CommentNothingLever_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentNothingLever", "CYS_BC_CommentNothingLever_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentNothingLever", "CYS_BC_CommentNothingLever_2_2",2,2);
DB_PlayerComments("CYS_BC_CommentNothingLever", "CYS_BC_CommentNothingLever_2_3",2,3);

DB_PlayerComments("CYS_BC_CommentEntry", "CYS_BC_CommentEntry_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentEntry", "CYS_BC_CommentEntry_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentEntry", "CYS_BC_CommentEntry_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_CYS_BC_CommentEntrance,"CYS_BC_CommentEntry");

DB_PlayerComments("CYS_BC_CommentPontiusOffice", "CYS_BC_CommentPontiusOffice_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentPontiusOffice", "CYS_BC_CommentPontiusOffice_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentPontiusOffice", "CYS_BC_CommentPontiusOffice_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_CYS_CommentPontiusOffice,"CYS_BC_CommentPontiusOffice");


DB_PlayerComments("CYS_BC_CommentOrcsVsUD", "CYS_BC_CommentOrcsVsUD_1_1",1,1);
DB_PlayerComments("CYS_BC_CommentOrcsVsUD", "CYS_BC_CommentOrcsVsUD_2_1",2,1);
DB_PlayerComments("CYS_BC_CommentOrcsVsUD", "CYS_BC_CommentOrcsVsUD_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_CYS_BC_StartOrcFight,"CYS_BC_CommentOrcsVsUD");

//END_REGION
//REGION Dialog
DB_Dialogs(CHARACTER_CYS_BC_Desdemona, "CYS_BC_Desdemona");
DB_OneShot_DialogTrigger_NewSystem(TRIGGER_CYS_BC_PontiusTalk,"CYS_BC_Pontius", CHARACTER_CYS_BC_PontiusSkeleton);
SneakTriggerSpotter(TRIGGER_CYS_BC_PontiusTalk, CHARACTER_CYS_BC_PontiusSkeleton);
//END_REGION

CharacterSetHitpointsPercentage(CHARACTER_CYS_BC_DeadOrc, 1);
CharacterApplyStatus(CHARACTER_CYS_BC_DeadOrc, "UNHEALABLE", -1.0, 1);
DB_NoDefaultBehavior(CHARACTER_CYS_BC_DeadOrc);
ItemToCharacter(ITEM_CYS_BC_PontiusKey, CHARACTER_CYS_BC_PontiusSkeleton);
ItemToContainer(ITEM_CYS_BC_ShipBloodStone, ITEM_CYS_PontiusChest,-1);
ItemToContainer(ITEM_CYS_BC_PontiusNote, ITEM_CYS_PontiusChest, -1);

//ItemSetOnstage(ITEM_CYS_BC_MCH_Tesla_Part_Destroyed_01,0);
//ItemSetOnstage(ITEM_CYS_BC_MCH_Tesla_Part_Destroyed_02,0);
ItemSetOnstage(ITEM_CYS_BC_OfficeButton, 0);
KBSECTION
//REGION Initialization
PROC
PROC_CYS_BC_TurnOffMines()
AND
DB_CYS_BC_LeverMines(_Item)
THEN
ItemSetOnstage(_Item, 0);
CharacterSetOnStage(CHARACTER_CYS_BC_Skeleton8_Hardcore,0);
CharacterSetOnStage(CHARACTER_CYS_BC_Skeleton9_Hardcore,0);
CharacterSetOnStage(CHARACTER_CYS_BC_Skeleton10_Hardcore,0);
CharacterSetOnStage(CHARACTER_CYS_BC_Skeleton11_Hardcore,0);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_CYS_BC_DeadOrComment)
THEN
DialogStartOneSpeakerDialog("CYS_BC_AD_DeadOrc", CHARACTER_CYS_BC_DeadOrc);

IF
AutomatedDialogEnded("CYS_BC_AD_DeadOrc", _)
THEN
CharacterDie(CHARACTER_CYS_BC_DeadOrc,1);
QuestAdd("CYS_BC_BlackCove");
QuestUpdate("CYS_BC_BlackCove", "Start");
//END_REGION

//REGION Village part
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_OfficeLever, "activated")
THEN
ItemSetOnstage(ITEM_CYS_BC_OfficeButton, 1);
ItemMoveToTrigger(ITEM_CYS_BC_PaintingOffice, TRIGGER_CYS_BC_PaintingEndSpot, 0.75,0.0,0);
CharacterDisplayText(_Player, "GLO_ObviousActivated");
ItemSetCanInteract(ITEM_CYS_BC_OfficeLever, 0);


IF
CharacterItemEvent(_Player, ITEM_CYS_BC_OfficeButton, "activated")
AND
NOT ITEM_CYS_BC_OfficeButton.done()
THEN
ItemSetOnstage(ITEM_CYS_BC_LeverRoomHatch, 1);
ITEM_CYS_BC_OfficeButton.done();
PlayEffectAtItem("FX_GP_TeleportSmoke_A", ITEM_CYS_BC_LeverRoomHatch);
PROC_CommentHiddenEffect(_Player);
QuestUpdate("CYS_BC_BlackCove", "Hatch");


IF
CharacterItemEvent(_Player, ITEM_CYS_BC_TrappedPlate, "triggered")
AND
_Player.DB_IsPlayer()
AND
NOT ITEM_CYS_BC_TrappedPlate.done()
THEN
CharacterAppear(CHARACTER_CYS_BC_Belligar, 1);
CharacterPlayEffect(CHARACTER_CYS_BC_Belligar, "FX_GP_TeleportSmoke_A");
QuestAdd("CYS_BC_BillehGar");
QuestUpdate("CYS_BC_BillehGar", "Start");
DB_BelligarDialog(_Player);

IF
CharacterWentOnStage(CHARACTER_CYS_BC_Belligar, 1)
AND
DB_BelligarDialog(_Player)
THEN
NOT DB_BelligarDialog(_Player);
PROC_MendatoryDialog("CYS_BC_Belligar", CHARACTER_CYS_BC_Belligar, _Player);

IF
DialogEnded("CYS_BC_Belligar", _)
THEN
CharacterSetOnStage(CHARACTER_CYS_BC_Belligar, 0);
PlayEffectAtCharacter("FX_GP_TeleportSmoke_A", CHARACTER_CYS_BC_Belligar);

IF
CharacterItemEvent(_player, ITEM_CYS_BC_BelligarButton, "activated")
AND
_Player.DB_IsPlayer()
THEN
ITEM_CYS_BC_TrappedPlate.turnedOff();
QuestUpdate("CYS_BC_BillehGar", "Finished");
PROC_CommentHiddenEffect(_Player);
QuestClose("CYS_BC_BillehGar");


IF
CharacterItemEvent(_, ITEM_CYS_BC_TrappedPlate, "fired")
AND
NOT ITEM_CYS_BC_TrappedPlate.turnedOff()
AND 
NOT ITEM_CYS_BC_TrappedPlate.done()
AND
DB_CYS_BC_Gargoyle(_Turret)
THEN
CharacterItemSetEvent(CHARACTER_CYS_BC_Belligar, _Turret, "fire");
ITEM_CYS_BC_TrappedPlate.done();
QuestUpdate("CYS_BC_BillehGar", "FinishedBad");
QuestClose("CYS_BC_BillehGar");

IF
CharacterItemEvent(_, ITEM_CYS_BC_TrappedPlate, "fired")
AND
ITEM_CYS_BC_TrappedPlate.turnedOff()
THEN
ITEM_CYS_BC_TrappedPlate.done();

IF
CharacterEnteredCombat(CHARACTER_CYS_CrabInvocator,_)
AND
CharacterGetClosestPlayer(CHARACTER_CYS_CrabInvocator, _Player)
THEN
Launch_PlayerComment(_Player, "CYS_BC_CommentCrabSummoner");

//END_REGION

//REGION Lever room
PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_CYS_BC_LeverRoom)
THEN
QuestUpdate("CYS_BC_BlackCove", "Lever");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_TeleporterHoleLever, "CYS_BC_TeleportBack")
THEN
CharacterTeleportToTrigger(_Player, TRIGGER_CYS_BC_TeleporterHoleBackPoint, "");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverRunofthemill, "activated")
THEN
Launch_PlayerComment(_Player, "CYS_BC_CommentNothingLever");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverDodgy, "activated")
THEN
CharacterApplyDamage(_Player, 10, "Shadow");
CharacterApplyStatus(_Player, "INFECTIOUS_DISEASED", 18.0);
CharacterDisplayText(_Player, "GLO_ObviousActivated");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverOrnated, "activated")
THEN
CharacterCreateAtTrigger(TRIGGER_CYS_BC_LeverMonster, "041af1cc-2f0f-446a-ae1e-48cc75189a7b", 1);
CharacterDisplayText(_Player, "GLO_ObviousActivated");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverAngry, "activated")
THEN
ItemSetOnstage(ITEM_CYS_BC_LeverGoodChest, 1);
ItemPlayAnimation(ITEM_CYS_BC_LeverGoodChest, "custom1");
CharacterDisplayText(_Player, "GLO_ObviousActivated");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverSmirky, "activated")
AND
CharacterGetEquippedWeapon(_Player, _Weapon)
THEN
CharacterDisplayText(_Player, "CYS_BC_WeaponDurability");
CharacterApplyStatus(_Player, "CRIPPLED", 18.0);
ItemSetDurability(_Weapon, 1);
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverLusty, "activated")
THEN
CharacterItemSetEvent(_Player, ITEM_CYS_BC_LeverGargoyle1, "Fire");
CharacterItemSetEvent(_Player, ITEM_CYS_BC_LeverGargoyle2, "Fire");
CharacterItemSetEvent(_Player, ITEM_CYS_BC_LeverGargoyle3, "Fire");
CharacterItemSetEvent(_Player, ITEM_CYS_BC_LeverGargoyle4, "Fire");
CharacterApplyStatus(_Player, "BURNING", 18.0);
CharacterDisplayText(_Player, "GLO_ObviousActivated");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverWary, "activated")
THEN
ItemSetOnstage(ITEM_CYS_BC_HiddenWallPontius, 0);
ItemSetOnstage(ITEM_CYS_BC_FireRightEye, 0);
ItemSetOnstage(ITEM_CYS_BC_RightEyeLight, 0);
ItemSetOnstage(ITEM_CYS_BC_FireLeftEye, 0);
ItemSetOnstage(ITEM_CYS_BC_LeftEyeLight, 0);
PlayEffectAtItem("FX_GP_TeleportSmoke_A", ITEM_CYS_BC_HiddenWallPontius);
QuestUpdate("CYS_BC_BlackCove", "LeverGood");
CharacterDisplayText(_Player, "GLO_ObviousActivated");
ItemPlaySound(ITEM_CYS_BC_LeverWary,"Item_Door_Stone_Slide_Open");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverPretty, "activated")
THEN
CharacterPlayEffect(_Player, "FX_GP_Death_Lightning_A_Medium");
CharacterSetHitpointsPercentage(_Player, 1);
CharacterApplyStatus(_Player, "STUNNED", 18.0);
CharacterDisplayText(_Player, "GLO_ObviousActivated");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverDontTouch, "activated")
AND
CharacterGetPosition(CHARACTER_Player1, _X, _Y, _Z)
AND
CharacterGetPosition(CHARACTER_Player2, _X2, _Y2, _Z2)
THEN
CharacterTeleportToPosition(CHARACTER_Player1, _X2, _Y2, _Z2,"");
CharacterTeleportToPosition(CHARACTER_Player2, _X, _Y, _Z,"");
CharacterDisplayText(_Player, "GLO_ObviousActivated");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverOld, "activated")
THEN
ItemSetOnstage(ITEM_CYS_BC_LeverTrappedChest, 1);
ItemPlayAnimation(ITEM_CYS_BC_LeverTrappedChest, "custom1");
CharacterDisplayText(_Player, "GLO_ObviousActivated");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverShiny, "activated")
THEN
PROC_CYS_BC_LaunchMineField();
CharacterApplyStatus(_Player, "BLIND", 18.0);
CharacterDisplayText(_Player, "GLO_ObviousActivated");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverRaze, "activated")
THEN
CharacterDisplayText(_Player, "GLO_ObviousActivated");
ItemSetOnstage(ITEM_CYS_BC_RazeDoll, 1);
PlayEffectAtItem("FX_GP_TeleportSmoke_A", ITEM_CYS_BC_RazeDoll);
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverUnaware, "activated")
THEN
CharacterApplyStatus(_Player, "FROZEN", 12.0);
CharacterDisplayText(_Player, "GLO_ObviousActivated");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverLever, "activated")
THEN
UnlockAchievement("quest50");

IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverSquicky, "activated")
THEN
CharacterTeleportToTrigger(_Player, TRIGGER_CYS_BlackCoveIn,"");
CharacterDisplayText(_Player, "CYS_BC_ReactExoticLever");
 
IF
CharacterItemEvent(_Player, ITEM_CYS_BC_LeverPullMe, "activated")
THEN
CharacterTeleportToTrigger(_Player, TRIGGER_CYS_BC_LeverHole,"");
CharacterDisplayText(_Player, "CYS_BC_ReactOminousLever");

IF
CharacterItemEvent(_Player, _Lever, "activated")
AND
DB_CYS_BC_Levers(_Lever)
THEN
ItemSetCanInteract(_Lever, 0);
NOT DB_CYS_BC_Levers(_Lever);

IF
CharacterUsedItem(_Player, ITEM_CYS_BC_LeverTrappedChest)
AND
NOT ITEM_CYS_BC_LeverTrappedChest.alreadyOpened()
THEN
ITEM_CYS_BC_LeverTrappedChest.alreadyOpened();
CharacterApplyDamage(_Player, 10, "shadow");

PROC
PROC_CYS_BC_LaunchMineField()
AND
DB_CYS_BC_LeverMines(_Mine)
THEN
ItemSetOnstage(_Mine, 1);
//END_REGION

/*//REGION Beach part
IF
CharacterKilledByCharacter(CHARACTER_CYS_BC_CrabBoss, _Player)
THEN
Launch_PlayerComment(_Player,"CYS_BC_CommentCrabKilled");*/

/*IF
CharacterSawCharacter(_Player, CHARACTER_CYS_BC_CrabBoss)
AND
NOT CHARACTER_CYS_BC_CrabBoss.isRevealed()
THEN
CHARACTER_CYS_BC_CrabBoss.isRevealed();
CharacterDisplayText(_Player, "CYS_BC_CrabBoss");
//END_REGION*/

//REGION Pontius part
IF
DialogEnded("CYS_BC_Pontius", _)
THEN
TimerLaunch("CYS_BC_Pirate1", 2500);
CharacterPlayAnimation(CHARACTER_CYS_BC_PontiusSkeleton, "cast_shout_cast", "enemyMode");

IF
CharacterEvent(CHARACTER_CYS_BC_PontiusSkeleton, "enemyMode")
THEN
CharacterSetCanFight(CHARACTER_CYS_BC_PontiusSkeleton, 1);

IF
CharacterAttackedByCharacter(CHARACTER_CYS_BC_PontiusSkeleton, _Player)
AND
CharacterCanFight(CHARACTER_CYS_BC_PontiusSkeleton, 0)
THEN
TimerLaunch("CYS_BC_Pirate1", 2500);
CharacterPlayAnimation(CHARACTER_CYS_BC_PontiusSkeleton, "cast_shout_cast", "enemyMode");

IF
TimerFinished(_Timer)
AND
DB_CYS_BC_PontiusSkeleton(_Skeleton, _Timer, _NextTimer)
THEN
NOT DB_CYS_BC_PontiusSkeleton(_Skeleton, _Timer, _NextTimer);
CharacterAppear(_Skeleton, 1);
TimerLaunch(_NextTimer, 1000);

IF
CharacterDied(CHARACTER_CYS_BC_PontiusSkeleton)
THEN
QuestUpdate("CYS_BC_BlackCove", "KilledPontius");
QuestClose("CYS_BC_BlackCove");
GlobalSetEvent("CYS_BC_PontiusIsDead");
//END_REGION

//REGION Pontius Warmachine
/*PROC
PROC_CYSBC_StoreWarmachineAttacker((CHARACTER)_)
AND
DB_CYSBC_PontiusWarmachine(_WarmachineAttacker)
THEN
NOT DB_CYSBC_PontiusWarmachine(_WarmachineAttacker);						//clear database from that character (so we can replace it with a new one)

PROC
PROC_CYSBC_StoreWarmachineAttacker((CHARACTER)_WarmachineAttacker)			//it's okay if proc has the same name, everything gets executed from top to bottom
THEN
DB_CYSBC_PontiusWarmachine(_WarmachineAttacker);							//store said character

IF
CharacterEvent(_WarmachineAttacker,"CYSBC_WarmachineDamaged_CharEvent")		//get whoever damaged the machine from itemscript then run the proc above
THEN
PROC_CYSBC_StoreWarmachineAttacker(_WarmachineAttacker);

IF	
CharacterTurnStarted(_WarmachineAttacker)
AND
DB_Combat(CHARACTER_CYS_BC_PontiusSkeleton,_Combat)							//check that said character & Pontius are in the same fight
AND
DB_Combat(_WarmachineAttacker,_Combat)
AND
DB_CYSBC_PontiusWarmachine(_WarmachineAttacker)
THEN
ItemSetEvent(ITEM_CYS_BC_PontiusWarmachine_01,"CYSBC_WarmachineTurn_ItemEvent");

IF
ItemEvent(ITEM_CYS_BC_PontiusWarmachine_01,"CYSBC_WarmachineDestroyed_ItemEvent")
THEN
ItemSetOnstage(ITEM_CYS_BC_MCH_Tesla_Part_Destroyed_01,1);
ItemSetOnstage(ITEM_CYS_BC_MCH_Tesla_Part_Destroyed_02,1);*/
//END_REGION

//REGION Orcs vs. Skeletons
IF
RegionStarted("CYS_BlackCove")
AND
DB_CYS_BC_Orc(_Orc)
THEN
CharacterSetFaction(_Orc, "CYS_BC_Orc");

IF
CharacterEnteredTrigger(_Player, TRIGGER_CYS_BC_StartOrcFight)
THEN
QuestUpdate("CYS_BC_BlackCove", "OrcSkeleton");

IF
RegionStarted("CYS_BlackCove")
AND
DB_CYS_BC_Skeleton(_Orc)
THEN
CharacterSetFaction(_Orc, "CYS_BC_Denizen");

IF
CharacterAttackedByCharacter(_Orc, _Skeleton)
AND
DB_CYS_BC_Orc(_Orc)
AND
DB_CYS_BC_Skeleton(_Skeleton)
AND
Random(2, _Rand)
AND
_Rand == 1
THEN
DialogStartOneSpeakerDialog("CYS_BC_AD_SkeletonInsults", _Skeleton);

IF
CharacterAttackedByCharacter(_Skeleton, _Orc)
AND
DB_CYS_BC_Orc(_Orc)
AND
DB_CYS_BC_Skeleton(_Skeleton)
AND
Random(2, _Rand)
AND
_Rand == 1
THEN
DialogStartOneSpeakerDialog("CYS_BC_AD_OrcsInsult", _Orc);

PROC
ReactOnKillCounter("CYS_BC_OrcSkeleton")
AND
IsHardcoreMode(0)
AND
CharacterGetClosestPlayer(CHARACTER_CYS_BC_Skeleton2, _Player)
THEN
Launch_PlayerComment(_Player, "CYS_BC_CommentOrcSkeleton");

//------- HARDCORE
IF
CharacterEnteredTrigger(_Player, TRIGGER_CYS_BC_StartOrcFight)
AND
IsHardcoreMode(1)
AND
DB_CYS_BC_Skeleton_HC(_Enemy,_Trigger)
THEN
CharacterAppear(CHARACTER_CYS_BC_Skeleton8_Hardcore,0);
CharacterAppear(CHARACTER_CYS_BC_Skeleton9_Hardcore,0);
CharacterAppear(CHARACTER_CYS_BC_Skeleton10_Hardcore,0);
CharacterAppear(CHARACTER_CYS_BC_Skeleton11_Hardcore,0);
ProcCharacterMoveToTrigger(_Enemy,_Trigger,1,"");

IF
CharacterUsedItem(_Player,ITEM_CYS_BC_Door_Hardcore_01)
AND
IsHardcoreMode(1)
AND
DB_CYS_BC_Skeleton_HC(_Enemy,_)
THEN
CharacterAppear(_Enemy,0);

IF
ItemDestroyed(ITEM_CYS_BC_Door_Hardcore_01)
AND
IsHardcoreMode(1)
AND
DB_CYS_BC_Skeleton_HC(_Enemy,_)
THEN
CharacterAppear(_Enemy,0);

PROC
ReactOnKillCounter("CYS_BC_OrcSkeleton_Hardcore")
AND
IsHardcoreMode(1)
AND
CharacterGetClosestPlayer(CHARACTER_CYS_BC_Skeleton2, _Player)
THEN
Launch_PlayerComment(_Player, "CYS_BC_CommentOrcSkeleton");
//END_REGION

//REGION Ship part

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
