Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_LUC_Ruby,"LUC_Ruby");
DB_Dialogs(CHARACTER_LUC_Ramon,"LUC_Ramon");

DB_EventGivesPartyDialog("LUC_RubyDialog","LUC_DD_AfterRuby");
ProcTriggerRegisterForPlayers(TRIGGER_LUC_AreaProstituteRoom);

DB_LUC_Prostitutes(1,CHARACTER_LUC_Ruby,"LUC_Ruby");
DB_LUC_Prostitutes(3,CHARACTER_LUC_Ramon,"LUC_Ramon");

ItemToCharacter(ITEM_LUC_ProsituteKey1,CHARACTER_LUC_Ruby);
ItemToCharacter(ITEM_LUC_ProsituteKey2,CHARACTER_LUC_Bonnie);
ItemToCharacter(ITEM_LUC_ProsituteKey3,CHARACTER_LUC_Ramon);
ItemToCharacter(ITEM_LUC_Ramon_Key,CHARACTER_LUC_Ramon);
ItemSetOnstage(ITEM_LUC_WPN_Ramon_Dagger,0);

DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon1);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon2);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon3);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon4);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon5);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon6);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon7);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon8);
DB_LUC_ProstitutesWeapons(ITEM_LUC_Prostitute_Weapon9);

PROC_LUC_WornProstituteWeapons();
KBSECTION
PROC
PROC_LUC_WornProstituteWeapons()
AND
DB_LUC_ProstitutesWeapons(_Weapon)
AND
ItemGetHandle(_Weapon,_Handle)
THEN
ItemSetDurability(_Handle, 2);
//REGION HIRING A PROSTITUTE
IF
DialogCharacterEventSet("HiresProstitute",_Player,_)
AND
CharacterGetVarInteger(_Player,"HiresProstitute",_Value)
AND
DB_LUC_Prostitutes(_Value,_Prostitute,_)
THEN
DB_LUC_ProstituteHired(_Player, _Prostitute);
_Prostitute.isGoingToHouse();

IF
CHARACTER_LUC_Ramon.isGoingToHouse()
THEN
CharacterSetVarInteger(CHARACTER_LUC_Overseer,"RamonAvailable",0);

IF
DialogEnded(_Dialog,_)
AND
DB_LUC_Prostitutes(_,_Prostitute,_Dialog)
AND
_Prostitute.isGoingToHouse()
THEN
NOT _Prostitute.isGoingToHouse();
CharacterSetHasDialog(_Prostitute,0);
ProcCharacterMoveToTrigger(_Prostitute,TRIGGER_LUC_Point_Inn_Down,0,"ProstituteReachedStairsUp");

IF
CharacterEvent(_Prostitute,"ProstituteReachedStairsUp")
THEN
CharacterTeleportToTrigger(_Prostitute,TRIGGER_LUC_Point_Inn_Up,"");
ProcCharacterMoveToTrigger(_Prostitute,TRIGGER_LUC_PointProstitute1,0,"ProstituteReachedDoor");

IF
CharacterEnteredCombat(CHARACTER_LUC_Ramon,_)
AND
ItemGetHandle(ITEM_LUC_WPN_Ramon_Dagger,_RamonDagger)
THEN
CharacterEquipItemhandle(CHARACTER_LUC_Ramon,_RamonDagger);

IF
CharacterEvent(_Prostitute,"ProstituteReachedDoor")
THEN
ItemUnlockAndOpen(ITEM_LUC_DoorProstitute1);
ProcCharacterMoveToTrigger(_Prostitute,TRIGGER_LUC_PointProstitute2,0,"ProstituteReachedBed");

IF
CharacterEvent(_Prostitute,"ProstituteReachedBed")
THEN
CharacterSetVarFixedString(_Prostitute,"currentState","State_WaitingInRoom");
CharacterSetVarInteger(_Prostitute,"WaitingForClient",1);
CharacterSetHasDialog(_Prostitute,1);

IF
CharacterEnteredTrigger(_Player,TRIGGER_LUC_AreaProstituteRoom)
AND
NOT DB_LUC_ProstituteHired(_Player, _)
THEN
GlobalSetEvent("LUC_ProstituteRoomIntruder");

IF
CharacterLeftTrigger(_Player,TRIGGER_LUC_AreaProstituteRoom)
AND
NOT DB_InRegion(_, TRIGGER_LUC_AreaProstituteRoom)
THEN
GlobalClearEvent("LUC_ProstituteRoomIntruder");

IF
CharacterLeftTrigger(_Player,TRIGGER_LUC_AreaProstituteRoom)
AND
DB_InRegion(_OtherPlayer, TRIGGER_LUC_AreaProstituteRoom)
AND
DB_LUC_ProstituteHired(_OtherPlayer, _)
THEN
GlobalClearEvent("LUC_ProstituteRoomIntruder");

IF
DialogCharacterEventSet("UsesProstitute",_Player,_)
AND
CharacterGetVarInteger(_Player,"UsesProstitute",_Value)
AND
DB_LUC_Prostitutes(_Value,CHARACTER_LUC_Ruby,_)
THEN
ItemCloseAndLock(ITEM_LUC_DoorProstitute1,"LUC_DoorProstitute1");
CharacterSetVarInteger(_Player,"HiresProstitute",0);
CharacterSetVarInteger(_Player,"UsesProstitute",0);
DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ruby);

IF
DialogEnded("LUC_Ruby",_)
AND
DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ruby)
THEN
CharacterSetHasDialog(CHARACTER_LUC_Ruby, 0);
CharacterUseItem(CHARACTER_LUC_Ruby, ITEM_LUC_RubyChair, "sitForStory");

IF
CharacterEvent(CHARACTER_LUC_Ruby, "sitForStory")
THEN
DialogStartOneSpeakerDialog("LUC_AD_Ruby", CHARACTER_LUC_Ruby);
GlobalSetEvent("LUC_RubyTellsStory");

IF
CharacterLeftTrigger(_Player, TRIGGER_LUC_AreaProstituteRoom)
AND
DB_GlobalEvent("LUC_RubyTellsStory")
THEN
DialogRequestStopForCharacter(CHARACTER_LUC_Ruby);

IF
AutomatedDialogEnded("LUC_AD_Ruby", _)
AND
DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ruby)
THEN
GlobalClearEvent("LUC_RubyTellsStory");
NOT DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ruby);
NOT DB_LUC_ProstituteHired(_Player, CHARACTER_LUC_Ruby);
ItemUnlockAndOpen(ITEM_LUC_DoorProstitute1);
ProcCharacterMoveToTrigger(CHARACTER_LUC_Ruby,TRIGGER_LUC_Point_Inn_Up,0,"ProstituteReachedStairsDown");
DB_LUC_PlayerHeardRubyStory(_Player);

IF
CharacterEvent(_Prostitute,"ProstituteReachedStairsDown")
THEN
CharacterTeleportToTrigger(_Prostitute,TRIGGER_LUC_Point_Inn_Down,"");
GlobalClearEvent("LUC_ProstituteRoomOccupied");
CharacterSetVarInteger(_Prostitute,"WaitingForClient",0);
CharacterSetVarFixedString(_Prostitute,"currentState","State_WaitingForClients");
CharacterSetHasDialog(_Prostitute,1);

IF
CharacterEvent(CHARACTER_LUC_Ramon,"ProstituteReachedStairsDown")
THEN
CharacterSetVarInteger(CHARACTER_LUC_Overseer,"RamonAvailable",1);

IF
DialogCharacterEventSet("UsesProstitute",_Player,_)
AND
CharacterGetVarInteger(_Player,"UsesProstitute",_Value)
AND
DB_LUC_Prostitutes(_Value,CHARACTER_LUC_Ramon,_)
THEN
ItemCloseAndLock(ITEM_LUC_DoorProstitute1,"LUC_DoorProstitute1");
CharacterMoveAllItemsToContainer(_Player, ITEM_LUC_RamonChest);
CharacterSetVarInteger(_Player,"HiresProstitute",0);
CharacterSetVarInteger(_Player,"UsesProstitute",0);
DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ramon);

IF
DialogEnded("LUC_Ramon", _ID)
AND
DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ramon)
THEN
CharacterSetHasDialog(CHARACTER_LUC_Ramon, 0);
CharacterDisplayText(CHARACTER_LUC_Ramon, "LUC_RamonTheKiller");

IF
CharacterDisplayTextEnded(CHARACTER_LUC_Ramon, "LUC_RamonTheKiller")
THEN
CharacterSetFaction(CHARACTER_LUC_Ramon, "Evil NPC");

IF
CharacterDied(CHARACTER_LUC_Ramon)
AND
DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ramon)
THEN
NOT DB_LUC_ProstituteUsed(_Player, CHARACTER_LUC_Ramon);
NOT DB_LUC_ProstituteHired(_Player, CHARACTER_LUC_Ramon);
ItemUnlockAndOpen(ITEM_LUC_DoorProstitute1);
GlobalClearEvent("LUC_ProstituteRoomOccupied");

IF
DialogEnded("LUC_Ramon", _ID)
AND
DB_LUC_ProstituteHired(_Player, CHARACTER_LUC_Ramon)
AND
CharacterGetVarInteger(CHARACTER_LUC_Ramon,"leaveAngry",1)
THEN
NOT DB_LUC_ProstituteHired(_Player, CHARACTER_LUC_Ramon);
CharacterSetVarInteger(CHARACTER_LUC_Ramon,"leaveAngry",0);
ProcCharacterMoveToTrigger(CHARACTER_LUC_Ramon,TRIGGER_LUC_Point_Inn_Up,0,"ProstituteReachedStairsDown");

//END_REGION

//REGION RUBY 
IF
DialogEnded("LUC_Ruby",_)
AND
CharacterGetVarInteger(CHARACTER_LUC_Ruby,"LUC_RubyLeaving",1)
THEN
CharacterSetVarInteger(CHARACTER_LUC_Ruby,"LUC_RubyLeaving",0);
ProcCharacterMoveToTrigger(CHARACTER_LUC_Ruby,TRIGGER_LUC_PointRubyToTroll,0,"RubyArrivedToTroll");
CharacterSetHasDialog(CHARACTER_LUC_Ruby,0);

IF
CharacterEvent(CHARACTER_LUC_Ruby,"RubyArrivedToTroll")
AND
NOT DB_Dead(CHARACTER_LUC_BrokenBridgeTroll02)
THEN
CharacterSetVarFixedString(CHARACTER_LUC_Ruby,"currentState","State_Troll");
CharacterSetVarFixedString(CHARACTER_LUC_BrokenBridgeTroll02,"currentState","State_WithRuby");
CharacterSetHasDialog(CHARACTER_LUC_Ruby,1);
GlobalSetEvent("LUC_RubyWithTroll");

IF
CharacterEvent(CHARACTER_LUC_Ruby,"RubyArrivedToTroll")
AND
DB_Dead(CHARACTER_LUC_BrokenBridgeTroll02)
THEN
CharacterSetHasDialog(CHARACTER_LUC_Ruby,1);
ProcCharacterMoveToTrigger(CHARACTER_LUC_Ruby,TRIGGER_LUC_Point_Inn_Down,"");
CharacterSetVarFixedString(CHARACTER_LUC_Ruby,"currentState","State_WaitingForClients");

IF
CharacterDied(CHARACTER_LUC_BrokenBridgeTroll02)
AND
DB_GlobalEvent("LUC_RubyWithTroll")
THEN
CharacterSetHasDialog(CHARACTER_LUC_Ruby,1);
GlobalClearEvent("LUC_RubyWithTroll");
ProcCharacterMoveToTrigger(CHARACTER_LUC_Ruby,TRIGGER_LUC_Point_Inn_Down,"");
CharacterSetVarFixedString(CHARACTER_LUC_Ruby,"currentState","State_WaitingForClients");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
