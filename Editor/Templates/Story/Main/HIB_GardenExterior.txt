Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION CHARACTERS SETUP, DIALOGS & BEHAVIOURS
DB_Dialogs(CHARACTER_HIB_RedImp,"HIB_RedImp");
DB_ItemDialogs(ITEM_HIB_CastleGate1,"HIB_Forcefield");
DB_NoCompanionReplace(ITEM_HIB_CastleGate1);
DB_ItemDialogs(ITEM_HIB_IronMaiden,"HIB_IronMaiden");

DB_IsNotMessingAround(CHARACTER_HIB_Hopper_Snowman);
DB_IsNotMessingAround(CHARACTER_HIB_Hopper);
DB_IsNotMessingAround(CHARACTER_HIB_ShamblingMound1);
DB_NoDefaultBehavior(CHARACTER_HIB_WhiteStag);

TriggerRegisterForPlayers(TRIGGER_HIB_Area_InsideCastle);
//END_REGION

//REGION WHITE STAG
CharacterSetOnStage(CHARACTER_HIB_WhiteStag,0);
DB_PlayerComments("HIB_CommentWhiteStag","HIB_CommentWhiteStag_1_1",1,1);
DB_PlayerComments("HIB_CommentWhiteStag","HIB_CommentWhiteStag_2_1",2,1);
DB_PlayerComments("HIB_CommentWhiteStag","HIB_CommentWhiteStag_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_HIB_CommentWhiteStag,"HIB_CommentWhiteStag");
//END_REGION

//REGION ENTERING THE PRISON
ItemSetOnstage(ITEM_HIB_PrisonHatchOut,0);
DB_ShovelArea(TRIGGER_HIB_ShovelPrisonHatch,"HIB_ShovelPrisonHatch",ITEM_HIB_ShovelPrisonHatchDirt);
DB_ShovelRewardEvent("HIB_ShovelPrisonHatch","HIB_PrisonHatchRevealed");
DB_ShovelRewardComment("HIB_ShovelPrisonHatch","HIB_CommentFoundHatchUnknown");

DB_PlayerComments("HIB_CommentFoundHatchUnknown","HIB_CommentFoundHatchUnknown_1_1",1,1);
DB_PlayerComments("HIB_CommentFoundHatchUnknown","HIB_CommentFoundHatchUnknown_2_1",2,1);
DB_PlayerComments("HIB_CommentFoundHatchUnknown","HIB_CommentFoundHatchUnknown_2_2",2,2);
DB_PlayerComments("HIB_CommentFoundHatchKnown","HIB_CommentFoundHatchKnown_1_1",1,1);
DB_PlayerComments("HIB_CommentFoundHatchKnown","HIB_CommentFoundHatchKnown_2_1",2,1);
DB_PlayerComments("HIB_CommentFoundHatchKnown","HIB_CommentFoundHatchKnown_2_2",2,2);
DB_PlayerComments("HIB_CommentForcefield","HIB_CommentForcefield_1_1",1,1);
DB_PlayerComments("HIB_CommentForcefield","HIB_CommentForcefield_2_1",2,1);
DB_PlayerComments("HIB_CommentForcefield","HIB_CommentForcefield_2_2",2,2);
DB_PlayerComment_Trigger(TRIGGER_HIB_CommentForcefield,"HIB_CommentForcefield");
//END_REGION

//REGION ELEMENTALS RIOT
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Wind4,"HIB_RiotGroup1","HIB_Elemental_Wind");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Fire4,"HIB_RiotGroup1","HIB_Elemental_Fire");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Ice2,"HIB_RiotGroup1","HIB_Elemental_Ice");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Ice3,"HIB_RiotGroup1","HIB_Elemental_Ice");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Earth1,"HIB_RiotGroup2","HIB_Elemental_Earth");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Fire1,"HIB_RiotGroup2","HIB_Elemental_Fire");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Fire2,"HIB_RiotGroup2","HIB_Elemental_Fire");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Wind1,"HIB_RiotGroup3","HIB_Elemental_Wind");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Wind2,"HIB_RiotGroup3","HIB_Elemental_Wind");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Ice4,"HIB_RiotGroup3","HIB_Elemental_Ice");
DB_HIB_ElementalsRiot(CHARACTER_HIB_ElementalRiot_Earth2,"HIB_RiotGroup3","HIB_Elemental_Earth");

CharacterSetHitpointsPercentage(CHARACTER_HIB_ElementalRiot_Earth1,10);
CharacterSetHitpointsPercentage(CHARACTER_HIB_ElementalRiot_Wind4,10);
CharacterSetHitpointsPercentage(CHARACTER_HIB_ElementalRiot_Fire4,20);
CharacterSetHitpointsPercentage(CHARACTER_HIB_ElementalRiot_Ice4,20);
CharacterSetHitpointsPercentage(CHARACTER_HIB_ElementalRiot_Earth2,10);

DB_PlayerComments("HIB_CommentElementalsRiot","HIB_CommentElementalsRiot_1_1",1,1);
DB_PlayerComments("HIB_CommentElementalsRiot","HIB_CommentElementalsRiot_2_1",2,1);
DB_PlayerComments("HIB_CommentElementalsRiot","HIB_CommentElementalsRiot_2_2",2,2);
//END_REGION

//REGION Arhu
DB_Dialogs(CHARACTER_HIB_Arhu_Cat,"HIB_Arhu_Entrance");
TriggerRegisterForPlayers(TRIGGER_HIB_Arhu_SafeArea);
DB_NoDefaultBehavior(CHARACTER_HIB_Arhu_Cat);
CharacterSetCanFight(CHARACTER_HIB_Arhu_Cat,0);
//END_REGION

DB_ShovelArea(TRIGGER_HIB_ShovelDiggingSpot1,"HIB_DiggingSpot1",ITEM_HIB_ShovelDiggingSpot1Pile);
DB_ShovelRewardItemAppear("HIB_DiggingSpot1",ITEM_HIB_ShovelDiggingSpot1Chest,TRIGGER_HIB_ShovelDiggingSpot1Point);
DB_ShovelArea(TRIGGER_HIB_ShovelDiggingSpot2,"HIB_DiggingSpot2",ITEM_HIB_ShovelDiggingSpot2Pile);
DB_ShovelRewardItemAppear("HIB_DiggingSpot2",ITEM_HIB_ShovelDiggingSpot2Chest,TRIGGER_HIB_ShovelDiggingSpot2Point);
DB_ShovelArea(TRIGGER_HIB_ShovelDiggingSpot3,"HIB_DiggingSpot3",ITEM_HIB_ShovelDiggingSpot3Pile);
DB_ShovelRewardItemAppear("HIB_DiggingSpot3",ITEM_HIB_ShovelDiggingSpot3Chest,TRIGGER_HIB_ShovelDiggingSpot3Point);

DB_HIB_TrappedChests(ITEM_HIB_TrappedChest1, ITEM_HIB_TrappingCrystal1);
DB_HIB_TrappedChests(ITEM_HIB_TrappedChest2, ITEM_HIB_TrappingCrystal2);
DB_HIB_TrappedChests(ITEM_HIB_TrappedChest3, ITEM_HIB_TrappingCrystal3);

PROC_HIB_SetTrappedChest();
KBSECTION
//REGION WHITE STAG
IF
CharacterUsedItem(_Player,ITEM_HIB_CorpseWhiteStag)
AND
NOT DB_Combat(_Player,_)
THEN
ItemSetCanInteract(ITEM_HIB_CorpseWhiteStag,0);
PlayEffectAtCharacter("FX_GP_TeleportSmoke_A",CHARACTER_HIB_WhiteStag);
CharacterAppear(CHARACTER_HIB_WhiteStag,0);
CharTimer(_Player,"HIB_WhiteStag",1000);

IF
CharTimerFinished(_Player,"HIB_WhiteStag")
THEN
DB_Dialogs(CHARACTER_HIB_WhiteStag,"HIB_WhiteStag");

IF
CharTimerFinished(_Player,"HIB_WhiteStag")
AND
NOT DB_Dead(_Player)
AND
NOT _Player.DB_IsInDialog()
AND
DB_CurrentLevel("ThePrisonGardenOfStoneKing")
THEN
PROC_MendatoryDialog("HIB_WhiteStag",CHARACTER_HIB_WhiteStag,_Player);

IF
CharacterReceivedDamage(CHARACTER_HIB_WhiteStag)
THEN
DialogRequestStopForCharacter(CHARACTER_HIB_WhiteStag);
CharacterSetHasDialog(CHARACTER_HIB_WhiteStag,0);
CharacterPlayAnimation(CHARACTER_HIB_WhiteStag,"Vanish_01","HIB_WhiteStagDisappears");

IF
CharacterAttackedByCharacter(CHARACTER_HIB_WhiteStag,_)
THEN
DialogRequestStopForCharacter(CHARACTER_HIB_WhiteStag);
CharacterSetHasDialog(CHARACTER_HIB_WhiteStag,0);
CharacterPlayAnimation(CHARACTER_HIB_WhiteStag,"Vanish_01","HIB_WhiteStagDisappears");

IF
DialogEnded("HIB_WhiteStag",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_WhiteStag,"HIB_WhiteStagDisappears",1)
THEN
CharacterSetHasDialog(CHARACTER_HIB_WhiteStag,0);
CharacterSetVarInteger(CHARACTER_HIB_WhiteStag,"HIB_WhiteStagDisappears",0);
CharacterPlayAnimation(CHARACTER_HIB_WhiteStag,"Vanish_01","HIB_WhiteStagDisappears");

IF
CharacterEvent(CHARACTER_HIB_WhiteStag,"HIB_WhiteStagDisappears")
THEN
CharacterSetOnStage(CHARACTER_HIB_WhiteStag,0);
//END_REGION

//REGION ELEMENTALS RIOT
IF
CharacterSawCharacter(_Player, _Character)
AND
_Player.DB_IsPlayer()
AND
DB_HIB_ElementalsRiot(_Character,_Group,_)
THEN
PROC_HIB_ElementalsRiotComment(_Player);
PROC_HIB_ElementalsRiotFight(_Group);

PROC
PROC_HIB_ElementalsRiotFight((STRING)_Group)
AND
DB_HIB_ElementalsRiot(_Character,_Group,_Faction)
THEN
NOT DB_HIB_ElementalsRiot(_Character,_Group,_Faction);
CharacterSetFaction(_Character,_Faction);

PROC
PROC_HIB_ElementalsRiotComment((CHARACTER)_Player)
THEN
Launch_PlayerComment(_Player,"HIB_CommentElementalsRiot");
QuestAdd("HIB_Boreas");
QuestUpdate("HIB_Boreas","ElementalTrouble");
GlobalSetEvent("HIB_Elementals");
//END_REGION

//REGION ENTERING THE PRISON
IF
GlobalEventSet("HIB_KnowsHatchLocation")
THEN
ProcShowMarker("HIB_PrisonHatch");
NOT DB_ShovelRewardComment("HIB_ShovelPrisonHatch","HIB_CommentFoundHatchUnknown");
DB_ShovelRewardComment("HIB_ShovelPrisonHatch","HIB_CommentFoundHatchKnown");

IF
GlobalEventSet("HIB_PrisonHatchRevealed")
THEN
ItemSetOnstage(ITEM_HIB_PrisonHatchOut,1);
QuestUpdate("LUC_WhiteWitch","FoundHatch");
//END_REGION

//REGION ENTERING THE CASTLE
IF
DialogEnded("HIB_Forcefield",_)
AND
ItemGetVarInteger(ITEM_HIB_CastleGate1,"HIB_ForcefieldDisappears",1)
THEN
ItemSetVarInteger(ITEM_HIB_CastleGate1,"HIB_ForcefieldDisappears",0);
ItemSetEvent(ITEM_HIB_CastleGate1, "shutDown");
ItemSetCanInteract(ITEM_HIB_CastleGate1,0);
QuestAdd("HIB_Boreas");
QuestUpdate("HIB_Boreas","FoundWayIn");

IF
CharacterEnteredTrigger(_,TRIGGER_HIB_Area_InsideCastle)
THEN
QuestAdd("HIB_Boreas");
QuestUpdate("HIB_Boreas","FoundWayIn");
//END_REGION

//REGION HOPPER
IF
CharacterAttackedByCharacter(CHARACTER_HIB_Hopper_Snowman,_Player)
AND
_Player.DB_IsPlayer()
AND
NOT DB_Combat(CHARACTER_HIB_Hopper_Snowman,_)
AND
NOT DB_StartedHopperFight(1)
THEN
DB_StartedHopperFight(1);
CHARACTER_HIB_Hopper_Snowman.HIB_Attacked();
CharacterSetRelationFactionToFaction("HIB_HopperSnowman","HIB_Hopper",0);
CharacterSetRelationFactionToFaction("HIB_Hopper","HIB_HopperSnowman",0);
ProcSetRelationToPlayers(CHARACTER_HIB_Hopper_Snowman,0);
ProcSetRelationToPlayers(CHARACTER_HIB_Hopper,100);
CharacterDisplayText(CHARACTER_HIB_Hopper_Snowman,"HIB_HopperSnowman_1");

IF
CharacterAttackedByCharacter(CHARACTER_HIB_Hopper,_Player)
AND
_Player.DB_IsPlayer()
AND
NOT DB_Combat(CHARACTER_HIB_Hopper,_)
AND
NOT DB_StartedHopperFight(1)
THEN
NOT DB_IsNotMessingAround(CHARACTER_HIB_Hopper_Snowman);
DB_StartedHopperFight(1);
CHARACTER_HIB_Hopper.HIB_Attacked();
NOT DB_NoDefaultBehavior(CHARACTER_HIB_Hopper_Snowman);
DB_NoRunAway(CHARACTER_HIB_Hopper_Snowman);
CharacterSetRelationFactionToFaction("HIB_HopperSnowman","HIB_Hopper",0);
CharacterSetRelationFactionToFaction("HIB_Hopper","HIB_HopperSnowman",0);
ProcSetRelationToPlayers(CHARACTER_HIB_Hopper_Snowman,100);
ProcSetRelationToPlayers(CHARACTER_HIB_Hopper,0);
DialogStartOneSpeakerDialog("HIB_AD_HopperAttacked",CHARACTER_HIB_Hopper);

IF
CharacterDied(CHARACTER_HIB_Hopper_Snowman)
AND
CHARACTER_HIB_Hopper_Snowman.HIB_Attacked()
AND
CharacterGetClosestAlivePlayer(CHARACTER_HIB_Hopper,_Player)
THEN
NOT CHARACTER_HIB_Hopper_Snowman.HIB_Attacked();
CharacterSetVarFixedString(CHARACTER_HIB_Hopper,"currentState","State_Wandering");
DB_Dialogs(CHARACTER_HIB_Hopper,"HIB_Hopper");
PROC_MendatoryDialog("HIB_Hopper",CHARACTER_HIB_Hopper,_Player);

IF
CharacterDied(CHARACTER_HIB_Hopper)
AND
CHARACTER_HIB_Hopper.HIB_Attacked()
AND
CharacterGetClosestAlivePlayer(CHARACTER_HIB_Hopper_Snowman,_Player)
THEN
NOT CHARACTER_HIB_Hopper.HIB_Attacked();
DB_Dialogs(CHARACTER_HIB_Hopper_Snowman,"HIB_HopperSnowman");
PROC_MendatoryDialog("HIB_HopperSnowman",CHARACTER_HIB_Hopper_Snowman,_Player);

IF
DialogEnded("HIB_Hopper",_)
AND
CharacterGetVarInteger(CHARACTER_HIB_Hopper,"HIB_HopperLeads",1)
THEN
CharacterSetVarInteger(CHARACTER_HIB_Hopper,"HIB_HopperLeads",0);
CharacterSetHasDialog(CHARACTER_HIB_Hopper,0);
ProcCharacterMoveToTrigger(CHARACTER_HIB_Hopper,TRIGGER_HIB_Point_Hopper2,1,"HIB_HopperAtPit");

IF
CharacterEvent(CHARACTER_HIB_Hopper,"HIB_HopperAtPit")
THEN
CharacterSetHasDialog(CHARACTER_HIB_Hopper,1);
CharacterSetVarFixedString(CHARACTER_HIB_Hopper,"currentState","State_WaitingAtPit");
CharacterSetVarInteger(CHARACTER_HIB_Hopper,"HIB_HopperAtPit",1);
CHARACTER_HIB_Hopper.HIB_WaitingAtPit();

IF
Sees(CHARACTER_HIB_Hopper,_Player)
AND
CHARACTER_HIB_Hopper.HIB_WaitingAtPit()
THEN
NOT CHARACTER_HIB_Hopper.HIB_WaitingAtPit();
CharacterDisplayText(CHARACTER_HIB_Hopper,"HIB_Hopper_1");
//END_REGION

//REGION TEST
IF
TextEventSet("hiber")
THEN
GlobalSetEvent("LUC_KnowsIcaraHiberheim");
ItemToCharacter(ITEM_LUC_WitchCave_LakeSpell,CHARACTER_Player1);
PROC_CYS_AlistairToHiberheim();

IF
TextEventSet("icarareview")
THEN
GlobalSetEvent("CYS_FabulousFiveEnlist");
GlobalSetEvent("CYS_FabulousFiveGaveQuest");

IF
TextEventSet("snowstorm")
THEN
PROC_HIB_SnowStormOver();
//END_REGION

//REGION THE GUARDIAN
IF
CharacterDying(CHARACTER_HIB_TheGuardian)
THEN
PartyAddExperience(1, 12, 6);
//END_REGION

//REGION ARHU
IF
DialogEnded("HIB_Arhu_Entrance",_)
THEN
CharacterSetHasDialog(CHARACTER_HIB_Arhu_Cat,0);
DB_OnlyOnce("HIB_Arhu_Cat_Gone");
CharacterDisappearOutOfSight(CHARACTER_HIB_Arhu_Cat,180,1,"",1);
QuestAdd("HIB_Boreas");
QuestUpdate("HIB_Boreas","Start");
QuestUpdate("HIB_Boreas","MetArhu");

IF
CharacterEnteredTrigger(_,TRIGGER_HIB_Area_InJail)
AND
NOT DB_OnlyOnce("HIB_Arhu_Cat_Gone")
THEN
CharacterSetHasDialog(CHARACTER_HIB_Arhu_Cat,0);
DB_OnlyOnce("HIB_Arhu_Cat_Gone");
CharacterDisappearOutOfSight(CHARACTER_HIB_Arhu_Cat,180,1,"",1);

IF
CharacterReceivedDamage(CHARACTER_HIB_Arhu_Cat)
THEN
CharacterSetHasDialog(CHARACTER_HIB_Arhu_Cat,0);
DB_OnlyOnce("HIB_Arhu_Cat_Gone");
CharacterDisappearOutOfSight(CHARACTER_HIB_Arhu_Cat,180,1,"",1);

IF
CharacterEnteredCombat(CHARACTER_HIB_Arhu_Cat,_)
THEN
CharacterSetHasDialog(CHARACTER_HIB_Arhu_Cat,0);
DB_OnlyOnce("HIB_Arhu_Cat_Gone");
CharacterDisappearOutOfSight(CHARACTER_HIB_Arhu_Cat,180,1,"",1);

IF
CharacterEnteredTrigger(_Player,TRIGGER_HIB_Arhu_SafeArea)
THEN
CharacterSetVarInteger(CHARACTER_HIB_Arhu,"PlayersInSafeArea",1);

IF
CharacterLeftTrigger(_Player,TRIGGER_HIB_Arhu_SafeArea)
AND
NOT DB_InRegion(_,TRIGGER_HIB_Arhu_SafeArea)
THEN
CharacterSetVarInteger(CHARACTER_HIB_Arhu,"PlayersInSafeArea",0);
//END_REGION

//REGION Trapped chest
PROC
PROC_HIB_SetTrappedChest()
AND
DB_HIB_TrappedChests(_Chest, _)
THEN
ItemSetCanInteract(_Chest, 0);

IF
ItemDestroyed(_Crystal)
AND
DB_HIB_TrappedChests(_Chest, _Crystal)
THEN
ItemSetCanInteract(_Chest, 1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
