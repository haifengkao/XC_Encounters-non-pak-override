Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_Bairdotr,"CYS_Bairdotr");
DB_DismissEvent(CHARACTER_Bairdotr,"BairdotrDismissed");
DB_CustomWarningDialog(CHARACTER_Bairdotr,"Bairdotr_please_Dont_attack");
DB_AttackDialog(CHARACTER_Bairdotr,"Companion_Bairdotr_Attacks");
DB_NoRunAway(CHARACTER_Bairdotr);
CharacterSetCanFight(CHARACTER_Bairdotr,0);

DB_BRD_Corruption(2);
PROC_CheckIfBairdotrIsCorrupt(2);

DB_Dialogs(CHARACTER_CYS_Bairdotr_Guard_F,CHARACTER_CYS_Bairdotr_Guard_M,"BRD_CageGuardsIntro");
ItemToCharacter(ITEM_CYS_BairdotrCageKey,CHARACTER_CYS_Bairdotr_Guard_F);

DB_TrollDialog("DAF_TrollBridge");
DB_TrollDialog("LUC_TrollBridge1");
DB_TrollDialog("LUC_TrollBridge2");
DB_TrollDialog("LUC_TrollBridge6");
DB_TrollDialog("LUC_BabyTrolls");

ProcTriggerRegisterForPlayers(TRIGGER_CYS_BRD_CD_Beggar);
ProcTriggerRegisterForPlayers(TRIGGER_LUC_BRD_CC_Trollking);
TriggerRegisterForCharacter(TRIGGER_CYS_Bairdotr_Cagebox,CHARACTER_Bairdotr);
KBSECTION
IF
CharacterUsedItem(_Player,ITEM_CYS_BairdotrCageDoor)
AND
_Player.DB_IsPlayer()
AND
DB_DLCGiven(4)
AND
ItemIsLocked(ITEM_CYS_BairdotrCageDoor,1)
AND
NOT DB_Dead(CHARACTER_CYS_Bairdotr_Guard_F)
AND
NOT DB_Dead(CHARACTER_CYS_Bairdotr_Guard_M)
AND
NOT CHARACTER_CYS_Bairdotr_Guard_F.DB_IsInDialog()
AND
NOT CHARACTER_CYS_Bairdotr_Guard_M.DB_IsInDialog()
AND
NOT DB_OnlyOnce("Bairdotr_Recruited_Cage")
THEN
ProcDoThreeSpeakerDialog("BRD_CageGuardsIntro", CHARACTER_CYS_Bairdotr_Guard_F,CHARACTER_CYS_Bairdotr_Guard_M, _Player);

IF
ItemOpened(ITEM_CYS_BairdotrCageDoor)
AND
DB_DLCGiven(4)
AND
NOT DB_OnlyOnce("Bairdotr_Recruited_Cage")
THEN
DB_OnlyOnce("Bairdotr_Recruited_Cage");
DialogRequestStopForCharacter(CHARACTER_CYS_Bairdotr_Guard_F);
DialogRequestStopForCharacter(CHARACTER_CYS_Bairdotr_Guard_M);
DialogRequestStopForCharacter(CHARACTER_Bairdotr);
CharacterSetHasDialog(CHARACTER_CYS_Bairdotr_Guard_F,0);
CharacterSetHasDialog(CHARACTER_CYS_Bairdotr_Guard_M,0);
CharacterSetHasDefaultDialog(CHARACTER_CYS_Bairdotr_Guard_F,0);
CharacterSetHasDefaultDialog(CHARACTER_CYS_Bairdotr_Guard_M,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Bairdotr_Guard_F,"BRD_CageGuardsIntro");
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Bairdotr_Guard_M,"BRD_CageGuardsIntro");
CharacterDisappearOutOfSight(CHARACTER_CYS_Bairdotr_Guard_F,270,1,"",1);
CharacterDisappearOutOfSight(CHARACTER_CYS_Bairdotr_Guard_M,90,1,"",1);
GlobalSetEvent("BairdotrCageOpen");

IF
CharacterLeftTrigger(CHARACTER_Bairdotr,TRIGGER_CYS_Bairdotr_Cagebox)
AND
DB_DLCGiven(4)
AND
NOT DB_OnlyOnce("Bairdotr_Recruited_Cage")
THEN
DB_OnlyOnce("Bairdotr_Recruited_Cage");
DialogRequestStopForCharacter(CHARACTER_CYS_Bairdotr_Guard_F);
DialogRequestStopForCharacter(CHARACTER_CYS_Bairdotr_Guard_M);
DialogRequestStopForCharacter(CHARACTER_Bairdotr);
CharacterSetHasDialog(CHARACTER_CYS_Bairdotr_Guard_F,0);
CharacterSetHasDialog(CHARACTER_CYS_Bairdotr_Guard_M,0);
CharacterSetHasDefaultDialog(CHARACTER_CYS_Bairdotr_Guard_F,0);
CharacterSetHasDefaultDialog(CHARACTER_CYS_Bairdotr_Guard_M,0);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Bairdotr_Guard_F,"BRD_CageGuardsIntro");
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Bairdotr_Guard_M,"BRD_CageGuardsIntro");
CharacterDisappearOutOfSight(CHARACTER_CYS_Bairdotr_Guard_F,270,1,"",1);
CharacterDisappearOutOfSight(CHARACTER_CYS_Bairdotr_Guard_M,90,1,"",1);
GlobalSetEvent("BairdotrCageOpen");

IF
DialogEnded("BRD_CageGuardsIntro",_)
AND
DB_GlobalEvent("Bairdotr_Recruited")
AND
NOT DB_OnlyOnce("Bairdotr_Recruited_Cage")
THEN
DB_OnlyOnce("Bairdotr_Recruited_Cage");
ItemUnLockAndOpen(ITEM_CYS_BairdotrCageDoor);
CharacterSetHasDialog(CHARACTER_CYS_Bairdotr_Guard_F,0);
CharacterSetHasDialog(CHARACTER_CYS_Bairdotr_Guard_M,0);
DialogRequestStopForCharacter(CHARACTER_CYS_Bairdotr_Guard_F);
DialogRequestStopForCharacter(CHARACTER_CYS_Bairdotr_Guard_M);
DialogRequestStopForCharacter(CHARACTER_Bairdotr);
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Bairdotr_Guard_F,"BRD_CageGuardsIntro");
ProcRemoveDialogEntryForSpeaker(CHARACTER_CYS_Bairdotr_Guard_M,"BRD_CageGuardsIntro");
CharacterDisappearOutOfSight(CHARACTER_CYS_Bairdotr_Guard_F,90,0,"",1);
CharacterDisappearOutOfSight(CHARACTER_CYS_Bairdotr_Guard_M,90,0,"",1);

//REGION Party join/leave
IF
DialogCharacterEventSet("Bairdotr_Recruited",_Player,_)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_Bairdotr,"CYS_Bairdotr");
DB_Dialogs(CHARACTER_Bairdotr,"BRD_Default");
DB_CompanionRecruited(CHARACTER_Bairdotr);
CharacterMakeCompanion(CHARACTER_Bairdotr,_Player);
Proc_LimitAbilitiesForCompanion(CHARACTER_Bairdotr);
CharacterSetEvent(CHARACTER_Bairdotr,"Hired");
DB_Companion(CHARACTER_Bairdotr);
ProcSetPartyFullIfLoneWolf();
CharacterSetCanFight(CHARACTER_Bairdotr,1);
DialogRequestStopForCharacter(CHARACTER_Bairdotr);
CharacterSetOnStage(CHARACTER_Bairdotr,1);

IF
DialogEnded("BRD_Default",_)
AND 
DB_GlobalEvent("BairdotrDismissed")
AND
DB_Companion(CHARACTER_Bairdotr)
THEN
ProcDismissBairdotr();

PROC
ProcDoDismissOfCompanion(CHARACTER_Bairdotr)
THEN
ProcDismissBairdotr();

PROC
ProcDismissBairdotr()
AND
DB_Companion(CHARACTER_Bairdotr)
THEN
OnCompanionDismissed(CHARACTER_Bairdotr);
ProcHACKFixCompanionReputation(CHARACTER_Bairdotr);

PROC
ProcDismissBairdotr()
AND
NOT DB_GlobalEvent("HallOfHeroesUnlocked")
AND
CharacterGetPosition(CHARACTER_Bairdotr,_X,_Y,_Z)
AND
DB_Companion(CHARACTER_Bairdotr)
THEN
ProcCancelAllCompanionDialogs(CHARACTER_Bairdotr);
NOT DB_Companion(CHARACTER_Bairdotr);
PROC_END_RemoveCompInDBRoomB(CHARACTER_Bairdotr);
CharacterDetachFromGroup(CHARACTER_Bairdotr);
CharacterMakeNPC(CHARACTER_Bairdotr);
CharacterRemoveFromParty(CHARACTER_Bairdotr);
CharacterTeleportToTrigger(CHARACTER_Bairdotr,TRIGGER_CYS_Tavern_Bairdotr,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);

PROC
ProcDismissBairdotr()
AND
DB_GlobalEvent("HallOfHeroesUnlocked")
AND
CharacterGetPosition(CHARACTER_Bairdotr,_X,_Y,_Z)
AND
DB_Companion(CHARACTER_Bairdotr)
THEN
NOT DB_Companion(CHARACTER_Bairdotr);
CharacterDetachFromGroup(CHARACTER_Bairdotr);
CharacterMakeNPC(CHARACTER_Bairdotr);
CharacterRemoveFromParty(CHARACTER_Bairdotr);
CharacterTeleportToTrigger(CHARACTER_Bairdotr,TRIGGER_HOM_HoH_Bairdotr_Neutral,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PROC_END_RemoveCompInDBRoomB(CHARACTER_Bairdotr);

IF
DialogCharacterEventSet("BairdotrRejoins",_Player,_ID)
THEN
DB_Companion(CHARACTER_Bairdotr);
CharacterMakeCompanion(CHARACTER_Bairdotr,_Player);
DialogClearCharacterEvent("BairdotrRejoins",_Player,_ID);
ProcSetPartyFullIfLoneWolf();

IF
GlobalEventSet("HallOfHeroesUnlocked")
AND
DB_GlobalEvent("BairdotrDismissed")
THEN
CharacterTeleportToTrigger(CHARACTER_Bairdotr,TRIGGER_HOM_HoH_Bairdotr_Neutral,"");
//END_REGION

//REGION Humanity is Corrupt
IF
GlobalEventSet("BRD_PlusCorrupt")
AND
DB_BRD_Corruption(_OldVal)
AND
IntegerSum(_OldVal,1,_NewVal)
THEN
NOT DB_BRD_Corruption(_OldVal);
DB_BRD_Corruption(_NewVal);
GlobalClearEvent("BRD_PlusCorrupt");
PROC_CheckIfBairdotrIsCorrupt(_NewVal);

IF
GlobalEventSet("BRD_MinCorrupt")
AND
DB_BRD_Corruption(_OldVal)
AND
IntegerSum(_OldVal,-1,_NewVal)
THEN
NOT DB_BRD_Corruption(_OldVal);
DB_BRD_Corruption(_NewVal);
GlobalClearEvent("BRD_MinCorrupt");
GlobalClearEvent("BRD_Corrupt");
PROC_CheckIfBairdotrIsCorrupt(_NewVal);

PROC
PROC_CheckIfBairdotrIsCorrupt((INTEGER)_NewVal)
AND
_NewVal >= 0
THEN
GlobalSetEvent("BRD_Corrupt");
//END_REGION

//REGION Companion comments & dialogs
IF
CharacterEnteredTrigger(CHARACTER_Bairdotr,TRIGGER_LUC_SandstormEffect)
AND
CharacterGetClosestAlivePlayer(CHARACTER_Bairdotr,_Player)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_DustPlains",_Player);

IF
DialogEnded(_Dialog,_)
AND
DB_LUC_Prostitutes(_,_Prositute,_Dialog)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_Prostitute",_Prositute);

IF
DialogEnded(_Dialog,_Inst)
AND
DB_TrollDialog(_Dialog)
AND
DB_DialogPlayers(_Inst,_Player,1)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_MeetingTroll",_Player);

IF
DialogEnded("LUC_TrollBridge2",_)
AND
DB_GlobalEvent("LUC_RubyWithTroll")
THEN
TimerLaunch("BRD_CC_FumbleClose",5000);

IF
TimerFinished("BRD_CC_FumbleClose")
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_FumbleClose",CHARACTER_LUC_BrokenBridgeTroll02);

IF
DialogEnded("CYS_Bath",_)
AND
CharacterGetDistanceToCharacter(CHARACTER_Bairdotr,CHARACTER_CYS_CeciliaBath,_Dist2)
AND
_Dist2 < 10.0
THEN
TimerLaunch("CYS_Comment_BRD_CeciliaBath",5000);

IF
TimerFinished("CYS_Comment_BRD_CeciliaBath")
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_CeciliaBath",CHARACTER_CYS_CeciliaBath);

IF
DialogEnded("CYS_Charlene",_)
AND
DB_GlobalEvent("CYS_LieCharlene")
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CYS_CharleneTruth",CHARACTER_CYS_Charlene);

IF
CharacterEnteredTrigger(_Player,TRIGGER_CYS_BRD_CD_Beggar)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_Beggar",_Player);

IF
DialogEnded("LUC_RunningMiner", _)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_LUC_AfterSamid",CHARACTER_LUC_EscapingMiner);
QuestUpdate("BRD_Jareth","SamidStone");

IF
DialogEnded("LUC_FortuneTellerHusband", _)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_AfterMeetingMalbini",CHARACTER_LUC_FortuneTellerHusband);

IF
DialogEnded("LUC_Nadia",_)
AND
DB_GlobalEvent("NadiaRot")
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_AfterNadiaRot",CHARACTER_LUC_Nadia);

IF
DialogEnded("CYS_Mortician",_)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_Mortician",CHARACTER_CYS_Mortician);

IF
CharacterEnteredTrigger(_Player,TRIGGER_LUC_BRD_CC_Trollking)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_TrollCave",_Player);

IF
DialogEnded("LUC_Kadraskaz", _)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_Kadraskaz",CHARACTER_LUC_Kadraskaz);

PROC
ReactOnKillCounter("HIB_BloodStoneGuards")
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_AfterHIBLeandra",CHARACTER_HIB_Leandra);

IF
DialogEnded("HIB_RedImp", _)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_HIB_Axyx",CHARACTER_HIB_RedImp);

IF
DialogEnded("CYS_Bairdotr",_)
AND
DB_GlobalEvent("Bairdotr_Recruited")
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_FreeFromCage",CHARACTER_Bairdotr);

IF
DialogEnded("BRD_CageGuardsIntro",_)
AND
DB_GlobalEvent("Bairdotr_Recruited")
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_FreeFromCage",CHARACTER_Bairdotr);

IF
CharacterDied(CHARACTER_CYS_Braccus)
AND
CharacterGetClosestPlayer(CHARACTER_CYS_Braccus,_Player)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_BraccusDefeat",_Player);

PROC
Proc_AddExplorationEvent("CYS_EvelynLab")
AND
ItemGetClosestPlayer(ITEM_CYS_Evelyn_LabEntrance,_Player)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_EvelynHideout",_Player);

IF
DialogEnded("DAF_ForestSpiritApprentice",_)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_DAF_Apprentice",CHARACTER_DAF_ForestSpiritApprentice);

IF
DialogEnded(_Diag,_Inst)
AND
DB_GlobalEvent("HOM_StartMovie")
AND
DB_ZixZaxTalks(8,_,_,_Diag)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_Movie3",CHARACTER_CYS_Zixzax);

IF
ItemAddedToCharacter(ITEM_LUC_MiningOfficeLetter, _Player)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_CD_LawrenceTruth",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGER_LUC_ImmaculateVilage)
AND
NOT DB_Dead(CHARACTER_LUC_IV_Jareth)
THEN
Proc_CompanionDialog(CHARACTER_Bairdotr,"BRD_LUC_IV_SacredStone",_Player);
QuestUpdate("BRD_Jareth","SacredStone");

IF
DialogEnded("LUC_IV_Attenberah",_)
AND
NOT DB_Dead(CHARACTER_LUC_IV_Jareth)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_Attenberah",CHARACTER_LUC_IV_Attenberah);

IF
DialogEnded("CYS_Marcus",_)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_CysealPriest",CHARACTER_CYS_Marcus);

IF
DialogEnded("CYS_Jake",_)
THEN
Proc_CompanionComment(CHARACTER_Bairdotr,"BRD_CC_JakeBraccus",CHARACTER_CYS_ZombieJake);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
