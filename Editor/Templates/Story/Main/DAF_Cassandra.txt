Version 1
SubGoalCombiner SGC_AND
INITSECTION
SneakTriggerSpotter(TRIGGER_DAF_Cassandra_Sneak,CHARACTER_DAF_Cassandra);
DB_OneShot_DialogTrigger_NewSystem(TRIGGER_DAF_Cassandra_Sneak,"DAF_Cassandra",CHARACTER_DAF_Cassandra);
DB_Dialogs(CHARACTER_DAF_Cassandra,"DAF_Cassandra");
DB_IsNotMessingAround(CHARACTER_DAF_Cassandra);
CharacterApplyStatus(CHARACTER_DAF_Cassandra,"VOID_AURA",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_Cassandra_Fight,"VOID_AURA",-1.0,1);
DB_ShovelArea(TRIGGER_DFD_Druid_Box_Grave,"DFD_Druid_Grave",ITEM_DFD_Druid_GravePile);
DB_ShovelRewardItemAppear("DFD_Druid_Grave",ITEM_DFD_Druid_Cassandra_Corpse, TRIGGER_DFD_Druid_Point_Grave);
CharacterSetOnStage(CHARACTER_DAF_Cassandra_Fight,0);
DB_Dialogs(CHARACTER_DAF_Arhu_Cat,"CYS_Arhu");
DB_Dialogs(CHARACTER_DAF_Arhu,"CYS_Arhu");
CharacterSetOnStage(CHARACTER_DAF_Arhu,0);
ItemToCharacter(ITEM_DAF_Soulforge_Repair_Ritual,CHARACTER_DAF_Cassandra_Fight);

ItemToContainer(ITEM_DFD_Druid_Cass_Key,ITEM_DFD_Druid_Cassandra_Corpse,1);
ItemToCharacter(ITEM_DAF_Arhu_Cage_Key,CHARACTER_DAF_Cassandra);
ItemToCharacter(ITEM_DAF_Stasisfern,CHARACTER_DAF_ForestSpirit);

DB_PlayerComments("DAF_Comment_ArhuFreed","DAF_Comment_ArhuFreed_1_1",1,1);
DB_PlayerComments("DAF_Comment_ArhuFreed","DAF_Comment_ArhuFreed_2_1",2,1);
DB_PlayerComments("DAF_Comment_ArhuFreed","DAF_Comment_ArhuFreed_2_2",2,2);

DB_PlayerComments("DAF_Comment_stasisFern","DAF_Comment_StasisFern_1_1",1,1);
DB_PlayerComments("DAF_Comment_stasisFern","DAF_Comment_StasisFern_2_1",2,1);
DB_PlayerComments("DAF_Comment_stasisFern","DAF_Comment_StasisFern_2_2",2,2);
KBSECTION
IF
GlobalEventSet("DAF_ShowTempleOfDeadMarker")
THEN
ProcShowMarker("DAF_TempleOfTheDead");

IF
CharacterUsedItem(_Character,ITEM_DFD_Druid_Tombstone)
THEN
ItemDisplayText(ITEM_DFD_Druid_Tombstone,"DFD_Druid_Tombstone");

PROC
ProcShovelRewards((CHARACTER)_Player,"DFD_Druid_Grave")
AND
DB_GlobalEvent("DAF_KnowCassandraMustBurn")
THEN
Launch_PlayerComment(_Player,"DFD_Druid_Comment_MustBurn");
QuestUpdate("DAF_Cassandra","CorpseFound");

IF
ItemDestroyed(ITEM_DFD_Druid_Cassandra_Corpse)
THEN
GlobalSetEvent("CassandraBodyBurnt");
CharacterRemoveStatus(CHARACTER_DAF_Cassandra,"VOID_AURA");
CharacterRemoveStatus(CHARACTER_DAF_Cassandra_Fight,"VOID_AURA");
QuestUpdate("DAF_Cassandra","CorpseBurnt");
CharacterMoveToTrigger(CHARACTER_DAF_Cassandra,TRIGGER_DAF_Cassandra_BeginFightPosition,1,"");

IF
ItemDestroyed(ITEM_DFD_Druid_Cassandra_Corpse)
AND
ItemGetClosestPlayer(ITEM_DFD_Druid_Tombstone,_Player)
AND
DB_GlobalEvent("DAF_KnowCassandraMustBurn")
THEN
Launch_PlayerComment(_Player,"DFD_Druid_Comment_BodyBurnt");

IF
DialogEnded("DAF_Cassandra",_)
AND
DB_GlobalEvent("CassandraBodyBurnt")
THEN
ProcSetRelationToPlayers(CHARACTER_DAF_Cassandra, 0);

IF
ItemOpened(ITEM_DAF_Arhu_Cage_Door)
THEN
ProcSetRelationToPlayers(CHARACTER_DAF_Cassandra, 0);
GlobalSetEvent("DAF_ArhuFreed");
CharacterSetCanFight(CHARACTER_CYS_Arhu,1);

IF
ItemOpened(ITEM_DAF_Arhu_Cage_Door)
AND
DB_GlobalEvent("ArhuPermaHuman")
AND
NOT DB_Dead(CHARACTER_CYS_Arhu)
AND
QueryOnlyOnce("CageDoorReaction")
THEN
DialogStartOneSpeakerDialog("DAF_AD_ArhuFreed_PermaHuman",CHARACTER_CYS_Arhu);

IF
ItemOpened(ITEM_DAF_Arhu_Cage_Door)
AND
DB_GlobalEvent("DAF_CassandraDead")
AND
NOT DB_GlobalEvent("ArhuPermaHuman")
AND
NOT DB_Dead(CHARACTER_CYS_Arhu)
AND
CharacterGetClosestAlivePlayer(CHARACTER_CYS_Arhu,_Player)
AND
QueryOnlyOnce("CageDoorReaction")
THEN
Proc_MendatoryDialog("DAF_ArhuFreed_CassDead",CHARACTER_CYS_Arhu,_Player);
GlobalClearEvent("ArhuInDAFCage");
GlobalClearEvent("ArhuRefusesToMorphToHuman");

IF
DialogEnded("DAF_ArhuFreed_CassDead",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
THEN
ProcRemoveAllDialogEntriesForSpeaker(CHARACTER_CYS_Arhu);
DB_Dialogs(CHARACTER_CYS_Arhu,"CYS_Arhu");
CharacterSetHasDialog(CHARACTER_CYS_Arhu,0);
CharacterDisappearOutOfSight(CHARACTER_CYS_Arhu,270,1,"AEM_Done",1);
Launch_PlayerComment(_Player,"DAF_Comment_ArhuFreed");
CharacterSetCanFight(CHARACTER_CYS_Arhu,1);

IF
AutomatedDialogEnded("DAF_AD_ArhuFreed_PermaHuman",_)
THEN
Poof(CHARACTER_CYS_Arhu);
CharacterSetCanFight(CHARACTER_CYS_Arhu,1);

IF
CharacterEnteredCombat(CHARACTER_DAF_Cassandra,_)
AND
CharacterGetPosition(CHARACTER_DAF_Cassandra,_X,_Y,_Z)
THEN
CharacterTeleportToPosition(CHARACTER_DAF_Cassandra_Fight,_X,_Y,_Z);
Poof(CHARACTER_DAF_Cassandra);
CharacterSetOnStage(CHARACTER_DAF_Cassandra_Fight,1);
ItemToCharacter(ITEM_DAF_Arhu_Cage_Key,CHARACTER_DAF_Cassandra_Fight);

IF
DialogStartRequested(CHARACTER_DAF_Cassandra,_Player)
AND
ItemIsInPartyInventory(ITEM_DAF_Stasisfern,_Player,0,1)
THEN
GlobalSetEvent("DAF_HasStasisFern");

IF
GlobalEventSet("GLO_ArhuKilled")
AND
DB_GlobalEvent("ArhuInDAFCage")
THEN
ProcSetRelationToPlayers(CHARACTER_DAF_Cassandra, 0);

IF
DialogEnded("DAF_Cassandra",_)
THEN
GlobalClearEvent("DAF_HasStasisFern");

IF
DialogEnded("DAF_Cassandra",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
DB_GlobalEvent("DAF_GaveFernToCassandra")
AND
NOT OnlyOnce("DAF_CassandraSpellOnArhu")
THEN
OnlyOnce("DAF_CassandraSpellOnArhu");
CharacterSetHasDialog(CHARACTER_DAF_Cassandra,0);
CharacterSetHasDialog(CHARACTER_CYS_Arhu,0);
ItemToCharacter(ITEM_DAF_Soulforge_Repair_Ritual,_Player);
CharacterMoveToTrigger(CHARACTER_DAF_Cassandra,TRIGGER_DAF_Cassandra_NearCage,0,"CassandraNearCage");
ItemRemove(ITEM_DAF_Stasisfern);
GlobalClearEvent("ArhuRefusesToMorphToHuman");

IF
CharacterEvent(CHARACTER_DAF_Cassandra,"CassandraNearCage")
THEN
DialogStartTwoSpeakerDialog("DAF_AD_CassArhuStasisFern",CHARACTER_DAF_Cassandra,CHARACTER_CYS_Arhu);

IF
TextEventSet("fern")
THEN
ItemToCharacter(ITEM_DAF_Stasisfern,CHARACTER_Player1);

IF
AutomatedDialogEnded("DAF_AD_CassArhuStasisFern",_)
THEN
CharacterPlayEffect(CHARACTER_DAF_Cassandra,"FX_Skills_WitchCraft_UndeadHeal_Cast_A");
CharacterPlayAnimation(CHARACTER_DAF_Cassandra,"cast_long_start","DAF_CassAnim1");

IF
CharacterEvent(CHARACTER_DAF_Cassandra,"DAF_CassAnim1")
THEN
CharacterPlayAnimation(CHARACTER_DAF_Cassandra,"cast_long_loop","DAF_CassAnim2");

IF
CharacterEvent(CHARACTER_DAF_Cassandra,"DAF_CassAnim2")
THEN
CharacterPlayAnimation(CHARACTER_DAF_Cassandra,"cast_long_cast","DAF_CassAnim3");
CharacterPlayEffect(CHARACTER_CYS_Arhu,"FX_Skills_WitchCraft_UndeadHeal_Impact_A");
DB_Singleton("IsArhuCat",0);
GlobalSetEvent("ArhuPermaHuman");

IF
CharacterEvent(CHARACTER_DAF_Cassandra,"DAF_CassAnim3")
THEN
CharacterSetHasDialog(CHARACTER_DAF_Cassandra,1);
CharacterSetHasDialog(CHARACTER_CYS_Arhu,1);

IF
CharacterDied(CHARACTER_DAF_Cassandra_Fight)
THEN
QuestAdd("DAF_Cassandra");
QuestUpdate("DAF_Cassandra","FinishCassandra");
QuestClose("DAF_Cassandra");
GlobalSetEvent("DAF_CassandraDead");
GlobalClearEvent("ArhuRefusesToMorphToHuman");

IF
CharacterDied(CHARACTER_DAF_Cassandra_Fight)
AND
ItemIsInPartyInventory(ITEM_DAF_Stasisfern,CHARACTER_Player1,0,0)
AND
CharacterGetClosestAlivePlayer(CHARACTER_DAF_Cassandra_Fight,_Player)
AND
DB_GlobalEvent("DAF_CassandraWantsStasisFern")
THEN
Launch_PlayerComment(_Player,"DAF_Comment_stasisFern");

IF
ItemAddedToCharacter(ITEM_DAF_Soulforge_Repair_Ritual,_Player)
AND
_Player.DB_IsPlayer()
THEN
GlobalSetEvent("HOM_HasSoulForgeRepair");
QuestUpdate("HOM_Starstones", "OnDiscoveringTheSoulForgeSpell");

IF
ItemRemovedFromCharacter(ITEM_DAF_Soulforge_Repair_Ritual,_Player)
AND
_Player.DB_IsPlayer()
THEN
GlobalClearEvent("HOM_HasSoulForgeRepair");

IF
RegionStarted("DarkForest")
THEN
GlobalSetEvent("ArhuInDarkForest");

IF
RegionEnded("DarkForest")
THEN
GlobalClearEvent("ArhuInDarkForest");

IF
DialogStartRequested(CHARACTER_CYS_Arhu,_Player)
AND
ItemIsInPartyInventory(ITEM_DAF_Stasisfern,_Player,0,1)
THEN
GlobalSetEvent("DAF_HasStasisFern2");

IF
DialogCharacterEventSet("DAF_TakeStasisFern",_Player,_)
AND
ItemIsInPartyInventory(ITEM_DAF_Stasisfern,_Player,1,1)
THEN
NOOP(1);

IF
DialogEnded("DAF_Arhu_Caged",_)
THEN
GlobalClearEvent("DAF_HasStasisFern2");

IF
DialogEnded("CYS_Arhu",_)
THEN
GlobalClearEvent("DAF_HasStasisFern2");

IF
DialogEnded("CYS_Arhu",_)
AND
DB_GlobalEvent("DAF_GaveFernToArhu")
AND
NOT DB_OnlyOnce("ArhuPermaCat_Homestead")
THEN
DB_OnlyOnce("ArhuPermaCat_Homestead");
GlobalSetEvent("ArhuPermaCat");
ItemRemove(ITEM_DAF_Stasisfern);

IF
DialogEnded("DAF_Arhu_Caged",_)
AND
DB_GlobalEvent("DAF_GaveFernToArhu")
AND
NOT DB_Dead(CHARACTER_DAF_Cassandra)
THEN
DialogStartTwoSpeakerDialog("DAF_AD_ArhuPermaCat",CHARACTER_CYS_Arhu,CHARACTER_DAF_Cassandra);
GlobalSetEvent("ArhuPermaCat");
ItemRemove(ITEM_DAF_Stasisfern);

IF
AutomatedDialogEnded("DAF_AD_ArhuPermaCat",_)
THEN
ProcSetRelationToPlayers(CHARACTER_DAF_Cassandra, 0);

IF
GlobalEventSet("ArhuPermaCat")
THEN
CharacterPlayEffect(CHARACTER_CYS_Arhu,"FX_Skills_WitchCraft_UndeadHeal_Impact_A");
DB_Singleton("IsArhuCat",1);

IF
DialogEnded("DAF_ForestSpirit",_Instance)
AND
ItemIsInPartyInventory(ITEM_DAF_Stasisfern,CHARACTER_Player1,0,1)
AND
QueryOnlyOnce("DAF_PD_AfterGettingStasisFern")
AND
NOT DB_GlobalEvent("DAF_CassandraDead")
AND
DB_GlobalEvent("DAF_CassandraWantsStasisFern")
THEN
ProcDefinePartyDialog("DAF_PD_AfterGettingStasisFern");

//REGION Test functions
IF
TextEventSet("cas")
THEN
GlobalSetEvent("CassandraBodyBurnt");
CharacterRemoveStatus(CHARACTER_DAF_Cassandra,"VOID_AURA");
CharacterRemoveStatus(CHARACTER_DAF_Cassandra_Fight,"VOID_AURA");
QuestUpdate("DAF_Cassandra","CorpseBurnt");
CharacterMoveToTrigger(CHARACTER_DAF_Cassandra,TRIGGER_DAF_Cassandra_BeginFightPosition,1,"");
//END_REGION 

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
