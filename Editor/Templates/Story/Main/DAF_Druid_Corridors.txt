Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Druid_Bridge(ITEM_DFD_Druid_Bridgepiece1,TRIGGER_DFD_Druid_Bridge1,"ITEM_DFD_Druid_Bridgepiece1",1200);
DB_Druid_Bridge(ITEM_DFD_Druid_Bridgepiece2,TRIGGER_DFD_Druid_Bridge2,"ITEM_DFD_Druid_Bridgepiece2",100);
DB_Druid_Bridge(ITEM_DFD_Druid_Bridgepiece3,TRIGGER_DFD_Druid_Bridge3,"ITEM_DFD_Druid_Bridgepiece3",1500);
DB_Druid_Bridge(ITEM_DFD_Druid_Bridgepiece4,TRIGGER_DFD_Druid_Bridge4,"ITEM_DFD_Druid_Bridgepiece4",1500);
DB_Druid_Bridge(ITEM_DFD_Druid_Bridgepiece5,TRIGGER_DFD_Druid_Bridge5,"ITEM_DFD_Druid_Bridgepiece5",800);
DB_Druid_Bridge(ITEM_DFD_Druid_Bridgepiece6,TRIGGER_DFD_Druid_Bridge6,"ITEM_DFD_Druid_Bridgepiece6",1200);
ProcTriggerRegisterForPlayers(TRIGGER_DFD_DD_WolfAmbush);

DB_OneShotPlayerTrigger(TRIGGER_DFD_Druid_Start);
DB_Druid_Wolf(CHARACTER_DFD_Druid_Wolf1,TRIGGER_DFD_Druid_Wolf_Point1);
DB_Druid_Wolf(CHARACTER_DFD_Druid_Wolf2,TRIGGER_DFD_Druid_Wolf_Point2);
DB_Druid_Wolf(CHARACTER_DFD_Druid_Wolf3,TRIGGER_DFD_Druid_Wolf_Point3);
DB_Druid_Wolf(CHARACTER_DFD_Druid_Wolf4,TRIGGER_DFD_Druid_Wolf_Point4);

DB_BigTrader(CHARACTER_DFD_Druid_Alchemist);
DB_Dialogs(CHARACTER_DFD_Druid_Alchemist,"DFD_Druid_Alchemist");
KBSECTION
PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,TRIGGER_DFD_Druid_Start)
THEN
QuestUpdate("DAF_Cassandra","Temple");

PROC
Proc_DFD_Druid_OpenBridge()
AND
DB_Druid_Bridge(_Item,_Trigger,_TimerName,_TimerDuration)
THEN
TimerLaunch(_TimerName,_TimerDuration);

PROC
Proc_DFD_Druid_OpenBridge()
THEN
TimerLaunch("DFD_Druid_BridgeOpen",3000);

IF
TimerFinished("DFD_Druid_BridgeOpen")
THEN
ItemSetOnstage(ITEM_DFD_Druid_BridgeBlocker,0);

IF
TimerFinished(_TimerName)
AND
DB_Druid_Bridge(_Item,_Trigger,_TimerName,_TimerDuration)
THEN
ItemMoveToTrigger(_Item,_Trigger,0.5,0.2,1);

IF
DB_Druid_Wolf(_Character,_Trigger)
THEN
CharacterSetOnStage(_Character,0);
CharacterSetCanFight(_Character,0);

IF
CharacterUsedItem(_,ITEM_DFD_Druid_Wolfbridge_Skull)
AND
NOT DB_OnlyOnce("Druid_Intro_BlockRock")
THEN
DB_OnlyOnce("Druid_Intro_BlockRock");
Proc_DFD_Druid_OpenBridge();
ItemSetCanInteract(ITEM_DFD_Druid_Wolfbridge_Skull,0);

IF
CharacterEnteredTrigger(_Char, TRIGGER_DFD_DD_WolfAmbush)
AND
DB_Druid_Wolf(_Wolf,_Destination)
THEN
ProcTriggerUnregisterForPlayers(TRIGGER_DFD_DD_WolfAmbush);
CharacterSetCanFight(_Wolf,1);
CharacterAppear(CHARACTER_DFD_Druid_Wolf1,1);
ProcCharacterMoveToTrigger(CHARACTER_DFD_Druid_Wolf1,TRIGGER_DFD_Druid_Wolf_Point1,1,"");
TimerLaunch("DAF_DD_Druid_Wolf_Timer_01",400);

IF
TimerFinished("DAF_DD_Druid_Wolf_Timer_01")
THEN
CharacterAppear(CHARACTER_DFD_Druid_Wolf2,1);
ProcCharacterMoveToTrigger(CHARACTER_DFD_Druid_Wolf2,TRIGGER_DFD_Druid_Wolf_Point2,1,"");
TimerLaunch("DAF_DD_Druid_Wolf_Timer_02",200);

IF
TimerFinished("DAF_DD_Druid_Wolf_Timer_02")
THEN
CharacterAppear(CHARACTER_DFD_Druid_Wolf3,1);
ProcCharacterMoveToTrigger(CHARACTER_DFD_Druid_Wolf3,TRIGGER_DFD_Druid_Wolf_Point3,1,"");
TimerLaunch("DAF_DD_Druid_Wolf_Timer_03",700);

IF
TimerFinished("DAF_DD_Druid_Wolf_Timer_03")
THEN
CharacterAppear(CHARACTER_DFD_Druid_Wolf4,1);
ProcCharacterMoveToTrigger(CHARACTER_DFD_Druid_Wolf4,TRIGGER_DFD_Druid_Wolf_Point4,1,"");

IF
CharacterAttackedByCharacter(_Wolf,_)
AND
DB_Druid_Wolf(_Wolf,_)
THEN
CharacterPurgeQueue(_Wolf);
CharacterSetCanFight(_Wolf,1);

IF
RegionStarted("DAF_Druid_Dungeon")
AND
NOT DB_OnlyOnce("DruidDrawBridge_Close")
THEN
DB_OnlyOnce("DruidDrawBridge_Close");
ItemPlayAnimation(ITEM_DAF_Druid_Drawbridge,"close");

IF
CharacterUsedItem(_,ITEM_DAF_Druid_Drawbridge_Skull)
AND
NOT DB_OnlyOnce("DruidDrawBridge_Open")
THEN
DB_OnlyOnce("DruidDrawBridge_Open");
ItemPlayAnimation(ITEM_DAF_Druid_Drawbridge,"open");
ItemSetOnstage(ITEM_DAF_Druid_Drawbridge_Blocker,0);
ItemSetCanInteract(ITEM_DAF_Druid_Drawbridge_Skull,0);
QuestUpdate("DAF_Cassandra","Bridges");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
