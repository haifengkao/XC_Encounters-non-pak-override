Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LUC_M_Priests(CHARACTER_LUC_M_Totem_Priest1);
DB_LUC_M_Priests(CHARACTER_LUC_M_Totem_Priest2);
DB_LUC_M_Priests(CHARACTER_LUC_M_Totem_Priest3);
DB_LUC_M_Priests(CHARACTER_LUC_M_Totem_Ranger1);
DB_LUC_M_Priests(CHARACTER_LUC_M_Totem_Ranger2);
CharacterSetEvent(CHARACTER_LUC_M_Totem_Priest2,"StartWander");
CharacterSetEvent(CHARACTER_LUC_M_Totem_Priest3,"StartWander");
DB_OneShot_DialogTrigger_NewSystem(TRIGGER_LUC_M_ControlCultist_Fight,"LUC_M_Priests",CHARACTER_LUC_M_Totem_Priest1);
DB_Dialogs(CHARACTER_LUC_M_Totem_Priest1,"LUC_M_Priests");
DB_Dialogs(CHARACTER_LUC_M_Totem_Priest2,"LUC_M_Priests_Minor");
DB_Dialogs(CHARACTER_LUC_M_Totem_Ranger1,"LUC_M_Priests_Minor");
DB_Dialogs(CHARACTER_LUC_M_Totem_Ranger2,"LUC_M_Priests_Minor");
DB_Dialogs(CHARACTER_LUC_M_Totem_Priest3,"LUC_M_Priests_Minor");


ProcSaveBoneTotemPos();

DB_PlayerComments("LUC_M_CommentTotemDestroyed","LUC_M_CommentTotemDestroyed_1_1",1,1);
DB_PlayerComments("LUC_M_CommentTotemDestroyed","LUC_M_CommentTotemDestroyed_2_1",2,1);
DB_PlayerComments("LUC_M_CommentTotemDestroyed","LUC_M_CommentTotemDestroyed_2_2",2,2);

DB_PlayerComment_Trigger(TRIGGER_LUC_M_FirstSkeleton1, "LUC_M_CommentNoticeUndeadMiner");
DB_PlayerComment_Trigger(TRIGGER_LUC_M_FirstSkeleton2, "LUC_M_CommentNoticeUndeadMiner");
ProcTriggerRegisterForPlayers(TRIGGER_LUC_M_FirstSkeleton1);
ProcTriggerRegisterForPlayers(TRIGGER_LUC_M_FirstSkeleton2);
DB_PlayerComments("LUC_M_CommentNoticeUndeadMiner","LUC_M_CommentNoticeUndeadMiner_1_1",1,1);
DB_PlayerComments("LUC_M_CommentNoticeUndeadMiner","LUC_M_CommentNoticeUndeadMiner_2_1",2,1);
DB_PlayerComments("LUC_M_CommentNoticeUndeadMiner","LUC_M_CommentNoticeUndeadMiner_2_2",2,2);

SetRelationFactionToPlayers("LUC_M_ControllingCultists",100);

DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner1,TRIGGER_LUC_M_MeetingSpot_01);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner2,TRIGGER_LUC_M_MeetingSpot_02);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner3,TRIGGER_LUC_M_MeetingSpot_03);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner4,TRIGGER_LUC_M_MeetingSpot_04);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner5,TRIGGER_LUC_M_MeetingSpot_05);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner6,TRIGGER_LUC_M_MeetingSpot_06);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner7,TRIGGER_LUC_M_MeetingSpot_07);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner8,TRIGGER_LUC_M_MeetingSpot_08);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner9,TRIGGER_LUC_M_MeetingSpot_09);
DB_MeetingSkelly(CHARACTER_LUC_GeneralMiner10,TRIGGER_LUC_M_MeetingSpot_10);
DB_MeetingSkelly(CHARACTER_LUC_FuseMiner01,TRIGGER_LUC_M_MeetingSpot_11);
DB_MeetingSkelly(CHARACTER_LUC_FuseMiner02,TRIGGER_LUC_M_MeetingSpot_12);
DB_MeetingSkelly(CHARACTER_LUC_FuseMiner03,TRIGGER_LUC_M_MeetingSpot_13);
DB_MeetingSkelly(CHARACTER_LUC_MinerPlatform,TRIGGER_LUC_M_MeetingSpot_14);

ItemSetOnstage(ITEM_LUC_M_TempleDoorPassword,0);
ItemToCharacter(ITEM_LUC_M_PriestDiary,CHARACTER_LUC_M_Totem_Priest1);

DB_BigTrader(CHARACTER_LUC_M_Totem_Priest1);
DB_BigTrader(CHARACTER_LUC_M_Worker1);
DB_BigTrader(CHARACTER_LUC_M_Prisonguard1);
KBSECTION
// Hiding Priests
IF
DB_LUC_M_Priests(_Char)
THEN
DB_IsNotMessingAround(_Char);
SneakTriggerSpotter(TRIGGER_LUC_M_ControlCultist_Fight,_Char);

IF
CharacterEnteredTrigger(_Character,TRIGGER_LUC_M_FirstSkeleton1)
THEN
QuestAdd("LUC_M_CaveIn");
QuestUpdate("LUC_M_CaveIn","Start");
QuestAdd("LUC_M_RogueDK");
QuestUpdate("LUC_M_RogueDK","StartNoMike");
Proc_MadoraCommentMineSkeletons(_Character);

IF
CharacterEnteredTrigger(_Character,TRIGGER_LUC_M_FirstSkeleton2)
THEN
QuestAdd("LUC_M_CaveIn");
QuestUpdate("LUC_M_CaveIn","Start");
QuestAdd("LUC_M_RogueDK");
QuestUpdate("LUC_M_RogueDK","StartNoMike");
Proc_MadoraCommentMineSkeletons(_Character);

PROC
Proc_MadoraCommentMineSkeletons(CHARACTER_Madoc)
AND
DB_GlobalEvent("MadoraTalkedToMike")
THEN
Proc_CompanionComment(CHARACTER_Madoc,"MAD_CC_MinesSkeletons",CHARACTER_Madoc);

IF
DialogStarted("LUC_M_Priests",_)
THEN
QuestUpdate("LUC_M_CaveIn","FoundPriests");

IF
DialogEnded("LUC_M_Priests",_)
AND
DB_GlobalEvent("LUC_M_PriestsAttack")
THEN
Proc_LUC_M_PriestsGoAggressive();

IF
DialogEnded("LUC_M_Priests",_)
AND
NOT DB_GlobalEvent("LUC_M_PriestsAttack")
AND
DB_LUC_M_Priests(_Char)
THEN
CharacterSetEvent(_Char,"StartWander");

PROC
Proc_LUC_M_PriestsGoAggressive()
AND
DB_LUC_M_Priests(_Char)
THEN
ProcSetRelationToPlayers(_Char,0);

IF
CharacterUsedItem(_Character,ITEM_LUC_M_Bone_Totem)
THEN
CharacterDisplayText(_Character,"LUC_M_TouchBoneTotem1");

IF
CharacterDisplayTextEnded(_Character,"LUC_M_TouchBoneTotem1")
THEN
CharacterDisplayText(CHARACTER_LUC_M_Totem_Priest1,"LUC_M_TouchBoneTotem2");

IF
ItemAttackedByCharacter(ITEM_LUC_M_Bone_Totem,_,_)
AND
NOT DB_OnlyOnce("LUC_M_TouchBoneTotem3")
THEN
DB_OnlyOnce("LUC_M_TouchBoneTotem3");
CharacterDisplayText(CHARACTER_LUC_M_Totem_Priest1,"LUC_M_TouchBoneTotem3");
Proc_LUC_M_PriestsGoAggressive();

IF
ItemDestroyed(ITEM_LUC_M_Bone_Totem)
AND
NOT DB_OnlyOnce("LUC_M_TouchBoneTotem3")
THEN
DB_OnlyOnce("LUC_M_TouchBoneTotem3");
CharacterDisplayText(CHARACTER_LUC_M_Totem_Priest1,"LUC_M_TouchBoneTotem3");
Proc_LUC_M_PriestsGoAggressive();

PROC
ProcSaveBoneTotemPos()
AND
ItemGetPosition(ITEM_LUC_M_Bone_Totem,_X,_Y,_Z)
THEN
DB_LUC_M_BoneTotemPos(_X,_Y,_Z);

IF
ItemDestroyed(ITEM_LUC_M_Bone_Totem)
AND
DB_LUC_M_BoneTotemPos(_X,_Y,_Z)
AND
GetClosestPlayerToPosition(_X,_Y,_Z,_Player)
THEN
PROC_SkellyGoToMeeting();
GlobalSetEvent("LUC_M_Control_Ended");
QuestUpdate("LUC_M_CaveIn","DestroyedTotem");
Launch_PlayerComment(_Player,"LUC_M_CommentTotemDestroyed");

// Existencial Skeleton Gathering
PROC
PROC_SkellyGoToMeeting()
AND
DB_MeetingSkelly(_Character,_Trigger)
AND
CharacterGetEquippedWeapon(_Character,_ItemHandle)
THEN
ItemHandleDelete(_ItemHandle);
ProcCharacterMoveToTrigger(_Character,_Trigger,1,"LUC_M_WentToMeetingSpot");
CharacterSetEvent(_Character,"GoToMeetingSpot");
CharacterSetHasDialog(_Character,0);
NOT DB_NoCharacterCompanionReplace(_Character);

IF
CharacterEvent(_Character,"LUC_M_WentToMeetingSpot")
AND
DB_MeetingSkelly(_Character,_Trigger)
THEN
CharacterLookFromTrigger(_Character,_Trigger,0);

IF
DialogEnded("LUC_GeneralMinerUnderControl",_ID)
AND
DB_GlobalEvent("LUC_M_Control_Ended")
THEN
QuestUpdate("LUC_M_CaveIn","SkeletonsFreed");
QuestClose("LUC_M_CaveIn");

IF
DialogEnded("LUC_GeneralMinerUnderControl",_ID)
AND
DB_GlobalEvent("LUC_M_PasswordKnown")
AND
NOT DB_OnlyOnce("LUC_DD_SkeletonsFreed")
THEN
DB_OnlyOnce("LUC_DD_SkeletonsFreed");
ProcDefinePartyDialog("LUC_DD_SkeletonsFreed");

IF
DialogEnded("LUC_GeneralMinerUnderControl",_ID)
AND
DB_GlobalEvent("LUC_M_PasswordKnown")
AND
DB_MeetingSkelly(_Character,_)
AND
Random(3000,_Rand)
AND
CharacterGetUUID(_Character,_CharUUID)
AND
StringConcatenate("LUC_M_CaveIn_DieTimer",_CharUUID,_TimerName)
THEN
TimerLaunch(_TimerName,_Rand);
DB_LUC_M_TimerNameToCharacter(_TimerName,_Character);

IF
TimerFinished(_TimerName)
AND
DB_LUC_M_TimerNameToCharacter(_TimerName,_Character)
THEN
DialogRequestStopForCharacter(_Character);
CharacterDie(_Character,0);

// BACKUP PLAN: IF ALL MEETING SKELLIES DIES: 

IF
CharacterDied(_Character)
AND
DB_MeetingSkelly(_Character,_Trigger)
THEN
NOT DB_MeetingSkelly(_Character,_Trigger);
PROC_CheckMeetingSkellyDeath();

PROC
PROC_CheckMeetingSkellyDeath()
AND
NOT DB_MeetingSkelly(_,_)
AND
NOT DB_GlobalEvent("LUC_M_PasswordKnown")
THEN
ItemSetOnstage(ITEM_LUC_M_TempleDoorPassword,1);
QuestUpdate("LUC_M_CaveIn", "SkeletonsDead");
QuestClose("LUC_M_CaveIn");

IF
CharacterUsedItem(_Character,ITEM_LUC_M_TempleDoorPassword)
THEN
GlobalSetEvent("LUC_M_PasswordKnown");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
