Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Barley(ITEM_DAF_Barley1);
DB_Barley(ITEM_DAF_Barley2);
DB_Barley(ITEM_DAF_Barley3);
DB_Barley(ITEM_DAF_Barley4);
DB_Barley(ITEM_DAF_Barley5);
DB_Barley(ITEM_DAF_Barley6);
DB_Barley(ITEM_DAF_Barley7);
DB_Barley(ITEM_DAF_Barley8);

DB_PlayerComments("DAF_Comment_SpiritCreated","DAF_Comment_SpiritCreated_1_1",1,1);
DB_PlayerComments("DAF_Comment_SpiritCreated","DAF_Comment_SpiritCreated_1_2",1,2);
DB_PlayerComments("DAF_Comment_SpiritCreated","DAF_Comment_SpiritCreated_2_1",2,1);
DB_PlayerComments("DAF_Comment_SpiritCreated","DAF_Comment_SpiritCreated_2_2",2,2);
KBSECTION
IF
CharacterUsedItem(_Character,_Item)
AND
DB_Barley(_Item)
AND
DB_OnlyOnce("BarleyPicked")
THEN
CharacterDisplayText(_Character,"DAF_BarleyAlreadyPicked");

IF
CharacterUsedItem(_Character,_Item)
AND
DB_Barley(_Item)
AND
NOT DB_OnlyOnce("BarleyPicked")
AND
ItemGetPosition(_Item,_X,_Y,_Z)
AND
CreateItemTemplateAtPosition("Quest_Ingred_Barley_A_e9efa068-b4f9-4322-bed5-88a5b449cb29",_X,_Y,_Z,_Handle)
THEN
DB_OnlyOnce("BarleyPicked");
CharacterPlayAnimation(_Character,"use_loot","");
ItemHandleToCharacter(_Handle,_Character,-1);

IF
GameBookInterfaceClosed(ITEM_DAF_Whiskybook,_)
THEN
QuestAdd("DAF_DrunkOrcs");
QuestUpdate("DAF_DrunkOrcs","Book");

IF
ItemTemplateAddedToCharacter("Quest_Bar_Barrel_A_Spirit_A_767f05d3-c512-4521-a840-3765f95688d0",_,_Character)
AND
_Character.DB_IsPlayer()
AND
NOT DB_GlobalEvent("DAF_SpiritCreated")
THEN
Launch_PlayerComment(_Character,"DAF_Comment_SpiritCreated");
GlobalSetEvent("DAF_SpiritCreated");
QuestUpdate("DAF_DrunkOrcs","Young");

IF
DialogStartRequested(CHARACTER_CYS_Zixzax,_Player)
AND
ItemTemplateIsInPartyInventory(_Player,"Quest_Bar_Barrel_A_Spirit_A_767f05d3-c512-4521-a840-3765f95688d0",0,_Count)
AND
_Count > 0
THEN
GlobalSetEvent("DAF_HasSpirit");
DB_CharHasSpirit(_Player);

IF
DialogEnded("HOM_ZixZax",_)
AND
DB_CharHasSpirit(_Player)
THEN
GlobalClearEvent("DAF_HasSpirit");
NOT DB_CharHasSpirit(_Player);

IF
GlobalEventSet("DAF_AgeWhisky")
AND
DB_CharHasSpirit(_Player)
AND
ItemTemplateIsInPartyInventory(_Player,"Quest_Bar_Barrel_A_Spirit_A_767f05d3-c512-4521-a840-3765f95688d0",1,_Count)
AND
_Count > 0
THEN
ItemTemplateRemoveFromCharacter("Quest_Bar_Barrel_A_Spirit_A_767f05d3-c512-4521-a840-3765f95688d0",_Player,1);
GlobalClearEvent("DAF_AgeWhisky");
GlobalClearEvent("DAF_HasSpirit");
Poof(CHARACTER_CYS_Zixzax);

IF
GlobalEventSet("DAF_AgeWhisky2")
AND
DB_CharHasSpirit(_Player)
THEN
ItemTemplateAddToCharacter("Quest_Bar_Barrel_A_Whisky_A_3f64bc7e-aaf0-4f7c-9b86-7d0ba4843cfd",_Player,1);
GlobalClearEvent("DAF_AgeWhisky2");
Foop(CHARACTER_CYS_Zixzax);
CharacterApplyStatus(CHARACTER_CYS_Zixzax,"DRUNK",30.0,1);
QuestUpdate("DAF_DrunkOrcs","Old");

IF
DialogStartRequested(CHARACTER_DAF_Hershel,_Player)
AND
ItemTemplateIsInPartyInventory(_Player,"Quest_Bar_Barrel_A_Whisky_A_3f64bc7e-aaf0-4f7c-9b86-7d0ba4843cfd",0,_Count)
AND
_Count > 0
THEN
GlobalSetEvent("DAF_HasWhisky");
DB_CharHasWhisky(_Player);

IF
DialogEnded("DAF_Hershel",_)
AND
DB_CharHasWhisky(_Player)
THEN
GlobalClearEvent("DAF_HasWhisky");
NOT DB_CharHasWhisky(_Player);

IF
GlobalEventSet("DAF_TakeWhisky")
AND
DB_CharHasWhisky(_Player)
AND
ItemTemplateIsInPartyInventory(_Player,"Quest_Bar_Barrel_A_Whisky_A_3f64bc7e-aaf0-4f7c-9b86-7d0ba4843cfd",1,_Count)
AND
_Count > 0
THEN
ItemTemplateRemoveFromCharacter("Quest_Bar_Barrel_A_Whisky_A_3f64bc7e-aaf0-4f7c-9b86-7d0ba4843cfd",_Player,1);
GlobalSetEvent("DAF_OrcsDrunk");

IF
DialogEnded("DAF_Hershel",_)
AND
DB_GlobalEvent("DAF_OrcsDrunk")
THEN
CharacterDisplayText(CHARACTER_DAF_Hershel,"DAF_HershelGoodWhisky");
CharacterApplyStatus(CHARACTER_DAF_BO_TavernOrc1,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_BO_TavernOrc2,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_BO_TavernOrc3,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_BO_TavernOrc4,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_Grutilda,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_BO_ScoutOrc1,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_BO_ScoutOrc2,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Grutilda,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Male1,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Male2,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Male3,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Male4,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Male5,"DRUNK",-1.0,1);
CharacterApplyStatus(CHARACTER_DAF_DreadOrc_Male6,"DRUNK",-1.0,1);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
