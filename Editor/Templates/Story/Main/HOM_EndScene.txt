Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ImpPlayers(CHARACTER_HOM_EndscenePlayer1);
DB_ImpPlayers(CHARACTER_HOM_EndscenePlayer2);

CharacterSetOnStage(CHARACTER_HOM_EndscenePlayer1,0);
CharacterSetOnStage(CHARACTER_HOM_EndscenePlayer2,0);

DB_PlayerEndsceneLink(CHARACTER_Player1,CHARACTER_HOM_EndscenePlayer1);
DB_PlayerEndsceneLink(CHARACTER_Player2,CHARACTER_HOM_EndscenePlayer2);
KBSECTION
IF
DialogEnded("SAC_PD_SourceAcademy_EndGame",_)
THEN
CharacterFreeze(CHARACTER_Player1);
CharacterFreeze(CHARACTER_Player2);
FadeToBlack(CHARACTER_Player1,2.0,0);
FadeToBlack(CHARACTER_Player2,2.0,0);
TimerLaunch("SAC_SwapPlayers",2500);

IF
TimerFinished("SAC_SwapPlayers")
THEN
ProcSavePeerInfo();
ProcCreateImpPlayers();

PROC
ProcSavePeerInfo()
AND
CharacterGetReservedPeerID(CHARACTER_Player1,_Peer1)
AND
CharacterGetReservedPeerID(CHARACTER_Player2,_Peer2)
THEN
DB_PeerToAssign(_Peer1);
DB_PeerToAssign(_Peer2);

PROC
ProcEmptyParty()
AND
_Player.DB_IsPlayer()
AND
NOT DB_ImpPlayers(_Player)
THEN
NOT DB_Companion(_Player);
NOT _Player.DB_IsPlayer();
CharacterDetachFromGroup(_Player);
CharacterMakeNPC(_Player);
CharacterRemoveFromParty(_Player);
CharacterSetOnStage(_Player,0);

PROC
ProcCreateImpPlayers()
AND
DB_ImpPlayers(_Player)
THEN
CharacterSetOnStage(_Player,1);
CharacterMakePlayer(_Player);
CharacterFreeze(_Player);
CharacterDetachFromGroup(_Player);

IF
CharacterIsPlayer(_Char)
AND
DB_ImpPlayers(_Char)
THEN
ProcAssignPeer();
NOT DB_ImpPlayers(_Char);

PROC
ProcAssignPeer()
AND
DB_PeerToAssign(_PeerID)
THEN
NOT DB_PeerToAssign(_PeerID);
CharacterAssign(_PeerID);

IF
DB_PeerToAssign(_Peer)
THEN
DB_PeerToAssignCheck(_Peer);

IF
DB_PeerToAssignCheck(_Peer)
AND
NOT DB_PeerToAssign(_Peer)
THEN
NOT DB_PeerToAssignCheck(_Peer);
ProcCheckIfPeerListEmpty();

PROC
ProcCheckIfPeerListEmpty()
AND
NOT DB_PeerToAssign(_)
THEN
DB_AllPeersAssigned(1);

IF
DB_AllPeersAssigned(1)
THEN
SetTransferItemsOnPartyRemove(0);
ProcEmptyParty();
ProcSetEndgamePlayerFlags();

IF
RegionStarted("Homestead")
AND
DB_HOM_LastScene(1)
THEN
CharacterTeleportToTrigger(CHARACTER_HOM_EndscenePlayer1,TRIGGER_HOM_Point_ImpHeroEndscene1,"");
CharacterTeleportToTrigger(CHARACTER_HOM_EndscenePlayer2,TRIGGER_HOM_Point_ImpHeroEndscene2,"");
TimerLaunch("HOMStartEndScene",1500);

IF
TimerFinished("HOMStartEndScene")
THEN
PROC_MendatoryDialogThreeSpeaker("HOM_Zixzax_EndDialog",CHARACTER_HOM_EndSceneZixZax,CHARACTER_HOM_EndscenePlayer1,CHARACTER_HOM_EndscenePlayer2);

IF
DialogEnded("HOM_Zixzax_EndDialog",_)
THEN
NotifyGameProgress(100.0);
TriggerSetAtmosphere("901ee098-e616-48f2-9875-aa651979dc2e","Under_Attack");
CharacterPlayEffect(CHARACTER_CYS_Zixzax,"FX_GP_CameraShake_A");
GlobalSetEvent("HOM_EndGame_Panic");
FadeToBlack(CHARACTER_HOM_EndscenePlayer1,4.0,0);
FadeToBlack(CHARACTER_HOM_EndscenePlayer2,4.0,0);
TimerLaunch("ShowCredits",4100);

IF
TimerFinished("ShowCredits")
THEN
FadeToBlack(CHARACTER_HOM_EndscenePlayer1,0.2,1);
FadeToBlack(CHARACTER_HOM_EndscenePlayer2,0.2,1);
ProcShowEndCredits();

PROC
ProcShowEndCredits()
THEN
UnlockAchievement("quest51");
GameEnd();

//REGION End dialog flags

PROC
ProcSetImpAffectionCount((CHARACTER)_Imp,(STRING)_Type,(INTEGER)_)
AND
DB_AffectionTypeCounts(_Type,_Imp,_count)
THEN
NOT DB_AffectionTypeCounts(_Type,_Imp,_count);

PROC
ProcSetImpAffectionCount((CHARACTER)_Imp,(STRING)_Type,(INTEGER)_Count)
THEN
DB_AffectionTypeCounts(_Type,_Imp,_count);

IF
DB_AffectionTypeCounts(_Type,_Player,_Count)
AND
DB_PlayerEndsceneLink(_Player,_Imp)
THEN
ProcSetImpAffectionCount(_Imp,_Type,_Count);

PROC 
ProcSetEndgamePlayerFlags()
AND
DB_PlayerEndsceneLink(_,_Imp)
THEN
ProcCalculateDominantAffectionType(_Imp);
ProcCalculateAffinity();

PROC 
ProcSetEndgamePlayerFlags()
THEN
DB_HOM_LastScene(1);
CharacterTeleportPartyToTrigger(CHARACTER_HOM_EndscenePlayer1,TRIGGER_HOM_Point_TP_EndSceneImps,"");

IF
TextEventSet("credits")
THEN
GameEnd();

IF
TextEventSet("testcredits")
THEN
CharacterTeleportPartyToTrigger(TRIGGER_SAC_Behavior_Jahan,"OE_Credits");

IF
CharacterEvent(_,"OE_Credits")
THEN
CharacterFreeze(CHARACTER_Player1);
CharacterFreeze(CHARACTER_Player2);
FadeToBlack(CHARACTER_Player1,2.0,0);
FadeToBlack(CHARACTER_Player2,2.0,0);
TimerLaunch("SAC_SwapPlayers",2500);


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
