Version 1
SubGoalCombiner SGC_AND
INITSECTION
ItemSetCanInteract(ITEM_HIB_TreasureRoom_ScriptureChest,0);
DB_ItemDialogs(ITEM_HIB_TreasureRoom_Scripture,"HIB_TreasureRoom_Scripture");
DB_NoCompanionReplace(ITEM_HIB_TreasureRoom_Scripture);
ItemToContainer(ITEM_HIB_TreasureRoom_Amulet,ITEM_HIB_TreasureRoom_AmuletChest,-1);
ProcTriggerRegisterForPlayers(TRIGGER_HIB_Area_TreasureRoom_Talisman);

DB_PlayerComments("HIB_CommentOnWinterRing","HIB_CommentOnWinterRing_1_1",1,1);
DB_PlayerComments("HIB_CommentOnWinterRing","HIB_CommentOnWinterRing_2_1",2,1);
DB_PlayerComments("HIB_CommentOnWinterRing","HIB_CommentOnWinterRing_2_2",2,2);
KBSECTION
/*
IF
CharacterUsedItem(_,ITEM_HIB_TreasureRoom_AmuletChest)
AND
ItemIsLocked(ITEM_HIB_TreasureRoom_AmuletChest,1)
THEN
GlobalSetEvent("HIB_KnowsAmuletChest");
*/

IF
DialogEnded("HIB_TreasureRoom_Scripture",_)
AND
ItemGetVarInteger(ITEM_HIB_TreasureRoom_Scripture,"HIB_TreasureRoom_Scripture_Opens",1)
THEN
ItemSetCanInteract(ITEM_HIB_TreasureRoom_Scripture,0);
ItemMoveToTrigger(ITEM_HIB_TreasureRoom_Scripture,TRIGGER_HIB_Point_TreasureRoom_Scripture,2.0,1.0,0);
ItemSetCanInteract(ITEM_HIB_TreasureRoom_ScriptureChest,1);
ItemUnLock(ITEM_HIB_TreasureRoom_ScriptureChest);

IF
ItemEquipped(ITEM_HIB_TreasureRoom_Amulet,_Player)
THEN
_Player.HIB_WearingTalisman();
PROC_HIB_TalismanArea_Check(_Player);

IF
ItemUnEquipped(ITEM_HIB_TreasureRoom_Amulet,_Player)
THEN
NOT _Player.HIB_WearingTalisman();
CharTimerCancel(_Player,"HIB_TreasureRoom_Talisman");

PROC
PROC_HIB_TalismanArea_Check((CHARACTER)_Player)
AND
DB_InRegion(_Player,TRIGGER_HIB_Area_TreasureRoom_Talisman)
THEN
CharTimer(_Player,"HIB_TreasureRoom_Talisman",10000);

IF
CharacterEnteredTrigger(_Player,TRIGGER_HIB_Area_TreasureRoom_Talisman)
AND
_Player.HIB_WearingTalisman()
THEN
CharTimer(_Player,"HIB_TreasureRoom_Talisman",10000);

IF
CharacterLeftTrigger(_Player,TRIGGER_HIB_Area_TreasureRoom_Talisman)
THEN
CharTimerCancel(_Player,"HIB_TreasureRoom_Talisman");

IF
CharTimerFinished(_Player,"HIB_TreasureRoom_Talisman")
AND
CharacterPlayLoopEffect(CHARACTER_HIB_Sentinel_TreasureRoom1,"FX_GP_Light_Green_A","",_)
AND
CharacterPlayLoopEffect(CHARACTER_HIB_Sentinel_TreasureRoom2,"FX_GP_Light_Green_A","",_)
AND
CharacterPlayLoopEffect(CHARACTER_HIB_Sentinel_TreasureRoom3,"FX_GP_Light_Green_A","",_)
AND
CharacterPlayLoopEffect(CHARACTER_HIB_Sentinel_TreasureRoom4,"FX_GP_Light_Green_A","",_)
THEN
CharacterLookAtItem(CHARACTER_HIB_Sentinel_TreasureRoom1,ITEM_HIB_TreasureRoom_TalismanGrating,0);
CharacterLookAtItem(CHARACTER_HIB_Sentinel_TreasureRoom2,ITEM_HIB_TreasureRoom_TalismanGrating,0);
CharacterLookAtItem(CHARACTER_HIB_Sentinel_TreasureRoom3,ITEM_HIB_TreasureRoom_TalismanGrating,0);
CharacterLookAtItem(CHARACTER_HIB_Sentinel_TreasureRoom4,ITEM_HIB_TreasureRoom_TalismanGrating,0);
PlayEffectAtCharacter("FX_GP_Regenerate",_Player);
ItemRemove(ITEM_HIB_TreasureRoom_Amulet);
ItemToCharacter(ITEM_HIB_WinterRing,_Player);
PartyAddActualExperience(5000);
CharTimer(_Player,"HIB_TreasureRoom_Talisman2",1000);

IF
CharTimerFinished(_Player,"HIB_TreasureRoom_Talisman2")
THEN
Launch_PlayerComment(_Player,"HIB_CommentOnWinterRing");

IF
TextEventSet("tali")
THEN
ItemToCharacter(ITEM_HIB_TreasureRoom_Amulet,CHARACTER_Player2);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
