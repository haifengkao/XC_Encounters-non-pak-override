Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TeleporterPyramid(ITEM_TeleportPyramidA);
DB_TeleporterPyramid(ITEM_TeleportPyramidB);

DB_OtherPyramid(ITEM_TeleportPyramidA,ITEM_TeleportPyramidB);
DB_OtherPyramid(ITEM_TeleportPyramidB,ITEM_TeleportPyramidA);

DB_RegionBlocksTeleporting("LUC_Mines");

DB_PlayerComments("CYS_CommentPyramidWrongRegion","CYS_CommentPyramidWrongRegion_1_1",1,1);
DB_PlayerComments("CYS_CommentPyramidWrongRegion","CYS_CommentPyramidWrongRegion_2_1",2,1);
DB_PlayerComments("CYS_CommentPyramidWrongRegion","CYS_CommentPyramidWrongRegion_2_2",2,2);
KBSECTION
IF
CharacterUsedItem(_Character,_FirstPyramid)
AND
NOT DB_BlockTeleporterUse(1)
AND
NOT TeleportingRegionBlock(1)
AND
DB_OtherPyramid(_FirstPyramid,_SecondPyramid)
AND
DB_TeleporterPyramidUnlocked(_SecondPyramid)
AND
NOT DB_PyramidInForbiddenRegion(_SecondPyramid)
THEN
Proc_CompanionDialog(CHARACTER_Jahan,"JAH_CD_Pyramid",_Character);

IF
CharacterUsedItem(_Character,_FirstPyramid)
AND
TeleportingRegionBlock(1)
AND
NOT DB_OnlyOnce("LUC_M_DD_TeleportersBroken")
AND
DB_OtherPyramid(_FirstPyramid,_)
AND
CharacterGetRegion(_Character,"LUC_Mines")
THEN
ProcDefinePartyDialog("LUC_M_DD_TeleportersBroken");
DB_OnlyOnce("LUC_M_DD_TeleportersBroken");

IF
CharacterUsedItem(_Character,ITEM_TeleportPyramidA)
AND
NOT DB_TeleporterPyramidUnlocked(ITEM_TeleportPyramidB)
AND
NOT DB_CurrentLevel("Cyseal")
THEN
Launch_PlayerComment(_Character,"CYS_CommentPyramidWrongRegion");

IF
CharacterUsedItem(_Character,ITEM_TeleportPyramidA)
AND
NOT DB_TeleporterPyramidUnlocked(ITEM_TeleportPyramidB)
AND
DB_CurrentLevel("Cyseal")
AND
ItemGetPosition(ITEM_TeleportPyramidB,_X,_Y,_Z)
AND
CharacterGetPosition(_Character,_X2,_Y2,_Z2)
THEN
CharacterTeleportToPosition(_Character,_X,_Y,_Z,"");
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X,_Y,_Z);
PlayEffectAtPosition("FX_GP_TeleportSmoke_A",_X2,_Y2,_Z2);

IF
ItemAddedToCharacter(_Pyramid,_Player)
AND
DB_Illusionist((CHARACTER)_Player)
AND
DB_TeleporterPyramid(_Pyramid)
THEN
PROC_TeleportingRegionBlockOn();
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
