Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTER_HOM_Shrike,"HOM_Shrike1");
CharacterSetOnStage(CHARACTER_HOM_Shrike,0);
DB_OneShotPlayerOnlyTrigger(TRIGGER_HOM_MeetShrike);

DB_RoomDialogInfo(ITEM_HOM_TeleportToFireRoom,TRIGGER_HOM_BoxAstarteFireAD,CHARACTER_HOM_Fire_Skill_Trainer,TRIGGER_HOM_Point_FireAstarte);
DB_RoomDialogInfo(ITEM_HOM_TeleportToIceRoom,TRIGGER_HOM_BoxShirkeIceAD,CHARACTER_HOM_Ice_Skill_Trainer,TRIGGER_HOM_Point_IceShrike);
DB_RoomDialogInfo(ITEM_HOM_TeleportToTellerOfSecrets,TRIGGER_HOM_MeetAstarte3,CHARACTER_NULL,TRIGGER_HOM_Point_TellerOfSecretsAstarte);
DB_RoomDialogInfo(ITEM_HOM_TeleportToMephisto,TRIGGER_HOM_MeetShrike3,CHARACTER_NULL,TRIGGER_HOM_Point_MephistoShrike);
DB_RoomDialogInfo(ITEM_HOM_TeleportToArmoury,TRIGGER_HOM_MeetAstarte4,CHARACTER_NULL,TRIGGER_HOM_Point_ArmouryAstarte);
DB_RoomDialogInfo(ITEM_HOM_TeleportToTrader,TRIGGER_HOM_MeetShrike4,CHARACTER_NULL,TRIGGER_HOM_Point_TraderShrike);
DB_RoomDialogInfo(ITEM_HOM_TeleportToChapel,TRIGGER_HOM_MeetAstarte5,CHARACTER_NULL,TRIGGER_HOM_Point_ChapelAstarte);
DB_RoomDialogInfo(ITEM_HOM_TeleportToEarth,TRIGGER_HOM_MeetShrike5,CHARACTER_HOM_Earth_Skill_Trainer,TRIGGER_HOM_Point_EarthShrike);
DB_RoomDialogInfo(ITEM_HOM_TeleportToAir,TRIGGER_HOM_MeetAstarte6,CHARACTER_HOM_Wind_Skill_Trainer,TRIGGER_HOM_Point_AirAstarte);
KBSECTION
//REGION Astarte/Shrike room dialogs
IF
DB_RoomDialogInfo(_,_MeetTrigger,_,_)
AND
_MeetTrigger!=TRIGGER_NULL
THEN
DB_OneShotPlayerOnlyTrigger(_MeetTrigger);

IF
DB_RoomToUnlock(_Unlocked,_Room,_,_)
AND
DB_RoomDialogInfo(_Unlocked,_MeetTrigger,_,_Trigger)
AND
DB_RoomDialogs(_Diag,_Room,_SpeakChar,0)
THEN
ProcInterruptRoomAD(_SpeakChar);
CharacterTeleportToTrigger(_SpeakChar,_Trigger,"");
CharacterFlushQueue(_SpeakChar);

PROC
ProcInterruptRoomAD((CHARACTER)_Char)
AND
DB_CurRoomAD(_Diag,_Char)
THEN
DialogRequestStopForCharacter(_Char);
Poof(_Char);
NOT DB_CurRoomAD(_Diag,_Char);

IF
DB_RoomToUnlock(_Unlocked,_Room,_,_)
AND
DB_RoomDialogInfo(_Unlocked,_MeetTrigger,_Char,_)
AND
DB_RoomDialogs(_Diag,_Room,_SpeakChar,1)
THEN
ProcInterruptRoomAD(_SpeakChar);
CharacterTeleportToCharacter(_SpeakChar,_Char,"");
CharacterFlushQueue(_SpeakChar);

PROC
ProcOneShotTriggerEntered((CHARACTER)_Player,_Trigger)
AND
DB_RoomToUnlock(_Unlocked,_Room,_,_)
AND
DB_RoomDialogInfo(_,_Trigger,_Char,_)
AND
DB_RoomDialogs(_Diag,_Room,_SpeakChar,_)
THEN

Foop(_SpeakChar);
DB_RoomDialog(_Room,_Player,_Char);
TimerLaunch("HOM_StartRoomDialog",200);

IF
TimerFinished("HOM_StartRoomDialog")
AND
DB_RoomDialog(_Room,_Player,_Char)
AND
DB_RoomDialogs(_Diag,_Room,_SpeakChar,1)
THEN
NOT DB_RoomDialog(_Room,_Player,_Char);
DialogRequestStopForCharacter(_Char);
DialogStartTwoSpeakerDialog(_Diag,_SpeakChar,_Char);

IF
TimerFinished("HOM_StartRoomDialog")
AND
DB_RoomDialog(_Room,_Player,_Char)
AND
DB_RoomDialogs(_Diag,_Room,_SpeakChar,0)
THEN
NOT DB_RoomDialog(_Room,_Player,_Char);
PROC_MendatoryDialog(_Diag, _SpeakChar, _Player);

//END_REGION


//REGION First Astarte Encounter: Hall of Heroes
IF
CharacterSawCharacter(_Player,CHARACTER_HOM_Astarte)
AND
_Player.DB_IsPlayer()
AND
NOT DB_OnlyOnce("HOM_AstarteSinging")
THEN
DialogStartOneSpeakerDialog("HOM_AD_Astarte1",CHARACTER_HOM_Astarte);
DB_Dialogs(CHARACTER_HOM_Astarte,"HOM_Astarte1");
DB_OnlyOnce("HOM_AstarteSinging");

IF
DialogEnded("HOM_Astarte1",_)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_HOM_Astarte,"HOM_Astarte1");
CharacterSetHasDialog(CHARACTER_HOM_Astarte,0);
CharacterClearVar(CHARACTER_HOM_Astarte,"Seat");
ProcCharacterMoveToItem(CHARACTER_HOM_Astarte,ITEM_HOM_TeleportFromHallHeroes,0,"HOM_Astarte_Room1Done");
DialogStartOneSpeakerDialog("HOM_AD_Astarte1b",CHARACTER_HOM_Astarte);
GlobalSetEvent("HallOfHeroesUnlocked");

IF
DialogEnded("HOM_Astarte1",_)
AND
_Player.DB_IsPlayer()
THEN
PROC_CheckPlayTutWithDelay(_Player,"TUT_HoHUnlocked",2000);

IF
CharacterEvent(CHARACTER_HOM_Astarte,"HOM_Astarte_Room1Done")
THEN
Poof(CHARACTER_HOM_Astarte);
//END_REGION

//REGION First Shrike Encounter: Living Quarters
IF
CharacterEnteredTrigger(_Character,TRIGGER_HOM_MeetShrike)
THEN
Foop(CHARACTER_HOM_Shrike);
PROC_MendatoryDialog("HOM_Shrike1",CHARACTER_HOM_Shrike,_Character);

IF
DialogEnded("HOM_Shrike1",_)
THEN
ProcRemoveDialogEntryForSpeaker(CHARACTER_HOM_Shrike,"HOM_Shrike1");
CharacterSetHasDialog(CHARACTER_HOM_Shrike,0);
CharacterDisappearOutOfSight(CHARACTER_HOM_Shrike,180,0,"HOM_Shrike_Room1Done",1);

IF
CharacterEvent(CHARACTER_HOM_Shrike,"HOM_Shrike_Room1Done")
THEN
Poof(CHARACTER_HOM_Shrike);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOSStart"
