Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION Henchman pool 
IF
DB_HenchmanPool(_Char)
THEN
DB_NoDefaultBehavior(_Char);
_Char.isHenchMan();
DB_Dialogs(_Char,"Default_Henchman");
CharacterSetOnStage(_Char,0);
CharacterSetHasDialog(_Char,0);
CharacterSetHasDefaultDialog(_Char,0);
IsStoryNPC(_Char);
SetStoryNpcStatus(_Char);
CharacterSetCanFight(_Char,0);

PROC
ProcGetAvailableHenchman((CHARACTER)_)
AND
DB_ChosenHenchman(_Npc,_Char)
THEN
NOT DB_ChosenHenchman(_Npc,_Char);

PROC
ProcGetAvailableHenchman((CHARACTER)_Npc)
AND
DB_HenchmanPool(_Char)
AND
NOT DB_PickedHenchman(1)
THEN
DB_PickedHenchman(1);
DB_ChosenHenchman(_Npc,_Char);

PROC
ProcGetAvailableHenchman((CHARACTER)_Npc)
THEN
NOT DB_PickedHenchman(1);

//END_REGION

PROC
ProcHireHenchman((CHARACTER)_Char)
THEN
NOT DB_NoDefaultBehavior(_Char);
NOT DB_HenchmanPool(_Char);
DB_Companion(_Char);
ProcSetPartyFullIfLoneWolf();
CharacterSetHasDialog(_Char,1);
NOT IsStoryNPC(_Char);
SetStoryNpcStatus(_Char);
CharacterSetCanFight(_Char,1);
DialogClearCharacterEvent("GEN_DismissedHenchman",_Char,0);

PROC
ProcRemoveHenchmanFromParty((CHARACTER)_Char)
AND
NOT DB_Companion(_Char)
THEN
DB_NoDefaultBehavior(_Char);
Poof(_Char);
CharacterSetHasDialog(_Char,0);
CharacterSetHasDefaultDialog(_Char,0);
IsStoryNPC(_Char);
SetStoryNpcStatus(_Char);
CharacterSetCanFight(_Char,0);

PROC
ProcRemoveHenchmanFromParty((CHARACTER)_Char)
AND
DB_Companion(_Char)
AND
DB_Compcount(_Count)
AND
IntegerSubtract(_Count,1,_New)
AND
DB_HenchmanDismissTrigger((TRIGGER)_Trigger)
THEN
NOT DB_Companion(_Char);
PROC_END_RemoveCompInDBRoomB(_Char);
CharacterDetachFromGroup(_Char);
CharacterMakeNPC(_Char);
CharacterRemoveFromParty(_Char);
NOT DB_Compcount(_Count);
DB_Compcount(_New);
ProcCheckPartyState();
OnCompanionDismissed(_Char);
PlayEffectAtCharacter("FX_GP_TeleportSmoke_A", _Char);
CharacterTeleportToTrigger(_Char,_Trigger,"");
CharacterFlushQueue(_Char);
PlayEffectAtCharacter("FX_GP_TeleportSmoke_A", _Char);
DialogSetCharacterEvent("GEN_DismissedHenchman",_Char,0);

PROC
ProcDismissHenchman((CHARACTER)_Char)
THEN
ProcRemoveHenchmanFromParty(_Char);

IF
DialogCharacterEventSet("GEN_SetupHenchman",_Player,_Instance)
AND
DB_DialogNPCs(_Instance,_Npc,1)
THEN
DialogClearCharacterEvent("GEN_SetupHenchman",_Player,0);
ProcSetupHenchmanForTrader(_Npc);

IF
DialogCharacterEventSet("GEN_StartHenchmenInterface",_Player,_Instance)
AND
DB_DialogNPCs(_Instance,_Npc,1)
THEN
DialogClearCharacterEvent("GEN_StartHenchmenInterface",_Player,0);
ProcStartHenchmanInterfaceForTrader(_Npc,_Player);

IF
DialogCharacterEventSet("GEN_DismissHenchman",_Player,_Instance)
AND
DB_DialogPlayers(_Instance,_Npc,1)
THEN
DialogClearCharacterEvent("GEN_DismissHenchman",_Player,0);
DB_DoDismissHenchMan(_Npc);

IF
DialogEnded(_,_Inst)
AND
DB_DialogPlayers(_Instance,_Npc,1)
AND
DB_DoDismissHenchMan(_Npc)
THEN
NOT DB_DoDismissHenchMan(_Npc);
ProcDismissHenchman(_Npc);

PROC
ProcTeleportHenchmanIntoLevel((CHARACTER)_Npc)
AND
DB_ChosenHenchman(_Npc,_Char)
AND
DB_HenchmanTrader((CHARACTER)_Npc,(TRIGGER)_Trigger)
THEN
CharacterTeleportToTrigger(_Char,_Trigger,"");
CharacterFlushQueue(_Char);
Foop(_Char);

PROC
ProcSetupHenchmanForTrader((CHARACTER)_Npc)
THEN
ProcGetAvailableHenchman(_Npc);
ProcTeleportHenchmanIntoLevel(_Npc);

PROC
ProcStartHenchmanInterfaceForTrader((CHARACTER)_Npc,(CHARACTER)_Player)
THEN
CharacterSetHasDialog(_Npc,0);
DB_HenchmanWaitForDialogEnd(_Npc,_Player);

PROC
ProcDoStartHenchmanInterfaceForTrader((CHARACTER)_Npc,(CHARACTER)_Player)
AND
DB_ChosenHenchman(_Npc,_Char)
THEN
DB_HenchManInUI(_Npc,_Char);

PROC
ProcDoStartHenchmanInterfaceForTrader((CHARACTER)_Npc,(CHARACTER)_Player)
AND
DB_ChosenHenchman(_Npc,_Char)
AND
CharacterCreateHenchMan(_Char,_Player,_Ret)
THEN
ProcHandleHenchmanInterfaceRet(_Npc,_Ret);

PROC
ProcHandleHenchmanInterfaceRet((CHARACTER)_Npc,0)
AND
DB_ChosenHenchman(_Npc,_Char)
THEN
CharacterSetHasDialog(_Npc,1);
NOT DB_HenchManInUI(_Npc,_Char);
ProcDismissHenchman(_Char);
NOT DB_ChosenHenchman(_Npc,_Char);

IF
DialogEnded(_,_Inst)
AND
DB_DialogNPCs(_Inst,_Npc,1)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
DB_HenchmanWaitForDialogEnd(_Npc,_Player)
THEN
NOT DB_HenchmanWaitForDialogEnd(_Npc,_Player);
ProcDoStartHenchmanInterfaceForTrader(_Npc,_Player);

IF
HenchManHiredFinished(_Henchman,0)
AND
DB_HenchManInUI(_Trader,_Henchman)
THEN
CharacterSetHasDialog(_Trader,1);
NOT DB_HenchManInUI(_Trader,_Henchman);
ProcDismissHenchman(_Henchman);

IF
HenchManHiredFinished(_Henchman,1)
AND
DB_HenchManInUI(_Trader,_Henchman)
THEN
CharacterSetHasDialog(_Trader,1);
NOT DB_HenchManInUI(_Trader,_Henchman);
ProcHireHenchman(_Henchman);

IF
SavegameLoaded(_,_,_,_)
AND
DB_HenchManInUI(_Trader,_Henchman)
THEN
CharacterSetHasDialog(_Trader,1);
NOT DB_HenchManInUI(_Trader,_Henchman);
ProcDismissHenchman(_Henchman);

IF
DialogCharacterEventSet("GEN_RehireHenchman",_Char,_)
THEN
DB_RehireHenchman(_Char);

IF
DialogEnded(_,_Inst)
AND
DB_DialogNPCs(_Inst,_Npc,1)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
DB_RehireHenchman(_Npc)
THEN
NOT DB_RehireHenchman(_Npc);
ProcHireHenchman(_Npc);
CharacterMakeCompanion(_Npc,_Player);

IF
DialogStartRequested(_Character,_)
AND
DB_HenchmanTrader(_Character)
THEN
ProcCheckHenchmanPoolState();

PROC
ProcCheckHenchmanPoolState()
AND
NOT DB_HenchmanPool(_)
THEN
GlobalSetEvent("GEN_NoMoreHenchmanAvailable");

PROC
ProcCheckHenchmanPoolState()
AND
DB_HenchmanPool(_)
THEN
GlobalClearEvent("GEN_NoMoreHenchmanAvailable");

PROC
ProcCheckHenchmanPoolState()
AND
DB_HenchmanPool(_Hench)
AND
DB_HenchmanPool(_HenchOther)
AND
_Hench!=_HenchOther
THEN
DB_HaveMultipleHenchman(1);

PROC
ProcCheckHenchmanPoolState()
AND
NOT DB_HaveMultipleHenchman(1)
THEN
GlobalSetEvent("GEN_LastHenchMan");

PROC
ProcCheckHenchmanPoolState()
AND
DB_HaveMultipleHenchman(1)
THEN
GlobalClearEvent("GEN_LastHenchMan");

PROC
ProcCheckHenchmanPoolState()
THEN
NOT DB_HaveMultipleHenchman(1);

//REGION Global dismiss for lone wolf

PROC
ProcDismissAllCompanions()
AND
DB_Companion(_Comp)
AND
_Comp.isHenchMan()
THEN
ProcDismissHenchman(_Comp);

//END_REGION

IF
CharacterResurrected(_Henchman)
AND
_Henchman.isHenchMan()
THEN
DB_Dialogs(_Henchman,"Default_Henchman");


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
